很简单，题目意思很明确让我们跑 lca ，于是我就老老实实写了一遍模板。注意：边跑边异或，用倍增跑。

对 lca 不太熟的我竟然一遍水过~~
```pascal
var
        n,m,i,j,k,s,x,y,z:longint;
        first,next,en,len,deep:array[0..1000001] of longint;
        rmq,g:array[0..200001,0..20] of longint;
procedure add(x,y,z:longint); //邻接表存储
begin
        inc(s);
        next[s]:=first[x];
        first[x]:=s;
        en[s]:=y;
        len[s]:=z;
end;
procedure dfs(x:longint); //搜索，求出深度及每个点的父节点，并存进初值。
var
        t:longint;
begin
        t:=first[x];
        while t>0 do
        begin
        		// x 为父节点，en[t] 为子节点。
                if en[t]<>rmq[x,0] then
                begin
                        rmq[en[t],0]:=x;
                        deep[en[t]]:=deep[x]+1; 
                        //子节点比父节点深度多1。
                        g[en[t],0]:=len[t];
                        dfs(en[t]);
                end;
                t:=next[t];
        end;
end;
function lca(x,y:longint):longint; //跑 lca
var
        ans,i:longint;
begin
        ans:=0;
        if deep[x]>deep[y] then //把大的放后面，后文好处理。
        begin
                i:=x;
                x:=y;
                y:=i;
        end;
        for i:=19 downto 0 do
                if deep[y]-deep[x]>=1<<i then
                //先让深度大的往上跑（倍增），直到跑到和另一个点深度一样。
                begin
                        ans:=ans xor g[y,i];
                        //边跑边异或
                        y:=rmq[y,i];
                end;
        if x=y then exit(ans); //如果到了同一个点，退出。
        for i:=19 downto 0 do
                if rmq[x,i]<>rmq[y,i] then
                //两个点同时向上跑。
                begin
                        ans:=ans xor g[x,i] xor g[y,i];
                        x:=rmq[x,i];
                        y:=rmq[y,i];
                end;
        //跑完后两个点的父节点就是 lca 
        ans:=ans xor g[x,0] xor g[y,0];
        //把到 lca 的边也异或一遍。
        exit(ans);
end;
begin
        readln(n);
        for i:=1 to n-1 do //读入，用邻接表存
        begin
                readln(x,y,z);
                add(x,y,z);
                add(y,x,z);
        end;
        dfs(1); //搜索
        //rmq[i,j] 表示 i 向上跑 2^j 的深度所到的点
        //g[i,j] 表示 i 向上跑 2^j 的深度时所经过所有边的异或值
        for j:=1 to 19 do //做 rmq（倍增跑）
                for i:=1 to n do
                begin
                        rmq[i,j]:=rmq[rmq[i,j-1],j-1];
                        g[i,j]:=g[i,j-1] xor g[rmq[i,j-1],j-1];
                end;
        readln(m);
        for i:=1 to m do //读入询问
        begin
                readln(x,y);
                writeln(lca(x,y)); //离线 lca 搞定！
        end;
end.
```