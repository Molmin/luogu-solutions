这道题目有2个难点

1、如何处理区间动归的转移，尤其要注意子树为空情况。

解决方法：我们不妨设 f[i,i-1]=1 来达到空子树的分数为1的条件

2、如何输出这棵树的先序遍历，这是DP中常考的题型。DP规划的本质是枚举所有方案，但根据“无后效性”原则，只有一种转移会被记录为此状态的最终答案，我们只需要记录哪一种转移是有效的即可。


```cpp
var
    n,i,j,k,tot:longint;
    f,g:array[0..105,0..105] of int64; //f[i,j]表示a[i]~a[j]的最大字段和
    a:array[0..105] of longint;
procedure print(x,y:longint);  //递归输出前序序列
begin
    if x>y then exit;
    if x=y then 
    begin
        if tot=0 then write(x)
        else write(' ',x);
        inc(tot);
        exit;
    end;
    if tot=0 then write(g[x,y])
    else write(' ',g[x,y]);
    inc(tot);
    print(x,g[x,y]-1);
    print(g[x,y]+1,y);
end;
begin
    readln(n);
    for i:=1 to n do read(a[i]);
    for i:=1 to n do 
    begin
        f[i,i]:=a[i];
        f[i,i-1]:=1;
    end;
    for i:=n-1 downto 1 do  //区间类DP需要注意枚举的顺序
        for j:=i+1 to n do 
        begin
            for k:=i to j do   //枚举转移k，k表示根的位置
                if f[i,k-1]*f[k+1,j]+a[k]>f[i,j] then 
                begin
                    f[i,j]:=f[i,k-1]*f[k+1,j]+a[k];  
                    g[i,j]:=k;  //用g[i,j]记录当前状态下的转移，即这一棵有最大字段和的树的根
                end;
        end;
    writeln(f[1,n]);
    tot:=0;
    print(1,n);
    writeln;
end.
```