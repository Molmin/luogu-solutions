因为题目给出的是中序遍历，所以根节点必定在左右子树之间，所以我们可以通过枚举根节点来计算加分值，从而得出最优解。在这里使用记忆化搜索。


------------

变量说明：

```
f,root:array[0..50,0..50]of int64;
//root[i,j]表示在区间[l,r]中,根节点的编号
son:array[0..50,1..2]of longint;
//son[i,1]表示i的左儿子,son[i,2]表示i的右儿子
a:array[0..50]of longint;
//中序遍历

```


------------

记忆化搜索：

```pascal
function dp(l,r:longint):int64;
var
 q,tmp:int64;
 i:longint;
begin
 if l>r then exit(1);
 if l=r then
 begin
  root[l,r]:=l;
  exit(a[l]);
 end;
 //边界
 if f[l,r]>-1 then exit(f[l,r]);//记忆化
 q:=-1;
 for i:=l to r do
 begin
  tmp:=dp(l,i-1)*dp(i+1,r)+a[i];//计算加分
  if tmp>q then//选择最优解
  begin
   q:=tmp;
   root[l,r]:=i;
  end;
 end;
 f[l,r]:=q;
 exit(q);
end;

```


------------

建树：
```pascal
procedure build(l,r:longint);
var
 p,s1,s2:longint;
begin
 if l>=r then exit;
 p:=root[l,r];
 s1:=root[l,p-1];//左儿子
 s2:=root[p+1,r];//右儿子
 son[p,1]:=s1;
 son[p,2]:=s2;
 //建立关系
 build(l,p-1);
 build(p+1,r);
 分治建树
end;


```

------------
输出前序遍历：
```pascal
procedure solve(t:longint);
begin
 if wrote then write(' ');
 write(t);
 wrote:=true;
 //保证行末不输出多余的空格
 if (son[t,1]=0)and(son[t,2]=0) then exit;//找到叶子节点为止
 if son[t,1]>0 then solve(son[t,1]);
 if son[t,2]>0 then solve(son[t,2]);
end;



```


------------
完整代码：
```pascal
var
 f,root:array[0..50,0..50]of int64;
 son:array[0..50,1..2]of longint;
 a:array[0..50]of longint;
 n,i,j:longint;
 wrote:boolean;
 ans:int64;
function dp(l,r:longint):int64;
var
 q,tmp:int64;
 i:longint;
begin
 if l>r then exit(1);
 if l=r then
 begin
  root[l,r]:=l;
  exit(a[l]);
 end;
 if f[l,r]>-1 then exit(f[l,r]);
 q:=-1;
 for i:=l to r do
 begin
  tmp:=dp(l,i-1)*dp(i+1,r)+a[i];
  if tmp>q then
  begin
   q:=tmp;
   root[l,r]:=i;
  end;
 end;
 f[l,r]:=q;
 exit(q);
end;
procedure build(l,r:longint);
var
 p,s1,s2:longint;
begin
 if l>=r then exit;
 p:=root[l,r];
 s1:=root[l,p-1];
 s2:=root[p+1,r];
 son[p,1]:=s1;
 son[p,2]:=s2;
 build(l,p-1);
 build(p+1,r);
end;
procedure solve(t:longint);
begin
 if wrote then write(' ');
 write(t);
 wrote:=true;
 if (son[t,1]=0)and(son[t,2]=0) then exit;
 if son[t,1]>0 then solve(son[t,1]);
 if son[t,2]>0 then solve(son[t,2]);
end;
begin
 wrote:=false;
 read(n);
 for i:=1 to n do read(a[i]);
 for i:=1 to n do
  for j:=1 to n do
   f[i,j]:=-1;
 ans:=dp(1,n);
 writeln(ans);
 build(1,n);
 solve(root[1,n]);
end.

```
