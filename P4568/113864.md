## p党的福利 

### 分层图板子合集（spfa+堆优化的dijkstra）

这题一看就是分层图的最短路问题

分层图最短路以我的理解就是将n个图合在一起跑最短路

那如何合图呢？
------------

- 其实就是先建k个图，每个图的主体是一样的（正常边权在每个图都能体现到）

- 然后将特殊边从第j-1个图向j个图连边就行了（1≤j≤k)
```pascal
//我这里采用的是链式前向星存的图
procedure link(x,y,z:longint);
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=z;
end;
//这一步和平常的存图一样
//但是要注意数组要开2*k*m那么大
//然后是读入操作，这里就有文章可做了
for i:=1 to m do begin
	readln(x,y,z);
	link(x,y,z);//首先是正常的无向图存图
	link(y,x,z);
	for j:=1 to k do begin//这里就是分层图独有的存图方法
		link((j-1)*n+x,j*n+y,0);//从第j-1个图的x点向第j个图的y点连一条权值为0的边
		link((j-1)*n+y,j*n+x,0);
		link(x+j*n,y+j*n,z);
		link(y+j*n,x+j*n,z);//在第j个图中建立正常的边权
	end;
end;
```
建完图，然后就快乐地跑最短路就行了~

### P4568(裸dijkstra)pas 
TLE4个点 64分
```pascal
var
        n,m,k,s,e,i,j,head,tail,tot,v,minn,x,y,z:longint;
        ans:int64;
        he,ne,t,w,dis:array[0..4000000] of int64;
        p:array[0..4000000] of boolean;
function min(x,y:int64):int64;
begin
        if x<y then exit(x) else exit(y);
end;
procedure link(x,y,z:longint);
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=z;
end;
begin
        readln(n,m,k);
        readln(s,e);
        for i:=1 to m do begin
                readln(x,y,z);
                link(x,y,z);
                link(y,x,z);
                for j:=1 to k do begin
                        link((j-1)*n+x,j*n+y,0);
                        link((j-1)*n+y,j*n+x,0);
                        link(x+j*n,y+j*n,z);
                        link(y+j*n,x+j*n,z);
                end;
        end;
        for i:=0 to n*(k+1) do dis[i]:=maxlongint*10000;
        dis[s]:=0;
        head:=s;
        while not p[head] do begin
                p[head]:=true;
                v:=he[head];
                while v>0 do begin
                        if (not (p[t[v]])) and (dis[t[v]]>dis[head]+w[v]) then
                         dis[t[v]]:=dis[head]+w[v];
                        v:=ne[v];
                end;
                minn:=maxlongint;
                for i:=0 to n*(1+k) do if (not p[i]) and (minn>dis[i]) then begin
                        minn:=dis[i];
                        head:=i;
                end;
        end;
        ans:=maxlongint*10000;
        for i:=0 to k do ans:=min(ans,dis[e+i*n]);
        writeln(ans);
end.
```
### P4568(堆+dijkstra).pas 
开o2 394ms 不开458ms
```pascal
var
        n,m,k,s,e,i,j,head,tot,v,x,y,z:longint;
        ans:int64;
        he,ne,t,w,dis,cj,num,heap:array[0..4000000] of int64;
function min(x,y:int64):int64;
begin
        if x<y then exit(x) else exit(y);
end;
function swap(var x,y:int64):int64;
var
        t:int64;
begin
        t:=x;
        x:=y;
        y:=t;
end;
procedure link(x,y,z:longint);
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=z;
end;
procedure insert(a,x:longint);
var
        i:longint;
begin
        if cj[x]<>0 then begin
                i:=cj[x];
                heap[i]:=a;
        end else begin
                inc(heap[0]);
                i:=heap[0];
                heap[i]:=a;
                num[i]:=x;
                cj[x]:=i;
        end;
        while (i>>1>0) and (heap[i]<heap[i>>1]) do begin
                swap(cj[num[i]],cj[num[i>>1]]);
                swap(heap[i],heap[i>>1]);
                swap(num[i],num[i>>1]);
                i:=i>>1;
        end;
end;
procedure del;
var
        i,m:longint;
begin
        cj[num[1]]:=0;
        heap[1]:=heap[heap[0]];
        num[1]:=num[heap[0]];
        dec(heap[0]);
        i:=1;
        if (heap[i<<1]<heap[i<<1+1]) or (i=heap[0]) then m:=i<<1 else m:=i<<1+1;
        while (m<=heap[0]) and (heap[m]<heap[i]) do begin
                swap(cj[num[i]],cj[num[m]]);
                swap(heap[i],heap[m]);
                swap(num[i],num[m]);
                i:=m;
                if (heap[i<<1]<heap[i<<1+1]) or (i<<1=heap[0]) then m:=i<<1 else m:=i<<1+1;
        end;
end;
begin
        readln(n,m,k);
        readln(s,e);
        for i:=1 to m do begin
                readln(x,y,z);
                link(x,y,z);
                link(y,x,z);
                for j:=1 to k do begin
                        link((j-1)*n+x,j*n+y,0);
                        link((j-1)*n+y,j*n+x,0);
                        link(x+j*n,y+j*n,z);
                        link(y+j*n,x+j*n,z);
                end;
        end;
        for i:=0 to n*(k+1) do dis[i]:=maxlongint*10000;
        heap[0]:=1;
        num[1]:=s;
        dis[s]:=0;
        head:=s;
        while heap[0]<>0 do begin
                v:=he[head];
                while v>0 do begin
                        if dis[t[v]]>dis[head]+w[v] then begin
                                dis[t[v]]:=dis[head]+w[v];
                                insert(dis[t[v]],t[v]);
                        end;
                        v:=ne[v];
                end;
                del;
                head:=num[1];
        end;
        ans:=maxlongint*10000;
        for i:=0 to k do ans:=min(ans,dis[e+i*n]);
        writeln(ans);
end.
```
### P4568(spfa).pas
413ms
```pascal
var
        n,m,k,i,j,x,y,z,head,tail,v,o,s,e:longint;
        tot,ans,maxn,g:int64;
        ne,he,t,w,f:array[0..200000] of int64;
        dis:array[0..200000,0..21] of int64;
        p:array[0..200000] of boolean;
        q:array[0..8000000] of longint;
function min(x,y:int64):int64;
begin
        if x<y then exit(x) else exit(y);
end;
procedure link(x,y,z:longint);
begin
        inc(tot);
        ne[he[x]]:=tot;
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=z;
        if f[x]=0 then f[x]:=tot;
end;
procedure swap(x,y:longint);
begin
        g:=q[x];
        q[x]:=q[y];
        q[y]:=g
end;
begin
        maxn:=maxlongint*100;
        readln(n,m,k);
        readln(s,e);
        inc(s);
        inc(e);
        for i:=1 to m do begin
                readln(x,y,z);
                inc(x);
                inc(y);
                link(x,y,z);
                link(y,x,z);
        end;
        for i:=1 to n+1 do for j:=0 to k do dis[i,j]:=maxn;
        dis[s,0]:=0;
        head:=0;
        tail:=1;
        p[s]:=true;
        q[1]:=s;
        for i:=0 to k do begin
                while head<>tail do begin
                        inc(head);
                        o:=q[head];
                        v:=f[o];
                        while v<>0 do begin
                                if dis[o,i]+w[v]<dis[t[v],i] then begin
                                        dis[t[v],i]:=dis[o,i]+w[v];
                                        if not p[t[v]] then begin
                                                inc(tail);
                                                q[tail]:=t[v];
                                                if dis[t[v],i]<dis[q[head+1],i] then swap(head+1,tail);
                                                p[t[v]]:=true;
                                        end;
                                end;
                                v:=ne[v];
                        end;
                        p[o]:=false;
                end;
                for j:=1 to n do begin
                        v:=f[j];
                        while v<>0 do begin
                                dis[t[v],i+1]:=min(dis[t[v],i+1],dis[j,i]);
                                v:=ne[v];
                        end;
                end;
                head:=0;
                tail:=0;
                for j:=1 to n do if dis[j,i+1]<>maxn then begin
                        inc(tail);
                        q[tail]:=j;
                        p[j]:=true;
                end else p[j]:=false;
        end;
        ans:=maxn;
        for i:=0 to k do ans:=min(ans,dis[e,i]);
        writeln(ans);
end.
```