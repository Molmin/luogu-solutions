这显然是一道分层图最短路。。。

然而我这个蒟蒻之前总是写错分层图，还好这次一遍A了（~~心情大好~~）。。。

我认为的分层图是这样的：

![](https://cdn.luogu.com.cn/upload/pic/29090.png)

~~本蒟蒻拙劣的画技可能已经让大佬鄙视了。。。~~

对于普通的最短路，数组只开一维，d[u]表示从起点到达u的最短路。。。

现在多加入一维，d[g][h]表示从起点到g，使用优惠次数为h的的最短路径。

转移也需要改变：

首先是不使用优惠的情况：

d[son][h]:=min(d[son][h],d[g][h]+cost)（son为从g出发能到达的节点，cost为所需的费用）

使用优惠？！

d[son][h+1]:=min(d[son][h+1],d[g][h])

本蒟蒻讨厌堆，所以打了个蒟蒻版的线段树优化。。。

~~Pascal~~ Code：

```cpp
// luogu-judger-enable-o2//洛谷O2
uses math;//懒得打max，min
type node=record//记录类型记录g和h
  g,h:longint;
end;
var
  head,next,d1,d2:array[0..500005]of longint;
  tree:array[0..500005]of int64;
  id:array[0..500005]of node;
  d:array[0..20005,0..25]of int64;
  ss:array[0..500005]of longint;
  n,m,k,s,t,i,j,x,y,z,num,zty,v,cost,getnode,ll:longint;
  ans,inf:int64;
  u,newnode:node;
procedure add(x,y,z:longint);//链式前向星记录边
begin
  inc(num);
  d1[num]:=y;//节点
  d2[num]:=z;//价值
  next[num]:=head[x];
  head[x]:=num;
end;
function titititing(u:node):longint;////将node类型变为一个数字
begin
  exit((u.g-1)*(k+1)+u.h+1);
end;
function titititingg(p:longint):longint;//通过数字取出g
begin
  exit((p-1) div (k+1)+1);
end;
function titititingh(p:longint):longint;//通过数字取出h
begin
  if p mod (k+1)=0 then
    exit(k);
  exit(p mod (k+1)-1);
end;
procedure build(p,l,r:longint);//建树
var
  mid,getg,geth:longint;
begin
  if l=r then
  begin
    getg:=titititingg(l);
    geth:=titititingh(l);
    tree[p]:=d[getg][geth];
    id[p].g:=getg;
    id[p].h:=geth;
    ss[l]:=p;
    exit;
  end;
  mid:=(l+r) >> 1;
  build(p+p,l,mid);
  build(p+p+1,mid+1,r);
  if tree[p+p]<=tree[p+p+1] then
  begin
    tree[p]:=tree[p+p];
    id[p]:=id[p+p];
  end else
  begin
    tree[p]:=tree[p+p+1];
    id[p]:=id[p+p+1];
  end;
end;
procedure change(p:longint);//修改
begin
  if p=0 then
    exit;
  if tree[p+p]<=tree[p+p+1] then
  begin
    tree[p]:=tree[p+p];
    id[p]:=id[p+p];
  end else
  begin
    tree[p]:=tree[p+p+1];
    id[p]:=id[p+p+1];
  end;
  change(p div 2);
end;
begin
  readln(n,m,k);
  readln(s,t);
  inc(s);inc(t);
  for i:=1 to m do
  begin
    readln(x,y,z);
    inc(x);inc(y);
    add(x,y,z);
    add(y,x,z);
  end;
  inf:=1000000007;
  for i:=1 to n do
    for j:=0 to k do
      d[i][j]:=inf;
  d[s][0]:=0;
  build(1,1,n*(k+1));
  while tree[1]<inf do
  begin
    u:=id[1];
    zty:=titititing(u);
    tree[ss[zty]]:=inf;
    change(ss[zty] div 2);
    ll:=head[u.g];
    while ll<>0 do
    begin
      v:=d1[ll];
      cost:=d2[ll];
      if d[u.g][u.h]+cost<d[v][u.h] then//第一种情况
      begin
        d[v][u.h]:=d[u.g][u.h]+cost;
        newnode.g:=v;
        newnode.h:=u.h;
        getnode:=titititing(newnode);
        tree[ss[getnode]]:=d[v][u.h];
        change(ss[getnode] div 2);
      end;
      if (u.h<k) and (d[u.g][u.h]<d[v][u.h+1]) then//第二种情况
      begin
        d[v][u.h+1]:=d[u.g][u.h];
        newnode.g:=v;
        newnode.h:=u.h+1;
        getnode:=titititing(newnode);
        tree[ss[getnode]]:=d[v][u.h+1];
        change(ss[getnode] div 2);
      end;
      ll:=next[ll];
    end;
  end;
  ans:=inf;
  for i:=0 to k do
    ans:=min(ans,d[t][i]);
  writeln(ans);
end.
```