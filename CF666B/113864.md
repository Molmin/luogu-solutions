看到没有Pascal，就来一波吧~


## 题意：

给你一张n个点m条边的有向图，让你给出四个点的序列s1->s2->s3->s4，使得这四个点依次间的最短路之和最大。(4 ≤ n ≤ 3000, 3 ≤ m ≤ 5000) 

时空限制 5000ms / 512MB （非常充裕~）

## 思路：

1. 正向和反向各建一个图，然后各跑n次最短路($ 2n^3 $)

这里我用的是链式前向星+堆优化的dijkstra

2. 暴力枚举找3个最长的最短路的点作为备选，多选一个的原因是怕选了重点($ 2n^2 $)

3. 最后再从3个中暴力选出2个点，就搞定了($ 9n^2 $)

总计  $ 13n^2 $

------------


这样的思路看起来很暴力，但是打得很爽，感觉能特别深刻的感受到暴力枚举和最短路的魅力~

下面附上5.2k的工整代码 

无指针，阅读体验极佳

## CF666B(真6).pas

```pascal
var
        n,m,i,j,k,l,tot,v,head,st1,st2,st3,ans,bns1,ans1,ans2,ans3,s1,s2,s3,s4:longint;
        he,ne,t,w,cj,num,heap,dis:array[0..400010] of longint;
        x,y:array[0..300010] of longint;
        bis,bh1,yh1,bh2,yh2,bj:array[0..3010,0..3010] of longint;
procedure swap(var x,y:longint);//自定义交换函数
var
        t:longint;
begin
        t:=x;
        x:=y;
        y:=t;
end;
procedure link(x,y,z:longint);//链式前向星
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=z;
end;
//以下为堆优化
//**
procedure insert(a,x:longint);
var
        i:longint;
begin
        if cj[x]<>0 then begin
                heap[cj[x]]:=a;
                i:=cj[x];
        end else begin
                inc(heap[0]);
                heap[heap[0]]:=a;
                num[heap[0]]:=x;
                i:=heap[0];
                cj[x]:=heap[0];
        end;
        while (i>>1>0) and (heap[i]<heap[i>>1]) do begin
                swap(cj[num[i]],cj[num[i>>1]]);
                swap(heap[i],heap[i>>1]);
                swap(num[i],num[i>>1]);
                i:=i>>1;
        end;
end;
procedure del;
var
        mid,i:longint;
begin
        cj[num[1]]:=0;
        heap[1]:=heap[heap[0]];
        num[1]:=num[heap[0]];
        dec(heap[0]);
        i:=1;
        if (heap[i<<1]<heap[i<<1+1]) or (i=heap[0]) then mid:=i<<1 else mid:=i<<1+1;
        while (mid<=heap[0]) and (heap[mid]<heap[i]) do begin
                swap(cj[num[i]],cj[num[mid]]);
                swap(heap[i],heap[mid]);
                swap(num[i],num[mid]);
                i:=mid;
                if (heap[i<<1]<heap[i<<1+1]) or (i<<1=heap[0]) then mid:=i<<1 else mid:=i<<1+1;
        end;
end;
//**
procedure dij(x:longint);//标准dijkstra
var
        i:longint;
begin
        for i:=1 to n do begin
                dis[i]:=maxlongint;
                heap[i]:=0;
                num[i]:=0;
                cj[i]:=0;
        end;
        dis[x]:=0;
        head:=x;
        heap[0]:=1;
        heap[1]:=0;
        num[1]:=x;
        while heap[0]<>0 do begin
                v:=he[head];
                while v<>0 do begin
                        if dis[t[v]]>dis[head]+w[v] then begin
                                dis[t[v]]:=dis[head]+w[v];
                                insert(dis[t[v]],t[v]);
                        end;
                        v:=ne[v];
                end;
                del;
                head:=num[1];
        end;
//暴力枚举3个点
//**
        ans1:=0;
        ans2:=0;
        ans3:=0;
        st1:=0;
        st2:=0;
        st3:=0;
        for j:=1 to n do begin
                if (dis[j]=maxlongint) or (dis[j]=0) then continue;
                if dis[j]>ans3 then begin
                        if dis[j]>ans2 then begin
                                if dis[j]>ans1 then begin
                                        ans3:=ans2;
                                        st3:=st2;
                                        ans2:=ans1;
                                        st2:=st1;
                                        ans1:=dis[j];
                                        st1:=j;
                                 end else begin
                                        ans3:=ans2;
                                        st3:=st2;
                                        ans2:=dis[j];
                                        st2:=j;
                                 end
                        end else begin
                                ans3:=dis[j];
                                st3:=j;
                        end;
                end;
        end;
//**
end;
procedure fz1(x,y:longint);//将找到的点保存在数组里面
begin
        bh1[x,y+1]:=ans1;
        bh1[x,y+2]:=ans2;
        bh1[x,y+3]:=ans3;
        yh1[x,y+1]:=st1;
        yh1[x,y+2]:=st2;
        yh1[x,y+3]:=st3;
end;
procedure fz2(x,y:longint);
begin
        bh2[x,y+1]:=ans1;
        bh2[x,y+2]:=ans2;
        bh2[x,y+3]:=ans3;
        yh2[x,y+1]:=st1;
        yh2[x,y+2]:=st2;
        yh2[x,y+3]:=st3;
end;
begin
        readln(n,m);
        for i:=1 to m do begin
                read(x[i],y[i]);
                if x[i]=y[i] then continue;
                link(x[i],y[i],1);//正向存图
        end;
        for i:=1 to n do begin
                dij(i);
                fz1(i,0);
                for j:=1 to n do bis[i,j]:=dis[j];
        end;
        tot:=0;
        fillchar(he,sizeof(he),0);
        for i:=1 to m do begin
                if x[i]=y[i] then continue;
                link(y[i],x[i],1);//反向存图
        end;
        for i:=1 to n do begin
                dij(i);
                fz2(i,0);
        end;
//3中选2
        s1:=1;
        s2:=2;
        s3:=3;
        s4:=4;
        ans:=-maxlongint;
        for i:=1 to n do for j:=1 to n do if (bis[i,j]<>maxlongint) and (bis[i,j]<>0) then
         for k:=1 to 3 do for l:=1 to 3 do begin
                ans1:=yh1[j,k];
                bns1:=yh2[i,l];
                if (bns1<>i) and (bns1<>j) and (bns1<>ans1) and (i<>j) and (i<>ans1) and (j<>ans1) and (ans1<>0) and (bns1<>0) then
                 if bh1[j,k]+bh2[i,l]+bis[i,j]>ans then begin
                        s1:=bns1;
                        s2:=i;
                        s3:=j;
                        s4:=ans1;
                        ans:=bh1[j,k]+bh2[i,l]+bis[i,j];
                end;
        end;
        write(s1,' ',s2,' ',s3,' ',s4);//完美输出
end.
```