这题一开始想到直接暴力，先建一个并查集，再进行操作，但是万万没有想到题目居然让我们拆边，而并查集又不支持拆边操作。。

所以我们需要换一个思路。

考虑这一个图中一开始没有小镇，我们再将要删除的小镇一个个倒着加进来，再判断图是否联通。

比如题目中关闭顺序为：3 4 1 2

那么我们加小镇的顺序就是：2 1 4 3 

这样就很好的解决了并查集不能支持拆边操作的缺点。

接下来就只要考虑在加点时如何判断与其他点是否联通，这点可以用一个二维数组存关系（这题数据小，只有3000），或者用邻接表存。

最后就是判断整个图是否联通，这点我们需要找到第一个已经加进去的小镇，再与其他加进来的小镇做一次判断，就可以了。

在加上优化了的并查集，总的时间复杂度为O(n^2×k),这里的k是一个小小的并查集操作的时间复杂度。(当然用邻接表可能有O(n))

放代码：
```pascal
var n,m,i,x,y,x1,y1,j,t:longint; flag:boolean;
a,c,f:array[0..100001] of longint;
e,d:array[0..100001] of boolean;
b:array[0..3001,0..3001] of longint;
function find(x:longint):longint;
begin
if f[x]=x then exit(x);
f[x]:=find(f[x]);
exit(f[x]);
end;                      //优化了的并查集，不解释

begin
readln(n,m);
for i:=1 to n do f[i]:=i;      //默认祖先是他自己
for i:=1 to m do
  begin
  readln(x,y);
  inc(a[x]); b[x,a[x]]:=y;
  inc(a[y]); b[y,a[y]]:=x;       //二维数组保存关系
  end;
for i:=1 to n do readln(c[i]);
for i:=n downto 1 do                  //倒着加入
  begin
  d[c[i]]:=true;                 //加入这一个小镇
  flag:=true;                    //默认联通
  for j:=1 to a[c[i]] do         
    if d[b[c[i],j]] then         
                 //如果有关系并且已经加入，那么就连边
      begin
      x1:=find(b[c[i],j]); y1:=find(c[i]);   //找祖先
      if x1<>y1 then f[x1]:=y1;         //连边
      end; 
  t:=1; while not d[t] do inc(t);     //找第一个加入的小镇
  for j:=t+1 to n do
    if (find(j)<>find(t))and(d[j]) then 
      flag:=false;         //如果有一个不连通，就变为false
  e[i]:=flag; 
  end;
for i:=1 to n do            //上面倒着，下边就顺着
  if e[i] then writeln('YES') else writeln('NO');
end.
```


