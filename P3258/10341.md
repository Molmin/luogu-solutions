刚学完树剖，准备来刷模板题。。。

第一次看到题目，觉得这就是裸的树剖，但一看题解，LCA+树上差分……

瞄一眼数据范围，感觉有点慌。。。

但蒟蒻还是打了一波，结果过了？？？

讲一讲解法：

我们可以看成n-1次赋值，每次赋值都是从a[i]->a[i+1]全部加1，然后输出每一个点的权值。

注意：a[2]……a[n]这些点就多算一次，需要减1。

注释写在代码里吧。。。

Pascal Code：

```cpp
// luogu-judger-enable-o2//O2。。。
var
  tree,lazy:array[0..2000005]of longint;
  head,next,d:array[0..800005]of longint;
  id,top,size,f,deep,son,a:array[0..400005]of longint;
  n,i,x,y,num,cnt:longint;
procedure add(x,y:longint);//链式前向星
begin
  inc(num);
  d[num]:=y;
  next[num]:=head[x];
  head[x]:=num;
end;
procedure swap(var x,y:longint);//pascal 呜呜呜~
var
  t:longint;
begin
  t:=x;
  x:=y;
  y:=t;
end;
procedure dfs1(u,fa:longint);//dfs1求出每一个点的子树大小、深度、重儿子和父亲
var
  v,ll,sum:longint;
begin
  size[u]:=1;//size记录子树大小
  sum:=-1;
  ll:=head[u];
  while ll<>0 do
  begin
    v:=d[ll];
    if v<>fa then
    begin
      deep[v]:=deep[u]+1;//深度
      dfs1(v,u);
      size[u]:=size[u]+size[v];//子树大小
      f[v]:=u;//父亲
      if size[v]>sum then//更新重儿子
      begin
        sum:=size[v];
        son[u]:=v;
      end;
    end;
    ll:=next[ll];
  end;
end;
procedure dfs2(u,topl:longint);//dfs2记录重链顶和id
var
  v,ll:longint;
begin
  inc(cnt);
  id[u]:=cnt;
  top[u]:=topl;
  if son[u]=0 then
    exit;
  dfs2(son[u],topl);
  ll:=head[u];
  while ll<>0 do
  begin
    v:=d[ll];
    if (v<>f[u]) and (v<>son[u]) then
      dfs2(v,v);
    ll:=next[ll];
  end;
end;
//--------以下为线段树--------
procedure down(p,l,r:longint);//标记下传
begin
  if l=r then
  begin
    tree[p]:=tree[p]+lazy[p];
    lazy[p]:=0;
    exit;
  end;
  lazy[p+p]:=lazy[p+p]+lazy[p];
  lazy[p+p+1]:=lazy[p+p+1]+lazy[p];
  tree[p]:=tree[p]+(r-l+1)*lazy[p];
  lazy[p]:=0;
end;
procedure update(p,l,r:longint);//标记更新
var
  mid:longint;
begin
  if l=r then
    exit;
  mid:=(l+r) div 2;
  down(p+p,l,mid);
  down(p+p+1,mid+1,r);
  tree[p]:=tree[p+p]+tree[p+p+1];
end;
procedure change(p,l,r,x,y:longint);//将[x,y]都增加1
var
  mid:longint;
begin
  down(p,l,r);
  if (l=x) and (r=y) then
  begin
    inc(lazy[p]);
    exit;
  end;
  mid:=(l+r) div 2;
  if y<=mid then
    change(p+p,l,mid,x,y) else
  if x>mid then
    change(p+p+1,mid+1,r,x,y) else
    begin
      change(p+p,l,mid,x,mid);
      change(p+p+1,mid+1,r,mid+1,y);
    end;
  update(p,l,r);
end;
function calc(p,l,r,x:longint):longint;//计算x节点的值
var
  mid:longint;
begin
  down(p,l,r);
  if l=r then
    exit(tree[p]);
  mid:=(l+r) div 2;
  if x<=mid then
    exit(calc(p+p,l,mid,x)) else
    exit(calc(p+p+1,mid+1,r,x));
  update(p,l,r);
end;
//--------以上为线段树--------
//--------以下为树链剖分--------
procedure ChangeRange(x,y:longint);//将x->y的路径+1
begin
  while top[x]<>top[y] do//当x，y在同一条链上停止
  begin
    if deep[top[x]]<deep[top[y]] then
      swap(x,y);
    change(1,1,n,id[top[x]],id[x]);
    x:=f[top[x]];
  end;
  if deep[x]>deep[y] then
    swap(x,y);
  change(1,1,n,id[x],id[y]);
end;
function GetRange(x:longint):longint;
var
  ans:longint;
begin
  ans:=calc(1,1,n,id[x]);//注意是id[x]而不是x
  exit(ans);
end;
//--------以上为树链剖分--------
begin
  readln(n);
  for i:=1 to n do
    read(a[i]);
  readln;
  for i:=1 to n-1 do
  begin
    readln(x,y);
    add(x,y);
    add(y,x);
  end;
  dfs1(1,0);
  dfs2(1,1);
  for i:=1 to n-1 do
    ChangeRange(a[i],a[i+1]);
  for i:=1 to n do
  begin
    if i=a[1] then//特判
      writeln(GetRange(i)) else
      writeln(GetRange(i)-1);
  end;
end.
```

结束了吧。。。