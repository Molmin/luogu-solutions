由于是求的最多能放置的炮兵个数，就是求某一个状态下，它对应的炮兵个数最多，所以就想到dp方程肯定是那种dp[i+1]=max{dp[i-1]..}的形式，又考虑到每一行


的状态只和前两行有关系，所以考虑用dp来做，下面考虑如何用二进制位来表示一个状态及转移方程。


对于原始的矩阵，我们用1来表示可以放置炮兵，即对应图中的P，这样每一行都有一个可以放置炮兵的状态，存到rstate[N]中，用来check该行的状态是否合法。


由于当前行和前两行有关系，所以得用3维矩阵来保存一个状态下最多的炮兵个数，用dp[i][curst][prest]表示当前第i行状态对curst，前一行状态为prest的最大炮兵数。


转移方程为dp[i][curst][prest]=max{dp[i-1][prest][preprest]}，这样求到最后一行之后，答案就是最后一行所有状态中最大的那个。程序初始化的时候需要对第一行


进行预处理，设置dp[0][st][0]=st合法&st中1的个数。这样进行下面的计算的时候，由于0状态肯定是和所有状态兼容的，所以就不会影响计算结果。


```delphi

var 
    n,m,i,j,k,p,s,t    :longint;
    a                :array[0..100,0..100] of longint;
    map                :array[0..101] of longint;
    list,num        :array[0..1050] of longint;
    x                :string;
    f                :array[0..100,0..100,0..100] of longint;
    
function get_num(x:longint):longint;
var sum                :longint;
begin
    sum:=0;
    while x>0 do
    begin
        if ((x and 1)<>0) then inc(sum);
        x:=x>>1;
    end;
    exit(sum);
end;

function max(x,y:longint):longint;
begin
    if x>y then exit(x) else exit(y);
end;

begin
    readln(n,m);
    for i:=1 to n do
    begin    
        readln(x);
        for j:=1 to m do 
            if x[j]='P' then map[i]:=(map[i]<<1) or 1
            else if x[j]='H' then map[i]:=map[i]<<1;
    end;
    for i:=0 to (1<<m-1) do
    begin
        if (i and (i<<1)=0) and (i and (i<<2)=0) then
        begin
            list[t]:=i;
            num[t]:=get_num(i);
            inc(t);
        end;
    end;
    for i:=0 to (t-1) do
        if ((map[1] or list[i])=map[1]) then f[1,i,0]:=num[i];
    for i:=2 to n do
    for j:=0 to (t-1) do
    begin
        if (list[j] or map[i]=map[i]) then
        begin
            for k:=0 to (t-1) do
            begin
                if (list[j] and list[k]=0) then
                begin
                    for p:=0 to (t-1) do
                    begin
                        if (list[p] and list[k]=0) and (list[p] and list[j]=0) then
                            f[i,j,k]:=max(f[i,j,k],f[i-1,k,p]+num[j]);
                    end;
                end;
            end;
        end;
    end;
    s:=0;
    for j:=0 to (t-1) do
    for k:=0 to (t-1) do
        s:=max(s,f[n,j,k]);
    writeln(s);
end.

```