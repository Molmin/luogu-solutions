### 第一次写题解祭
# 此题为一道典型的最短路模版题

由于数据范围<=50000，故考虑采用**SPFA算法**。

关于SPFA算法不再赘述，这里值得提一点的是对于数组邻接表的**空间优化**。

倘若采用朴素的邻接表存储邻边，则空间复杂度 50000*50000，必炸无疑。

我们考虑将二维压成一维。



设c[i] 表示 结点i位于第几条邻边

  suc[i] 表示 第i条邻边的后继结点（如果x，y两点间存在从x到y的有向边，则suc[i]=y）
  
  last[i] 表示 当前邻边的上一条邻边编号
  
  len[i] 表示 第i条邻边长度
  
  
  这样，我们便可以很方便地调用每一条邻边。
  
  具体见代码：
  ```pascal
var n,m,i,t,w,cnt,x,y,z:longint;
    suc,last,c,len,f,sq:array[1..5000000] of longint;  //注意数据范围，太小会TLE（莫名其妙？？），太大会爆内存
  procedure addedge(x,y,z:longint);  //连边，从结点x到结点y的长度为z的有向边
  begin
    inc(cnt); suc[cnt]:=y; last[cnt]:=c[x]; c[x]:=cnt; len[cnt]:=z;
  end;
begin
  readln(n,m);
  for i:=1 to m do
  begin
    readln(x,y,z); addedge(x,y,z); addedge(y,x,z);  //由于为双向边，故连边两次
  end;
  t:=1; w:=1;
  fillchar(f,sizeof(f),63);  //最短路数组初始化
  f[1]:=0; sq[1]:=1;  //1号结点入队列，最短路初始为0
  while t<=w do
  begin
    i:=c[sq[t]];  //取出当前头结点所在的邻边编号
    while i>0 do  //当该条邻边存在时
    begin
      if f[sq[t]]+len[i]<f[suc[i]] then  //如果对于结点suc[i]存在一条更短的路径，则松弛此结点
      begin
        inc(w); sq[w]:=suc[i]; f[suc[i]]:=f[sq[t]]+len[i];  //该结点入队尾，同时答案更新
      end;
      i:=last[i];  //取该边的上一条边
    end;
    inc(t);
  end;
  writeln(f[n]);  //f[n]存放从结点1至结点n的最短路，即为所求
end.
```