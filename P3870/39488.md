都是大佬，区间修改的线段树随便写写，我太蒟蒻了，弄不懂线段树的lazy数组怎么用，所以只能写分块来过，时间复杂度O(N√N)

分块其实也是一种暴力，但是一种很优秀的暴力，它的主要思想是吧根号N分为一块，对于一个要处理的区间，它将中间跨过得块直接加上懒标记，对于两边多出来的元素直接暴力处理。这题操作1操作2都是这样的

详细解释见我的代码(**P党福利！！！**)
```
var
  a1,a2,a3:longint;
  ans,tot,block,i,j,m,n,k,p:longint;
  flag,l,r,point,key:array[0..100005]of longint;
  a:array[0..100005]of boolean;
procedure down(t:longint);//将懒数组下放，清零
var
  i:longint;
begin
  for i:=l[t] to r[t] do
    begin
      if a[i]=true then dec(key[t])
      else inc(key[t]);
      a[i]:=not(a[i]);
    end;
  flag[t]:=0;
end;
begin
  readln(n,m); block:=trunc(sqrt(n)); tot:=0;
  fillchar(a,sizeof(a),false);
  for i:=1 to n do
    begin
      if i mod block=1 then
        begin
          inc(tot); l[tot]:=i;
        end;
      if i mod block=0 then
        r[tot]:=i;
      point[i]:=tot;
    end;//l,r数组是这个块左右边界是什么,point数组指这个位置属于哪个块
  if r[tot]=0 then r[tot]:=n;
  {for i:=1 to tot do
    writeln(l[i],' ',r[i]);
  writeln(block);         }
  for p:=1 to m do
    begin
      readln(a1,a2,a3);
      if a1=0 then
        begin
          if point[a2]=point[a3] then//如果要处理的区间在一个块里
            begin
              if flag[point[a2]] mod 2=1 then
                down(point[a2]);//如果这个区间被关开了奇数次就要变
              for i:=a2 to a3 do//直接暴力修改
                begin
                  if a[i]=false then
                    begin a[i]:=true; inc(key[point[a2]]); end
                  else
                    begin a[i]:=false; dec(key[point[a2]]); end;
                end;
            end
          else
            begin
              if flag[point[a2]] mod 2=1 then//处理左边多出的元素
                down(point[a2]);
              for i:=a2 to r[point[a2]] do//处理左边多出的元素
                begin
                  if a[i]=false then
                    begin a[i]:=true; inc(key[point[a2]]); end
                  else
                    begin a[i]:=false; dec(key[point[a2]]); end;
                end;

              for i:=point[a2]+1 to point[a3]-1 do//给中间跨过得块直接打上懒标记
                inc(flag[i]);

              if flag[point[a3]] mod 2=1 then//处理右边多出的元素
                down(point[a3]);
              for i:=l[point[a3]] to a3 do//处理右边多出的元素
                begin
                  if a[i]=false then
                    begin a[i]:=true; inc(key[point[a3]]); end
                  else
                    begin a[i]:=false; dec(key[point[a3]]); end;
                end;
            end;
        end
      else
        begin
          ans:=0;
          if point[a2]=point[a3] then//在一个块里暴力求解
            begin
              if flag[point[a2]] mod 2=1 then
                down(point[a2]);
              for i:=a2 to a3 do
                if a[i]=true then
                  inc(ans);
              writeln(ans);
            end
          else
            begin
              if flag[point[a2]] mod 2=1 then//暴力求解左右边多余元素
                down(point[a2]);
              for i:=a2 to r[point[a2]] do
                if a[i]=true then
                  inc(ans);
              if flag[point[a3]] mod 2=1 then
                down(point[a3]);
              for i:=l[point[a3]] to a3 do
                if a[i]=true then
                  inc(ans);
              for i:=point[a2]+1 to point[a3]-1 do
              //中间跨过得块如果不需要修改，那么就是key值，
              //如果要修改，就是整个长度减去key值，这里不需要下放懒标记
                if flag[i] mod 2=0 then
                  ans:=ans+key[i]
                else
                  ans:=ans+(r[i]-l[i]+1)-key[i];
              writeln(ans); 
            end;
        end;
    end;
end.
```
**各位大佬给个赞吧(可怜的萌新)**