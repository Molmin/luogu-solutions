这道题的实质应该是排列组合，只要求出每一位的数字可以转换成的数字的个数的乘积即可。

本题一共有两个难点，第一个就是求出每一位的数字可以转换成的数字的个数。

这一点可以用最短路径中的floyd来实现，用二维数组a[i,j]来表示数字i是否可以转换为数字j，最后进行累加统计。

高精度可以无需用冗长的代码实现，只要先将ans数组中的每一位与待乘数字个数相乘，再依次取模进位即可。

代码如下（拜托了一定要通过审核啊qwq）


```cpp
var
  s:string;
  ch:char;
  p,i,j,k,x,y,n:longint;
  f:array[0..9]of longint;
  ans:array[0..101]of longint;
  a:array[0..9,0..9]of longint;

procedure init;//预处理
begin
  readln(s);
  p:=pos(' ',s);
  val(copy(s,p+1,length(s)-p),n);
  s:=copy(s,1,p-1);
  for i:=1 to n do
    begin
      readln(x,y);
      a[x,y]:=1;
    end;
  for k:=0 to 9 do
    for i:=0 to 9 do
      for j:=0 to 9 do
        if(i<>j)and(j<>k)and(k<>i)and(a[i,k]+a[k,j]=2)then a[i,j]:=1;//floyd算法
  for i:=0 to 9 do
    for j:=0 to 9 do
      f[i]:=f[i]+a[i,j];//统计每一位可转化数字的个数
end;

procedure times(x:longint);//高精度乘法
var
  i:longint;
begin
  for i:=1 to 100 do ans[i]:=ans[i]*x;
  for i:=1 to 100 do
    begin
      ans[i+1]:=ans[i+1]+ans[i] div 10;
      ans[i]:=ans[i] mod 10;
    end;
end;

procedure work;
begin
  ans[1]:=1;
  for i:=1 to length(s) do times(f[ord(s[i])-48]+1);//累乘
  k:=100;
  while ans[k]=0 do dec(k);//去除首位的0
  for i:=k downto 1 do write(ans[i]);
end;

begin
  init;
  work;
end.

```