图论之最短路之Floyd

先用Floyd求出两点之间的最短距离，再依次枚举在每个点设医院人要走的距离

请注意：这是一个无向图，所以要将两边都赋成可以通过

当然也可以考虑Dijskla或SPFA，在此不做多述



```pascal
uses math;

var
 i,j,y,z,n,minn,ans:longint;
 b:array[0..110] of longint;
//b[i]代表每一个点的人数
 a:array[0..110,0..110] of int64;
//a[i,j]代表从i到j的最短距离

procedure floyd;//弗洛伊德
 var
  i,j,k:longint;
 begin
  for k:=1 to n do
   for i:=1 to n do
    for j:=1 to n do
     a[i,j]:=min(a[i,j],a[i,k]+a[k,j]);
end;

begin
 readln(n);
 for i:=1 to n do
  for j:=1 to n do
   if i<>j then a[i,j]:=maxlongint;//别忘了赋初值！
 for i:=1 to n do
  begin
   readln(b[i],y,z);
   if y<>0 then//左边有路
    begin
     a[i,y]:=1;
     a[y,i]:=1;
    end;
   if z<>0 then//右边有路
    begin
     a[i,z]:=1;
     a[z,i]:=1;
    end;
  end;

 floyd;

 minn:=maxlongint;
 for i:=1 to n do//穷举设医院的点
  begin
   ans:=0;
   for j:=1 to n do//穷举每个点的居民
    inc(ans,a[i,j]*b[j]);//居民要走的路的和
   minn:=min(minn,ans);//取较小值
  end;
 writeln(minn);
end.
```

附测评结果：

#1 AC 0ms/4855KB

#2 AC 0ms/4855KB

#3 AC 0ms/4855KB

#4 AC 0ms/4855KB

#5 AC 4ms/4855KB
