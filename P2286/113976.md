~~哎呀呀，要退役惹~~
在做（mo）你赛时碰到的一道题

也不知道为什么刚开始入平衡树时SBT（雾——

看了眼题解~~好像既没有P也没有SBT~~

是时候升华一下自己了（顺便存代码

通常 SBT 的每一个结点包含 key，left，right 和 size 等域。size 是一个额外但是十分有用的数据域，它一直在更新，它在前面已经
定义了。
每一个在 SBT 中的结点 t，我们保证：

性质 a：
s[right [t ]]>s[left [left [t ]]], s[right [left [t ]]]

性质 b：
s[left [t]]>s[right [right [t]]], s[left [right [t]]]

发现不会放图，呃呃呃

           T
         L   R	
        A B C D
       
~~假装这里有图~~：
上述性质在这里表示为：

s[a],s[b]<s[r];

s[c],s[d]<s[l];

那么在插入完之后这些性质就可能无法满足（也就4种情况）
我们可以直接看出伪代码~~(毕竟时重复的）~~：

Maintain (t)

1 If s[left[left[t]]>s[right[t]] then

2 Right-Rotate(t)

3 Maintain(right[t])

4 Maintain(t)

5 Exit

6 If s[right[left[t]]>s[right[t]] then

7 Left-Rotate(left[t])

8 Right-Rotate(t)

9 Maintain(left[t])

10 Maintain(right[t])

11 Maintain(t)

12 Exit

13 If s[right[right[t]]>s[left[t]] then

14 Left-Rotate(t)

15 Maintain(left[t])

16 Maintain(t)

17 Exit

18 If s[left[right[t]]>s[left[t]] then

19 Right-Rotate(right[t])

20 Left-Rotate(t)

21 Maintain(left[t])

22 Maintain(right[t])

23 Maintain(t)

然鹅还有着更简单的maintain（~~删除也是~~）（见程序）

```pascal
label 10;
const inf=233333333;p=1000000;
type int=longint;bo=boolean;
type SBT=object
         tot,root:int;
         key:array[0..400000]of int64;
         a,l,r:array[0..400000]of int;
         procedure clean();
         procedure lt(var x:int);
         procedure rt(var x:int);
         procedure maintain(var x:int;flag:bo);
         procedure ins(var x:int;y:int64);
         function find(x:int;y:int64):bo;
         function delete(var x:int;y:int64):int64;
         function getpre(x:int;y:int64):int64;
         function getsuc(x:int;y:int64):int64;
         end;
         //还少了rank和selete的操作，这题不需要啦啦啦；
procedure SBT.clean();
begin
  tot:=0;root:=0;
  fillchar(key,sizeof(key),0);
  fillchar(a,sizeof(a),0);
  fillchar(l,sizeof(l),0);
  fillchar(r,sizeof(r),0);
end;
procedure SBT.lt(var x:int);
var f:int;
begin
  f:=r[x];
  r[x]:=l[f];
  l[f]:=x;
  a[f]:=a[x];
  a[x]:=a[l[x]]+a[r[x]]+1;
  x:=f;
end;
procedure SBT.rt(var x:int);
var f:int;
begin
  f:=l[x];
  l[x]:=r[f];
  r[f]:=x;
  a[f]:=a[x];
  a[x]:=a[l[x]]+a[r[x]]+1;
  x:=f;
end;
procedure SBT.maintain(var x:int;flag:bo);
begin
  if not flag then
    begin
      if a[l[l[x]]]>a[r[x]] then
        rt(x)
      else
        if a[r[l[x]]]>a[r[x]] then
          begin
            lt(l[x]);
            rt(x);
          end
        else
          exit();
    end
  else
    begin
      if a[r[r[x]]]>a[l[x]] then
        lt(x)
      else
        if a[l[r[x]]]>a[l[x]] then
          begin
            rt(r[x]);
            lt(x);
          end
        else
          exit();
    end;
  maintain(l[x],false);
  maintain(r[x],true);
  maintain(x,false);
  maintain(x,true);
end;
procedure SBT.ins(var x:int;y:int64);
begin
  if x=0 then
    begin
      inc(tot);
      x:=tot;
      a[x]:=1;
      l[x]:=0;
      r[x]:=0;
      key[x]:=y;
    end
  else
    begin
      inc(a[x]);
      if y<key[x] then
        ins(l[x],y)
      else
        ins(r[x],y);
      maintain(x,y>=key[x]);
    end;
end;
function SBT.find(x:int;y:int64):bo;
begin
  if x=0 then
    exit(false);
  if y<key[x] then
    exit(find(l[x],y))
  else
    exit((key[x]=y)or(find(r[x],y)));
end;
function SBT.delete(var x:int;y:int64):int64;
begin
  dec(a[x]);
  if(key[x]=y)or((y<key[x])and(l[x]=0))or((y>key[x])and(r[x]=0))then
    begin
      delete:=key[x];
      if(l[x]=0)or(r[x]=0)then
        x:=l[x]+r[x]
      else
        key[x]:=delete(l[x],key[x]+1);
    end
  else
    if y<key[x] then
      delete:=delete(l[x],y)
    else
      delete:=delete(r[x],y);
end;
function SBT.getpre(x:int;y:int64):int64;
begin
  if x=0 then
    exit(y);
  if y<=key[x] then
    exit(getpre(l[x],y))
  else
    begin
      getpre:=getpre(r[x],y);
      if getpre=y then
        exit(key[x]);
    end;
end;
function SBT.getsuc(x:int;y:int64):int64;
begin
  if x=0 then
    exit(y);
  if y>=key[x] then
    exit(getsuc(r[x],y))
  else
    begin
      getsuc:=getsuc(l[x],y);
      if getsuc=y then
        exit(key[x]);
    end;
end;
var human,pet:SBT;
    i,suc,pre,n,x,y,ans:int;
begin
  human.clean();
  pet.clean();
  pet.clean();
  human.ins(human.root,inf);
  human.ins(human.root,-inf);
  pet.ins(pet.root,inf);
  pet.ins(pet.root,-inf);
  readln(n);
  ans:=0;
  repeat
    dec(N);
    readln(x,y);
    if x=0 then
      begin
        if human.a[human.root]=2 then//是否有人
          begin
            pet.ins(pet.root,y);
            goto 10;
          end;
        if human.find(human.root,y) then//是否有着特点值一样的
          begin
            human.delete(human.root,y);
            goto 10;
          end;
        suc:=human.getsuc(human.root,y);//后继
        pre:=human.getpre(human.root,y);//前驱
        if(suc-y)<(y-pre)then
          begin
            ans:=(ans+suc-y)mod p;
            human.delete(human.root,suc);
          end
        else
          begin
            ans:=(ans+y-pre)mod p;
            human.delete(human.root,pre);
          end;
      end
    else//下同
      begin
        if pet.a[pet.root]=2 then
          begin
            human.ins(human.root,y);
            goto 10;
          end;
        if pet.find(pet.root,y) then
          begin
            pet.delete(pet.root,y);
            goto 10;
          end;
        suc:=pet.getsuc(pet.root,y);
        pre:=pet.getpre(pet.root,y);
        if(suc-y)<(y-pre)then
          begin
            ans:=(ans+suc-y)mod p;
            pet.delete(pet.root,suc);
          end
        else
          begin
            ans:=(ans+y-pre)mod p;
            pet.delete(pet.root,pre);
          end;
      end;
  10:until n=0;
  writeln(ans);
end.
```
似乎有点长。。。。
用了神奇的object和label；
so我很想知道public怎么用（会的私我）！！至今未搞懂QAQ

~~（发现用了pascal总是打；）；~~