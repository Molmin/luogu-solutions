第一次写难度高一些的题解。

其实我也借鉴了各位高人的方法，结合了点自己的思考。

总的来说一个节点未被覆盖就尽量往爷爷靠。

首先将第1个点记作根节点，然后dfs算每个结点的层数，并记录每个点的父亲。

算完层数后再bfs，将能覆盖的点都覆盖掉，遇到未被覆盖的，往爷爷那里加一所消防局。

最后输出消防局总数。

代码如下：

```cpp
program p2279;
  var map:array[0..1001,0..1001]of boolean;//判断两点之间是否有路径连接。
      hash:array[0..1001]of boolean;//判断是否覆盖。
      f,dis:array[0..1001]of longint;//f记录父亲，dis记录层数。
      i,j,m,n,x,ans,temp:longint;
procedure dfs(x:longint);//计算x点所在层次。
  var i:longint;
begin
  for i:=1 to n do
  if (map[x,i]) and (not(hash[i])) then
  begin
    hash[i]:=true;
    dis[i]:=dis[x]+1;
    f[i]:=x;
    dfs(i);
  end;
end;
procedure bfs(x:longint);//将孙子，儿子，自己，父亲，爷爷覆盖，顺便统计消防局个数。
  var i,j:longint;
begin
  if not(hash[x]) then inc(m);
  hash[x]:=true;
  inc(ans);//覆盖自己，并在自己上加消防局。
  for i:=1 to n do//覆盖儿子，父亲。
  if map[x,i] then
  begin
    if not(hash[i]) then inc(m);
    hash[i]:=true;
    for j:=1 to n do//覆盖孙子，爷爷。
    if map[i,j] then
    begin
      if not(hash[j]) then inc(m);
      hash[j]:=true;
    end;
  end;
end;
begin
  readln(n);
  for i:=2 to n do
  begin
    read(j);
    map[i,j]:=true;
    map[j,i]:=true;
  end;
  hash[1]:=true;
  dis[1]:=1;
  dfs(1);
  fillchar(hash,sizeof(hash),false);
  while m<n do
  begin
    temp:=0;
    for i:=1 to n do
    if not(hash[i]) and (dis[i]>temp) then
    begin
      temp:=dis[i];
      x:=i;
    end;
    bfs(f[f[x]]);//把消防局建在爷爷上。
  end;
  writeln(ans);
end.
总的来说，细心分析题意还是不难的，用搜索，贪心，即可求解。
```