## 重点+坑点：必须反向建边，从点n开始跑最短路

注意读题：**每次到达一个节点，GPS都是推荐你走从这一个节点到农场的最短路径**

也就是说，你必须从终点开始跑最短路。

显然，对于两套不同的$GPS$系统（仅权值不同的两张图），我们需要跑两遍最短路，将这些最短路所经过的边打标记。

然后我们用这两套系统的最短路重新建图，权值为走这条路的抱怨数，从节点$n$开始再跑一遍最短路即可。

 对于这样庞大的无负权稠密图，**强烈推荐使用堆优化版的$Dijkstra$**。
 
 ## Code（Pascal）
 
 ```c
 program Luogu_P3720;
var a,b,cost1,cost2,next,f:array[0..1000010] of qword;
    vis:array[0..100010] of boolean;
    dis,head,h,p,from:array[0..100010] of qword;
    n,m,s,x,y,z,zz,cnt,len:qword; i:longint;
  procedure add(x,y,z,zz:qword);
  begin
    inc(cnt); a[cnt]:=x; b[cnt]:=y;
    cost1[cnt]:=z; cost2[cnt]:=zz;
    next[cnt]:=head[x]; head[x]:=cnt;
  end;
  procedure push(x,y:qword);  //堆操作
  var t:qword; i:longint;
  begin
    inc(len); h[len]:=x; p[len]:=y;
    i:=len;
    while (i>1)and(h[i]<h[i>>1]) do
    begin
      t:=h[i]; h[i]:=h[i>>1]; h[i>>1]:=t;
      t:=p[i]; p[i]:=p[i>>1]; p[i>>1]:=t;
      i:=i>>1;
    end;
  end;
  procedure pop;
  var t,x:qword; i:longint;
  begin
    h[1]:=h[len]; p[1]:=p[len];
    dec(len);
    i:=1;
    while i<<1<=len do
    begin
      if (i<<1+1>len)or(h[i<<1]<h[i<<1+1]) then x:=i<<1 else x:=i<<1+1;
      if h[i]>h[x] then
      begin
        t:=h[i]; h[i]:=h[x]; h[x]:=t;
        t:=p[i]; p[i]:=p[x]; p[x]:=t;
        i:=x;
      end
      else break;
    end;
  end;
  procedure dijkstra(s,num:longint);
  var u,d,cost:qword; i:longint;
  begin
    for i:=1 to n do dis[i]:=99999999999999;
    dis[s]:=0;
    push(0,s);
    fillchar(vis,sizeof(vis),false);
    while len>0 do
    begin
      d:=h[1]; u:=p[1];
      pop;
      if vis[u] then continue;
      vis[u]:=true;
      dec(f[from[u]]);  //是最短路的一部分，抱怨值--
      i:=head[u];
      while i<>0 do
      begin
        if num=1 then cost:=cost1[i]
        else if num=2 then cost:=cost2[i]
        else cost:=f[i];
        if d+cost<dis[b[i]] then
        begin
          dis[b[i]]:=d+cost; push(dis[b[i]],b[i]);
          from[b[i]]:=i;  //记录来时的路径
        end;
        i:=next[i];
      end;
    end;
  end;
begin
  readln(n,m);
  for i:=1 to m do
  begin
    readln(x,y,z,zz);
    add(y,x,z,zz);  //注意反向连边！
  end;
  for i:=1 to cnt do f[i]:=2;  //抱怨值初始为2
  dijkstra(n,1);
  dijkstra(n,2);
  dijkstra(n,3);
  writeln(dis[1]);
end.
 ```