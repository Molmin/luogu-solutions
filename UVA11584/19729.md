### 题意描述：

 现在给一个字符串s，把它分割成若干个互不相交的回文子串，求分割的回文子串的最少个数。
 
### 解题思路：

p[i][j]=true表示字符串st的第i个字符到第j个字符是回文串。

p[i][j]=false表示字符串st的第i个字符到第j个字符不是回文串。

接着就可以用dp了，dp方程是f[i]=min(f[i],f[j]+1)，

条件p[j+1,i]=true。

### AC代码：

```pascal
var n,t,i,j,cas,len,mid,l,r:longint;
    p:array[0..1005,0..1005] of boolean;
    f:array[0..1005] of longint;
    st:ansistring;
function min(o1,o2:longint):longint;//求min
begin
  if o1<o2 then exit(o1) else exit(o2);
end;
begin
  readln(cas);//读入多组数据
  for t:=1 to cas do
  begin
    readln(st);
    len:=length(st);//为了方便，先把长度求出来
    fillchar(p,sizeof(p),false);//初始化p数组，令它们都不是回文串
    for i:=1 to len do
      p[i,i]:=true;//一个字符保证是回文串
    for i:=1 to len do//枚举中心点，两个指针向两边扩展，这里是奇数
    begin
      j:=1;
      while (i-j>0) and (i+j<=len) and (st[i-j]=st[i+j]) do
      begin
        p[i-j,i+j]:=true;
        inc(j);
      end;
    end;
    for i:=1 to len-1 do//枚举中心点，两个指针向两边扩展，这里是偶数
    begin
      j:=1;
      while (i-j+1>0) and (i+j<=len) and (st[i-j+1]=st[i+j]) do
      begin
        p[i-j+1,i+j]:=true;
        inc(j);
      end;
    end;
    fillchar(f,sizeof(f),1);//把f数组初始化成最大值
    f[0]:=0;
    for i:=1 to len do//开始dp
      for j:=0 to i-1 do
        if p[j+1,i] then f[i]:=min(f[i],f[j]+1);//调用dp方程
    writeln(f[len]);//输出
  end;
end.
```