都说用spfa……然而对于我这种刚刚开始看spfa却还没搞懂的蒟蒻……还是用dfs+bfs把这题给ac了……

读入的时候存边存两组，正着走的一组倒着走的再一组，然后用dfs从终点往回遍历，砍掉不能到达终点的点以及指向不能到达终点的点的点，然后再用bfs从起点开始遍历，搜到终点就输出……（边权都是1所以就直接用bfs了）

代码就不加注释了反正变量名已经够长的了（front从前往后，back从后往前）……如果你们看到了些奇怪的空格，其实是被洛谷吞掉了的下划线……

```delphi
type
    int = longint;
    bool = boolean;
    ace = record
        v, next : int;
    end;
    acq = record
        u, l : int;
    end;

var
    head_front, head_back : array[1..10009] of int;
    edge_front, edge_back : array[1..200009] of ace;
    b_front, b_back : array[1..10009] of bool;
    queue : array[1..200009] of acq;
    n, m : int;

procedure add_front(u, v, i : int);
begin
    edge_front[i].v := v;
    edge_front[i].next := head_front[u];
    head_front[u] := i;
end;

procedure add_back(u, v, i : int);
begin
    edge_back[i].v := v;
    edge_back[i].next := head_back[u];
    head_back[u] := i;
end;

function bfs_front(u, back : int) : int;
var
    i, h, t : int;
    q, p : acq;
    e : ace;
begin
    h := 1;
    t := 0;
    queue[1].u := u;
    queue[1].l := 0;
    while h > t do
    begin
        inc(t);
        q := queue[t];
        if not b_front[q.u] then continue;
        b_front[q.u] := false;
        i := head_front[q.u];
        while i <> 0 do
        begin
            e := edge_front[i];
            if b_front[e.v] then
            begin
                p.u := e.v;
                p.l := q.l + 1;
                if p.u = back then exit(p.l);
                inc(h);
                queue[h] := p;
            end;
            i := e.next;
        end;        
    end;
end;

procedure dfs_back(u : int);
var
    i : int;
    e : ace;
begin
    i := head_back[u];
    while i <> 0 do
    begin
        e := edge_back[i];
        if not b_back[e.v] then
        begin
            b_back[e.v] := true;
            dfs_back(e.v);
        end;
        i := e.next;
    end;
end;

procedure dele_head(u : int);
var
    i : int;
    e : ace;
begin
    if not b_front[u] then exit;
    b_front[u] := false;
    i := head_back[u];
    while i <> 0 do
    begin
        e := edge_back[i];
        b_front[e.v] := false;
        i := e.next;
    end;
end;

function cyc() : int;
var
    i, s, t : int;
begin
    readln(n, m);
    fillchar(head_front, sizeof(head_front), 0);
    fillchar(head_back, sizeof(head_back), 0);
    for i := 1 to m do 
    begin
        readln(s, t);
        if s = t then continue;
        add_front(s, t, i);
        add_back(t, s, i);
    end;
    readln(s, t);
    if s = t then exit(0);
    fillchar(b_back, sizeof(b_back), false);
    b_back[t] := true;
    dfs_back(t);
    fillchar(b_front, sizeof(b_front), true);
    for i := 1 to n do
        if not b_back[i] then dele_head(i);
    if not b_front[s] then exit(-1)
    else exit(bfs_front(s, t));    
end;

begin
    assign(input, 'road.in');
    assign(output, 'road.out');
    reset(input);
    rewrite(output);
    writeln(cyc());
    close(input);
    close(output);
end .
```