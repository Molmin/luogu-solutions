# 没有题解?
### P党来一发
- 一看没有思路？
- 暴力出奇迹[滑稽]

## 好吧我们来进入正题
- 那么让我们来写写方程
- **f[i,j]表示最短的折叠长度**。
- 显然，f[i,j]=f[i,k]+f[k+1,j];(i<=k<=j)。
- **别忘了折叠**。
- 那么我们需要判断i~j是否能折叠。
- 怎么判断呢，取模大法好。
- 然后计算这个子串折叠后的长度，**别忘记那个数字长度，以及加上+2（括号）**。
- 像我这种蒟蒻一看题都能看出区间DP了。
- 然而，蒟蒻表示区间DP一脸懵逼，打了半天老是过不了。
- 所以呢，只好写记忆化了。

~~丢代码~~
```pascal
var st:ansistring;
    f:array[0..101,0..101] of longint;
    v:array[0..101,0..101] of boolean;
function min(x,y:longint):longint;
begin
  if x<y then
   exit(x);
  exit(y);
end;
function check(l,r,ll,rr:longint):boolean;  //判断是否能折叠
var i:longint;
begin
  if (r-l+1) mod (rr-ll+1)<>0 then
   exit(false);
  for i:=l to r do
   if st[i]<>st[(i-l) mod (rr-ll+1)+ll] then
    exit(false);
  exit(true);
end;
function leng(x:longint):longint; //统计数字长度
var ans:longint;
begin
  ans:=0;
  while x>0 do
   begin
    x:=x div 10;
    inc(ans);
   end;
  exit(ans);
end;
function ss(l,r:longint):longint;  //记忆化搜索
var i:longint;
begin
  if l=r then
   exit(1);
  if v[l,r] then
   exit(f[l,r]);
  v[l,r]:=true;
  f[l,r]:=r-l+1;
  for i:=l to r-1 do
   begin
    f[l,r]:=min(f[l,r],ss(l,i)+ss(i+1,r));
    if check(i+1,r,l,i) then
     f[l,r]:=min(f[l,r],ss(l,i)+leng((r-i) div (i-l+1)+1)+2);
   end;
  exit(f[l,r]);
end;
begin
  readln(st);
  writeln(ss(1,length(st)));
end.
```
代码太丑，大佬勿喷。