看到这道题，很容易想起NOIP2013Day1的货车运输。

事实上，这道题和货车运输基本一样，不过加了些黄金买卖和列车。

首先，将存在列车的点全部缩起来；

然后再跑一遍最大生成树；

其次，在树上倍增；

最后，模拟一下即可。

下面献上丑陋的代码：























```cpp
program p3280;
type link=^node;
     node=record
       data,v:longint;
       next:link;
     end;
var e:array[0..200010,1..2] of longint;
    v,sh,xi,lie,fa,bel:array[0..200010] of longint;
    b:array[0..200010] of link;
    n,m,q:longint;
    d:array[0..100010] of longint;
    vis:array[0..100010] of boolean;
    f,g:array[0..100010,0..20] of longint;
function min(x,y:int64):int64;
begin
  if x<y then exit(x) else exit(y);
end;
procedure init;
var i,mint:longint;
begin
  readln(n,m,q);
  for i:=1 to n do
  begin
    read(sh[i]);
    bel[i]:=i;
  end;
  for i:=1 to n do
    read(xi[i]);
  for i:=1 to m do
    readln(e[i,1],e[i,2],v[i]);
  mint:=n;
  for i:=1 to q do
  begin
    read(lie[i]);
    if lie[i]<mint then mint:=lie[i];
  end;
  for i:=1 to q do
    bel[lie[i]]:=bel[mint];
end;
procedure swap(var x,y:longint);
var t:longint;
begin
  t:=x;
  x:=y;
  y:=t;
end;
procedure heap(nn,ii:longint);
var i,j,x,y,z:longint;
begin
  i:=ii;
  j:=2*i;
  x:=e[i,1];
  y:=e[i,2];
  z:=v[i];
  while j<=nn do
  begin
    if (v[j]>v[j+1]) and (j<nn) then inc(j);
    if z>v[j] then
    begin
      v[i]:=v[j];
      e[i,1]:=e[j,1];
      e[i,2]:=e[j,2];
      i:=j;
      j:=2*i;
    end
    else
      j:=nn+1;
  end;
  v[i]:=z;
  e[i,1]:=x;
  e[i,2]:=y;
end;
procedure heapsort;
var i:longint;
begin
  for i:=m div 2 downto 1 do
    heap(m,i);
  for i:=m downto 2 do
  begin
    swap(e[i,1],e[1,1]);
    swap(e[i,2],e[1,2]);
    swap(v[i],v[1]);
    heap(i-1,1);
  end;
end;
function get(x:longint):longint;
begin
  if fa[x]=x then
    exit(x)
  else
    fa[x]:=get(fa[x]);
  exit(fa[x]);
end;
procedure kruskal;
var i,x,y:longint;
    p:link;
begin
  for i:=1 to n do
    fa[i]:=i;
  for i:=1 to m do
  begin
    x:=bel[e[i,1]];
    y:=bel[e[i,2]];
    if get(x)<>get(y) then
    begin
      fa[get(x)]:=get(y);
      new(p);
      p^.data:=x;
      p^.v:=v[i];
      p^.next:=b[y];
      b[y]:=p;
      new(p);
      p^.data:=y;
      p^.v:=v[i];
      p^.next:=b[x];
      b[x]:=p;
    end;
  end;
end;
procedure dfs(x:longint);
var p:link;
begin
  p:=b[x];
  while p<>nil do
  begin
    if not vis[p^.data] and (get(p^.data)=get(x)) then
    begin
      vis[p^.data]:=true;
      d[p^.data]:=d[x]+1;
      f[p^.data,0]:=x;
      g[p^.data,0]:=p^.v;
      dfs(p^.data);
    end;
    p:=p^.next;
  end;
end;
function minw(x,y:longint):int64;
var j:longint;
    s1,s2:int64;
begin
  x:=bel[x];
  y:=bel[y];
  if x=y then exit(high(s1));
  if d[x]>d[y] then swap(x,y);
  s1:=high(s1);
  for j:=0 to 20 do
    if (d[y]-d[x]) and (1 shl j)<>0 then
    begin
      s1:=min(s1,g[y,j]);
      y:=f[y,j];
    end;
  if x=y then exit(s1);
  s2:=high(s2);
  for j:=20 downto 0 do
    if (f[x,j]<>f[y,j]) then
    begin
      s1:=min(s1,g[y,j]);
      s2:=min(s2,g[x,j]);
      y:=f[y,j];
      x:=f[x,j];
    end;
  s1:=min(s1,g[y,0]);
  s2:=min(s2,g[x,0]);
  exit(min(s1,s2));
end;
procedure main;
var i,j,pre,now:longint;
    gold,sale:int64;
begin
  heapsort;
  kruskal;
  fillchar(d,sizeof(d),0);
  fillchar(f,sizeof(f),0);
  for i:=1 to n do
    for j:=0 to 20 do
      g[i,j]:=maxlongint;
  fillchar(vis,sizeof(vis),false);
  vis[1]:=true;
  d[1]:=1;
  dfs(1);
  for j:=1 to 20 do
    for i:=1 to n do
    begin
      f[i,j]:=f[f[i,j-1],j-1];
      g[i,j]:=min(g[i,j-1],g[f[i,j-1],j-1]);
    end;
  pre:=sh[1];
  gold:=0;
  sale:=0;
  if xi[sh[1]]>=0 then
    gold:=gold+xi[sh[1]]
  else
    writeln(0);
  for i:=2 to n do
  begin
    now:=sh[i];
    if xi[now]>0 then
      gold:=min(gold,minw(pre,now))+xi[now];
    if xi[now]<0 then
    begin
      gold:=min(gold,minw(pre,now));
      sale:=min(gold,-xi[now]);
      writeln(sale);
      gold:=gold-sale;
    end;
    pre:=now;
  end;
end;
begin
  init;
  main;
end.
```