#【Pascal】封锁阳光大学解题思路


**Q1:此题显然是用涂色来做的，那么怎么来涂色？**

初始化每个点为0，假设该点需要保留为1，删除为2.

则当我们设置了初始点的颜色（假设是1），那么与之有边相连的颜色必然是确定的（一个Dfs）


**Q2:如何判断河蟹无法封锁所有道路？**

当我们Dfs搜到一个点已经被访问，且该点的颜色与将要涂的颜色不相同，则无法满足


**Q3：如何防止爆内存？**

使用**链式前向星**可以有效解决


**Q4：最后的最小值怎么得到？**

首先，我们定义颜色为1的方案数为tot,总的点数为sum（这些数据在Dfs中都可以得到）

那么，颜色为2的方案数n=sum-tot，所以最小值为n(min)=min(tot,sum-tot)


**Q5:其他注意事项**

1.题目并没有保证n个点在图中都有边相连，所以有些点是不用做的

2.题目并没有保证只有一个图，所以要找出每一个小图


code:





```cpp
var n,m,i,x,y,min,tot,nowsum,ans:longint;
    s,w,e,color:array[1..100005*2] of longint;
    vis:array[1..100005] of boolean;
Function minx(a,b:longint):longint;
begin
  if a<b then exit(a)
   else exit(b);
end;
Procedure Add(x,y,v:longint);  //前向星添边
var i:longint;
begin
  if s[x]<>0 then w[v]:=s[x];
  s[x]:=v; e[v]:=y;
end;
Function Change_color(x:longint):longint; //改变颜色
begin
  if x=1 then exit(2);
  if x=2 then exit(1);
end;
Procedure Dfs(x,v:longint);
var temp:longint;
begin
  if vis[x] then  //如果访问过该点
   begin
    if (color[x]<>0) and (v<>color[x]) then   //如果被涂过颜色且颜色与当前所要涂的颜色不符
     begin
      writeln('Impossible');  //输出'不可能'
      halt;
     end;
    exit;
   end;
  inc(nowsum);  //统计当前小图中点的总数
  vis[x]:=true;    //添加访问
  color[x]:=v;     //涂色
  if v=1 then inc(tot);  //统计颜色为1的个数
  temp:=s[x];  //链式前向星求下一个点
  while temp<>0 do
   begin
    Dfs(e[temp],change_color(v));
    temp:=w[temp];
   end;
end;
begin
  readln(n,m);
  fillchar(s,sizeof(s),0);
  fillchar(w,sizeof(w),0);
  fillchar(e,sizeof(e),0);
  fillchar(vis,sizeof(vis),0);
  fillchar(color,sizeof(color),0);
  ans:=0;
  for i:=1 to m do
   begin
    readln(x,y);
    Add(x,y,i);Add(y,x,i+m);  //正反都存
   end;
  for i:=1 to n do
   begin
    tot:=0; nowsum:=0;
    if (not vis[i]) then Dfs(i,1);  //如果没有访问过就开始访问
    tot:=minx(tot,nowsum-tot);  //统计当前小图中的最小值
    inc(ans,tot);  //统计总值
   end;
  writeln(ans);
end.
```