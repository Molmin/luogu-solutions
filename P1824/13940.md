典型的二分答案。

附pascal代码+注释

```cpp
program p1316;
var a:array [1..100000] of longint;
n,b,i,l,r,m,ans:longint;
procedure sort(l,r:longint);
var i,j,y,z:longint;
begin
  i:=l;
  j:=r;
  z:=a[(l+r) div 2];
  repeat
    while a[i]<z do inc(i);
    while z<a[j] do dec(j);
    if not(i>j) then
    begin
      y:=a[i];
      a[i]:=a[j];
      a[j]:=y;
      inc(i);
      dec(j);
    end;
  until i>j;
  if l<j then sort(l,j);
  if i<r then sort(i,r);
end;//拷的快排
function judge(x:longint):boolean;//x代表最小的距离。即一旦大于等于它，就可以新增一枚硬币。
var i,j,ans,sum:longint;
begin
  j:=1;ans:=1;//不管怎样一枚硬币总是能放的。
  for i:=1 to n do
  begin
    if a[i]-a[j]>=x then
    begin
      j:=i;
      inc(ans);
    end;
  end;
  if ans>=b then exit(true);//x变大这儿的ans会变小，要找最大的x使ans>=b
  exit(false);
end;
begin
  read(n,b);
  for i:=1 to n do
    read(a[i]);
  sort(1,n);
  l:=1;
  r:=a[n]-a[1];
  while l<=r do
  begin
    m:=(l+r) div 2;
    if judge(m) then
    begin
      ans:=m;
      l:=m+1;
    end
    else r:=m-1;
  end;
  write(ans);
end.
```