#没人发题解额。。。

#一次过。。。（逃

#简单的线段树（滑稽）

我们先把相同的数值的区间看成是一个点（整题最关键的部分，蒟蒻的我想到这个，整道题就直接迎刃而解了），用l[i]和r[i]表示第i个区间的头和尾，一个数组记录第i个区间数的个数，还有一个数组记录第i个数属于哪个区间，这些做个O(n)的预处理就行了。

然后建树，对于每次读入的x~y，首先处理头上和尾上多出来的部分，剩下的部分用线段树的常规操作query就行了，最后在三者之中找个max就行了。。。

注意两种特殊情况，x~y只有一个区间则输出y-x+1，x~y有两个区间则不用query，直接输出头上和尾上多出来的部分的max

线段树不懂的同学请自行百度。。。

献上没有66666a代码丑陋的AC代码：




```cpp
uses math;
type
  tree=record
  sum,l,r:longint;
end;
var 
  a:array[0..400001]of tree;
  b,l,r,c,tt:array[0..100001]of longint;
  n,m,i,x,y,len,max1,max2,ans:longint;
procedure build(k,l,r:longint);
var
  mid:longint;
begin
  a[k].l:=l; a[k].r:=r;
  if l=r then 
    begin
      a[k].sum:=tt[l];
      exit;
    end;//叶子结点
  mid:=(l+r) >> 1;
  build(k*2,l,mid);
  build(k*2+1,mid+1,r);
  a[k].sum:=max(a[k*2].sum,a[k*2+1].sum);
end;
function query(k,l,r:longint):longint;
begin
  if (a[k].l>r)or(a[k].r<l) then exit(0);
  if (a[k].l>=l)and(a[k].r<=r) then exit(a[k].sum);
  exit(max(query(k*2,l,r),query(k*2+1,l,r)));
end;//线段树的常规操作
begin
  readln(n,m);
  for i:=1 to n do read(b[i]);
  len:=1;
  l[1]:=1;
  c[1]:=1;
  for i:=2 to n do 
    if b[i]<>b[i-1] then 
      begin
        c[i]:=c[i-1]+1;
        r[len]:=i-1;
        tt[len]:=r[len]-l[len]+1;
        inc(len);
        l[len]:=i;
      end
      else c[i]:=c[i-1];//预处理
  r[len]:=n;
  build(1,1,len);//建树
  for i:=1 to m do 
    begin
      readln(x,y);
      if c[y]-c[x]=0 then 
        begin
          writeln(y-x+1);
          continue;
        end;//只有1个区间
      max1:=r[c[x]]-x+1;//头
      max2:=y-l[c[y]]+1;//尾
      if c[y]-c[x]=1 then  
        begin
          writeln(max(max1,max2));
          continue;
        end;//只有2个区间
      ans:=query(1,c[x]+1,c[y]-1);//查询中间区间的最大值
      writeln(max(ans,max(max1,max2)));//输出三者的最大值
    end;
end.

```