### 刚开始看到题目真的被吓到了QAQ
## 第一眼看是图论，本蒟蒻觉得好难
## 第二眼一看数据范围，~~脑袋一片空白~~
## 第三次再读题目，发现根本不是图论，而是道玄学的模拟题

#### 本蒟蒻：哇哇哇~~此题真的好良心~~！……
 _画外音：什么？~~提高+/省选-~~？？你在跟我开玩笑？_ 

### 咳咳，进入正题：
[最简单方法：暴力模拟] 60pts（~~什么，暴力能拿60？？~~）

只需用a数组存储每个结点后继的左右两点，然后写一个k*m的循环模拟即可。

代码：

```
program cruise;
var n,m,k,i,j,x,s:longint;
    a:array[1..1000,1..2] of longint;
    ch:array[1..500] of char;
    t:char;
begin
  readln(n,m,k);
  for i:=1 to n do readln(a[i,1],a[i,2]);
  for i:=1 to m do
  begin
    read(ch[i]); if not(eof) then read(t);
  end;
  x:=1;
  for i:=1 to k do
    for j:=1 to m do
      if ch[j]='L' then x:=a[x,1] else x:=a[x,2];
  writeln(x);
end.

```

[满分方法] 期望得分：100pts

观察到k的范围非常大，而且我们知道直接模拟是行不通的，所以这个图中必有回环。

所以我们要尝试找出这条重复走的路径。

然而如何判断一条路径重复走了多次？可以新建一个数组f用于存储该点是否走到过，如果当前找到的点标记为走过，则接下来要遍历的路径必然与之前重复。

我们定义ans[i]表示从i号结点出发走了M次（按照方向序列）后所到达的点编号，则可以通过预处理算出ans[1]-ans[n]的值。

那么我们在遍历时直接调用ans[i]即可。

举例：从结点x走M次到达的结点：x:=ans[x];

与先前的O(M)复杂度比较，速度提升到O(1)。

这样一来，我们只需先找到一条回环，然后将k值对回环的长度取模，再做一遍模拟即可。

代码：
```
program cruise;
var n,m,k,i,j,x,s:longint;
    a:array[1..1000,1..2] of longint;
    ch:array[1..500] of char;
    ans:array[1..1000] of longint;
    f:array[1..1000] of longint;
    t:char;
begin
  readln(n,m,k);
  for i:=1 to n do readln(a[i,1],a[i,2]);  //读入邻边关系
  for i:=1 to m do
  begin
    read(ch[i]); if not(eof) then read(t);  //读入长度序列
  end;
  for i:=1 to n do  //对每个结点进行预处理，将走M次后到达的结点编号存储
  begin
    x:=i;
    for j:=1 to m do
      if ch[j]='L' then x:=a[x,1] else x:=a[x,2];
    ans[i]:=x;
  end;
  f[1]:=0; x:=1;  //初始化，从1号节点开始走
  while 0=0 do
  begin
    x:=ans[x]; inc(s);  //取后继结点编号，计数器加1
    if f[x]>0 then begin k:=k-f[x]; s:=s-f[x]; break; end;  //如果该结点走过，则跳出循环
                                                            //注意必须将k值减去回环之前的步数；然后s也要作同样处理，用于保存回环长度
    f[x]:=s;  //标记为走过
  end;
  k:=k mod s;  //对结果取模
  for i:=1 to k do x:=ans[x];  //最后从该回环起点开始，模拟一遍即可
  writeln(x);
end.
```