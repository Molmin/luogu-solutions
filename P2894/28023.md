没有pascal代码啊，来一发。

这道题是经典的线段树问题，用线段树维护区间最长连续0的长度，同时记录区间左起最长连续0长度和右起最长连续0长度就可以实现区间信息的合并。

```cpp
program rrr(input,output);
type
  treetype=record
     l,r,lmax,rmax,max,c:longint;    //l是区间左端点编号，r是区间右端点编号，lmax是区间左起最长连续0长度，rmax是区间右起最长连续0长度，max是区间最长连续0长度
  end;
var
  a:array[0..200020]of treetype;
  n,m,i,opt,x,y:longint;
function max(a,b:longint):longint;
begin
   if a>b then exit(a) else exit(b);
end;
procedure build(k,l,r:longint);
var
  mid,t:longint;
begin
   t:=r-l+1;
   a[k].l:=l;a[k].r:=r;a[k].max:=t;a[k].lmax:=t;a[k].rmax:=t;a[k].c:=-1;
   if l=r then exit;
   mid:=(l+r)>>1;
   build(k+k,l,mid);
   build(k+k+1,mid+1,r);
end;
procedure pushdown(k:longint);
var
  i,t:longint;
begin
   if a[k].c=-1 then exit;
   if a[k].l=a[k].r then begin a[k].c:=-1;exit; end;
   i:=k+k;
   if a[k].c=0 then
      begin
         t:=a[i].r-a[i].l+1;
         a[i].lmax:=t;a[i].rmax:=t;a[i].max:=t;a[i].c:=0;
         t:=a[i+1].r-a[i+1].l+1;
         a[i+1].lmax:=t;a[i+1].rmax:=t;a[i+1].max:=t;a[i+1].c:=0;
       end
   else
      begin
         a[i].lmax:=0;a[i].rmax:=0;a[i].max:=0;a[i].c:=1;
         a[i+1].lmax:=0;a[i+1].rmax:=0;a[i+1].max:=0;a[i+1].c:=1;
      end;
   a[k].c:=-1;
end;
function ask(k:longint):longint;
var
  i:longint;
begin
   pushdown(k);
   i:=k+k;
   if a[i].max>=x then exit(ask(i))
   else if a[i].rmax+a[i+1].lmax>=x then exit(a[i].r-a[i].rmax+1)
   else exit(ask(i+1));
end;
procedure change1(k,x,y:longint);
var
  mid,i:longint;
begin
   pushdown(k);
   if (x<=a[k].l) and (a[k].r<=y) then
      begin a[k].lmax:=0;a[k].rmax:=0;a[k].max:=0;a[k].c:=1;exit; end;
   mid:=(a[k].l+a[k].r)>>1;i:=k+k;
   if x<=mid then change1(i,x,y);
   if mid<y then change1(i+1,x,y);
   if a[i].lmax=a[i].r-a[i].l+1 then a[k].lmax:=a[i].lmax+a[i+1].lmax else a[k].lmax:=a[i].lmax;   //这里要注意，刚开始容易直接写a[k].lmax:=a[i].lmax
   if a[i+1].rmax=a[i+1].r-a[i+1].l+1 then a[k].rmax:=a[i].rmax+a[i+1].rmax else a[k].rmax:=a[i+1].rmax;   //这里也一样
   a[k].max:=max(max(a[i].max,a[i+1].max),a[i].rmax+a[i+1].lmax);
end;
procedure change0(k,x,y:longint);
var
  mid,t,i:longint;
begin
   pushdown(k);
   if (x<=a[k].l) and (a[k].r<=y) then
      begin t:=a[k].r-a[k].l+1;a[k].lmax:=t;a[k].rmax:=t;a[k].max:=t;a[k].c:=0;exit; end;
   mid:=(a[k].l+a[k].r)>>1;i:=k+k;
   if x<=mid then change0(i,x,y);
   if mid<y then change0(i+1,x,y);
   if a[i].lmax=a[i].r-a[i].l+1 then a[k].lmax:=a[i].lmax+a[i+1].lmax else a[k].lmax:=a[i].lmax;   //还有这儿
   if a[i+1].rmax=a[i+1].r-a[i+1].l+1 then a[k].rmax:=a[i].rmax+a[i+1].rmax else a[k].rmax:=a[i+1].rmax;   //和这儿
   a[k].max:=max(max(a[i].max,a[i+1].max),a[i].rmax+a[i+1].lmax);
end;
begin
   //assign(input,'r.in');assign(output,'r.out');reset(input);rewrite(output);
   readln(n,m);
   build(1,1,n);
   for i:=1 to m do
      begin
         read(opt);
         if opt=1 then
            begin
               readln(x);
               if a[1].max<x then writeln(0)
               else begin y:=ask(1);writeln(y);change1(1,y,y+x-1); end;
            end
         else begin readln(x,y);change0(1,x,x+y-1); end;
      end;
   //close(input);close(output);
end.
```