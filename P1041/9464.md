这题挺恶心，搜索时基本没有剪枝，只能看你程序好坏了，我是这样搜的：

先遍历一边树，把节点所在的层数处理出来然后一层一层的搜。每一层枚举一个点控制。我记录的是一定不会被传染的人数max，结果就是n-max了。









```cpp
var a,b:array[1..310,0..310]of integer;
    d,f:array[1..310]of longint;
    v:array[1..310]of boolean;
    i,n,p,x,y,max,ans:longint;
procedure dfs(k:longint);//这是遍历树，处理节点所在的层数的过程
var i:longint;
begin
for i:=1 to a[k,0] do
  begin
    d[a[k,i]]:=d[k]+1;//d[i]表示i点所在的层数
    dfs(a[k,i]);
  end;
end;
procedure dp(k:longint);//这是处理以k为根子树节点数的过程
var i:longint;
begin
f[k]:=1;
for i:=1 to a[k,0] do
  begin
    dp(a[k,i]);
    f[k]:=f[k]+f[a[k,i]];//f[k]表示以k为根的子树的节点总数
  end;
end;
procedure deal(k:longint);//这是将以k为根的子树全赋值为相反的boolean，搜索回溯时用的
var i:longint;
begin
v[k]:=not(v[k]);
for i:=1 to a[k,0] do
  deal(a[k,i]);
end;
procedure search(k,now:longint);//这就是搜索主过程，搜到第k层，有now个人一定不会被感染了
var i,j:longint;
    flag:boolean;
begin
if k=max then
  begin
    if now>ans then ans:=now;
    exit;
  end;
flag:=true;
for i:=1 to b[k,0] do
   if not(v[b[k,i]]) then
      for j:=1 to a[b[k,i],0] do
        begin
          flag:=false;
          deal(a[b[k,i],j]);
          search(k+1,now+f[a[b[k,i],j]]);
          deal(a[b[k,i],j]);
        end;
if flag then search(max,now);//如果此层没有可搜的，直接进入最后一层
end;
procedure build(k:longint);//重新建树
var i:longint;
begin
for i:=1 to b[k,0] do
  if not(v[b[k,i]]) then
    begin
      inc(a[k,0]);
      a[k,a[k,0]]:=b[k,i];
      v[b[k,i]]:=true;
      build(b[k,i]);
    end;
end;
begin
read(n,p);
for i:=1 to p do
  begin
    read(x,y);
    inc(b[x,0]);
    b[x,b[x,0]]:=y;
    inc(b[y,0]);
    b[y,b[y,0]]:=x;
  end;
fillchar(v,sizeof(v),0);
v[1]:=true;
build(1);//建树
fillchar(b,sizeof(b),0);
fillchar(d,sizeof(d),0);
d[1]:=1;
dfs(1);
max:=0;//记录最深层数
for i:=1 to n do
  begin
    if max<d[i] then max:=d[i];
    inc(b[d[i],0]);
    b[d[i],b[d[i],0]]:=i;//b[i,j]表示第i层第j个节点的标号
  end;
fillchar(f,sizeof(f),0);
dp(1);
ans:=0;
fillchar(v,sizeof(v),0);
search(1,0);
writeln(n-ans);
end.//ok了
```