这是本菜鸡第一次炸出[ chen_zhe ](https://www.luogu.com.cn/user/8457)，纪念一下555。

本来是想让[ chen_zhe ](https://www.luogu.com.cn/user/8457)大大修一下题面，结果[ chen_zhe ](https://www.luogu.com.cn/user/8457)大大不仅修改了题面，还直接改了难度（蓝掉绿）……

[帖子地址](https://www.luogu.com.cn/discuss/show/212175)

[ chen_zhe ](https://www.luogu.com.cn/user/8457)大大属实nb！！！

接下来进入正题，首先一看到这题你一定会想到树链剖分，并且还会不屑地说：切，这不就是模板题吗？！

然而我们这种不会树链剖分的菜鸡正在瑟瑟发抖……（没有歧视树链剖分的意思，当然建议大家去学树链剖分，本菜鸡也正在学习中）

这时候我们会想到一道类似的题：[[HAOI2015]树上操作](https://www.luogu.com.cn/problem/P3178)

在[[HAOI2015]树上操作](https://www.luogu.com.cn/problem/P3178)里，我们可以搞一个dfs序然后直接用线段树维护。

显然，我们这道题也可以这么做。

我们考虑用一种神奇的dfs序：[欧拉序](https://www.cnblogs.com/stxy-ferryman/p/7741970.html)

这个[欧拉序](https://www.cnblogs.com/stxy-ferryman/p/7741970.html)的有一个很好的性质：
>如果把每个点第一次出现记作 +，第二次出现记作 -，那么根节点到任意节点的权值和在欧拉序上对应一个前缀和。——摘自[YJZoier巨佬的题解](https://www.luogu.com.cn/blog/user31635/solution-p3178)

换句话说，如果把每个点第一次出现记作 +，第二次出现记作 -，那么任意节点到它的祖先的权值和在欧拉序上对应一段区间的和。

设一个点 $x$ 和它的祖先 $y$ ，那么树上 $x$ 到 $y$ 的权值和就是欧拉序上 $+x$ 到 $+y$ 的和

比如样例，[欧拉序](https://www.cnblogs.com/stxy-ferryman/p/7741970.html)是 $+0,+1,+2,+3,-3,-2,-1,-0$

那么我们看 $3$ 这个点，它的祖先中有 $1$ ，那么欧拉序上 $+1$ 到 $+3$ 部分的和即为树上 $1$ 到 $3$ 的和。

你可能会说：你这个例子不全面啊，样例只是个链啊。

其实很简单就能证明，如果 $y$ 还有别的子树，那么这些子树要么是还没有在这段欧拉序区间中出现过，要么是一个 $+$ 一个 $-$ 已经做完抵消了。接下来的节点同理。

那么我们可以得到一个很明确的做法：

1.预处理跑完欧拉序和 
$\operatorname{lca}$ 的预处理。

2.对于每个修改操作，将 $+\operatorname{lca}(x,y)$ 到 $+x$ 与 $+\operatorname{lca}(x,y)$ 到 $+y$ 部分进行处理，**然后千万千万别忘了将$+\operatorname{lca}(x,y)$处理！！！**。这东西显然可以用线段树维护。只不过要注意下 $+$ 和 $-$ 的处理。

3.查询操作，显然答案是 $+x$ 到 $-x$ 部分的和。

接下来放上代码：

```pascal
uses math;
type rec=record
     l,r:longint;
     sum,lz:int64;
     end;
     edge=record
     x,y,next:longint;
     end;
     ou=record
     z,u:longint;
     end;
var tree:array[0..1000000]of rec;
    zf,a,shang,xia,h,dep:array[0..1000000]of int64;
    e:array[0..1000000]of edge;
    ol:array[0..1000000]of ou;
    f:array[0..500000,0..20]of longint;
    cnt,cnt1,n,m,i,k:longint;
    a1,x,y:int64;
    ch,ch1:char;
procedure push_up(k:longint);
begin
  tree[k].sum:=tree[k*2].sum+tree[k*2+1].sum;
end;
procedure push_down(k:longint);
begin
  if tree[k].lz<>0 then
  begin
    tree[k*2].sum:=tree[k*2].sum+(zf[tree[k*2].r]-zf[tree[k*2].l-1])*tree[k].lz;
    tree[k*2].lz:=tree[k*2].lz+tree[k].lz;
    tree[k*2+1].sum:=tree[k*2+1].sum+(zf[tree[k*2+1].r]-zf[tree[k*2+1].l-1])*tree[k].lz;
    tree[k*2+1].lz:=tree[k*2+1].lz+tree[k].lz;
    tree[k].lz:=0;
  end;
end;
procedure build(k,l,r:longint);
begin
  tree[k].l:=l;tree[k].r:=r;
  if l=r then tree[k].sum:=ol[l].z*a[ol[l].u] else
  begin
    build(k*2,l,l+(r-l) div 2);
    build(k*2+1,l+(r-l) div 2+1,r);
    push_up(k);
  end;
end;
procedure xg1(k,l,r,a:longint);
var mid:longint;
begin
  if (tree[k].l=l)and(tree[k].r=r) then
  begin
    tree[k].sum:=tree[k].sum+(zf[r]-zf[l-1])*a;//只将
    tree[k].lz:=tree[k].lz+a;
    exit;
  end;
  mid:=tree[k].l+(tree[k].r-tree[k].l) div 2;
  push_down(k);
  if l<=mid then xg1(k*2,l,min(r,mid),a);
  if r>mid then xg1(k*2+1,max(mid+1,l),r,a);
  push_up(k);
end;
function cx(k,l,r:longint):int64;
var mid:longint;
begin
  if (tree[k].l=l)and(tree[k].r=r) then exit(tree[k].sum);
  mid:=tree[k].l+(tree[k].r-tree[k].l) div 2;
  push_down(k);
  cx:=0;
  if l<=mid then cx:=cx+cx(k*2,l,min(r,mid));
  if r>mid then cx:=cx+cx(k*2+1,max(mid+1,l),r);
  push_up(k);
end;
procedure ad(x,y:longint);
begin
  inc(cnt);
  e[cnt].x:=y;
  e[cnt].next:=h[x];
  h[x]:=cnt;
end;
procedure olx(now,fa:longint);
var t:longint;
begin
  inc(cnt1);
  ol[cnt1].z:=1;
  ol[cnt1].u:=now;
  xia[now]:=cnt1;
  t:=h[now];
  while t>0 do
  begin
    if e[t].x<>fa then olx(e[t].x,now);
    t:=e[t].next;
  end;
  inc(cnt1);
  ol[cnt1].z:=-1;
  ol[cnt1].u:=now;
  shang[now]:=cnt1;
end;
procedure dfs(k,l:longint);
var t,i:longint;
begin
  dep[k]:=dep[l]+1;
  f[k,0]:=l;
  for i:=1 to trunc(log2(dep[k])) do
    f[k,i]:=f[f[k,i-1],i-1];
  t:=h[k];
  while t>0 do
  begin
    if e[t].x<>l then dfs(e[t].x,k);
    t:=e[t].next;
  end;
end;
function lca(x,y:longint):longint;
var i,t:longint;
begin
  if dep[x]<dep[y] then
  begin
    t:=x;x:=y;y:=t;
  end;
  for i:=20 downto 0 do
  begin
    if dep[f[x,i]]>=dep[y] then x:=f[x,i];
    if x=y then exit(x);
  end;
  for i:=20 downto 0 do
    if f[x,i]<>f[y,i] then
    begin
      x:=f[x,i];
      y:=f[y,i];
    end;
  exit(f[x,0]);
end;
begin
  readln(n);
  for i:=1 to n-1 do
  begin
    readln(x,y);
    inc(x);inc(y);
    ad(x,y);
    ad(y,x);
  end;
  readln(m);
  olx(1,0);
  dfs(1,0);
  for i:=1 to 2*n do zf[i]:=zf[i-1]+ol[i].z;
  build(1,1,2*n);
  for i:=1 to m do
  begin
    read(ch,ch1);
    case ch of
    'A':begin
        readln(x,y,a1);inc(x);inc(y);
        xg1(1,xia[lca(x,y)],xia[x],a1);
        xg1(1,xia[lca(x,y)],xia[y],a1);
        xg1(1,xia[lca(x,y)],xia[lca(x,y)],-a1);
      end;
    'Q':begin
        readln(x);  inc(x);
        writeln(cx(1,xia[x],shang[x]));
      end;
    end;
  end;
end.

```
题解如有错误请指出，感激不尽！！！
