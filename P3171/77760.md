来刷水题。

题目讲得很清楚,我们就直接干下面的事情就好了: 跑最短路然后连边跑最大流。

注意到我们要知道哪条边在最短路里面,所以我们可以这样子判断: ``dis[from[i]]+value[i]=dis[reach[i]]`` 那么 $i$ 号边就是一条最短路里面的边。

注意到给的是点的流量限制,我们直接拆点就好了。

很多人用邻接矩阵,我还是照常用链式前向星。

~~至于我为什么一页的 $20$ 分:是因为我的流量开的是 $\max longint$ 而显然答案可能会超。(还让我改出了一堆细节错误)~~

```pascal
// luogu-judger-enable-o2
Uses math;

var
    value,reach,next:array[-1..210000] of int64;
    gap,dis,cnt,long:array[-1..210000] of int64;
    queue:array[-1..620000,1..2] of int64;
    ask:array[-1..210000] of boolean;
    _:array[-1..210000] of int64;
    i:longint;
    n,m,l,r,k,maxflow,j,tot,tail,sink,source:int64;

procedure add(l,r,sum:int64);
begin
    inc(tot); reach[tot]:=r; value[tot]:=sum; next[tot]:=cnt[l]; cnt[l]:=tot;
end;

procedure SPFA;
var
    i,l,head,tail:int64;
begin
    fillchar(ask,sizeof(ask),False);
    head:=1; tail:=1; _[1]:=1; long[1]:=0; ask[1]:=True;
    repeat
        l:=_[head]; ask[l]:=False;
        i:=cnt[l];
        while i<>-1 do
        begin
            if long[l]+value[i]<=long[reach[i]] then
            begin
                long[reach[i]]:=long[l]+value[i];
                if ask[reach[i]]=False then
                begin
                    inc(tail); ask[reach[i]]:=True;
                    _[tail]:=reach[i];
                end;
            end;
            i:=next[i];
        end;
        inc(head);
    until head>tail;
end;

function Dfs(now,flow:int64):int64;
var 
    i,k,mindis,ret:int64;
begin
    mindis:=n-1; ret:=flow;
    if now=sink then exit(flow);
    i:=cnt[now];
    while i<>-1 do
    begin
        if value[i]>0 then
        begin
            if dis[now]=dis[reach[i]]+1 then
            begin
                k:=Dfs(reach[i],min(ret,value[i]));
                dec(value[i],k);inc(value[i xor 1],k);
                dec(ret,k);
                if dis[source]>=n then exit(flow-ret);
                if ret=0 then break;
            end;
             mindis:=min(mindis,dis[reach[i]]);
        end;
        i:=next[i];
    end;
    if ret=flow then
    begin
        dec(gap[dis[now]]);
        if gap[dis[now]]=0 then dis[source]:=n;
        dis[now]:=mindis+1;
        inc(gap[dis[now]]);
    end;
    exit(flow-ret);
end;

begin
    filldword(cnt,sizeof(cnt) div 4,maxlongint*2+1);
    fillchar(long,sizeof(long),1);
    read(n,m);
    for i:=1 to m do begin read(l,r,k); add(l,r,k); add(r,l,k); end;
    SPFA;
    for i:=1 to n do
    begin
        j:=cnt[i];
        while j<>-1 do
        begin
            if (long[i]+value[j]=long[reach[j]]) then
            begin
                inc(tail);
                queue[tail,1]:=i;
                queue[tail,2]:=reach[j];
            end;
            j:=next[j];
        end;
    end;

    filldword(cnt,sizeof(cnt) div 4,maxlongint*2+1);
    fillchar(value,sizeof(value),0);
    fillchar(reach,sizeof(reach),0);
    fillchar(next,sizeof(next),0); tot:=1;
    for i:=1 to tail do
    begin
        add(queue[i,1]+n,queue[i,2],maxlongint*888);
        add(queue[i,2],queue[i,1]+n,0);
    end;
    for i:=1 to n do
    begin
        read(k);
        if (i=1)or(i=n) then
        begin
            add(i,i+n,maxlongint*888);
            add(i+n,i,0);
        end else
        begin
            add(i,i+n,k); add(i+n,i,0);
        end;
    end;
    source:=1; sink:=n << 1; n:=sink; gap[source]:=n;
    while dis[source]<n do inc(maxflow,Dfs(source,maxlongint*888));
    writeln(maxflow);
end.
```