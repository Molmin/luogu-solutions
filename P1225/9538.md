本题是一道典型的宽度优先搜索题。宽度优先搜索的方法本身应该是很显然的：根据题目的描述，对于任意一个棋盘状态，可以通过交换相邻两个格子中的棋子得到新的状态（一次最多得到24个新状态）。所以，我们可以从题目给出的起始状态开始不停的扩展，直至扩展出目标状态。最后，只需输出扩展的路径即可。

上述算法已经可以完全解决本题了。但是，我们现在要继续往细节里讨论本题，讨论本题的实现。

宽度优先搜索的核心是状态的扩展，状态的扩展是通过状态转换实现的。普通的状态转换的方法就是按照题目的定义模拟实现。这里，我们要讨论的是高效简洁的状态转换的方法。

首先是状态的表示。题目中的棋盘是由16个格子组成的(4×4)，如下图。

a1    a2    a3    a4

a5    a6    a7    a8

a9    a10    a11    a12

a13    a14    a15    a16

这16个格子，每个格子里非0即1，所以可以将棋盘写成一个长度为16的01串。

a1    a2    a3    a4    a5    a6    a7    a8    a9    a10    a11    a12    a13    a14    a15    a16

这个0l串可以用一个16bit的整数来表示。也就是说，我们可以用一个0～65535的整数来表示一个状态。

下面最关键的就是状态的转换了。根据题目的定义，每次操作可以交换棋盘上相邻两个格子中的棋子。显然，如果相邻两个格子中的棋子相同，交换是没有意义的，所以我们只需要考虑相邻格子中棋子颜色不同的情况。相邻有两种情况，左右相邻和上下相邻。如图，a1和a2为左右相邻，而a8和a12为上下相邻。我们讨论状态转换的时候，将对这两种“相邻”分别处理。

a1    a2    a3    a4

a5    a6    a7    a8

a9    a10    a11    a12

a13    a14    a15    a16

图7-2
首先来看左右相邻的情况，以a15和a16为例。它们交换以后，得到的棋盘状态为：

a1    a2    a3    a4

a5    a6    a7    a8

a9    a10    a11    a12

a13    a14    a16    a15

图7-3
但是，从另一个角度来考虑问题，a15＝??a16，所以经过转换后，就相当于将a15和a16取反。从位操作的角度来看，设原状态为s，那么a15和a16交换后得到的新状态s15为：

    s15=s xor 3

同样的，还可以推出a14和a15交换后得到的新状态s14为：

    s14=s xor 6=s xor (3\*21)

当然，还有以下很多状态公式：

    s13=s xor 12=s xor(3\*22)

    s11=s xor 48=s xor(3\*24)

    s10=s xor 96=s xor(3\*25)

这里有两个需要注意之点：

（1）交换的两个棋子的颜色必须不同，否则公式不成立；

（2）根据状态转换的定义s4、s8、s12、s16对应的公式不成立，因为它们右边没有相邻的棋子。

最后，我们总结一下左右相邻情况下的状态转换公式（棋子颜色必须不同）

    sk=s xor(3\*215-k)，其中k≠4, 8, 12, 16

与“左右相邻”对应的是“上下相邻”。“上下相邻”情况的分析与“左右相邻”类似，这里就不详细展开了，只列出转换的公式（同样，棋子颜色也必须不同）

    sk=s xor(17\*212-k)，其中k≤12

有了上面两个状态转换的公式，我们只需将起始状态和目标状态转换成16bit的整数，利用公式从起始状态扩展至目标状态即可。整个过程的时间复杂度是O(24\*216)。

从另一个角度考虑问题。本题给出了起始状态和目标状态，那么我们完全可以从这两个状态开始，分别扩展——也就是用双向宽度优先搜索的方法来解决本题。一般来说，双向搜索扩展出的状态总数要比单向少很多，时间和空间复杂度都会有所降低。





```cpp
type arr=array[1..5,1..5] of char;
      rec=record
      m:arr;
      step:longint;
      ans:array[1..100,1..5] of char;
 end;
 const dx:array[1..4] of longint=(1,-1,0,0);
       dy:array[1..4] of longint=(0,0,-1,1);
 var start,ans:rec;
     f:array[0..200000] of boolean;
     spos,epos:arr;
     fr,r,i,j:longint;
     a:array[1..200000] of rec;
 function equal(m:arr):boolean;
 var i,j:longint;
 begin
  for i:=1 to 4 do
   for j:=1 to 4 do
    if m[i,j]<>epos[i,j] then exit(false);
  exit(true);
 end;
 function hash(m:arr):longint;//二进制转十进制，用位运算，效率高
 var res,i,j:longint;
 begin
  res:=0;
  for i:=1 to 4 do
   for j:=1 to 4 do
    begin
     res:=(res shl 1);
     res:=res+ord(m[i,j])-ord('0');
    end;
  exit(res);
 end;
 procedure bfs;
 var t,i,j,k,x,y:longint;
     now,next:rec;
     temp:char;
 begin
  fillchar(f,sizeof(f),true);
  t:=hash(spos);
  f[t]:=false;
  fr:=0;r:=1;
  while fr<>r do
   begin
    inc(fr);
    now:=a[fr];
    if equal(now.m) then
     begin
      ans:=now;
      exit;
     end;
    for i:=1 to 4 do
     for j:=1 to 4 do
      for k:=1 to 4 do
       begin
        next:=now;
        inc(next.step);
        x:=i+dx[k];
        y:=j+dy[k];
        if (x>=1)and(x<=4)and(y>=1)and(y<=4) then
         begin
          temp:=next.m[i,j];
          next.m[i,j]:=next.m[x,y];
          next.m[x,y]:=temp;
          next.ans[next.step,1]:=chr(ord('0')+i);
          next.ans[next.step,2]:=chr(ord('0')+j);
          next.ans[next.step,3]:=chr(ord('0')+x);
          next.ans[next.step,4]:=chr(ord('0')+y);
          next.ans[next.step,5]:=#0;//存储哪两个坐标换了位置
          if equal(next.m) then
           begin
            ans:=next;
            exit;
           end;
          t:=hash(next.m);
          if f[t] then
           begin
            f[t]:=false;
            inc(r);
            a[r]:=next;
           end;
         end;
       end;
    end;
 end;
 begin
  for i:=1 to 4 do
   begin
    for j:=1 to 4 do
     read(spos[i,j]);
    readln;
   end;
  for i:=1 to 4 do
   begin
    for j:=1 to 4 do
     read(epos[i,j]);
    readln;
   end;
  for i:=1 to 4 do
   for j:=1 to 4 do
    start.m[i,j]:=spos[i,j];
  start.step:=0;
  a[1]:=start;
  bfs;
  writeln(ans.step);
  for i:=1 to ans.step do
  begin
    for j:=1 to 4 do
     write(ans.ans[i,j]);
    writeln;
   end;
 end.

```