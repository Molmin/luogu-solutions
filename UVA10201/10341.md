AC的第一道灰题诶，发个题解纪念一下~~lalala~~

先给一个题目描述吧。。。

题目的意思是，有一辆车，从起点0到终点len，途中有n个加油站，汽车从0出发，一开始有100L油，油箱的容量为200L，每走一公里需要耗费一升油，你可以在加油站加油，需要你在到达终点时油箱内的油量大于等于100L，如果达终点时油箱内的油量小于100L或者无法到达终点，则输出‘Impossible’

这是一道动态规划。。。

f[i][j]表示到达第i个加油站，油箱内还有jL油的最小花费。

DP分为两步：

①求出到达i的最小花费

DP方程：

**f[i][j]:=min(f[i][j],f[k][j+d[k][i]])  **

**[0<=k<i]**

注意，当j+d[k][i]大于油箱容量时就应该停止了！

②计算加多少油

DP方程：

**f[i][k]:=f[i][j]+(k-j)*a[i][2];   ** 

**[j<k<=200]**

注意，需要保证f[i][j]是可以到达的，且i不是终点。

Pascal Code：

```cpp
//uses math;
var
  f:array[0..405,0..405]of int64;
  d:array[0..405,0..405]of longint;
  a:array[0..405,0..10]of longint;
  n,i,j,k,zty,len,kong:longint;
  ans,inf:int64;
  s,s1:string;
  ch:char;
function min(x,y:int64):int64;
begin
  if x<y then
    exit(x);
  exit(y);
end;
begin
  inf:=1000000000000007;
  readln(zty);
  while zty<>0 do
  begin
    dec(zty);
    readln(len);
    n:=0;
    repeat//读入比较恶心，一不小心就会格式错误
      readln(s);
      if s='' then
        break;
      kong:=pos(' ',s);
      s1:=copy(s,1,kong-1);
      delete(s,1,kong);
      inc(n);
      val(s1,a[n][1]);
      val(s,a[n][2]);
    until false;
    if a[n][1]<>len then//将终点也作为一个加油站
    begin
      inc(n);
      a[n][1]:=len;
      a[n][2]:=0;
    end;
    for i:=0 to n do
      for j:=0 to 200 do
        f[i][j]:=inf;
    for i:=0 to n do
      for j:=i to n do
        d[i][j]:=a[j][1]-a[i][1];//计算加油站之间的距离
    f[0][100]:=0;//显然的初始化
    for i:=1 to n do
      for j:=0 to 200 do
      begin
        for k:=i-1 downto 0 do//第一种情况
          if j+d[k][i]<=200 then
            f[i][j]:=min(f[i][j],f[k][j+d[k][i]]) else
            break;
        if (a[i][2]<>0) and (f[i][j]<>inf) then//第二种情况
        begin
          for k:=j+1 to 200 do
            f[i][k]:=f[i][j]+(k-j)*a[i][2];
        end;
      end;
    ans:=inf;
    for i:=100 to 200 do
      ans:=min(ans,f[n][i]);//只要>=100L均为可行解
    if ans=inf then
      writeln('Impossible') else
      writeln(ans);
    if zty<>0 then
      writeln;//格式。。。
  end;
end.
```



