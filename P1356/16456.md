题目大意：在n个数间加入n-1个 + - 符号，问能否被k整除。

一开始想的是baoli但是，数据太大没有AC可能，于是就想DP

解题思路：数列的和可能有非常多种

只考虑是否能被k整除；只考虑被k除的余数。

以数列和mod k为状态：

f[i][j]表示是否存在前i个数的和被k除的余数为j的情况。

若f[i-1][j]为真，

则f[i][j+a[i]],f[i][j-a[i]]也为真。

非常容易理解，前i-1个数的余数是j那么算上第i个数a[i]的余数就是(j+a[i])mod k

（注意对j+a[i],j-a[i]取模时的运算）

如果第i-1个数和第i个数中间运算有+-，那么a[i]要正负，

我们一开始对数组全部改为正数，方便操作（否则越界201）

初始条件对a[1]进行分类：

如果a[1]<0 那么 f[1,(k+(a[1] mod k)) mod k]:=true（避免负数的干扰）

否则：f[1,a[1] mod k]:=true;

于是数列能否整除k的答案为f[n][0]


```cpp
var n,k,i,j,m,t,temp:longint;
    f:array[0..10050,0..105]of boolean;
    a:array[1..10050]of longint;
begin
 readln(m);
 for t:=1 to m do begin
 readln(n,k);
 fillchar(f,sizeof(f),false);
 for i:=1 to n do read(a[i]);
 if a[1]<0 then f[1,(k+(a[1] mod k)) mod k]:=true
 else f[1,a[1] mod k]:=true;
 for i:=2 to n do a[i]:=abs(a[i]);
 for i:=2 to n do
  for j:=0 to k-1 do
   if f[i-1,j] then begin
    f[i,(j+a[i])mod k]:=true;
    temp:=j-(a[i] mod k);
    if temp<0 then temp:=temp+k;
    f[i,temp]:=true;
   end;
 if f[n,0] then writeln('Divisible')
 else writeln('Not divisible');
 end;
end.
```