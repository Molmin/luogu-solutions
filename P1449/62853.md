这道题目是用栈stack来做的

读到数字，压入栈

读到符号，弹出栈两次（因为加减乘除都是双目运算符），运算后压入栈

读到点，不理睬

读到@，结束程序

用Pascal实现100分

判断使用case语句
```
var
  ch:char;//读入使用的字符
  s:array[1..1000]of longint;//栈定义1000
  top{栈顶},x,y{两个操作数}:longint;
begin
  repeat
    read(ch);//读入
    case ch of//等同于c++的switch
      '@':break;//读到@，结束程序
      '.':continue;//读到点，不理睬
      '0'..'9'://读到数字
      begin
        inc(top);//栈顶+1
        s[top]:=ord(ch)-48;//压入栈
        repeat
          read(ch);
          if ch='.'then//是点就跳出，因为后缀表达式中符号在后面，所以无需判断@
            break;
          s[top]:=ord(ch)-48+s[top]*10;//当持续读入数字时累计数字
        until false;
      end;
      '+'://加法
      begin
        x:=s[top];//取走栈顶
        y:=s[top-1];//此处偷懒，直接取栈下面，非标准写法
        dec(top);//将top-2+1合并为dec(top)
        s[top]:=x+y;//重新压入栈
      end;
      '-'://下面几乎同上
      begin
        x:=s[top];
        y:=s[top-1];
        dec(top);
        s[top]:=y-x;//先进后出，所以操作数顺序颠倒，为y-x，非x-y
      end;
      '*':
      begin
        x:=s[top];
        y:=s[top-1];
        dec(top);
        s[top]:=x*y;
      end;
      '/':
      begin
        x:=s[top];
        y:=s[top-1];
        dec(top);
        s[top]:=y div x;//同上操作数颠倒
      end;
    end;
  until eoln;//行末结束
  writeln(s[top]);//输出栈顶元素（如果表达式规范，此时top=1，即为标准答案）
end.

