楼下的哈希表简短精悍……在看题解之前不懂哈希的本蒟蒻只好用链表+邻接表……麻烦又容易写错，但是还好过了

搜索祖先的话，首先想到的是并查集，蒟蒻就想，如果那些名字都是序号就快了。所以我把所有人名转成了序号，然后就是简单的并查集。重点在最后要怎么把人名对应的序号找出来。于是我用了邻接表，每个字母作为一个节点，有三个域：字母，下一个字母位置以及同级的字母位置。比如说George和Geoeee在链表中就共用前三个节点，而r这个节点的“同级字母位置域”就存Geoeee中第二个e的位置，r的下一个字母就是存g……以此类推。当然，最后一个节点还要再拉一个节点，这个节点的字母域就存这一串单词的序号。

看着有点绕，接下来看代码吧……辛苦各位看官了……


```cpp
type point=^node;
     node=record
      letter:longint;//字母域，用longint防止序号超出ASCII范围，当然字母也只能转换成数字              来存
      next,other:point;
     end;
var a,b,c,d,n,m,gb,temp,tian:longint;
    names:array[1..50000] of string[6];
    father:array[0..50000] of longint;
    s,s2:string;
    p,q:point;
    head:array['A'..'Z'] of point;
    ch:char;
function search(x:string):longint;//判断人名是否存在（不存在就返回0），以及查找存在时名字的序号
var v:longint;
begin
 v:=2;
 if head[x[1]]=nil then exit(0);
 p:=head[x[1]]^.next;
 if p=nil then exit(0);
 repeat
  if p^.letter<>ord(x[v]) then
    begin if p^.other=nil then exit(0) else p:=p^.other; end
    else begin p:=p^.next; inc(v); end;
 until v>6;
 exit(p^.letter);
end;
procedure zaolian(x:string);//将人名添加进链表
var v:longint;
begin
 if head[x[1]]=nil then//head的目的是加快首字母查询的速度，如果这个首字母不存在直接创建新的一串节点
   begin
    new(head[x[1]]);
    p:=head[x[1]];
    v:=2;
    while v<=6 do
      begin
       new(p^.next);
       p:=p^.next;
       p^.letter:=ord(x[v]);
       inc(v);
      end;
    new(p^.next);
    p:=p^.next;
    p^.letter:=gb;
   end
   else begin
    p:=head[x[1]]^.next;
    v:=2;
    while v<=6 do
      begin
       while (p^.letter<>ord(x[v])) and (p^.other<>nil) do p:=p^.other;//找找共用几个节点
       if p^.letter=ord(x[v]) then begin p:=p^.next; inc(v); end
         else if p^.other=nil then
           begin
            new(p^.other);
            p:=p^.other;
            p^.letter:=ord(x[v]);
            inc(v);
            while v<=6 do//没共用的就新建
              begin
               new(p^.next);
               p:=p^.next;
               p^.letter:=ord(x[v]);
               inc(v);
              end;
            new(p^.next);
            p:=p^.next;
            p^.letter:=gb;
            break;
           end
      end;
   end;
end;
function find(o:longint):longint;
begin
 if father[o]<>o then father[o]:=find(father[o]);
 find:=father[o];
end;
procedure union(fu,zi:longint);
begin
 fu:=find(fu);
 zi:=find(zi);
 father[zi]:=fu;
end;
begin
 for ch:='A' to 'Z' do head[ch]:=nil;
 for a:=0 to 50000 do father[a]:=a;
 gb:=1; readln(s);
 while s[1]<>'?' do
   begin
    if s[1]='#' then
      begin
       names[gb]:=copy(s,2,6);
       tian:=search(names[gb]);
       if tian=0 then
         begin
          zaolian(names[gb]);
          temp:=gb; inc(gb);
         end
         else temp:=tian;
      end;
    if s[1]='+' then
      begin
       names[gb]:=copy(s,2,6);
       tian:=search(names[gb]);
       if tian=0 then
         begin
          union(temp,gb);
          zaolian(names[gb]);
          inc(gb);
         end
         else union(temp,tian);
      end;
    readln(s);
   end;
 while s[1]<>'$' do
   begin
    s2:=copy(s,2,6);
    temp:=search(s2);
    writeln(s2,' ',names[find(temp)]);
    readln(s);
   end;
end.

```