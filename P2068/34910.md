标准的树状数组。。。。。

算法分析

如果直接做的话,修改的复杂度是O(1),询问的复杂度是O(N),M次询问的复杂度是M\*N.M,N的范围可以有100000以上,所以这样做会超时,但是如果用线段树的话,还是很不错的! 有很多线段树能实现但树状数组却实现不了的题目。

线段树解法分析

可以看出,这棵树的构造用二分便可以实现.复杂度是2\*N.

每个结点用数组a来表示该结点所表示范围内的数据之和.

修改一个位置上数字的值,就是修改一个叶子结点的值,而当程序由叶子结点返回根节点的同时顺便修改掉路径上的结点的a数组的值.

对于询问的回答,可以直接查找i..j范围内的值,遇到分叉时就兵分两路,最后在合起来.也可以先找出0..i-1的值和0..j的值,两个值减一减就行了.后者的实际操作次数比前者小一些.

这样修改与维护的复杂度是logN.询问的复杂度也是logN,对于M次询问,复杂度是MlogN.

然而不难发现,线段树的编程复杂度大,空间复杂度大,时间效率也不高!!!!

所以我们来想用树形数组来实现:

那么,何为树形数组呢??

C数组就是树状数组,a数组是原数组;

对于序列a，我们设一个数组C定义C[t] = a[t – 2^k + 1] + … + a[t]，k为t在二进制下末尾0的个数。

K的计算可以这样:

2^k=t and (t xor (t-1))

以6为例

```cpp
(6)10=(0110)2
xor 6-1=(5)10=(0101)2
(0011)2
and (6)10=(0110)2
(0010)2
```
所以问题变的很简单,重要写几个函数就可以了;
```cpp
var
  sum:array[0..100005]of longint;
  n,i,x,a,b,w:longint;
  ch:char;
function lowbit(x:longint):longint;
begin
  exit(x and -x);
end;
procedure add(x,k:longint);
begin
  while x<=n do
  begin
    sum[x]:=sum[x]+k;
    x:=x+lowbit(x);
  end;
end;
function query(x:longint):longint;
begin
  query:=0;
  while x>0 do
  begin
    query:=query+sum[x];
    x:=x-lowbit(x);
  end;
  exit(query);
end;
begin
  readln(n);
  readln(w);
  for i:=1 to w do
  begin
    readln(ch,a,b);
    if ch='x' then add(a,b);
    if ch='y' then writeln(query(b)-query(a-1));
  end;
end.

```