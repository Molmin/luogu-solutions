这题范围好小。。。1亿多的复杂度居然能过。。

入正题

看到楼下大佬们的高深算法，本蒟蒻自叹不如

我用的方法是BFS

模拟每一条路都炸一次的情况，用BFS判断是否全联通

若不为全联通，则此路可以炸，输出

道路可先排序，避免字典序的问题

时间复杂度O(mn^2)，可能空跑的比较多，所以AC

代码如下：

···pas



```cpp
var a,b:array[1..5000] of longint;//道路下标
    c:array[1..150,1..150] of longint;//图
    d:array[1..150] of longint;//队列
    f:array[1..150] of boolean;//判断每个城市是否有路联通
    i,j,n,m,head,tail:longint;
procedure sort(l,r: longint);//道路快排，在此不多叙述
var i,j,x,y,z: longint;
begin
  i:=l;  j:=r;
  x:=a[(l+r) div 2]; z:=b[(l+r) div 2];
  repeat
    while (a[i]<x)or(a[i]=x)and(b[i]<z) do inc(i);
    while (x<a[j])or(a[j]=x)and(b[j]>z) do dec(j);
           if not(i>j) then
             begin
                y:=a[i]; a[i]:=a[j]; a[j]:=y;
                y:=b[i]; b[i]:=b[j]; b[j]:=y;
                inc(i); j:=j-1;
             end;
         until i>j;
         if l<j then sort(l,j);
         if i<r then sort(i,r);
      end;
begin
  read(n,m);//读入
  for i:=1 to m do
  begin
    read(a[i],b[i]);
    if a[i]>b[i] then
    begin
      j:=a[i]; 
      a[i]:=b[i];
      b[i]:=j;
    end;//题目要求输出时a<b,所以在这里预先调换好
    c[a[i],b[i]]:=1;
    c[b[i],a[i]]:=1;
  end;
  sort(1,m);//道路快排
  for i:=1 to m do
  begin
    f[1]:=false;
    for j:=2 to n do f[j]:=true;//从第一个城市开始BFS
    c[a[i],b[i]]:=0;
    c[b[i],a[i]]:=0;//炸掉铁路
    head:=0; tail:=1;
    d[1]:=1;//第一个城市入队
    repeat
      head:=head+1;
      for j:=1 to n do
      if (c[d[head],j]=1)and(f[j]) then//是否有路通向其他城市，若有路，则城市入队
      begin
        f[j]:=false;//城市get
        tail:=tail+1;//尾指针+1
        d[tail]:=j;//入队
      end;
    until head>=tail;
    for j:=1 to n do
    if f[j] then break;
    if f[j] then writeln(a[i],' ',b[i]);//若有城市没有遍历到，则输出这条铁路
    c[a[i],b[i]]:=1;
    c[b[i],a[i]]:=1;//补回铁路
  end;
end.
```
···

代码时间复杂度较高，勿喷
