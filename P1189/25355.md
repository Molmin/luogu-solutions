#一道练剪枝的好题

每次往第k个方向一直走，直到碰到障碍或是出了边界，每走一步都算这个方向走过了，然后就调用过程，去走下个一方向。

然而这样只有30分，所以我们还需要剪枝。

f[i,j,k]表示第k次到了(i,j)有没有尝试过，如果有，直接exit掉。

献上没有66666a代码丑陋的代码：

```cpp
const
  x:array[1..4]of longint=(-1,1,0,0);
  y:array[1..4]of longint=(0,0,-1,1);//四个方向
var
  a:array[0..51,0..51]of char;
  b:array[0..51,0..51]of boolean;
  f:array[0..51,0..51,1..1000]of boolean;
  s:array[0..1001]of longint;
  n,m,k,i,j,p,q:longint;
  s1:ansistring;
function zz(s1:ansistring):longint;
begin
  if s1='NORTH' then exit(1);//上
  if s1='SOUTH' then exit(2);//下
  if s1='WEST' then exit(3);//左
  if s1='EAST' then exit(4);//右
end;
procedure try(kk,l,r:longint);
var
  i,j:longint;
  begin
    if f[l,r,kk] then exit;//剪枝
    f[l,r,kk]:=true;
    if kk>k then 
      begin
        b[l,r]:=true;
        exit;
      end;//边界
    i:=l+x[s[kk]];
    j:=r+y[s[kk]];
    while (a[i,j]<>'X') do //直到碰上障碍，因为边界被围上了一圈障碍
      begin
        try(kk+1,i,j);//每走一步，都尝试去走下个一方向
        inc(i,x[s[kk]]);
        inc(j,y[s[kk]]);
      end;
  end;//dfs
begin
  readln(n,m);
  for i:=1 to n do 
    begin
      for j:=1 to m do 
        begin
          read(a[i,j]);
          if a[i,j]='*' then 
            begin 
              p:=i;
              q:=j;
            end;
        end;
      readln;
    end;
  for i:=0 to n+1 do 
    begin
      a[i,0]:='X';
      a[i,m+1]:='X';
    end;
  for i:=0 to m+1 do 
    begin
      a[0,i]:='X';
      a[n+1,i]:='X';
    end;//给地图的周围一圈围上障碍
  readln(k);
  for i:=1 to k do 
    begin
      readln(s1);
      s[i]:=zz(s1);
    end;//把字符串表示的方向转换成数字
  try(1,p,q);
  for i:=1 to n do 
    begin
      for j:=1 to m do 
        if b[i,j] then write('*')
          else if a[i,j]='*' then write('.')
            else write(a[i,j]);
      writeln;
    end;//输出
end.

```