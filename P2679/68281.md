~~蒟蒻的第一篇题解~~

个人第一眼觉得这道题可能比较像[P1077 摆花](https://www.luogu.org/problemnew/show/P1077)，所以之后的DP与之类似。

设定**f[i,j,k]**表示：从字符串A中的前i位中 取k个子串 拼成字符串A中的前j位 的方法总数。

自然可以得到

**f[i,j,k]=f[i-1,j,k]+sum(f[i-l,j-l,k-1]);(0<l<i)**

其中**f[i-1,j,k]**表示不用到字符串A中的第i位的方法数；

**sum(f[i-l,j-l,k-1])**表示把从第i位开始前l位分割为一个子串的方案数；

故可以得到程序：
```pascal
for i:=1 to n do
  for j:=1 to min(i,m) do
    for k:=1 to min(j,kk) do
      begin
        f[i,j,k]:=f[i-1,j,k];
        for l:=1 to j do
          if a[i-l+1]=b[j-l+1] then
            f[i,j,k]:=f[i,j,k]+f[i-l,j-l,k-1]
          else
            break;
      end;
```

但是四维循环对于数据稍强的情况就会出现TLE的情况，所以只能得80。

所以可以将l循环舍去，再建立一个数组来将累加的结果储存。

最后再把i数组滚动节省空间，此题完成。

附代码：
```
var
  n,m,kk,i,j,k,l,s,x,y,t:longint;
  a,b:ansistring;
  dp:array[0..1000,0..200] of longint;//储存累加结果
  f:array[0..1,0..200,0..200] of longint;
function min(a,b:longint):longint;
  begin
    if a>b then
      exit(b)
    else
      exit(a);
  end;
begin
  readln(n,m,kk);
  readln(a);
  readln(b);
  for i:=1 to 1000 do
    dp[i,0]:=1;
  s:=1000;x:=1;y:=0;
  for i:=1 to n do//dp
  begin
    for j:=1 to min(i,m) do
      for k:=1 to min(j,kk) do
        begin
          f[x,j,k]:=f[y,j,k];
          if b[j]=a[i] then
            begin
              dp[s-i+j,k-1]:=(dp[s-i+j,k-1]+f[y,j-1,k-1]) mod 1000000007;
              f[x,j,k]:=(f[x,j,k]+dp[s-i+j,k-1]) mod 1000000007;
            end
          else
            dp[s-i+j,k-1]:=0;
        end;
    t:=x;
    x:=y;
    y:=t;//滚动数组
  end;
  write(f[y,m,kk]);
end.
```