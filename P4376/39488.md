只有这么几篇？(而且还没有Pascal的)

**二分答案+tarjan判环+堆**

**不急，我们一步一步来**

首先，要求满足最多的前x个观察结果要相同，如果我们一步一步看一下能不能满足，就太慢了，这里就是用二分答案来写

接下来，对于每一个二分出来的答案mid，怎么判断是否合法呢，如果一头奶牛要在另一头奶牛前面，又要在一头奶牛后面，那么这种情况就是满足不了的，**更广泛的来说，我们对于每一组谁在谁前面的条件，从前面的奶牛建一条有向边指向后面那头奶牛，如果成环，那么就是不合法的**

**那么一个新的问题：我们怎么判环呢？**

**第一种方法：spfa**

我们建边的时候把边的权值改为-1，然后判有没有负环就可以了，具体可以参照洛谷P3385模板

这里dfs版的spfa并不可取，在没有负环的情况下dfs就很慢（我的一个同学亲测，tle了6个点），但是写bfs用一个点有没有进入队列超过n次的话也很慢，[这里可以参照我之前写的题解](https://www.luogu.org/blog/user39488/solution-p3385)

**第2种办法:tarjan判环**

对于任意一个强连通分量，如果其中有超过1个点组成，那么就是不合法的

**这时候我们已经求好了最大的x是多少了，接下来怎么输出呢？**

**题目可是要求字典序最小啊(乱七八糟的事情真多（逃）)**

我们记录一下每个点的入度是多少，很显然要先输出入度为0的点

这时候写一个小根堆来维护输出序列，对于输出的每一个点，我们删掉他连出去的所有边，这时候可能会重新出现入度为0的点，就加到堆里去

**看，这样一个一个分开不都是很简单的吗**

by the way，这里二分的时候有一个小细节，我们每得到一个新的mid，图也会因此而变化，你可以选择重新建图（可以过的），或者用一个比较巧妙的方法，**记录一下每一个观察结果所产生的图的最大的那条边是什么**，就是我代码里的last数组，如果你进行什么操作的时候要用的边大于last[mid]的时候，就不用这条边，因为它不在前mid信息里面

**说了好多好累啊.....看代码吧**

**勉强挤入最优解第一页（我好蒟蒻啊）**
```
var
  bi:boolean;
  ans,look,t,l,r,mid,tot,top,a1,a2,len:longint;
  i,j,m,n,k,p:longint;
  ru,next,other,a,last,dfn,low,stack,num,check,f:array[0..200005]of longint;
function min(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
procedure tarjan(u:longint);
var
  j,v,k:longint;
begin
  if bi=false then exit;
  inc(t); dfn[u]:=t; low[u]:=t;
  check[u]:=1; inc(top); stack[top]:=u;
  k:=a[u];
  while k<>0 do
    begin
      v:=other[k];
      if k>last[mid] then
        begin
          k:=next[k]; continue;
        end;
      if check[v]=0 then
        begin
          tarjan(v);
          low[u]:=min(low[u],low[v]);
        end
      else
        if check[v]=1 then
          low[u]:=min(low[u],dfn[v]);
      k:=next[k];
    end;
  if dfn[u]=low[u] then
    begin
      inc(tot);
      repeat
        j:=stack[top]; dec(top);
        check[j]:=-1;
        inc(num[tot]);
        if num[tot]=2 then
          begin
            bi:=false; exit;
          end;
      until j=u;
    end;
end;
procedure put(x:longint);
var
  now,father,a1:longint;
begin
  inc(look); f[look]:=x;
  now:=look;
  while now<>1 do
    begin
      father:=now div 2;
      if f[father]<f[now] then break
      else
        begin
          a1:=f[father]; f[father]:=f[now]; f[now]:=a1;
          now:=father;
        end;
    end;
end;
procedure get;
var
  now,a1,son:longint;
begin
  f[1]:=f[look]; dec(look);
  now:=1;
  while now*2<=look do
    begin
      son:=now*2;
      if (son+1<=look)and(f[son+1]<f[son]) then inc(son);
      if f[son]>=f[now] then break
      else
        begin
          a1:=f[son]; f[son]:=f[now]; f[now]:=a1;
          now:=son;
        end;
    end;
end;
begin
  {assign(input,'11.in');
  assign(output,'11.out');
  reset(input); rewrite(output);}
  readln(n,m);
  for i:=1 to m do
    begin
      read(k);
      if k<>0 then
        begin
          read(a1);
          for j:=2 to k do
            begin
              read(a2);
              inc(len);
              next[len]:=a[a1]; a[a1]:=len; other[len]:=a2;
              a1:=a2;
            end;
        end;
      readln;
      last[i]:=len;
    end;
  {for i:=1 to m do
    writeln(last[i],' ');}
  l:=0; r:=m;
  while l<r do
    begin
      mid:=(l+r+1) div 2;
      t:=0; tot:=0; top:=0;
      fillchar(check,sizeof(check),0); {fillchar(dfn,sizeof(dfn),0);
      fillchar(low,sizeof(low),0);} fillchar(num,sizeof(num),0);
      bi:=true;
      for i:=1 to n do
        if (check[i]=0)and(bi=true) then
          tarjan(i);
      if bi=true then l:=mid
      else r:=mid-1;
    end;
  //writeln(l);
  for i:=1 to n do
    begin
      k:=a[i];
      while k<>0 do
        begin
          p:=other[k];
          if k>last[l] then
            begin
              k:=next[k]; continue;
            end;
          inc(ru[p]);
          k:=next[k];
        end;
    end;
  {for i:=1 to n do
    writeln(ru[i]);}
  look:=0;
  for i:=1 to n do
    if ru[i]=0 then
      put(i);
  ans:=n;
  while ans>0 do
    begin
      a1:=f[1]; write(a1,' '); get;
      dec(ans);
      k:=a[a1];
      while k<>0 do
        begin
          p:=other[k];
          if k>last[l] then
            begin
              k:=next[k]; continue;
            end;
          dec(ru[p]);
          if ru[p]=0 then
            put(p);
          k:=next[k];
        end;
    end;
  //close(input); close(output);
end.
```
祝大家2018 RP++