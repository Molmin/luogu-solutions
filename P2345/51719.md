**P党的福利**（借鉴于在下某位基友）

这道题目是一道不错的练手题

我来发个树状数组的

max(vi,vj)\*abs(xi-xj)

看出两点

1、根据v从小到大排序，这个max就迎刃而解了

2、看这个abs，我刚开始天真的以为第i个牛与前面的牛（这里是按照v排序的）的总和为v\*(i\*xi-sigma(xj))

忽略了一个绝对值的性质，abs(a-(a+b))+abs(a-(a-b))=2b<>abs(2a-(a+b+a-b))=0

得分为两类，一类为位置比当前小的，另一类为位置比当前大的

令num为比当前小的个数，sum为比当前小的和,s为前缀和（用于求出比当前大的和）

那么第一类的和为v\*(num\*xi-sum)

第二类的和为v\*(s-sum-(i-1-num)\*xi)

另外，树状数组的下标得注意，i是表示<i还是<=i，会不一样









```cpp
type
    ar=array[0..100000] of int64;
var
    a,b,num,sum:ar;
    i:longint;
    ans,n,s:int64;
function lowbit(x:int64):int64;
begin
    exit(x and -x);
end;
procedure change(var c:ar;x,y:int64);
begin
    while x<=20000 do  //特别注意，开到足够，不然拿63分
        begin
            inc(c[x],y);
            inc(x,lowbit(x));
        end;
end;
function getsum(var c:ar;x:int64):int64;
begin
    getsum:=0;
    while x>0 do
        begin
            inc(getsum,c[x]);
            dec(x,lowbit(x));
        end;
end;
procedure sort(l,r: longint);
      var
         i,j,x,y: longint;
      begin
         i:=l;
         j:=r;
         x:=a[(l+r) div 2];
         repeat
           while a[i]<x do
            inc(i);
           while x<a[j] do
            dec(j);
           if not(i>j) then
             begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
                y:=b[i];
                b[i]:=b[j];
                b[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
begin
    readln(n);
    for i:=1 to n do
        readln(a[i],b[i]);
    sort(1,n);
    for i:=1 to n do
        begin
            inc(ans,a[i]*(getsum(num,b[i]-1)*b[i]-getsum(sum,b[i]-1)));
            inc(ans,a[i]*(s-getsum(sum,b[i]-1)-(i-1-getsum(num,b[i]-1))*b[i]));
            change(num,b[i],1);
            change(sum,b[i],b[i]);  //这边下标是<=i的情况
            inc(s,b[i]);
        end;
    writeln(ans);
end.

```