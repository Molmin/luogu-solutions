这是pascal代码。

首先，你有没有看出来这题就是求逆序对？

最优方案每移动一次必定消且仅消去一个逆序对。

so，逆序对用得上树状数组吗？

据说，有一种东西叫归并排序。（n log n）

在归并过程中改一点东西就行啦。

```cpp
var n,i:longint;
ans:int64;//由于是五十万的数据，最多的逆序对个数会爆longint。
a,b:array [1..500000] of longint;
procedure merge(l,p,r:longint);
var i,j,k,t:longint;
begin
  i:=l;
  j:=p;
  for k:=l to r do
  if (i<p) and ((j>r) or (a[i]<=a[j])) then
  begin
    b[k]:=a[i];
    inc(i);
  end
  else
  begin
    b[k]:=a[j];
    inc(j);
    ans:=ans+p-i;//小改的地方。
  end;
  for i:=l to r do
  a[i]:=b[i];
end;
procedure merge_sort(l,r:longint);
var x:longint;
begin
  if l=r then exit;
  x:=(l+r) div 2;
  merge_sort(l,x);
  merge_sort(x+1,r);
  merge(l,x+1,r);
end;
begin
  read(n);
  for i:=1 to n do
  read(a[i]);
  merge_sort(1,n);
  write(ans);
end.
```