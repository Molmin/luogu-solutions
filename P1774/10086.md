基本思路：求逆序对；

方法1：插入排序思想，时间复杂度O(n^2)，会超时，不适用。

方法2：归并排序思想，本题解采用这种思想，优点在于时间复杂度低，且易于理解。

方法3：树状数组思想，时间复杂度低，但是对于普及组平均水平来说难以掌握。

实现细节：不断地归并排序，交换的次数可转换为逆序对的数量。

注意：每次排序不要复制整个数组，否则会产生大量的时间开销。最优的做法是将排好序的数据保存在一个临时数组t里，排完序复制到主数组a中。临时数组t也不要每一个过程就开一次，可以开在主程序中，这样可以节省大量的内存开销

代码：

'''pascal

```cpp
var
    n,i:longint;
    ans:qword;
    a,t:array[1..500000]of longint;
procedure deal(l,r:longint);
    var
        mid,p,q,i:longint;
    begin
        if l=r then begin
            exit()
        end;
        mid:=(l+r)>>1;
        deal(l,mid);
        deal(mid+1,r);
        p:=l;
        q:=mid+1;
        i:=l;
        repeat
            if (a[p]<=a[q]) and (p<=mid) or (q>r) then begin
                t[i]:=a[p];
                inc(p)
            end
            else begin
                t[i]:=a[q];
                inc(q);
                ans+=mid-p+1
            end;
            inc(i)
        until (i>r);
        for i:=l to r do begin
            a[i]:=t[i]
        end;
    end;
begin
    readln(n);
    for i:=1 to n do begin
        read(a[i])
    end;
    readln;
    ans:=0;
    deal(1,n);
    writeln(ans)
end.
'''
```