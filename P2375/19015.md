[戳连接食用](http://www.cnblogs.com/tonylim/p/6919212.html)


感谢 [@ 某沙茶的OI代码库 ](http://my.csdn.net/qpswwww)   写的题解，主要思路(引用部分)来自[[省选前题目整理][BZOJ 3670][NOI 2014]动物园(KMP)](http://blog.csdn.net/qpswwww/article/details/44937169)


我们可以先用kmp求出next数组，然后我们构造一个cnt数组，

>cnt[i]=为满足既是前缀i的前缀，又是前缀i的后缀的子串个数(包括前缀i本身)，这个数组可以通过递推在O(n)时间内得出：


>然后我们可以通过cnt[]和next[]数组求得num[]数组。具体做法是，对于每个i>=2(i=1时显然num[i]=0)，通过沿着next[]指针向前走找到最大的j,j满足前缀j既是前缀i的前缀，也是它的后缀，且2j<=i(即题面中所限制的前缀和后缀不重叠)，那么num[i]=cnt[j]

//注意：前缀i指的是前i个字符组成的字符串

cnt[1]=1  //前缀1只有本身

cnt[i]=cnt[next[i]]+1(1<i<=len) //前缀i的最长匹配next[i]+他自身

胡弄一番证明——:

∵当前缀i的前缀j和他长度为j的后缀不重叠

∴前缀j的长度为u后缀=前缀i的长度为u的后缀   //当1<=u<=cnt[j]

又∵前缀j的长度为u后缀=前缀j的长度为u前缀=前缀i的长度为u的前缀

∴前缀i的长度为u的前缀=前缀i的长度为u的后缀=前缀j的长度为u前缀(这个的种数也就是cnt[j])=前缀j的长度为u后缀

又∵我们找到的j满足2j<=i

∴num[i]=cnt[j]


然后我们就得到了一个50分的做法with TLE(注意ans为int64)


```pascal

program no;
const
  m=1000000007;
var
  b:ansistring;
  ans:int64;
  n,p,i,j,l,num:Longint;
  next,cnt:array[0..1000001] of longint;

procedure get_next_and_cnt;
begin
     j:=0;
    next[1]:=0;  cnt[1]:=1;
    for i:= 2 to l do
    begin
      while (j>0) and (b[j+1]<>b[i]) do j:=next[j];
      if b[j+1]=b[i] then inc(j);
      next[i]:=j;              //kmp计算next
      cnt[i]:=cnt[next[i]]+1;  //顺带计算cnt
    end;
end;

procedure cal;
begin
    ans:=1;
    for i:= 2 to l do
      begin
          j:=next[i];
          while 2*j>i do j:=next[j];  //查找最大的符合条件的J
          num:=cnt[j];
          ans:=((ans mod m)*((num+1) mod m)) mod m;
      end;
end;

begin
   //assign(input,'1.in'); assign(output,'1.out');
   reset(input); rewrite(output);

   readln(n);
   for p:= 1 to n do
   begin
     readln(b);   l:=length(b);

     get_next_and_cnt();
     cal();

     writeln(ans);
   end;

   close(input);  close(output);
end.

```

于是下面是100分的做法：

我们可以把查找最大的符合条件的J的部分改为像kmp中的一样：


 ```pascal

 while(j<>0) and (b[j+1]<>b[i]) do j:=next[j];
            if(b[j+1]=b[i]) then inc(j);

```

我们在循环到i时当前的j是不是可以重前一次的最长的不重叠的j得到呢？

答案是肯定的,要么小于等于上一次的j，要么等于上一次的j+1

===
猜想：会不会是j+2或更大呢？


不，可以用反证法

假设第i−1次循环得到j为j,这次(第i次)最终得到的为j+2

那么可以发现前缀i的前缀j+2=他长度为j+2的后缀

那么前缀j+2=前缀i的长度为j+2的后缀的长度为j+1的前缀(结束为i−1)

又因为我们得到的j+2满足不重叠，那么j+1自然也不会重叠

那么我们可以得到前缀i−1的的最优j为j+1

这样就冲突啦，于是这是不可能的

=====
然后我们每次求j的操作就接近O(1),那cal()也就接近O(n)啦！











```cpp
program no;
const
  m=1000000007;
var
  b:ansistring;
  ans:int64;
  n,p,i,j,l,num:Longint;
  next,cnt:array[0..1000001] of longint;
procedure get_next_and_cnt;
begin
     j:=0;
    next[1]:=0;  cnt[1]:=1;
    for i:= 2 to l do
    begin
      while (j>0) and (b[j+1]<>b[i]) do j:=next[j];
      if b[j+1]=b[i] then inc(j);
      next[i]:=j;
      cnt[i]:=cnt[next[i]]+1;
    end;
end;
procedure cal;
begin
    ans:=1;j:=0;
    for i:= 2 to l do
      begin
        while(j<>0) and (b[j+1]<>b[i]) do j:=next[j];
            if(b[j+1]=b[i]) then inc(j);            //修改的地方
          while 2*j>i do j:=next[j];
          num:=cnt[j];
          ans:=((ans mod m)*((num+1) mod m)) mod m;
      end;
end;
begin
  // assign(input,'1.in'); assign(output,'1.out');
   reset(input); rewrite(output);
   readln(n);
   for p:= 1 to n do
   begin
     readln(b);   l:=length(b);
     get_next_and_cnt();
     cal();
     writeln(ans);
   end;
   close(input);  close(output);
end.
```