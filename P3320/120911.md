其实我的思路和大部分题解是差不多的。(具体思路见题解，下面我说的话和思路没任何关系，完全在讲pascal如何实现）

但是作为一个不会平衡树的小菜鸡我实在想不到如何来维护这个可怕的宝藏村庄序列。

于是我找到了机房大佬，请求他助我一臂之力！

于是……我就使用了Pascal版的STL库来做了！(实际上我原先还以为只有c++有这东西)

然而这东西比赛是不能用的(~~ccf太*了~~)

于是我们就可以用sets来维护序列了！！！

~~这是我目前做过最装逼的题目了……~~

```pascal
uses Gset,Gutil,math;
type
  cmp=specialize Tless<longint>;
  sets=specialize Tset<int64,cmp>;
  rec=record
  x,next,dis:longint;
  end;
var f,d:array[0..100000,0..20] of int64;
    dian,dep,h,xu:array[0..100000] of longint;
    b:array[0..100000] of boolean;
    q:array[0..200000] of rec;
    k1,n,m,i,x,y,z,u,cnt1,cnt,k:longint;
    ans:int64;
    s:sets;
procedure ad(x,y,z:longint);//链式前向星存图
begin
  inc(cnt);
  q[cnt].x:=x;q[cnt].dis:=z;q[cnt].next:=h[y];
  h[y]:=cnt;
end;
procedure dfs(k,l,dist:longint);//日常dfs预处理
var t,i:longint;
begin
  inc(k1);
  dian[k1]:=k;xu[k]:=k1;//其实我觉得这样写更方便一点(在空间不炸的情况下)
  dep[k]:=dep[l]+1;
  f[k,0]:=l;d[k,0]:=dist;
  for i:=1 to trunc(log2(dep[k])) do
  begin
    f[k,i]:=f[f[k,i-1],i-1];
    d[k,i]:=d[k,i-1]+d[f[k,i-1],i-1];
  end;
  t:=h[k];
  while t>0 do
  begin
    if q[t].x<>l then dfs(q[t].x,k,q[t].dis);
    t:=q[t].next;
  end;
end;
function lca(x,y:longint):int64;//lca(x,y)表示x与y的距离
var i,t:longint;
begin
  lca:=0;if (x=0)or(y=0) then exit(0);
  if dep[x]<dep[y] then
  begin
    t:=x;x:=y;y:=t;
  end;
  for i:=20 downto 0 do
  begin
    if dep[f[x,i]]>=dep[y] then
    begin
      lca:=lca+d[x,i];
      x:=f[x,i];
    end;
    if x=y then exit;
  end;
  for i:=20 downto 0 do
    if f[x,i]<>f[y,i] then
    begin
      lca:=lca+d[x,i]+d[y,i];
      x:=f[x,i];
      y:=f[y,i];
    end;
  lca:=lca+d[x,0]+d[y,0];
end;
begin
  readln(n,m);ans:=0;
  s:=sets.create;
  s.insert(-maxlongint);
  s.insert(maxlongint);
  for i:=1 to n-1 do
  begin
    readln(x,y,z);
    ad(x,y,z);
    ad(y,x,z);
  end;
  dfs(1,0,0);
  for i:=1 to m do
  begin
    readln(u);
    if b[u] then
    begin
      x:=s.findgreater(xu[u]).data;if x=maxlongint then x:=s.findgreater(-maxlongint).data;
      y:=s.findless(xu[u]).data;if y=-maxlongint then y:=s.findless(maxlongint).data;//找前驱和后继
      if x=maxlongint then x:=0 else x:=dian[x];
      if y=-maxlongint then y:=0 else y:=dian[y];
      ans:=ans-lca(x,u)-lca(y,u)+lca(x,y);//更新答案
      s.delete(xu[u]);b[u]:=false;dec(cnt1);
    end else
    begin
      x:=s.findgreaterEqual(xu[u]).data;if x=maxlongint then x:=s.findgreater(-maxlongint).data;
      y:=s.findlessEqual(xu[u]).data;if y=-maxlongint then y:=s.findless(maxlongint).data;//同上
      if x=maxlongint then x:=0 else x:=dian[x];
      if y=-maxlongint then y:=0 else y:=dian[y];
      ans:=ans+lca(x,u)+lca(y,u)-lca(x,y);//同上
      s.insert(xu[u]);b[u]:=true;inc(cnt1);
    end;
    writeln(ans);
  end;
end.


```
