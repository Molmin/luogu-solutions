数据很弱，又因为我比较懒，就写了个$\mathcal{O(n^3)}$的$\text{floyed}$，虽然比二分答案和最小生成树慢，但也是可以轻松AC的

------------

### 先贴上AC代码：

```
var
	x,y:array[0..60] of longint;
	s:array[0..60,0..60] of int64;
	n,i,j,k,max,t:longint;
function sum(a,b:longint):int64;
begin
	sum:=abs(x[a]-x[b])+abs(y[a]-y[b]);
end;
begin
	readln(n);
	for i:=1 to n do read(x[i],y[i]);
	for i:=1 to n do
		for j:=1 to n do s[i,j]:=(sum(i,j)+1) div 2; 
	for k:=1 to n do
		for i:=1 to n do
			for j:=1 to n do
				if (i<>j) and (j<>k) and (i<>k) then
				begin
					if s[i,k]>s[k,j] then t:=s[i,k] else t:=s[k,j];
					if t<s[i,j] then s[i,j]:=t;
				end;
	max:=0;
	for i:=1 to n do
		for j:=1 to n do
			if s[i,j]>max then max:=s[i,j];
	writeln(max);
end.
```

--------

### 代码很简单，但是还是有一些需要注意的地方：

1. $\text{sum}$函数计算的是两点间的曼哈顿距离。
	
    简单介绍一下：在平面直角坐标系中，设两点坐标分别为$(x_a,y_a)$和$(x_b,y_b)$，那么这两点间的曼哈顿距离就是$|x_a-x_b|+|y_a-y_b|$。
    
    其实做这道题并不需要了解关于曼哈顿距离的知识，只要注意到**两点之间按直线扩散的方式缩短距离**，就可以理解这一点了。

2. 为什么开$\text{int64(long long)}$ ？

	题目说$1 \leq x_i,y_i \leq 10^9$，所以两点间的距离最大可以到约$\text{2e9}$，计算最短路时，两点距离相加就可以爆$\text{longint(int)}$了。
    
    有没有这样的数据呢？我不知道qwq

3.  $\text{(sum(i,j)+1) div 2}$表示什么？（$\text{Pascal}$中的$\text{div}$同$\text{C++}$中的整除）

	很容易看出，两点间距离的减少速度是点扩散速度的**2倍**（因为两个点都在扩散），那么此处本来应该写成这样：
    
    ```
    	// 这个是伪代码
        if (sum(i,j)为奇数) 
        	两点间距离为sum(i,j)/2+1;
        else
        	两点间距离为sum(i,j)/2;
        // '/'表示整除
    ```
    
    让$\text{sum(i,j)}$分别取奇数和偶数，分别代入$\text{(sum(i,j)+1) div 2}$和伪代码，就会发现它们是等价的。所以这处代码没有问题。
    
4. 似乎和普通的$\text{floyed}$不太一样？

	因题而异：中转点$\text{k}$在**同时**往两边扩散，所以总时间不是两段时间的和，而是它们中的较大值。
    
-----------------

本人语文不是很好，如有语病欢迎指出qwq