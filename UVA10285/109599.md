题意：给出N * N的矩阵，要求找到一条路  径，路径上的值是递减的，求这样的路径的最长长度。

思路：记忆化搜索。因为要求最长的路径那么就需要将所有的这样的路径找出，但是直接dfs会超时的。对于同一个位置，从这个点出发的最长路径长度是固定的。所以在找的时候就要将这个位置的最长路径算出来并且保存下来。一开始还担心会走回头路就是一直兜圈子。但是后面发现能往右走就不可能在从右边的那个位置走回去，因为路径上的值是要求递减的。

    f[i,j] 代表从x，y这个位置开能够走的最长的路径长度。

一道非常经典的记忆化搜索题

上代码

```pascal
const e:array[1..4,1..2] of longint=((-1,0),(1,0),(0,-1),(0,1));//定义一个指标数组分别为上下左右
var a,f:array[1..100,1..100] of longint;
s:string;
max,i,j,k,n,m,t,l:longint;
function sc(x,y:longint):longint;
var i,x1,y1,s:longint;
begin
 if f[x,y]<>0 then exit(f[x,y]);  //记忆化搜索，如果搜过了就直接exit
 s:=1;  //s的初始值要为1，如果高度一样则只有一条跑道
 for i:=1 to 4 do
  begin
   x1:=x+e[i,1];y1:=y+e[i,2];
   if (x1>=1)and(x1<=n)and(y1>=1)and(y1<=m)and(a[x1,y1]<a[x,y]) then  //判断边界条件，而且只能往低的地方滑
     if sc(x1,y1)+1>s then s:=sc(x1,y1)+1;  //往下搜索，找最优解
  end;
 f[x,y]:=s;
 exit(s);
end;
begin
 readln(t);
 for l:=1 to t do
  begin
   max:=0;
   fillchar(f,sizeof(f),0);     
   readln(s);  //c++可以直接用%s法读入
   k:=pos(' ',s);  //开始了坑爹的字符串处理，作为P党的忧伤
   write(copy(s,1,k-1),': ');
   delete(s,1,k);
   k:=pos(' ',s);
   val(copy(s,1,k-1),n);
   delete(s,1,k);
   val(copy(s,1,length(s)),m);
   for i:=1 to n do
    begin
     for j:=1 to m do read(a[i,j]);
     readln;
    end;
    for i:=1 to n do
     for j:=1 to m do
      if f[i,j]=0 then
       begin
        if sc(i,j)>max then max:=sc(i,j);  //记录最优解
        f[i,j]:=sc(i,j);
       end;
    writeln(max);
  end;
end.
```