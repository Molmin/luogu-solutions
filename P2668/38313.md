psacal好久没见（来自蒟蒻的解析），尽管大家思想都差不多，但我觉得我的代码更美观和容易懂（...我觉得）；

思想都是搜索回溯，那么怎么搜索回溯呢？


楼下等人都是说先什么再什么，我认为是先顺子，再找带【盲区：顺子并不是越长越好；举个例子：（34567，88899），这里原只用两次就能打完，但如果按（345678，88，99）来打的话，就会打三次】，并且顺子条件比较苛刻，而带比较方便（单牌，对子这种直接最后加个数就好）


带方便？方便在什么地方才导致我要先进行顺子呢？——带不分大小、顺序，只要有三个，有四个，带什么都没关系，包括王哦，于是乎，打完顺子之后剩下的牌就好打了，按四带优先后三带（自己也可以思考一下为什么四带先一定比三带优）


下面是我的code，有必要进行一下说明：我的dfs过程大抵分为两部分，一个是找顺子，一个是找带，其他必要说明code旁会有





```cpp
program landlord;
const  maxn1=15;
var
        sum:array[1..maxn1]of longint;//记录各种牌有多少张
        i,j,n,t,a,b,ans:longint;
function min(x,y:longint):longint;
begin
        if x<y then exit(x) else exit(y);
end;
procedure dfs(p:longint);//深度优先搜索
var
        s1,s2,s3,s4,i,j:longint;//（不用管带的是谁，才这样设）
 //s1：单张牌的数量；s2：对子的对数；s3：用三带某某打出去的套牌套数；s4：同s3，换为四带某某；
begin
        if p>ans then exit;//无脑但实用无比的剪枝，烧掉多少循环；
        s1:=0;s2:=0;s3:=0;s4:=0;//切记
        for i:=1 to 14 do if sum[i]=1 then inc(s1);//单牌
        for i:=1 to 14 do if sum[i]=2 then inc(s2);//对子
        for i:=1 to 14 do
        if sum[i]=4 then//四带
        begin
                inc(s4);//无论后面如何带，四张一定打出去（四张，四带两单，四带两对，四带一对【两张单牌，肯定可以一样啦】）
                if (s1>=2)then s1:=s1-2//尽量从单张打出去，因为对子远比单张容易打
                else if s2>=2 then s2:=s2-2//遵循原则，能多则多
                else if s2>=1 then dec(s2);
        end;
        for i:=1 to 14 do
        if sum[i]=3 then//这里三带，解释与四带相似，不多说
        begin
                inc(s3);
                if (s1>=1)then dec(s1)
                else if s2>=1 then dec(s2);
        end;
        ans:=min(ans,p+s1+s2+s3+s4);
//在这里统计，万一后面没顺子，防止没有答案的尴尬（有人说，dalao，你骗人，不是先顺子再带吗？）：。。。其实没什么关系，只要决策权在于顺子就行了，因为下面顺子改变了sum数组（主程序会说明sum数组的存储）
        for i:=1 to 8 do
        begin
                for j:=i to 12 do
                begin
                        dec(sum[j]);
                        if sum[j]<0 then break;
                        if j-i>=4 then dfs(p+1);//单顺子至少5张
                end;
                while j>=i do//回溯
                begin
                        inc(sum[j]);
                        dec(j);
                end;
        end;
        for i:=1 to 10 do
        begin
                for j:=i to 12 do
                begin
                        sum[j]:=sum[j]-2;
                        if sum[j]<0 then break;
                        if j-i>=2 then dfs(p+1);//双顺子至少三对
                end;
                while j>=i do//继续回溯
                begin
                        inc(sum[j],2);
                        dec(j);
                end;
        end;
        for i:=1 to 11 do
        begin
                for j:=i to 12 do
                begin
                        sum[j]:=sum[j]-3;
                        if sum[j]<0 then break;
                        if j>i then dfs(p+1);//有两对就好了
                end;
                while j>=i do//怎么还是回溯
                begin
                        inc(sum[j],3);
                        dec(j);
                end;
        end;
end;
//上面三个顺子都会循环到，顺序就没什么关系，下面主程序：
begin
        //assign(input,'landlord.in'); reset(input);
        //assign(output,'landlord.out'); rewrite(output);
        read(t,n);
        for i:=1 to t do
        begin
                ans:=23;//最多23张牌，全出单张也就23次，其实弄个maxlongint 也没什么关系（随便啦）
                fillchar(sum,sizeof(sum),0);//切记
                for j:=1 to n do
                begin
                        read(a,b);//花色懵逼，一边去，和题目半毛钱关系没有
                        if a=0 then inc(sum[14]);//王，上面不用担心对子会带入王（可以的）
                        if a=1 then inc(sum[12]);//A放12，在K：11后面，顺子舒服一点
                        if a=2 then inc(sum[13]);//2放这里
                        if a>=3 then inc(sum[a-2]);//挤开A,2，都往前了
                end;
                dfs(0);//初始没打牌，p步数为0
                writeln(ans);//输出》~《
        end;
        //close(input);close(output);
end.
===============================================================
```
发牢骚专用分割线，先流几滴眼泪（。。》^《。。），做了我两天的题目，一开始写了出来，还没这么好看（哇咔咔），结果全wa，冥思苦想了好久，看了题解中的dalao所言，似乎和我的思想如出一辙，但为啥呢，我又把第二组数据测了一下，无论如何结果是5，。。。模拟了一下，是6啊，毛病呢？。。。。。。结果，突然想了一下。。。。。。我的局部变量定义额，忘记加j了，一开始我觉得没什么问题，现在我也还没懂。。。。但吃一堑长一智，下次一定记得好吧。（好长，快追上题解了）
快noip2017了，大家加油（来自——即将退役的选手）
