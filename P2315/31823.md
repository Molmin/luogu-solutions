统计有多少个三角形


枚举底边所在的直线


对于一个三角形，它的条件是底边是实线，左右两边要比底边长


我们先预处理出exl和exr，分别表示往左上最多延伸多长，往右上最多延伸多长


对于底边两点i和j，i<j


它需要满足的条件是（实线我们可以直接一段一段的处理）



j-i<=exr[i]，j-i<=exl[j]

所以j<=exr[i]+i，j-exl[j]<=i


然后我们先把j-exl[j]排一个序，从小到大枚举i，加入j-exl[j]<=i的点（即a[j]++），注意要把j<=i的减掉


a[i]数组用树状数组维护，再统计a数组中[1..exr[i]+i]的和就可以了


 

一开始不知道，所以我把每一个j看成一个平面上的点，坐标为（j,j-exl[j]），枚举i，统计横坐标小于等于exr[i]+i且纵坐标小于等于i的点


用二维树状数组维护这个和，然后过了

 
 
 
 
 
 
 
 
 
```cpp
const
    maxn=1010;
var
    tri:array[0..maxn,0..maxn,1..3]of integer;
    exl,exr,c:array[0..maxn,0..maxn]of integer;
    n,ans:longint;
function max(x,y:longint):longint;
begin
        if x>y then exit(x);
        exit(y);
end;
function min(x,y:longint):longint;
begin
        if x<y then exit(x);
        exit(y);
end;
procedure init;
var
    i,j:integer;
begin
    read(n);
    for i:=1 to n do
      for j:=1 to i do
        read(tri[i,j,1],tri[i,j,2],tri[i,j,3]);
end;
function lowbit(x:longint):longint;
begin
    exit(x and -x);
end;
procedure add(x,y,z:longint);
var
    h:longint;
begin
    while x<=n+1 do
      begin
        h:=y;
        while h<=n+1 do
          begin
            inc(c[x,h],z);
            h:=h+lowbit(h);
          end;
        x:=x+lowbit(x);
      end;
end;
function sum(x,y:longint):longint;
var
    h:longint;
begin
    sum:=0;
    while x>0 do
      begin
        h:=y;
        while h>0 do
          begin
            inc(sum,c[x,h]);
            h:=h-lowbit(h);
          end;
        x:=x-lowbit(x);
      end;
end;
procedure get;
var
    h,l,r,i:longint;
begin
    for h:=1 to n do
      begin
        l:=1;
        while l<=h do
          begin
            while (tri[h,l,3]=0)and(l<=h) do
              inc(l);
            if l>h then break;
            r:=l+1;
            while tri[h,r,3]=1 do
              inc(r);
            for i:=l to r do
              add(i,max(1,i-exl[h,i]),1);
            for i:=l to r do
              begin
                add(i,max(1,i-exl[h,i]),-1);
                inc(ans,sum(min(n+1,exr[h,i]+i),i));
              end;
            l:=r+1;
          end;
      end;
end;
procedure work;
var
    i,j:longint;
begin
    for i:=1 to n do
      for j:=1 to i do
        begin
          if tri[i,j,1]=1 then exr[i,j]:=exr[i-1,j]+1;
          if tri[i,j,2]=1 then exl[i,j+1]:=exl[i-1,j]+1;
        end;
    get;
    for i:=1 to n do
      for j:=1 to i+1 do
        begin
          exl[i,j]:=0;
          exr[i,j]:=0;
        end;
    for i:=n downto 1 do
      for j:=1 to i do
        begin
          if tri[i,j,1]=1 then exl[i-1,j]:=exl[i,j]+1;
          if tri[i,j,2]=1 then exr[i-1,j]:=exr[i,j+1]+1;
        end;
    get;
    write(ans);
end;
begin
    init;
    work;
end.

```