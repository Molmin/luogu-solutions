此题其实可以不用断环为链。（为了这个爆零TAT）

下面贴良心AC代码：

```pascal
var
  a:array[1..100] of longint;
  f,w:array[1..100,1..100] of longint;
  n,i,j,k,x,s:longint;
  //a数组存储原重量，w[i，j]表示从i开始的j堆的总重量，f[i，j]表示从i开始合并j堆的最小（最大）得分
function max(a,b:longint):longint;
  begin
    if a>b then exit(a)
    else exit(b)
  end;
function min(a,b:longint):longint;
  begin
    if a<b then exit(a)
    else exit(b)
  end;
  //max，min函数（自己动手丰衣足食）
begin
  readln(n);
  for i:=1 to n do read(a[i]);  //读入
  for i:=1 to n do begin
    w[i,1]:=a[i];
    for j:=2 to n do w[i,j]:=w[i,j-1]+a[(i+j-2) mod n+1]  //w数组初始化
  end;
  fillchar(f,sizeof(f),0);
  for j:=2 to n do
    for i:=1 to n do begin
      f[i,j]:=maxlongint;
      for k:=1 to j-1 do begin
        x:=(i+k-1) mod n+1;
        f[i,j]:=min(f[i,j],f[i,k]+f[x,j-k]+w[i,j])  //计算最小值（状态转移方程楼下许多大神已经讲过了，我就不讲了，自己理解）
      end;
    end;
  s:=maxlongint;
  for i:=1 to n do s:=min(s,f[i,n]);
  writeln(s);  //输出最小值
  fillchar(f,sizeof(f),0);
  for j:=2 to n do
    for i:=1 to n do begin
      f[i,j]:=-maxlongint;
      for k:=1 to j-1 do begin
        x:=(i+k-1) mod n+1;
        f[i,j]:=max(f[i,j],f[i,k]+f[x,j-k]+w[i,j])  //计算最大值（原理同上）
      end;
    end;
  s:=-maxlongint;
  for i:=1 to n do s:=max(s,f[i,n]);
  write(s)  //输出最大值
end.
```
谢绝Ctrl+c，营造美好洛谷！
