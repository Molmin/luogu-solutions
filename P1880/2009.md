环变成线性:长度: 2n-1

a[1],a[2]......a[n],a[n+1].....a[2n-1];  a[n+i]=a[i]

f[i,j]:合并i到j堆的最小得分。


f[i,j]=min{f[i,k]+f[k+1,j]}+s[j]-s[i-1] .                          (i<=k<=j-1)

目标：ans=min{f[1,n],f[2,n+1],...,f[n,2n-1]}




```delphi

var fmin,fmax:array[1..199,1..199]of longint;
    a:array[1..199]of longint;
    s:array[0..199]of longint;
    n,i,j,amin,amax:longint;

function min(l,r:longint):longint;
var m,k:longint;
begin
    m:=fmin[l,l]+fmin[l+1,r];
    for k:=l+1 to r-1 do
    if m>fmin[l,k]+fmin[k+1,r] then m:=fmin[l,k]+fmin[k+1,r];
    exit(m);
end;

function max(l,r:longint):longint;
var m,k:longint;
begin
    m:=fmax[l,l]+fmax[l+1,r];
    for k:=l+1 to r-1 do
    if m<fmax[l,k]+fmax[k+1,r] then m:=fmax[l,k]+fmax[k+1,r];
    exit(m);
end;

begin
    readln(n);
    s[0]:=0;
    for i:=1 to 2*n-1 do
    if i<=n then
    begin
        fmin[i,i]:=0;
        fmax[i,i]:=0;
        read(a[i]);
        s[i]:=s[i-1]+a[i];
        for j:=i-1 downto 1 do
        begin
            fmin[j,i]:=min(j,i)-s[j-1]+s[i];
            fmax[j,i]:=max(j,i)-s[j-1]+s[i];
        end;
        if i=n then
        begin
            amin:=fmin[1,n];
            amax:=fmax[1,n];
        end;
    end
    else
    begin
        fmin[i,i]:=0;
        a[i]:=a[i-n];
        s[i]:=s[i-1]+a[i];
        for j:=i-1 downto 1 do
        begin
            fmin[j,i]:=min(j,i)-s[j-1]+s[i];
            fmax[j,i]:=max(j,i)-s[j-1]+s[i];
        end;
        if amin>fmin[i-n+1,i] then amin:=fmin[i-n+1,i];
        if amax<fmax[i-n+1,i] then amax:=fmax[i-n+1,i];
    end;
    writeln(amin);
    writeln(amax);
end.

time  O(n^3)}

```