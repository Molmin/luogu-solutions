#目测是当前跑的最快的代码，4ms

#数据有点水啊，emmmm，楼下许多贪心的做法时间复杂度应该是O(nm)，但是居然过了。。。

一道贪心题。

先按区间的右端点排序，然后从左往右做。

对于第i个区间，如果区间内的树的棵数达到要求则不用种了；

如果没有达到，则从后往前种树（前i-1个区间的问题我们都解决了，而前i-1个区间应该是在第i个区间前面，所以我们种的树应该要服务的是当前区间和后面的区间）

然后，对于判断区间内的树的棵数是否达到要求我们可以用一个树状数组维护。

丑陋的代码：

```cpp
var
  l,r,need:array[0..5001]of longint;
  c:array[0..30001]of longint;
  b:array[0..30001]of boolean;
  n,m,i,j,k,x,y,ans:longint;
procedure zz(i,j:longint);
var
  t:longint;
begin
  t:=need[i];need[i]:=need[j];need[j]:=t;
  t:=l[i];l[i]:=l[j];l[j]:=t;
  t:=r[i];r[i]:=r[j];r[j]:=t;
end;
procedure kp(ll,rr:longint);
var
  i,j,mid,midd:longint;
  begin
    i:=ll;
    j:=rr;
    mid:=r[(ll+rr) >> 1];
    repeat
      while (mid>r[i]) do inc(i);
      while (mid<r[j]) do dec(j);
      if i<=j then 
        begin
          zz(i,j);
          inc(i);
          dec(j);
        end;
    until i>j;
    if i<rr then kp(i,rr);
    if ll<j then kp(ll,j);
  end;
procedure swap(var x,y:longint);
var
  t:longint;
  begin
    t:=x;
    x:=y;
    y:=t;
  end;
function lowbit(x:longint):longint;
begin
  exit(x and (-x));
end;
procedure add(x:longint);
begin
  while x<=m do 
    begin
      inc(c[x]);
      x:=x+lowbit(x);
    end;
end;
function sum(x:longint):longint;
begin 
  sum:=0;
  while x>0 do
    begin
      inc(sum,c[x]);
      x:=x-lowbit(x);
    end; 
end;
begin
  readln(m);
  readln(n);
  for i:=1 to n do read(l[i],r[i],need[i]);
  kp(1,n);//排序
  for i:=1 to n do 
    begin
      x:=sum(r[i]);
      y:=sum(l[i]-1);
      if x-y<need[i] then //利用了前缀和的思想 
        begin
          k:=need[i]-(x-y);//还需要种几棵树
          for j:=r[i] downto l[i] do 
            if not b[j] then //用b数组标记第i个位置是否种树 
              begin
                dec(k);
                b[j]:=true;
                add(j);
                if k=0 then break; 
              end; //从后往前种树
        end;
    end;
  writeln(sum(m));//输出。。。
end.
```