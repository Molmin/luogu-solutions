# 这是一篇诡异的题解！慎入！（P党的福利）

SY表示他脑子已经非常乱了，他看了看题解发现是状压。但他不喜欢状压，因为想转移方程太讨厌了……所以他看到这道题时就想打个随机水过去……毕竟N≤18，随机到正确答案概率为1/18，还是可以接受的

等等，随机？SY突然发现N≤18，那我们可不可以试试看某些诡异的随机算法水过去呢？


# 模拟退火！

N≤18这种数据虽然还算比较小的，但是对于模拟退火这种人品算法还是很悬的……SY表示他随机了N遍才把这道题水过去了……而且SY表示下次拿这个程序交的时候说不定还要WA掉。


ZX：这算法看起来很厉害的样子……会不会学不会

SY：我们抛开这个听起来非常厉害的名字，实际上他就是一个随机骗分算法……

ZX：随机？既然是分组，那我们随机分组情况吧！

SY：不行，经过多次试验发现，这样不仅时间很慢，而且因此正确率也不高。我们需要想一些更优秀的随机方法。


我们可以每次交换当前序列中的两个元素！

例如我们当前的排列是1 3 2 4，经过随机交换两个元素，我们得到的新的序列可能是1 2 3 4，或者是1 4 2 3……有什么用呢？

我们要定义一种计算方法。我们用一种贪心的思想，定义一个序列a的答案cnt的计算方法如下：（w表示不能超过的体积）
1. cnt置0，当前和sum置0
2. i从1~n循环。如果sum+a[i]>w，则答案+1，sum置0，否则不进行操作。然后sum加上a[i]。
3. 如果sum>0，那么cnt+1。

例如样例中我们随机到一个排列为3,6,5,7,w=10。

i=1时，sum=3,cnt=0

i=2时，sum=9,cnt=0

i=3时，sum=5,cnt=1

i=4时，sum=7,cnt=2

最后发现sum=7>0，所以cnt=3，也就是这样的计算方式下这个序列的答案为3。

显然这样的贪心策略是错误的，不然本题就可以O(N)轻轻松松水过。但是，在模拟退火中，这是对的。原因是模拟退火可能会把所有排列都做出来，而正确的排列在这种贪心策略下显然是可以得出答案的。所以只要随机次数过多，就很有可能得到正确答案。


# 接下来就是重头戏，模拟退火的做法了！
模拟退火是一种模拟金属冷却的过程blabla之类的话我们不喜欢听，因为我们听不懂，我们只需要知道怎么做就行了。

我们现在有一种求答案的策略。每次随机生成一个新的排列（方法即为随机交换两个数）。如果这个排列在刚刚那种计算方式下更优，就用这个序列继续做，否则序列不变。

这种方法叫爬山算法。但是本题中不适合用爬山算法，因为一个更优解可能需要从当前解经过一个更劣解才能得到，而爬山算法只能往更优解去做。而模拟退火就能摆脱这个限制，能经过较劣解进而得到更优解。

模拟退火的做法是，如果当前解更优，就选择这个解。否则，以一定概率接受这一个更劣解，并且随着时间推进这个概率逐渐降低。也就是说，答案最终会渐渐稳定下来。而这个概率的计算方法为exp(P)，也就是e的P次方，其中P是参数，一般控制P<0。P一般由两个量决定，一个是当前的温度Temp，一个是上一个答案距离当前答案的差值delta，则P=delta/Temp（如果delta为正则取相反数进行计算）。然后将exp(P)与一个随机量r（0<r<1）进行比较，以此确定是否接受更劣解。

```pascal
function accept(temp,delta:extended):boolean;
var rand:extended;
begin
    if delta>=0 then exit(true);
    rand:=random(2348284)/2348284;
    if rand<exp(delta/temp) then exit(true) else
        exit(false);
end;
```
以上代码的意思是：如果delta>=0（就是说上一个答案计算出来比现在这个答案大或相等，也就是说当前这个解不会更劣），就接受这个解，返回true，否则，在0~1中随机一个数rand，如果rand<exp(delta/temp)，就接受它，返回true，否则不接受，返回false。

ZX：等等，你刚刚说随着时间推进，接受更劣解的概率会越来越低，这点如何体现？

SY：我们来分析一下。显然，我们很早就说过这是一个模拟金属冷却的过程，所以temp会随着时间推进逐渐降低。当delta为常数且为负时，temp降低，delta/temp将会减小（例如，-1/3>-1/2）。由于exp是一个幂函数且e>1，所以exp是个单调递增的函数。P=delta/temp减小时，exp(P)也会跟着减小，因此rand<exp(delta/temp)的概率将会更小。

ZX：知道如何接受较劣解了，然后怎么做呢？

SY：然后就很简单了。你可以先设定一个初温temp（例如1e40），设一个降温系数delt（一般设为0.9,0.99,0.999之类的），以及一个终止温度endtemp（例如1e-60）。当temp>endtemp的时候进行如下操作：
1. 记上一次的答案为lastans，按照之前的算法随机一个新解，新解的答案为nowans，并判断是否接受这个新解（delta=lastans-nowans）。如果不接受，则还原。
2. 记录一个全局最优解best，如果nowans<best，则best=nowans
3. 降温，即temp=temp×delta; 

最终输出best即可。
如果你对这种算法的正确率不放心，你可以进行2、3次甚至更多次的模拟退火。

详见代码咯（不知道我有没有讲清楚……）
```pascal
（我怎么会告诉你我开了O2呢）
const
    endtemp=1e-60;
    delt=0.9999;
var n,w,i,lastans,best,x,y,nowans,j,max,maxt:longint;
sum,pos,recsum,recpos,a:array[0..19] of longint;
vis:array[0..19] of boolean;
flag:boolean;
temp:extended=1e40;

function accept(temp,delta:extended):boolean;
var rand:extended;
begin
    if delta>=0 then exit(true);
    rand:=random(2348284)/2348284;
    if rand<exp(delta/temp) then exit(true) else
        exit(false);
end;//判断是否接受新解

function calc:longint;
var cnt,i:longint;
begin
    cnt:=0;calc:=0;
    for i:=1 to n do
        begin
            if (cnt+a[i]>w) then
                begin
                    inc(calc);
                    cnt:=0;
                end;
            cnt:=cnt+a[i];
        end;
    if cnt>0 then inc(calc);
end;//计算当前这种排列的答案

procedure swap(i,j:longint);
var t:longint;
begin
    t:=a[i];a[i]:=a[j];a[j]:=t;
end;

begin
    randomize;
    readln(n,w);
    for i:=1 to n do
        read(a[i]);
    lastans:=calc;best:=lastans;//初始状态即初始解
    while temp>endtemp do
        begin
            x:=random(n)+1;
            y:=x;
            while y=x do y:=random(n)+1;
            swap(x,y);//随机一个新解
            nowans:=calc;//计算新解的答案
            if accept(temp,lastans-nowans) then
                lastans:=nowans else
                    swap(x,y);//接受新解则转移，否则变成原来的状态
            if nowans<best then best:=nowans;
            temp:=temp*delt;//记录全局答案并降温
        end;
    temp:=1e40;//恬不知耻的进行多次模拟退火提升正确率
    while temp>endtemp do
        begin
            x:=random(n)+1;
            y:=x;
            while y=x do y:=random(n)+1;
            swap(x,y);
            nowans:=calc;
            if accept(temp,lastans-nowans) then
                lastans:=nowans else
                    swap(x,y);
            if nowans<best then best:=nowans;
            temp:=temp*delt;
        end;
    temp:=1e40;
    while temp>endtemp do
        begin
            x:=random(n)+1;
            y:=x;
            while y=x do y:=random(n)+1;
            swap(x,y);
            nowans:=calc;
            if accept(temp,lastans-nowans) then
                lastans:=nowans else
                    swap(x,y);
            if nowans<best then best:=nowans;
            temp:=temp*delt;
        end;
    writeln(best);
end.






```



