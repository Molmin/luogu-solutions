发一波Codeforces的Pascal题解~~~

又是大数据，显然$O(n)$。

首先我们先算出开始黑白块的数量。黑白各一半，因为左下角为白色，所以如果有奇数块，白色还要+1。

然后第一次染色。把一个矩形涂成黑色。不难处理，把这块矩形里的白色块染成黑色就行了，原来的黑色块不用管。

这里要注意怎么计算白色块的数量：照例是平分。但是如果有奇数块的话要判断了，如果左下角是白色块，白色块比黑色块多，那么白色块+1，否则就不需要了。

判断一个某一块是白是黑不难，算$(x+y) MOD 2$，是$1$就是白色，是$0$就是黑色。

第二次染色同第一次。

但是有特殊情况：两个矩形重叠了，那么重叠的部分还要进行一些判断。

这里给出计算两个矩形的办法：$(x1,y1,x2,y2)\cap (x3,y3,x4,y4)=(max(x1,x3),max(y1,y3),min(x2,x4),min(y2,y4))$

明显当$max(x1,x3)>min(x2,x4)$或$max(y1,y3)>min(y2,y4)$时没有真正的重叠部分。

那么有重叠部分处理也不难。

重叠部分的这一块，白色因为已经被染成了黑色，但后来又染成了白色，应该是要计算的，但是漏了。所以实际上要把这部分白色变成黑色。

所以代码就来了：

```pas
var
  t,i,n,m,x1,x2,x3,x4,y1,y2,y3,y4,x5,y5,x6,y6:longint;
  s1,s2,t1,t2,t3,t4:int64;
function max(x,y:longint):longint;//没什么好说的
begin
  if x>y then
    exit(x)
  else
    exit(y);
end;
function min(x,y:longint):longint;
begin
  if x<y then
    exit(x)
  else
    exit(y);
end;
begin
  read(t);
  for i:=1 to t do
  begin
    read(n,m,x1,y1,x2,y2,x3,y3,x4,y4);
    x5:=max(x1,x3);            //这里处理一下重叠的部分
    y5:=max(y1,y3);
    x6:=min(x2,x4);
    y6:=min(y2,y4);
    t1:=int64(n)*m;            //棋盘有几块
    t2:=int64(x2-x1+1)*(y2-y1+1);//涂黑的有几块
    t3:=int64(x4-x3+1)*(y4-y3+1);//涂白的有几块
    s1:=t1 div 2+t1 mod 2;     //开始棋盘上的白块
    s2:=t1 div 2;              //开始棋盘上的黑块
    inc(s1,t2 div 2);          //这里处理涂黑
    dec(s2,t2 div 2);
    if (x1+y1) mod 2=1 then    //如果左下角是白块特殊处理
    begin
      inc(s1,t2 mod 2);
      dec(s2,t2 mod 2);
    end;
    dec(s1,t3 div 2);          //这里处理涂白，跟上面差不多
    inc(s2,t3 div 2);
    if (x3+y3) mod 2=0 then    //如果左下角是黑块特殊处理
    begin
      dec(s1,t3 mod 2);
      inc(s2,t3 mod 2);
    end;
    if (x5<=x6) and (y5<=y6) then//如果有重叠部分
    begin
      t4:=int64(x6-x5+1)*(y6-y5+1);//重叠部分有几块
      dec(s1,t4 div 2);        //处理：把这里的白块变成黑块
      inc(s2,t4 div 2);
      if (x5+y5) mod 2=1 then
      begin
        dec(s1,t4 mod 2);
        inc(s2,t4 mod 2);
      end;
    end;
    writeln(s1,' ',s2);
  end;
end.
```