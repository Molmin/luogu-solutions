大质数判定，为什么不用**Miller_Rabin**算法呢？

首先，要使A/B是一个质数，前提条件是A中因子个数严格比B中的多1个（别忘了去除1）。

然后对剩下的那个数Miller_Rabin判定就好了。。。

Pascal Code：

```cpp
const prime:array[1..14]of longint=(2,3,5,7,11,13,17,19,37,43,67,73,97,101);
var
  a,b:array[0..200005]of int64;
  t,i,j,jn,jm,n,m:longint;
  x,s:int64;
procedure qsort1(l,r:longint);
var
  i,j,k:longint;
  t:int64;
begin
  i:=l;
  j:=r;
  k:=(i+j) div 2;
  t:=a[k];
  a[k]:=a[i];
  while i<j do
  begin
    while (i<j) and (a[j]>t) do
      dec(j);
    if i<j then
    begin
      a[i]:=a[j];
      inc(i);
    end;
    while (i<j) and (a[i]<t) do
      inc(i);
    if i<j then
    begin
      a[j]:=a[i];
      dec(j);
    end;
  end;
  a[i]:=t;
  if i-1>l then
    qsort1(l,i-1);
  if i+1<r then
    qsort1(i+1,r);
end;
procedure qsort2(l,r:longint);
var
  i,j,k:longint;
  t:int64;
begin
  i:=l;
  j:=r;
  k:=(i+j) div 2;
  t:=b[k];
  b[k]:=b[i];
  while i<j do
  begin
    while (i<j) and (b[j]>t) do
      dec(j);
    if i<j then
    begin
      b[i]:=b[j];
      inc(i);
    end;
    while (i<j) and (b[i]<t) do
      inc(i);
    if i<j then
    begin
      b[j]:=b[i];
      dec(j);
    end;
  end;
  b[i]:=t;
  if i-1>l then
    qsort2(l,i-1);
  if i+1<r then
    qsort2(i+1,r);
end;
function cf(n,m,p:int64):int64;//大整数乘+mod
var
  ans:int64;
begin
  ans:=0;
  while m<>0 do
  begin
    if m and 1<>0 then
      ans:=(ans+n) mod p;
    m:=m >> 1;
    n:=n << 1 mod p;
  end;
  exit(ans);
end;
function ksm(x,y,p:int64):int64;//快速幂
var
  zc:int64;
begin
  if y=1 then
    exit(x mod p);
  zc:=ksm(x,y div 2,p);
  if odd(y) then
    exit(cf(zc,zc,p) mod p*x mod p) else
    exit(cf(zc,zc,p) mod p);
end;
function Miller_Rabin(n:int64):boolean;//素数判定
var
  i,j,t:longint;
  now,res,u:int64;
begin
  Miller_Rabin:=true;
  if n=1 then
    exit(false);
  for i:=1 to 5 do
  begin
    if prime[i]=n then
      exit(true);
    if n mod prime[i]=0 then
      exit(false);
  end;
  u:=n-1;
  t:=0;
  while u mod 2=0 do
  begin
    u:=u >> 1;
    inc(t);
  end;
  for i:=1 to 5 do
  begin
    now:=ksm(prime[i],u,n);
    for j:=1 to t do
    begin
      res:=cf(now,now,n);
      if (res=1) and (now<>1) and (now<>n-1) then
        exit(false);
      now:=res;
    end;
    if now<>1 then
      exit(false);
  end;
end;
begin
  readln(t);
  for i:=1 to t do
  begin
    readln(jn,jm);
    n:=0;
    m:=0;
    for j:=1 to jn do
    begin
      read(x);
      if x<>1 then//去除1
      begin
        inc(n);
        a[n]:=x;
      end;
    end;
    readln;
    for j:=1 to jm do
    begin
      read(x);
      if x<>1 then//去除1
      begin
        inc(m);
        b[m]:=x;
      end;
    end;
    readln;
    if (n=0) or (n<>m+1) then
      writeln('NO') else
      begin
        qsort1(1,n);
        qsort2(1,m);
        s:=-1;
        for j:=1 to m do
        if a[j]<>b[j] then//找出数
        begin
          s:=a[j];
          break;
        end;
        if s=-1 then
          s:=a[n];
        if Miller_Rabin(s) then
          writeln('YES') else
          writeln('NO');
      end;
  end;
end.
```