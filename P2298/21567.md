发现没有pascal题解，水一发

一道很典型的广搜


```cpp
const
dx:array[1..4] of longint=(0,1,0,-1);
dy:array[1..4] of longint=(1,0,-1,0);
var
n,m,i,j,h,t,x,y,x1,y1,x2,y2:longint;
f:array[1..4000000] of longint;
a:array[1..2000,1..2000] of boolean;
ch:char;
b:array[1..4000000,1..2] of longint;
begin
  readln(n,m);
  for i:=1 to n do
  begin
    for j:=1 to m do
    begin
      read(ch);
      if ch='#' then a[i,j]:=false else a[i,j]:=true;//用布尔数组存该点能否走到
      if ch='d' then
      begin
        x2:=i;
        y2:=j;
      end;//储存男家丁位置
      if ch='m' then
      begin
        x1:=i;
        y1:=j;
      end;//储存Mzc位置
    end;
    readln;
  end;
  h:=0;
  t:=1;
  f[t]:=0;
  b[t,1]:=x1;
  b[t,2]:=y1;
  while h<=t do//广搜begin
  begin
    inc(h);
    for i:=1 to 4 do
    begin
      x:=b[h,1]+dx[i];
      y:=b[h,2]+dy[i];//四个方向搜索
      if (x>0) and (y>0) and (x<=n) and (y<=m) and a[x,y] then//判断能否走到和是否走到过
      begin
        a[x,y]:=false;//标记已走过
        inc(t);
        b[t,1]:=x;
        b[t,2]:=y;
        f[t]:=f[h]+1;//进队列
        if (x=x2) and (y=y2) then
        begin
          writeln(f[t]);
          halt;
        end;//到达目的地输出，跳出程序
      end;
    end;
  end;
  writeln('No Way!');//广搜结束后还没跳出程序即证明无法到达
end.
一遍AC！
```