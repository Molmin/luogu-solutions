没有Pascal的题解，我来发一个，算是P党福利

（话说Pascal过的人也就2个。。）

------分割线------

**题意**

一张图有若干个起点，问从每个起点访问到K(K>=T)个格子，且访问到的格子与起点高度最大差的最小值。

**输入、输出**

详见题面~~（话说做这种题的人难道还不会吗。。）~~

**算法**

我一开始看到这种最大值最小，第一想法是二分，而且这道题算法标签的确有二分，然后就光荣的TLE了，错了至少5遍（可能有大佬的确是二分做的，希望也能发一下题解拓展一下思路）。。

然后请教了楼下两位大佬，发现并查集才是正解啊。。

把相邻的点连边，权值为两点高度差的绝对值，然后按权值从小到大排序，最后跑一遍类最小生成树就可以了。

对于更新答案，我是参考巨型方块的，所以我这篇题解主要是对他们算法的补充说明，让更多像我一样的蒟蒻看懂（雾

所以我把代码分成好几个模块，一点点讲。


以下为代码：
```cpp
type _edge=record x,y,v:longint;end;
//定义记录类型，x，y为当前边连的两个点的代号，v为权值。
//至于代号是什么，下面说。
var
n,m,k,i,j,t,tx,ty,tot:longint;
ans:int64;      //int64勿忘！！
f,size,head,next,p:array[1..250000]of longint;
//f为并查集中的父亲数组，size为当前联通块的大小。
//head，next，p是数组模拟链表，下面讲。
d:array[1..250000]of int64;
//d[i]表示代号为i的点满足题意的最小值
a:array[1..500,1..500]of longint;//初始地图
edge:array[1..500000]of _edge;   //储存边
st:array[1..500,1..500]of boolean;//判断是否为起点
procedure sort(l,r:longint);//按边权从小到大排序，C++选手可以参考另两篇题解
  var i,j:longint;x,y:_edge;
  begin
    i:=l;j:=r;x:=edge[(l+r)>>1];//x>>1就相当于x div 2
    repeat
      while edge[i].v<x.v do inc(i);
      while x.v<edge[j].v do dec(j);
      if not(i>j) then
        begin
           y:=edge[i];edge[i]:=edge[j];edge[j]:=y;
           inc(i);dec(j);
        end;
    until i>j;
    if l<j then sort(l,j);
    if i<r then sort(i,r);
  end;
//以下为get函数重载
function get(x,y:longint):longint;//第一个get函数：获得点（x,y)的代号。
  begin exit((x-1)*m+y); end;
//其实就是for i:=1 to NxM时访问的序号
function get(k:longint):longint;//第二个get函数：获得k的父亲。
  begin
    if f[k]=0 then exit(k);
    //这和平时的并查集略有不同，如果f[k]=0说明k并没有接在别的点上
    //换句话说，当f[k]=0时，k就是当前联通块的根，别的都挂在k上面。
    f[k]:=get(f[k]);
    exit(f[k]);
  end;
procedure init();//初始化，读入，这没什么好讲的。。
  begin
    read(n,m,k);
    for i:=1 to n do
      for j:=1 to m do
        read(a[i,j]);
    for i:=1 to n do
      for j:=1 to m do
        begin
          read(t);
          if t=1 then st[i,j]:=true else st[i,j]:=false;//标记起点
        end;
  end;
procedure build();//根据读入的数据建边
//题目中说是4个方向，但其实我们建边时只需要与下方和右方连边即可，因为边是双向的
//例如对于(x,y)只需连(x+1,y)，(x,y+1)，因为在(x-1,y)，(x,y-1)时已经连过边了
  begin
    for i:=1 to n do 
      for j:=1 to m do
        begin
          if i<>n then//和下方的点建边，当i=n时，原图中不存在(i+1,j)
            begin
              inc(tot);//边数加一
              edge[tot].x:=get(i,j);edge[tot].y:=get(i+1,j);
              //左边点为(i,j)的代号，右边点为(i+1,j)的代号。
              edge[tot].v:=abs(a[i,j]-a[i+1,j]);
              //权值为a[i,j]与a[i+1,j]差的绝对值
            end;
          if j<>m then//和右方的点连边，原理同上
            begin
              inc(tot);
              edge[tot].x:=get(i,j);edge[tot].y:=get(i,j+1);
              edge[tot].v:=abs(a[i,j]-a[i,j+1]);
            end;
        end;
    sort(1,tot);//按权值排序
  end;
procedure add(x,y:longint);//把y这个联通块合并到x上
  begin
    f[y]:=x;//把x联通块的父亲赋给y联通块
    inc(size[x],size[y]);//把y联通块的大小加到x联通块里
    if head[y]>0 then
      begin
        if head[x]=0 then head[x]:=head[y];
        if head[x]>0 then p[next[x]]:=head[y];
        next[x]:=next[y];
      end;
    //head相当于链表的头指针，next相当于一个指针指向的下一个节点，p为指针指向的点的代号
  end;
procedure solve();//这才是真正开始处理问题，前面那么长的铺垫233
  begin
    //初始化
    for i:=1 to n do
      for j:=1 to m do
        begin
          t:=get(i,j);//获得(i,j)的代号
          size[t]:=1;//自己一个点，联通块大小为1
          head[t]:=t;//链表头指针指向自己
          next[t]:=t;//下一个指针也指向自己
          //注意，此时的p[t]仍然为0，相当于没有下一个点
        end;
    for i:=1 to tot do//类最小生成树开始，从小到大枚举
      begin
        tx:=get(edge[i].x);ty:=get(edge[i].y);
        if tx=ty then continue;//如果当前两个点在同一个联通块则跳过
        add(tx,ty);//如果当前两个点不在同一个联通块则合并
        if size[tx]>=k then//合并后的大联通块符合题目要求了
          begin
            t:=head[tx];//沿着链表向下查找
            while t>0 do//还有下一个点
              begin
                d[t]:=edge[i].v;
                //等级为当前边的权值，因为是从小到大排序的，第一个满足要求的即为最优。
                t:=p[t];//下一个点
              end;
            head[tx]:=0;next[tx]:=0;
            //把当前联通块整个删除，只需删除头指针和下一个指针，就相当于找不到了。
            //至于为什么要这样更新，详见巨型方块的题解
          end;
      end;
  end;
procedure print();//输出，也没什么好讲的
  begin
    for i:=1 to n do
      for j:=1 to m do
        if st[i,j] then
          inc(ans,d[get(i,j)]);//如果当前点为起点，则把等级累加到ans里
    write(ans);
  end;
//接下来是华丽的主程序233
begin
init();//读入
build();//建边
solve();//解决
print();//输出
end.
```