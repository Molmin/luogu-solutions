# 基数排序

本来我像比较字符串大小的(只需要先比较length和s[i]-s[j]就可以了),但是我还是没有那么做......


基数排序时间复杂度O(nq),也就是O(2000),很快。



## 如何排序？

先开一个10个位置的槽,下标为:0-9。每一次,我们把a[i]的第l位数字给a[i]分配位置:如1923,第一次的位置是(个位)place[3,o],第二次(十位)place[2,o],第三次place[9,o],第四次place[1,o]。当每一个数都分配完槽,将其[顺序]取出,覆盖a[i],在进行下一位的放槽。最后顺序出槽时,就是顺序的了。

![基数排序](http://pic.92to.com/2c/201609/29/201609060930471087.jpg)

所以我们要排q次(q为最大值的位数,如:max=123094,q=6),每一次放槽是n,取出覆盖是n。所以时间复杂度度2nq,也就是O(nq)。此排序只是内存耗的有点多(可以用内存来减少取位数的时间)。


[其实槽可以看作是栈啦...]


//---------------------------------以上是对基数排序的讲解,以下如何配用此题-----------------------------

## 进槽

上述的进槽是一个整数,但是这里却是一个字符串。所以我们分为3个进槽方式:


'0':直接给0槽。


个位数:给m槽,但是值为'0'


多位数:正常进槽。

## 巧用方法!

本题可以将a[i]变为s[i]string。排序时如上述一样,将每一位进槽出槽,最后将是最优解。(my基数排序得出来的是o[i],意思是原数组中的位置,而不是一个排好的数组!所以输出时也可以巧用!)



# 代码:

```pascal
var
        i,j,n,m,k,l:longint;
        place:array[0..9,1..20] of string;
        num,light:array[1..20] of string;
        op:array[0..9,1..20] of longint;
        na:array[0..9] of longint;
        o:array[1..20] of longint;
begin
        readln(n);
        for i:=1 to n do
        begin
                readln(num[i]);//票数
                light[i]:=num[i];//最后输出的票数
                k:=length(num[i]);
                o[i]:=i;//在数组中的位置
                if k>l then
                        l:=k;//q,也就是最大值的位数
        end;

        for i:=1 to l do//开始排序
        begin
                for j:=1 to n do//近槽
                begin
                        if num[j]='0' then//第一种情况'0'
                        begin
                                inc(na[0]);//给'0'槽
                                place[0,na[0]]:=num[j];
                                op[0,na[0]]:=o[j];
                        end
                        else
                        begin
                                k:=length(num[j]);
                                if k=1 then//个位数
                                begin
                                        val(num[j,1],m);
                                        inc(na[m]);//槽多了一个数
                                        place[m,na[m]]:='0';//进槽,值和下表都放进去
                                        op[m,na[m]]:=o[j];
                                end
                                else
                                begin
                                        val(num[j,k],m);//同上
                                        delete(num[j],k,1);
                                        inc(na[m]);
                                        place[m,na[m]]:=num[j];
                                        op[m,na[m]]:=o[j];
                                end;
                        end;
                end;

                m:=0;

                for j:=0 to 9 do//出槽
                begin
                        for k:=1 to na[j] do
                        begin
                                inc(m);//分配给原本的数组
                                num[m]:=place[j,k];
                                o[m]:=op[j,k];
                                {elete(place[j,k],1,length(place[j,k]));
                                op[j,k]:=0;}//清空槽,次句可以省略,因为进槽时会覆盖...
                        end;
                        na[j]:=0;//槽以清空
                end;
        end;

        writeln(o[n]);//最后一个
        writeln(light[o[n]]);//最大票数
end.

```