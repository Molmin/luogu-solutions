> ## 静态主席树做法

### 思路

此题解的先决是第$K$大(小)问题,不会主席树的请自行翻阅博客。

首先我们可以把这道题转化为**区间不同数数量**。然后我们可以想到,询问区间$l,r,$如果一个数字$num_i$的$last_i>=l$,就是说$num_i$是重复的。($last_i$代表一个数字$num_j=num_i,j<i$且$j$离$i$最近。通俗的说就是前面一个与自己相同的数字。实现最好用桶,数字不大)这个很好理解,就是在区间内有与自己相同的数字。

那么我们可以这样子:维护一个**权值线段树**,叶子结点储存的就是以这个叶子结点的编号为$last_i$的个数。

举个栗子:

```
num[i] : 1 2 3 4 3 5
last[i]: 0 0 0 0 3 0
```

我们可以发现$last_i$为$0$的有$5$个,为$3$的有$1$个。那么就是这样子的:

```
     6
    /\
   6   0
  /\    \
 5   1   0
/\  /\  /\
5 0 0 1 0 0
```

**一定注意线段树左边界为$0$,右边界为$n$**!!

又因为每一次的$last_i$都会呈现一个不同的权值线段树,我们就可以维护一个主席树。而且这个权值线段树恰好可以满足减法原理。

### 查询

我们要查询就很简单啦,如果询问是$l,r$,那么我们只要让$r$操作的线段树和$l$操作的线段树减一减(如第$K$大),然后查询$l,r$区间有多少个数就可以了。这些数就是多余的数,那么答案就是$r-l+1-find$($find$就是查询的数)。

如果还不懂,看$Code$。

### $Code$

要注意数据比较大,虽然不知道具体开多少,但是开了以下数据才不会$RE$。

关于主席树的知识必须能掌握,下面的主席树尝试注释不再重复。

各个变量,数组的注释在代码中间。
```pascal
// luogu-judger-enable-o2
var
        left,right,tree:array[-1..20100070] of longint; //left,right分别为左右儿子的位置(指针),tree为权值线段树的节点的值
        num,root:array[-1..5100070] of longint;
        recf:array[-1..2100007] of longint;
        n,m,i,l,r,cnt,fa,find:longint;

procedure Build(l,r,key:longint);
var
        mid,now:longint; //mid是线段树常识,now代表我这个点的编号(因为是动态开点,pascal需要存储一个这样的值)。
begin
        inc(cnt); //开一个点
        now:=cnt;
        if (l=r) then //如果到了目标
        begin
                if (l=key) then //拒绝0的情况,不然死卡
                        inc(tree[now]); //last[i]=key,这个叶子结点+1
                exit;
        end;

        mid:=(l+r) div 2;
        if key<=mid then
        begin
                left[now]:=now+1; //左儿子肯定是now+1,因为下一次开点肯定是儿子,而且还是一条链(确定了方向),并且不是叶子结点。这里与大家写的不同。
				right[now]:=right[fa]; //主席树常识
                fa:=left[fa]; //常识
                Build(l,mid,key); //往左走
        end;
        if key>mid then //如上
        begin
                right[now]:=now+1;
                left[now]:=left[fa];
                fa:=right[fa];
                Build(mid+1,r,key);
        end;
        tree[now]:=tree[left[now]]+tree[right[now]]; //注意是权值线段树,所以非叶子结点还是等于左右儿子的和的。注意不要写成了tree[now*2]+tree[now*2+1]
end;

function Query(l,r,x,y,key1,key2:longint):longint;
var
        mid:longint;
begin
        if (l>=x)and(r<=y) then //线段树常识
        begin
                inc(find,tree[key2]-tree[key1]); //差分,主席树常识
                exit;
        end;
        if l=r then //加上更稳
                exit;

        mid:=(l+r) div 2;
        if x<=mid then //往左
                Query(l,mid,x,y,left[key1],left[key2]);
        if y>mid then //往右
                Query(mid+1,r,x,y,right[key1],right[key2]);
end;

begin
        read(n); 
        for i:=1 to n do
        begin
                read(num[i]);
                root[i]:=cnt+1; 
                fa:=root[i-1];
                Build(0,n,recf[num[i]]); //recf代表跟num[i]相同的前一个数,注意一开始为0
                recf[num[i]]:=i; //更新
        end;

        read(m);
        for i:=1 to m do
        begin
                read(l,r);
                find:=0;
                Query(0,n,l,r,root[l-1],root[r]); //注意左右边界。l,r代表的是这段区间我需要查询(有多少个以这个区间某一个数字为last,那么一定有重复!!)。root[l-1]和root[r]是主席树常识,同时保证了num[i]在这个区间中,last[i]也在这个区间中。
                writeln(r-l+1-find); //答案,如上述。
        end;
end.
```