来一发比较详细的题解。

-----

## 思路

首先感性地思考一下：想达到最优解可能用哪几种策略？

无非就是以下两种：

 1. 让渡河最快的人带着别的人走，这样返程速度是最快的；
 2. 把两个比较慢的放在一起，因为如果把他们分开，会对总时间造成更大的影响。
 
 可以证明，没有其它的策略可以做到更优。所以每次渡人时，只要从这两种方案中选一个最优的就行。
 
-----

## 实现方法

 1. 将 $n$ 个人按渡河时间排序（这一点凭直觉应该也能想出来）。
 	
    为了叙述方便，设这 $n$ 个人的渡河时间分别为 $a_1,a_2,\dots,a_n$ 。
 2. 将策略转化为数学语言：
 	
    设 $f_i$ 为前 $i$ 个人渡河所需的最小时间。
 		
       方案一： $f_i=f_{i-1}+a_1+a_i$
       
       方案二： $f_i=f_{i-2}+a_1+a_i+a_2+a_2$
   	
    不明白的可以自己推一下，上面的连加式都是**按照操作顺序**写的，比较容易理解
    
 3. 写出状态转移方程：
 
 	两种策略取最优的，也就是 $f_i=\text{min}(f_{i-1}+a_1+a_i,f_{i-2}+a_1+2a_2+a_i)$
    
    然后就可以轻松地写出代码了。
    
    说时间复杂度是 $\mathcal{O(n)}$ 的，是不是忘记算排序时间了？
    
    时间复杂度 $\mathcal{O(n\log n)}$ ，空间复杂度 $\mathcal{O(n)}$
   
-----------

## 代码

```
var
    n,i,x,y:longint;
    f,a:array[0..100010] of longint;
procedure qsort(l,r:longint); //快排
var
    i,j,t,mid:longint;
begin
    i:=l; j:=r; mid:=a[(l+r) div 2];
    repeat
        while a[i]<mid do inc(i);
        while a[j]>mid do dec(j);
        if i<=j then
        begin
            t:=a[i]; a[i]:=a[j]; a[j]:=t;
            inc(i); dec(j);
        end;
    until i>j;
    if l<j then qsort(l,j);
    if i<r then qsort(i,r);
end;
begin
    readln(n);
    for i:=1 to n do read(a[i]);
    qsort(1,n);
    f[1]:=a[1]; f[2]:=a[2]; //边界条件
    for i:=3 to n do
    begin
        x:=f[i-1]+a[1]+a[i]; //方案一
        y:=f[i-2]+a[1]+2*a[2]+a[i]; //方案二
        if x<y then f[i]:=x else f[i]:=y; //取最优的
    end;
    writeln(f[n]);
end.
```

-------

如果你想快速理解这篇题解，就看一下这个式子吧：

![](https://i.loli.net/2019/02/24/5c721898cad2b.png)

其中 $x,y$ 分别是两种方案得出的答案

 $x=f_{i-1}+a_1+a_i,y=f_{i-2}+a_1+2a_2+a_i$
 
 （吐槽一下洛谷，$\LaTeX$在前台会炸，只能发图片）