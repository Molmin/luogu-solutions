看到题解中一堆c++大佬，我知道本人只是个pascal蒟蒻，~~但没关系，直接上啊（有点不要脸）~~
好了，废话少说，来讲一下这道题的思路。
### 首先，我们知道这道题的范围会比较大，如果直接暴力会超时，那么我们自然就想到了二分答案。
#### 1、二分必定要使数组有序排列，我们直接~~偷个~~快排就没事了
#### 2、我们二分的是距离，且是最小距离，我们可以分段，若当前距离需要移走的石头大于可以移走的石头，就不能继续二分下去



------------
```pascal
var l,n,m,i,t,w,mid,max:longint;
    a:array[0..100001] of longint;
 procedure sort(l,r: longint);//这里是快排，对a数组进行排序
      var
         i,j,x,y: longint;
      begin
         i:=l;
         j:=r;
         x:=a[(l+r) div 2];
         repeat
           while a[i]<x do
            inc(i);
           while x<a[j] do
            dec(j);
           if not(i>j) then
             begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
function pd(mid:longint):boolean;//函数用来判断当前距离是否可行
var s,t,i:longint;
begin
  s:=0;t:=0;
  for i:=1 to n+1 do
    if a[i]-s<mid then
    begin
      t:=t+1;
      if t>m then exit(false);
    end
    else s:=a[i];
  exit(true);
end;
begin
  readln(l,n,m);
  a[0]:=0;a[n+1]:=l;//进行范围限制
  for i:=1 to n do
    readln(a[i]);//读入石头的距离
  sort(1,n);
  t:=0;w:=l;//开始二分，t和w表示距离范围
  while t<=w do
  begin
    mid:=(t+w) div 2;
    if pd(mid) then//若可行则记录当前距离，前指针=mid+1
    begin
      max:=mid;
      t:=mid+1;
    end
    else w:=mid-1;//若不行，则后指针=mid-1
  end;
  writeln(max);//输出
end.
```


------------
以上就是本人的思路和算法，~~代码巨丑，大佬勿喷，QWQ~~，谢谢