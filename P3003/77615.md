# 题意
问一条最短路径，满足从 $ PB $ 出发，同时经过 $ PA_1 $ 与 $ PA_2 $。
# 基本思路
首先，我们知道路径一定是先到 $ PA_1 $ 再到 $ PA_2 $，或先到 $ PA_2 $ 再到 $ PA_1 $ 的，因为如果你不这么走，走的其他路是没有用处的，题目中要求最短，这样就不满足最短，也许有人会问，可以先回去，但是这样相对于直接从 $ PA_1 $ 到 $ PA_2 $ 或从 $ PA_2 $ 到 $ PA_1 $ 只会更长，不会更短。
# 实现
这就是最短路的板子题了，从我们从 $ PB $ ， $ PA_1 $ ， $ PA_2 $ 出发各跑一次最短路，就可以求出两条路径的值，在取较小值输出即可。
# 注意事项
这题 $ \text{Pascal} $ 基本不可用 $ \text{SPFA} $ 过，建议使用 $ \text{Dijkstra+} $堆优化。
# 代码
```pascal
Uses Math;
var hs,s,f1,f2,n,m,len:Longint;
    Used : array[0..100001] of Boolean;
    d : array[0..100001] of Longint;
    v,l,head,w:array[0..400001] of longint;
    h,ih:array[0..100001] of longint;
Procedure Swap(var o,o2: longint);
var buf:longint;
begin
 buf:=o; o:=o2; o2:=buf;
end;
procedure addvertex(a , b, c :longint);
begin
 inc(len);
 v[len]:=b;
 l[len]:=head[a];
 w[len]:=c;
 head[a]:=len;
end;
procedure Siftdown;
var cur,x:longint;
begin
 cur:=1;
 While cur shl 1<=hs do begin
 	x:=cur;
 	if (cur shl 1<=hs) and (d[h[x]] > d[h[cur shl 1]]) then x:=cur shl 1;
 	 if ((cur shl 1) + 1 <=hs) and (d[h[x]] > d[h[(cur shl 1) + 1]]) then
 	   x:=(cur shl 1) + 1;
 	  if cur = x then break else begin
 	  	Swap(ih[h[cur]],ih[h[x]]);
 	  	Swap(h[cur],h[x]);
 	  	cur:=x;
 	  end;
 end;
end;
Function getmin : longint;
begin
 If (hs <=0) or (d[h[1]] = maxlongint) then
  getmin:=-1 else begin
  	getmin:=h[1];
  	ih[h[1]]:=-1;
  	h[1]:=h[hs];
  	ih[h[1]]:=1;
  	h[hs]:=0;
  	dec(hs);
  	Siftdown;
  end;
end;
Procedure Siftup(x : Longint);
begin
 While  ( x > 1 ) and (d[h[x]] < d[h[x shr 1]]) do begin
 	Swap(ih[h[x]], ih[h[x shr 1]]);
 	Swap(h[x], h[x shr 1]);
 	x:=x shr 1;
 end;
end;
Function Dist ( a , b:longint) : longint;
var i,j:longint;
begin
 hs:=n;
 For i:=1 to n do begin
 	used[i]:=false;
 	d[i]:=MaxLongint;
 	ih[i]:=i;
 	h[i]:=i;
 end;
 Swap(h[a],h[1]);
 Swap(ih[a] , ih[1]);
 d[a]:=0;
 While Not Used[b] do begin
 	i:=GetMin;
 	if i = -1 then Break else begin
 		Used[i]:=true;
 		j:=head[i];
 		While j > 0 do begin
 		   If (Not Used[v[j]]) and (d[i] + w[j] < d[v[j]]) then begin
 		   	d[v[j]]:=d[i] + w[j];
 		   	Siftup(ih[v[j]]);
 		   end;
 		 j:=l[j];
 		end;
 	end;
 end;
 dist:=d[b];
end;
Function Path1 : longint;
begin
 Path1:= Dist(s, f1) + dist(f1, f2);
end;
Function Path2 : longint;
begin
 Path2:= Dist(s, f2) + Dist( f2 , f1 );
end;
Procedure reading;
var a,b,c,i:longint;
begin
 readln(m,n,s,f1,f2); len:=0;
 For i:=1 to m do begin
  readln(a,b,c);
  addvertex(a,b,c);
  addvertex(b,a,c);
 end;
 writeln(Min( Path1, Path2 ));
end;
begin
  reading;
end.    
```
