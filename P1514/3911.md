解题思路


1．使用BFS计算第n行的哪些点有水利设施


首先将第1行的m个点精简为k个点,按照“从高往低”的规则可遍访第1行的m个点。


{若j点通过BFS计算出可达的区间,则按照”由高到低”规则, j点可流遍递减区间[j,k],而递增区间[k,p]可被p点的水所流遍,因此下一次BFS的出发点为p。分别从第1行的k个点出发进行BFS：按照“从高往低”的规则扩展，遇到没做标记的点就将它加入队列，并做标记。最后统计第n行的m个点中没做标记的点数ans。}


2．若ans>0，则说明有ans个干旱区必定不能有水利设施，输出失败信息，否则说明第1行的m个点，通过“从高往低”的规则能够到达第n行的m个点。


3．此时有一个结论：第1行的任何一个点，能到达的第n排的点是连续的\*{


证明：A点能到达的点集在图中用灰色区域表示（不包括D点）。


假设A点能到达的第n排的点不连续，其中点D无法到达，由于第一排的m个点能够通过“从高往低”的规则到达第n排所有点，故必定存在点B，A点不能到达，它能到达点D。此时，B点到达D点的路径必定和A点能到达的点相交,设为C点，那么此时A点可以通过AàCàD来到达D，与假设矛盾。}


设L[j]表示第1行第j列的点通过“从高往低”规则能扩展到的第n行的最左侧的点为L[j]（第n行第1,2,..L[j]-1列的点不能扩展到）。同理有R[j]


设顶行的Ck点原来可达底行的区间为[i’,j’]，现计算出Ck可达底行的点i和点j，且i≤i’，j’≤j。由于Ck可达第n行的区间是连续的，因此Ck可达底行的区间调整为[i,j]，即L[Ck]=i，R[Ck]=j。


4. 既然已经得到第n行第m列是连续的，那么问题就转化为给定m条线段，问用最少几条线段能覆盖[1,m]，用DP做。


{ 设f[i]为覆盖[n,1]…[n,i]的最少线段数。显然f[0]=0;


有左而右递推n行的每一列i(1≤i≤m)；


在覆盖i+1列的所有线段中，计算右端点的最大值R                      ;


调整[i+1,R]区间的f值：f[k]=max{ f[i]+1,f[k]}  i+1≤k≤R


最后得出的f[m]即为问题解。}


时间复杂度：O(n\*m)。

```delphi

Program Flow_by_Dbh;
const
  dx:array[1..4]of longint=(0,1,-1,0);
  dy:array[1..4]of longint=(1,0,0,-1);
var
  n,m,i,j,k,l,r,ans:longint;
  q,p:array[1..2,1..250000]of longint;//p[1,i]=L[i];p[2,i]=R[i].
  f,s:array[0..510,0..510]of longint;
  d:array[0..200000]of longint;
function min(a,b:longint):longint;
begin
  if a>b then exit(b); exit(a);
end;
procedure init;
begin
  readln(n,m);
  for i:=1 to n do
    for j:=1 to m do
      begin
        read(s[i,j]);
        if j=m then readln;
      end;
end;
procedure bfs(xx,yy:longint);
var x,y,l,r,v:longint;
begin
  l:=0;r:=1;q[1,1]:=xx;q[2,1]:=yy;f[xx,yy]:=yy;
  repeat
    inc(l);
    for v:=1 to 4 do
      begin
        x:=q[1,l]+dx[v];y:=q[2,l]+dy[v];
        if (x<=n)and(y>0)and(x>0)and(y<=m)and(f[x,y]=0)
          and(s[x,y]<s[q[1,l],q[2,l]]) then
          begin
            f[x,y]:=yy;
            inc(r);
            q[1,r]:=x;q[2,r]:=y;
          end;
      end;
  until l=r;
end;
procedure bfsx(xx,yy:longint);//倒着bfs
var x,y,l,r,v:longint;
begin
  l:=0;r:=1;q[1,1]:=xx;q[2,1]:=yy;f[xx,yy]:=yy;
  repeat
    inc(l);
    for v:=1 to 4 do
      begin
        x:=q[1,l]+dx[v];y:=q[2,l]+dy[v];
        if (x<=n)and(y>0)and(x>0)and(y<=m)and(f[x,y]=0)
          and(s[x,y]>s[q[1,l],q[2,l]]) then
          begin
            f[x,y]:=yy;
            inc(r);
            q[1,r]:=x;q[2,r]:=y;
          end;
      end;
  until l=r;
end;
procedure DP;
begin
  d[0]:=0;
  for i:=1 to m do
    begin
      d[i]:=maxlongint;
      for j:=1 to m do
        if (p[2,j]>=i)and(p[1,j]<=i) then
          d[i]:=min(d[i],d[p[1,j]-1]+1);
    end;
  writeln(1);
  writeln(d[m]);
end;
procedure judge;
begin
  for i:=m downto 1 do if f[1,i]=0 then bfs(1,i);
  ans:=0;
  for i:=1 to m do
    if f[n,i]=0 then inc(ans);
  if ans<>0 then//如果不能满足情况
    begin
      writeln(0);writeln(ans);
      close(input);close(output);
      halt;
    end;
end;
procedure floodfill;
begin
  fillchar(f,sizeof(f),0);
  for i:=1 to m do if f[n,i]=0 then bfsx(n,i);//一定为最左边的点
  for i:=1 to m do p[1,i]:=f[1,i];
  fillchar(f,sizeof(f),0);
  for i:=m downto 1 do if f[n,i]=0 then bfsx(n,i);//一定为最右边的点
  for i:=1 to m do p[2,i]:=f[1,i];
end;
begin
  assign(input,'flow.in');reset(input);
  assign(output,'flow.out');rewrite(output);
  init;
  judge;//主要是判断是否能否满足要求
  floodfill;//
  DP;//动态规划
  close(input);close(output);
end.

```