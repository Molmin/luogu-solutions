引水入城的分析


第一小问是简单的，最快可以n^2处理每个上面的点管辖哪些下面的点，如果有没有受管辖的点则输出0以及数量。


若是都被管辖了，则进入第二小问。


每个点可以管辖一些点，求管辖所有点的最小方案，这看似是一个指数级的问题，也没有算法能处理，可是需要注意的是，


这题有个特殊的地方，那就是每个第一行的点管辖的最后一行的点一定是连续的这是可以证明的。


 

若中间有断开，则断开的点一定高于左右，若比之上方的点低，则其上方的点一定高于其左右，而起始点又是可以通过一条路到达右边被断开的区域的，则断开的点一定不能被其他点管辖，这属于第一小问范畴。


 

这样问题就简单了，l[i],r[i]表示i管辖的最左端或最右端，1~m枚举i，f[r[i]]:=f[l[i]-1]+1,f[l[i]到r[i]]都变成f[l[i]-1]+1。


然后输出f[m]就好了。。。


 

DFS应该比BFS快。VC++DFS共用143ms、


-------------------------------------------------------------------------------------------------------------------------------------------------------------


FLOODFILL+两遍BFS预处理+DP即可。


先判断有无解(染色)，无解直接输出；


(染色)(floodfill)：已有色的不染，类似图的遍历，可百度可谷歌。


对于无解情况，从第一行全部点开始向四个方向染色(也就是遍历整个图)，判断最后一行是否全染，否则直接计数+输出，结束。


 

而DP的前提是发现第一行任一点对于任意一行覆盖情况的连续性(即能输水的一定为一条线段)，则该问题转化为线段覆盖经典DP


预处理：C[i,1]表示点(1,i)所能覆盖的最后一行区间的左端点，同理C[i,2]表示右端点；


 

比较裸的方法是：对于每个点(1，i)，我们对全图还原+染遍色再循环一遍处理即可。时间微妙= =


 

易发现，如果我们可以避免对染色图的还原，则时间就会降下去了- -。。而这里的方法就是：从下往上染。


从左至右处理点(n，i)，即向上染色，染完后第一行点的颜色即为其覆盖区间左端点(请童鞋们仔细琢磨)


相对的，从右至左处理点(n，i)，右端点也就出来了。


接下来。。。。线段覆盖直接裸n^2 DP------>AC......


 

WARNING：不要图省事地用D代替B、、、除非你用模拟堆栈(orz)




 
```delphi

const h:array[1..4,1..2] of longint=((1,0),(-1,0),(0,-1),(0,1));
var col,a:array[0..501,0..501] of longint;
    f:array[0..500] of longint;
    c:array[0..500,1..2] of longint;
    d:array[0..500000] of record x,y:longint;end;
    n,m,i,j,color:longint;
function min(a,b:longint):longint;
begin if a<b then exit(a) else exit(b); end;
procedure Judge;
var i,j:longint;
        procedure bfs(x,y:integer);
        var i,t,f:longint;
        begin
             t:=1;f:=1;
             d[t].x:=x;d[t].y:=y;
             col[x,y]:=color;
             repeat
               for i:=1 to 4 do
                   if col[d[f].x+h[i,1],d[f].y+h[i,2]]=0 then     {未出界 && 高度符合}
                        if a[d[f].x+h[i,1],d[f].y+h[i,2]]<a[d[f].x,d[f].y] then begin
                           inc(t);
                           d[t].x:=d[f].x+h[i,1]; d[t].y:=d[f].y+h[i,2];
                           col[d[t].x,d[t].y]:=color;
                   end;
               inc(f);
             until f>t;
        end;   {end bfs}
begin  {judge}
    for i:=0 to n+1 do begin
        a[i,0]:=maxlongint;
        a[i,m+1]:=maxlongint;
    end;
    for i:=0 to m+1 do begin
        a[0,i]:=maxlongint;
        a[n+1,i]:=maxlongint;
    end;
    for color:=1 to m do bfs(1,color);
    j:=0;
    for i:=1 to m do
        if col[n,i]=0 then inc(j);
    if j>0 then begin
       writeln(0);
       writeln(j);
       halt;
    end;
end;
procedure floodfill;
        procedure bfs(x,y:integer);
        var i,t,f:longint;
        begin
             t:=1;f:=1;
             d[t].x:=x;d[t].y:=y;
             col[x,y]:=color;
             repeat
               for i:=1 to 4 do
                  if col[d[f].x+h[i,1],d[f].y+h[i,2]]=0 then
                    if a[d[f].x+h[i,1],d[f].y+h[i,2]]>a[d[f].x,d[f].y] then begin
                       inc(t);
                       d[t].x:=d[f].x+h[i,1]; d[t].y:=d[f].y+h[i,2];
                       col[d[t].x,d[t].y]:=color;
                  end;
               inc(f);
             until f>t;
        end;
var i,j:longint;
begin
     for i:=0 to n+1 do begin
         a[i,0]:=0; a[i,m+1]:=0;
     end;
     for i:=0 to m+1 do begin
         a[0,i]:=0; a[n+1,i]:=0;
     end;
     fillchar(col,sizeof(col),0);
     for color:=1 to m do
         if col[n,color]=0 then
            bfs(n,color);
     for i:=1 to m do
         c[i,1]:=col[1,i];
     fillchar(col,sizeof(col),0);
     for color:=m downto 1 do
         if col[n,color]=0 then bfs(n,color);
     for i:=1 to m do c[i,2]:=col[1,i];
end;
procedure DP;
var i,j:longint;
begin
f[0]:=0;
for i:=1 to m do
begin
 f[i]:=maxint;
 for j:=1 to m do
 if (c[j,2]>=i)and(c[j,1]<=i) then
f[i]:=min(f[i],f[c[j,1]-1]+1);
end;
writeln(1);
writeln(f[m]);
end;
BEGIN   {main}
     read(n,m);
     for i:=1 to n do
        for j:=1 to m do
            read(a[i,j]);
     judge;
     floodfill;
     DP;
END.

WARNING：(第4个点考验你的BFS。)

WARNING：(注意输出要求。。前三个点- -)

```