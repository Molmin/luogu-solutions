大家都用平衡树，我用线段树

这题其实先离散化一下可以用线段树做的

常数肯定小很多

时间复杂度nlogn

其实我不会平衡树啦啦~

代码：

```cpp
program rrr(input,output);
type
  tree=record
     l,r,tot:longint;
  end;
var
  a:array[0..400040]of tree;
  b,c,num:array[0..100000]of longint;
  m,n,i,x,y:longint;
procedure sort(q,h:longint);  //排序，用来离散化
var
  i,j,x,t:longint;
begin
   i:=q;j:=h;x:=num[(i+j)>>1];
   repeat
       while num[i]<x do inc(i);
       while x<num[j] do dec(j);
       if i<=j then
           begin
              t:=num[i];num[i]:=num[j];num[j]:=t;
              inc(i);dec(j);
           end;
   until i>j;
   if j>q then sort(q,j);
   if i<h then sort(i,h);
end;
function find(x:longint):longint;  //离散化后的二分查找
var
  l,r,mid:longint;
begin
   l:=1;r:=n;
   while l<r-1 do
       begin
          mid:=(l+r)>>1;
          if num[mid]<x then l:=mid+1 else r:=mid;
       end;
   if num[l]=x then exit(l) else exit(r);
end;
procedure build(k,l,r:longint);  //建线段树
var
  i,mid:longint;
begin
   a[k].l:=l;a[k].r:=r;a[k].tot:=0;
   if l=r then exit;
   mid:=(l+r)>>1;i:=k<<1;
   build(i,l,mid);
   build(i+1,mid+1,r);
end;
procedure ins;  //加一个x
var
  k:longint;
begin
   k:=1;
   while a[k].l<>a[k].r do
       begin
          inc(a[k].tot);
          if x<=(a[k].l+a[k].r)>>1 then k:=k<<1 else k:=k<<1+1;
       end;
   inc(a[k].tot);
end;
procedure del;  //删一个x
var
  k:longint;
begin
   k:=1;
   while a[k].l<>a[k].r do
       begin
          dec(a[k].tot);
          if x<=(a[k].l+a[k].r)>>1 then k:=k<<1 else k:=k<<1+1;
       end;
   dec(a[k].tot);
end;
function ask(k:longint):longint;  //询问x的排名，即统计1~x-1有几个数，再加1就是排名
var
  i,mid:longint;
begin
   if (x<=a[k].l) and (a[k].r<=y) then exit(a[k].tot);
   mid:=(a[k].l+a[k].r)>>1;i:=k<<1;
   ask:=0;
   if x<=mid then ask:=ask(i);
   if mid<y then ask:=ask+ask(i+1);
end;
function ask2(x:longint):longint;  //询问排名x的数
var
  i,j,k:longint;
begin
   k:=1;j:=0;
   while a[k].l<>a[k].r do
       begin
          i:=k<<1;
          if j+a[i].tot<x then begin j:=j+a[i].tot;k:=i+1; end
          else k:=i;   //如果左子树少了就往右，否则往左
       end;
   exit(a[k].l);
end;
function fin(x:longint):longint;  //查找x所在的线段树结点编号
var
  k:longint;
begin
   k:=1;
   while a[k].l<>a[k].r do
       if x<=(a[k].l+a[k].r)>>1 then k:=k<<1 else k:=k<<1+1;
   exit(k);
end;
function pre:longint;  //求x的前驱
var
  k:longint;
begin
   k:=fin(x);
   while (a[k].tot=a[k>>1].tot) or (k mod 2=0) do k:=k>>1;
   dec(k);
   while a[k].l<>a[k].r do
        if a[k<<1+1].tot>0 then k:=k<<1+1
            else k:=k<<1;
   exit(a[k].l);
end;
function suc:longint;  //求x的后继
var
  k:longint;
begin
   k:=fin(x);
   while (a[k].tot=a[k>>1].tot) or (k mod 2=1) do k:=k>>1;
   inc(k);
   while a[k].l<>a[k].r do
      if a[k<<1].tot>0 then k:=k<<1
         else k:=k<<1+1;
   exit(a[k].l);
end;
begin
   //assign(input,'r.in');assign(output,'r.out');reset(input);rewrite(output);
   readln(m);
   for i:=1 to m do begin readln(b[i],c[i]);if b[i]<>4 then num[i]:=c[i]; end;
   sort(1,m);
   n:=1;
   for i:=2 to m do
       if num[i]<>num[i-1] then
          begin
             inc(n);num[n]:=num[i];
          end;  //离散化后将重复的数字删去
   build(1,1,n);
   for i:=1 to m do
       begin
          x:=find(c[i]);
          if b[i]=1 then ins;
          if b[i]=2 then del;
          if b[i]=3 then
             begin
                if x=1 then writeln(1)
                else begin y:=x-1;x:=1;writeln(ask(1)+1); end;
             end;
          if b[i]=4 then writeln(num[ask2(c[i])]);
          if b[i]=5 then writeln(num[pre]);
          if b[i]=6 then writeln(num[suc]);
       end;
   //close(input);close(output);
end.
```