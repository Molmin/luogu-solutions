推广blog：<http://zhoushengjie.pw>

奇怪Pascal题解只有两篇，一篇线段树，一篇权值线段树。

~~都好巨啊~~

我来一篇正解treap。毕竟Pascal的题解真心少啊。

不讲了。C++的大佬们已经讲得很详细了。

Pascal代码：

```pas
//先解释一下变量名
//a是原来的数值
//l是左儿子
//r是右儿子
//s是子树的大小（几个数）
//p是随机优先值
//num是当前节点的数有几个
var
  a,l,r,s,p,num:array[0..100000]of longint;
  n,i,q,x,y,root:longint;
function max(x,y:longint):longint;//不解释
begin
  if x>y then
    exit(x)
  else
    exit(y);
end;
function min(x,y:longint):longint;
begin
  if x<y then
    exit(x)
  else
    exit(y);
end;
procedure zag(var x:longint);//左旋
var
  t:longint;
begin
  t:=r[x];
  r[x]:=l[t];
  l[t]:=x;
  s[t]:=s[x];
  s[x]:=s[l[x]]+s[r[x]]+num[x];
  x:=t;
end;
procedure zig(var x:longint);//右旋
var
  t:longint;
begin
  t:=l[x];
  l[x]:=r[t];
  r[t]:=x;
  s[t]:=s[x];
  s[x]:=s[l[x]]+s[r[x]]+num[x];
  x:=t;
end;
procedure insert(var x:longint;k:longint);//插入节点
begin
  if x=0 then
  begin
    inc(n);
    x:=n;
    s[x]:=1;
    a[x]:=k;
    p[x]:=random(maxlongint);
    num[x]:=1;
    exit;
  end;
  inc(s[x]);
  if k=a[x] then
    inc(num[x])
  else
    if k<a[x] then
    begin
      insert(l[x],k);
      if p[l[x]]<p[x] then
        zig(x);
    end
    else
    begin
      insert(r[x],k);
      if p[r[x]]<p[x] then
        zag(x);
    end;
end;
procedure delete(var x:longint;k:longint);//删除节点
begin
  if x=0 then
    exit;
  if k=a[x] then
  begin
    if num[x]>1 then
    begin
      dec(s[x]);
      dec(num[x]);
      exit;
    end;
    if (l[x]=0) or (r[x]=0) then
      x:=l[x]+r[x]
    else
    begin
      if p[l[x]]<p[x] then
        zig(x)
      else
        zag(x);
      delete(x,k);
    end;
  end
  else
  begin
    dec(s[x]);
    if k<a[x] then
      delete(l[x],k)
    else
      delete(r[x],k);
  end;
end;
function rank(x,k:longint):longint;//查询排名
begin
  if x=0 then
    exit(0);
  if k<a[x] then
    exit(rank(l[x],k));
  if k=a[x] then
    exit(s[l[x]]+1);
  if k>a[x] then
    exit(rank(r[x],k)+s[l[x]]+num[x]);
end;
function kth(x,k:longint):longint;//第k个数
begin
  while true do
  begin
    if k<=s[l[x]] then
      x:=l[x]
    else
      if k>s[l[x]]+num[x] then
      begin
        dec(k,s[l[x]]+num[x]);
        x:=r[x];
      end
      else
        exit(a[x]);
  end;
end;
function pred(x,k:longint):longint;//前驱
begin
  if x=0 then
    exit(-maxlongint-1);
  if k<=a[x] then
    exit(pred(l[x],k))
  else
    exit(max(a[x],pred(r[x],k)));
end;
function succ(x,k:longint):longint;//后缀
begin
  if x=0 then
    exit(maxlongint);
  if k>=a[x] then
    exit(succ(r[x],k))
  else
    exit(min(a[x],succ(l[x],k)));
end;
begin                          //主程序
  randomize;
  read(q);
  n:=0;
  root:=0;
  for i:=1 to q do
  begin
    read(x,y);
    case x of
    1:insert(root,y);
    2:delete(root,y);
    3:writeln(rank(root,y));
    4:writeln(kth(root,y));
    5:writeln(pred(root,y));
    6:writeln(succ(root,y));
    end;
  end;
end.
```