##11篇居然居然没有一个P。。

##来补一发吧

边表存边，tarjan缩点，dfn[u]为节点u搜索的次序编号(时间戳)，low[u]为u或u的子树能够追溯到的最早的栈中节点的次序号。。。

然后根据染色重新建图。。。

对于每个路径的起点跑一边SPFA。。。

主要看代码

今天刚学tarjan的蒟蒻 （滑稽

点这里：[luogu](https://www.byvoid.com/zhs/blog/scc-tarjan/)

丑陋的代码：

```cpp
uses math;
var
  q,dfn,low,b,a,last,next,x,y,c,color,rd,dis,f:array[0..100001]of longint;
  vis:array[0..100001]of boolean;
  n,m,top,tot,i,ans,d,sum:longint;
procedure add(x,y:longint);
begin
  inc(tot);
  a[tot]:=y;
  next[tot]:=last[x];
  last[x]:=tot;
  inc(rd[y]);
end;
procedure tarjan(k:longint);
var
  i,t:longint;
begin
  inc(d); inc(top);
  dfn[k]:=d;low[k]:=d;
  q[top]:=k;vis[k]:=true;
  i:=last[k];
  while i>0 do 
    begin
      t:=a[i];
      if dfn[t]=0 then //如果节点v未被访问过
        begin
          tarjan(t); //继续向下找
          low[k]:=min(low[k],low[t]);
        end
        else
          if vis[t] then low[k]:=min(low[k],dfn[t]); //如果节点t还在栈内
      i:=next[i];
    end;
  if low[k]=dfn[k] then  //如果节点k是强连通分量的根
    begin
      inc(sum);
      while q[top+1]<>k do 
        begin
          color[q[top]]:=sum;
          inc(b[sum],c[q[top]]);
          vis[q[top]]:=false;
          dec(top);
        end;//更新新的一个强连通分量
    end;
end;//tarjan
procedure spfa(start:longint);
var
  i,h,t,x,y:longint;
begin
  fillchar(vis,sizeof(vis),0);
  fillword(dis,sizeof(dis) >> 1,0);// writeln(start);
  h:=0;t:=1;
  f[1]:=start;
  dis[start]:=b[start];
  while h<t do 
    begin
      inc(h);
      x:=f[h];
      vis[x]:=false;
      i:=last[x];
      while i>0 do 
        begin
          y:=a[i];
          if dis[x]+b[y]>dis[y] then
            begin
              dis[y]:=dis[x]+b[y];
              if not vis[y] then 
                begin
                  vis[y]:=true;
                  inc(t);
                  f[t]:=y;
                end;
            end;
          i:=next[i];
        end;
    end;
  for i:=1 to sum do ans:=max(ans,dis[i]); 
end;//SPFA不想多说
begin
  readln(n,m);
  for i:=1 to n do read(c[i]);
  for i:=1 to m do 
    begin
      readln(x[i],y[i]);
      add(x[i],y[i]);//存边
    end;
  for i:=1 to n do 
    if color[i]=0 then tarjan(i);//如果当前点没有染色则去跑一趟tarjan
  //for i:=1 to n do writeln(color[i]);
  fillword(a,sizeof(a) >> 1,0);
  fillword(next,sizeof(next) >> 1,0);
  fillword(last,sizeof(last) >> 1,0);
  fillword(rd,sizeof(rd) >> 1,0);//清空边
  tot:=0;
  for i:=1 to m do 
    if color[x[i]]<>color[y[i]] then add(color[x[i]],color[y[i]]);//重新建图，注意缩点后每个点的新下表是color[i]
  for i:=1 to sum do 
    if rd[i]=0 then spfa(i);//对于每个入度为0的点去跑SPFA
  writeln(ans);
end.

```