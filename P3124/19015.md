## Solution


这是一道题意迷之hentai的奶牛题


看了一个多小时才看懂题意【放弃治疗


题目翻译真是跪了，各种错误，下次英文题还是自己翻译好。。。



这道题的一个思路是枚举bessie能到达的区间，


先进行$O(n log n)$的排序，然后找到离Bessie最近的右边的干草堆的编号$nearright$


每次寻找$(l,r)$为bessie可以突破$l$但是无法突破$r$时需要给$l$加上多少干草，每次对$ans$取$min$



这个做到$O(N)$，因为当$l$循环完了，在循环$l-1$时，$r$已经不一定要从最初的$nearright$开始，**顺延上次更新的r值就可以**


因为如果还是从最初的$nearright$开始，


当$place[r]-place[l]>height[l]$就不会进行while，


而当$place[r]-place[l]<=height[l]$时候，


$height[r]$还是不变的,


$r$不变，$l$减小，所以$place[r]-place[l]$增大，于是$place[r]-place[l]-height[r]$增大，所以就算进行了也不会更新$ans$


同理，在每次寻找$(l,r)$为bessie可以突破$r$但是无法突破$l$时需要给$r$加上多少干草



若$ans$没有改变则说明每个$(l,r)$都是两边都可以突破的，于是只给一个干草堆加肯定是不可以的（即输出$-1$）


否则输出答案就可以了


## Code


```cpp
program test;
uses math;
const  
  init = maxlongint;
  maxn = 100002;
  
type
  arr=array[0..maxn] of longint;

var
  n,t,i,ans,l,r,nearright:longint;
  place,size:arr;

procedure qsort(l,r: longint);
var
   i,j,x,y: longint;
begin

  i:=l;  j:=r;  x:=place[(l+r) div 2];

  repeat
   while place[i]<x do inc(i);
   while x<place[j] do dec(j);
   if not(i>j) then
   begin
    y:=place[i];   place[i]:=place[j]; place[j]:=y;
    y:=size[i];    size[i]:=size[j];   size[j]:=y;
    inc(i);    j:=j-1;
   end;
 until i>j;

 if l<j then qsort(l,j);
 if i<r then qsort(i,r);

end;

begin
    
    readln(n,t);
    for i:= 1 to n Do
      readln(size[i],place[i]);
    qsort(1,n);

    ans:=init;

    for i:= 1 to n Do 
      if place[i]>t then 
        begin 
           nearright:=i;
           break;
        end;

    r:=nearright;
      
    for l:= nearright-1 downto 0 do 
      while (r<=n) and (not(place[r]-place[l]>size[l])) do
        begin
          ans:=min(ans,place[r]-place[l]-size[r]);
          inc(r);
        end;

    l:=nearright-1;
    for r:= nearright to n do 
      while (l>=1) and (not(place[r]-place[l]>size[r])) do
        begin
          ans:=min(ans,place[r]-place[l]-size[l]);
          dec(l);
        end;

    if ans = init then writeln(-1)
      else writeln(max(0,ans));

end.
```