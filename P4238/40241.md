多项式Pascal题解·第二弹

直接倍增求就好了啦：$ G(x) = G'(x)(2 - F(x)G'(x)) $

代码写的巨丑，常数巨大，慎用……（由于是Pascal跑的就更慢了）

（其实把函数改成过程还是可以用一下的）

递推的代码：（听说递归跑的还比较快？）

```pas
type
  arr = array[0..524287] of longint;//定义多项式
const
  p  = 998244353;              //模数
  g  = 3;                      //原根
  gi = 332748118;
var
  a, b, pow, r: arr;
  n, i: longint;

  procedure swap(var x, y: longint);//交换
  var
    t: longint;
  begin
    t := x;
    x := y;
    y := t;
  end;

  function power(x, y: longint): longint;//快速幂
  begin
    power := 1;
    while y > 0 do
    begin
      if odd(y) then
        power := int64(power) * x mod p;
      x := sqr(int64(x)) mod p;
      y := y shr 1;
    end;
  end;

  function inv(x: longint): longint;//逆元
  begin
    exit(power(x, p - 2));
  end;

  procedure ntt(var a: arr; n, s: longint);//NTT，不详细写了
  var
    i, j, k, x, y, w: longint;
  begin
    for i := 0 to n - 1 do
      if i < r[i] then
        swap(a[i], a[r[i]]);
    i := 1;
    while i < n do
    begin
      j := 0;
      if s = 1 then
        w := power(g, (p - 1) div (i shl 1))
      else
        w := power(gi, (p - 1) div (i shl 1));
      pow[0] := 1;
      for k := 1 to i - 1 do
        pow[k] := int64(pow[k - 1]) * w mod p;
      while j < n do
      begin
        for k := 0 to i - 1 do
        begin
          x := a[j + k];
          y := int64(a[i + j + k]) * pow[k] mod p;
          a[j + k] := (x + y) mod p;
          a[i + j + k] := (x - y) mod p;
        end;
        Inc(j, i shl 1);
      end;
      i := i shl 1;
    end;
    if s = -1 then
    begin
      x := inv(n);
      for i := 0 to n - 1 do
        a[i] := (int64(a[i]) * x mod p + p) mod p;
    end;
  end;

  function inv(a: arr; n: longint): arr;//求逆元
  var
    t: arr;
    i, l, num: longint;
  begin
    num := 2;
    l := 1;
    inv[0] := inv(a[0]);       //先求初始项
    while num <= n shl 1 do
    begin
      num := num shl 1;
      Inc(l);
      //每次倍增都是num shr 1的长度，其他的都是误差，需要忽略（清零）
      for i := 0 to num - 1 do
        r[i] := (r[i shr 1] shr 1) or ((i and 1) shl (l - 1));
      for i := 0 to num shr 1 - 1 do//把a(f)复制下来，防止误差
        t[i] := a[i];
      ntt(inv, num, 1);
      ntt(t, num, 1);
      for i := 0 to num - 1 do //把这玩意用公式卷起来
        inv[i] := int64((p + 2 - int64(inv[i]) * t[i] mod p) mod p) * inv[i] mod p;
      ntt(inv, num, -1);
      for i := num shr 1 to num - 1 do//清零
        inv[i] := 0;
    end;
    for i := n + 1 to num shr 1 - 1 do//最后的清零
      inv[i] := 0;
  end;

begin                          //主程序，没什么好说的
  Read(n);
  for i := 0 to n - 1 do
    Read(a[i]);
  b := inv(a, n - 1);
  for i := 0 to n - 1 do
    Write(b[i], ' ');
  writeln;
end.
```