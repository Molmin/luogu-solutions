大佬们将就看吧，而且用Pascal的也不多。。。

------------
    这道题一看就是搜索与回溯，难度主要在如何存储上
    注意：我程序中采用的是文本读入和输出，切不可以直接提交
    
    第一个，我们肯定是字符串数组记录人名，这样每个人名对应一个相应的序号
    但是，在读入证词时如何判断并记录是谁呢？
    最简单的就是一个个搜索，找到相同的就取出序号，至多的至多，也就2000次吧
    这里我讲一下我的记录方法（勿喷）：
    我们按 序号+人名 的格式记录在一个字符串names上，
    序号推荐使用chr函数，将其把序号转换为一个字符，搜索时用pos函数搜索人名，返回值减去1，这时对应names中的就是序号转换为的字符，再用ord就可以取出序号，具体实现见下
    for i:=1 to M do
    begin
    readln(w,a[i].name);
    names:=names+chr(i)+a[i].name;
    end;
    
    function searchname(q:string):longint;
    begin
    searchname:=ord(names[pos(q,names)-1]);
    end;
    
    接下来，记录证词（这里我是搜索罪犯和时间，如果是直接搜索说谎者就不用看了）
    分为两块，一个是关于罪犯的证词，另一个是关于时间的
    
    type
    data=record
           name:string;
           sum:longint;
           sayer:array[1..100] of longint;
           said:array[1..100] of boolean;
           end;
    weather=record
              sum:longint;
              sayer:array[1..100] of longint;
              said:array[1..100] of longint;
              end;
              
    sum表示有多少人证词涉及，sayer表示证词来源，said表示证词内容，
    在罪犯中true代表被说为罪犯，反之，则不是，时间中1~7 代表星期几
    
    第三个记录这个人是否说谎
    用c,d,e三个数组，0代表未确认（譬如没说过证词，或者说的全是骚话）
    1代表说真话，2代表说假话
------------

    好的，下面用程序及注释解释，有点长，标识符有点混乱，忍一手吧
    
   ```pascal
type
  data=record
         name:string;
         sum:longint;
         sayer:array[1..100] of longint;
         said:array[1..100] of boolean;
         end;
  weather=record
            sum:longint;
            sayer:array[1..100] of longint;
            said:array[1..100] of longint;
            end;
var
  a:array[1..20] of data;
  b:weather;
  c,d:array[1..20] of longint;
  names:string;//理论上该用ansistring的，
              //但为了调试方便，
              //先用的这个（ansistring用不了变量监视），
              //后面忘记改了。但居然也可以过，不过别学我
  M,N,P,i,s:longint;
  r:boolean;
  w:text;

function searchname(q:string):longint;
begin
  searchname:=ord(names[pos(q,names)-1]);
end;

procedure add;//用于读入证词，具体为什么加上一个数或者减去一个数，
              //请自己列出来推吧，很简单的
var
  x:string;
  q,p,t1,t2,o:longint;

begin
  readln(w,x);
  q:=pos(':',x)-1;
  p:=searchname(copy(x,1,q));
  o:=0;
  t1:=pos('I am guilty',x);
  t2:=pos('I am not guilty',x);
  if (t1>0) or (t2>0) then
    begin
    inc(a[p].sum);
    a[p].sayer[a[p].sum]:=p;
    a[p].said[a[p].sum]:=t1>0;
    exit;
    end;
  t1:=pos(' is guilty',x);
  t2:=pos(' is not guilty',x);
  if (t1>0) or (t2>0) then
    begin
    q:=q+3;
    if t1>0 then o:=t1 else o:=t2;
    o:=searchname(copy(x,q,o-q));
    inc(a[o].sum);
    a[o].sayer[a[o].sum]:=p;
    a[o].said[a[o].sum]:=t1>0;
    exit;
    end;
  t1:=pos('Today is',x)+9;//时间
  if t1>9 then
    begin
    t2:=0;
    while (t1+t2<=length(x)) and (x[t1+t2]<>' ') do inc(t2);
    dec(t2);
    case copy(x,t1,t2) of
      'Monday':o:=1;
      'Tuesday':o:=2;
      'Wednesday':o:=3;
      'Thursday':o:=4;
      'Friday':o:=5;
      'Saturday':o:=6;
      'Sunday':o:=7;
      end;
    inc(b.sum);
    b.sayer[b.sum]:=p;
    b.said[b.sum]:=o;
    exit;
    end;
end;

function check:boolean;//最后用的，可以先看下面的
var
  i,t,f:longint;
begin
  t:=0;//记录不知道真假的人
  f:=0;//记录说谎的人
  for i:=1 to M do
    if c[i]=2 then inc(f)
    else if c[i]=0 then inc(t);
  if (f<=N) and (f+t>=N) then exit(true) else exit(false);
  //注意，不知道真假的应该也可以算作说假话的
end;

function sort1(p:longint;u:boolean):boolean;//搜索罪犯
var
  i,q:longint;
begin
  sort1:=true;
  for i:=1 to a[p].sum do
    begin
    q:=a[p].sayer[i];
    if a[p].said[i]=u then
      if c[q]<>2 then c[q]:=1
      else exit(false)
    else
      if c[q]<>1 then c[q]:=2
      else exit(false);
  end;//如果冲突就退吧
  if u then//偷懒，输入u=true实际上是假设这个人是罪犯，
           //以此搜索，那么剩下的就不是罪犯，检索剩下人的话，
           //这里把两个程序合并了
    begin
    for i:=1 to p-1 do if not(sort1(i,false)) then exit(false);
    for i:=p+1 to M do if not(sort1(i,false)) then exit(false);
    end;
end;

function sort3(p:longint):boolean;//先看下面的sort2
var
  i,q:longint;
begin
  for i:=1 to b.sum do//搜索时间
    begin
    q:=b.sayer[i];
    if b.said[i]=p then
      if c[q]<>2 then c[q]:=1
      else exit(false)
    else
      if c[q]<>1 then c[q]:=2
        else exit(false);冲突就退呗
    end;
  exit(true);
end;

function sort2:boolean;
var
  e:array[1..20] of longint;
  i:longint;
begin
  sort2:=false;
  e:=c;记录c的值，时间搜索时会反复更改，避免互相影响
  for i:=1 to 7 do
    if sort3(i) then exit(true) else c:=e;//依次枚举，假设今天是周i
    //来搜索时间，注意，这是存在性问题，只要有解即可，
    //有解就退，反之重置c
end;

begin
  assign(w,'in.in');
  reset(w);
  readln(w,M,N,P);
  for i:=1 to M do
    begin
    readln(w,a[i].name);
    names:=names+chr(i)+a[i].name;
    end;
  for i:=1 to P do add;
  close(w);
  assign(w,'out.out');
  rewrite(w);
  fillchar(c,sizeof(c),0);
  r:=false;d:=c;s:=0;//初始化并记录
  for i:=1 to M do//依次枚举罪犯
    begin
    if sort1(i,true) and sort2 and check then//不用担心多执行的，
            //依次调用函数，如果为false就直接过了，不会再掉用剩下的
      if r=true then//r代表是否已经找出了一个罪犯，
                   //如果找出了第二个...你懂得
        begin
        write(w,'Cannot Determine');
        close(w);
        halt;
        end
      else
        begin
        r:=true;
        s:=i;//记录罪犯是谁
        end;
    c:=d;//回归c
    end;
  if s>0 then write(w,a[s].name) else write(w,'Impossible');
  close(w);
end.

```
  所以说，这么长的程序，累啊