这道题我认为只是考了枚举和字符串处理。。。

弱弱认为写Pascal比较有优势。。。

```delphi
var
  kk:array[1..20] of longint;
  xiong,bu,butime:set of 1..20;
  time:longint;
  m,n,i,j,ge,p,pp,k,e:longint;
  a:array[1..1000,1..5] of longint;{a数组里1表示是否说过‘我是凶手’2表示是否说过‘我不是凶手’，5表示说了今天是星期几}
  a3,a4:array[1..1000,0..100] of longint;{a3表示说了哪些人是凶手，a4表示哪些人不是凶手}
  xiongge,dang,ans:longint;
  nam:array[1..1000] of string;
  day:array[1..7] of string[10];
  s:string;
procedure dfs(x:longint);
var
  i,j:longint;
  ff:boolean;
begin
  if x>m then
    begin
    if ge=n then
      begin
      ff:=true;
      xiong:=[];
      bu:=[];
      time:=0;
      butime:=[];
      for i:=1 to m do
        begin
        if kk[i]=0 then
          begin
          if a[i,1]=1 then
            begin
            if (i in bu) then begin ff:=false; break; end;
            xiong:=xiong+[i];
            end;
          if a[i,2]=1 then
            begin
            if (i in xiong) then begin ff:=false; break; end;
            bu:=bu+[i];
            end;
          for j:=1 to a3[i,0] do
            begin
            if a3[i,j] in bu then begin ff:=false; break; end;
            xiong:=xiong+[a3[i,j]];
            end;
          if ff=false then break;
          for j:=1 to a4[i,0] do
            begin
            if a4[i,j] in xiong then begin ff:=false; break; end;
            bu:=bu+[a4[i,j]];
            end;
          if ff=false then break;
          if a[i,5]<>0 then
            begin
            if a[i,5] in butime then begin ff:=false; break; end;
            if time=0 then time:=a[i,5]
                      else if time<>a[i,5] then begin ff:=false; break; end;
            end;
          end;
        if kk[i]=1 then
          begin
          if a[i,1]=1 then
            begin
            if (i in xiong) then begin ff:=false; break; end;
            bu:=bu+[i];
            end;
          if a[i,2]=1 then
            begin
            if (i in bu) then begin ff:=false; break; end;
            xiong:=xiong+[i];
            end;
          for j:=1 to a3[i,0] do
            begin
            if a3[i,j] in xiong then begin ff:=false; break; end;
            bu:=bu+[a3[i,j]];
            end;
          if ff=false then break;
          for j:=1 to a4[i,0] do
            begin
            if a4[i,j] in bu then begin ff:=false; break; end;
            xiong:=xiong+[a4[i,j]];
            end;
          if ff=false then break;
          if a[i,5]<>0 then
            begin
            butime:=butime+[a[i,5]];
            if time=a[i,5] then begin ff:=false; break; end;
            end;
          end;
        end;
      if ff then
        begin
        if ans=-1 then ans:=0;
        xiongge:=0;
        for i:=1 to m do
          if i in xiong then
            begin
            inc(xiongge);
            dang:=i;
            end;
        if xiongge=1 then
          if ans=0 then ans:=dang
                   else if dang<>ans then
                          begin
                          writeln('Cannot Determine');
                          halt;
                          end;
        if xiongge=0 then
          begin
          for i:=1 to m do
            if not(i in bu) then
              begin
              inc(xiongge);
              dang:=i;
              end;
          if xiongge=1 then
            if ans=0 then ans:=dang
                     else if dang<>ans then
                            begin
                            writeln('Cannot Determine');
                            halt;
                            end;
          end;
        end;
      end;
    exit;
    end;
  if ge<n then
    begin
    inc(ge);
    kk[x]:=1;
    dfs(x+1);
    kk[x]:=0;
    dec(ge);
    end;
  dfs(x+1);
end;
begin
  readln(m,n,p);
  for i:=1 to m do
    readln(nam[i]);
  day[1]:='Monday';
  day[2]:='Tuesday';
  day[3]:='Wednesday';
  day[4]:='Thursday';
  day[5]:='Friday';
  day[6]:='Saturday';
  day[7]:='Sunday';{直接把日期存进数组里再挨个比较比较方便。。}
  for i:=1 to p do
    begin
    readln(s);
    pp:=pos(': ',s);
    for j:=1 to m do
      if copy(s,1,pp-1)=nam[j] then
        begin
        delete(s,1,pp+1);
        pp:=pos('I am guilty.',s);{整句话寻找不容易找错，毕竟I'm a pig的话也有的}
        delete(s,pp,12);
        if pp<>0 then a[j,1]:=1;
        pp:=pos('I am not guilty.',s);
        delete(s,pp,16);
        if pp<>0 then a[j,2]:=1;
        pp:=pos(' is guilty.',s);
        while pp<>0 do
          begin
          inc(a3[j,0]);
          k:=pp;
          while (k>1) and (s[k-1]<>' ') do dec(k);
          for e:=1 to m do
            if copy(s,k,pp-k)=nam[e] then
              begin
              a3[j,a3[j,0]]:=e;
              break;
              end;
          delete(s,k,pp-k+11);
          pp:=pos(' is guilty.',s);
          end;
        pp:=pos(' is not guilty.',s);
        while pp<>0 do
          begin
          inc(a4[j,0]);
          k:=pp;
          while (k>1) and (s[k-1]<>' ') do dec(k);
          for e:=1 to m do
            if copy(s,k,pp-k)=nam[e] then
              begin
              a4[j,a4[j,0]]:=e;
              break;
              end;
          delete(s,k,pp-k+15);
          pp:=pos(' is not guilty.',s);
          end;
        pp:=pos('Today is ',s);
        if pp<>0 then
          begin
          k:=pp+8;
          while (k<length(s)) and (s[k+1]<>'.') do inc(k);
          for e:=1 to 7 do
            if copy(s,pp+9,k-pp-9+1)=day[e] then
              begin
              a[j,5]:=e;
              break;
              end;
          delete(s,pp,k-pp+2);
          end;
        break;
        end;
    end;
  ge:=0;
  ans:=-1;
  dfs(1);
  if ans>0 then writeln(nam[ans])
             else if ans=-1 then writeln('Impossible')
                            else writeln('Cannot Determine');
end.
```
其实只要处理好每个人说了哪些话，再枚举哪些人是说真话，判断是否符合，就可以了。

[color=red]注意判断Impossible和Cannot Determine的情况，如果能产生不发生冲突的情况，但得不出凶手，那就判断为Cannot Determine。[/color]
