下面发一个Pas题解：

这一题是为所有点求最短距离的题，所以可以有以下的做法：

- 深度优先搜索（DFS）；

- 宽（广）度优先搜索（BFS）。


DFS思路：

- 先读入，并找出哪些是白色的，将最短距离f[i,j]设为0；

- 循环，是白色就DFS；

- 将数组f输出。

子程序DFS：

- 循环，加上方向增量值后如果那里是黑的，且距离更短就更新f，再DFS。


详见代码：

```pascal
const dx:array[1..4] of longint=(0,0,1,-1);
dy:array[1..4] of longint=(1,-1,0,0);                                                         //方向增量值dx,dy
var f:array[0..200,0..200] of longint;                                                        //状态数组，f[i,j]表示像素点i,j的最短距离
a:array[0..200,0..200] of boolean;
n,m,i,j:longint;
s:string;
procedure dfs (x,y,s:longint);                                                                  //深搜，x,y表示点的横纵坐标，s表示改点的最短距离
var i:longint;
    begin
    for i:=1 to 4 do
    if not a[x+dx[i],y+dy[i]] and (f[x+dx[i],y+dy[i]]>s+1) then            //加上方向增量值后如果那里是黑的，且距离更短
        begin
    f[x+dx[i],y+dy[i]]:=s+1;                                                                 //更新f
    dfs(x+dx[i],y+dy[i],s+1);                                                                //DFS下一轮
        end;
    end;
begin
    readln(n,m);
    fillchar(a,sizeof(a),true);
    fillchar(f,sizeof(f),127);                                                                  //初始化
    for i:=1 to n do
        begin
    readln(s);
    for j:=1 to m do
        begin
    a[i,j]:=(s[j]='1');
    if a[i,j] then f[i,j]:=0;
        end;
        end;                                                                                      //读入的字符串处理
    for i:=1 to n do
        for j:=1 to m do
        if f[i,j]=0 then dfs(i,j,0);                                                         //DFS
    for i:=1 to n do
        begin
    for j:=1 to m do
    write(f[i,j],' ');
    writeln;
        end;                                                                                      //输出
end.
```
结果：

评测结果：Unaccepted                     得分：30

提交时间：2017-05-05 14:13            耗时：538ms

内存：8781kb                                    评测机：美国的Wallace

编译信息 编译成功 没有编译信息

分点信息（鼠标移到方块上有详细信息）

#1 AC 0ms/8703kB

#2 AC 0ms/8703kB

#3 AC 538ms/8781kB

#4 TLE
#5 TLE
#6 TLE
#7 TLE
#8 TLE
#9 TLE
#10 TLE

当当当当当！完美的爆了7个点，超时！！！


好了，经过这一番折腾，我改过自新，用了正解：BFS（宽（广）度优先搜索）：

BFS思路：

- 初始化；

- 读入，将白色的点先入队，并将信息存入数组f，x[i,j]改为0；

- BFS，头指针h+1，循环；

- 将即将出队的点的坐标加上方向增量值，若未入队，则尾指针t+1，保存该点的数据，将最短路径x更改为x[f[h,1],f[h,2]]+1，并设为不能走了（进度一样，先找到该点就是最快的）；

- 当所有成员全部出队时，退出循环。


详见代码：

```pascal
const dx:array[1..4] of longint=(0,0,1,-1);
dy:array[1..4] of longint=(1,-1,0,0);                         //方向增量值dx,dy
var x:array[1..200,1..200] of longint;
a:array[0..200,0..200] of boolean;
f:array[1..34000,1..2] of longint;
n,m,i,j,h,t:longint;
s:string;
begin
    readln(n,m);
    fillchar(a,sizeof(a),true);
    t:=0;                                                               //初始化
    for i:=1 to n do
        begin
    readln(s);
    for j:=1 to m do
        begin
    a[i,j]:=(s[j]='1');
    if a[i,j] then
        begin
    t:=t+1;
    f[t,1]:=i;
    f[t,2]:=j;
    x[i,j]:=0;                                                         //是白色就入队并保存数据
        end;
        end;
        end;
    h:=0;
    repeat                                                           //开始BFS
    h:=h+1;
    for i:=1 to 4 do                                             //枚举4个方向
    if not a[f[h,1]+dx[i],f[h,2]+dy[i]] then           //如果走后的位置还没被找到过
        begin
    t:=t+1;
    f[t,1]:=f[h,1]+dx[i];
    f[t,2]:=f[h,2]+dy[i];                                        //保存数据
    x[f[t,1],f[t,2]]:=x[f[h,1],f[h,2]]+1;                    //记录该点的最短距离
    a[f[t,1],f[t,2]]:=true;                                       //该点被标记为找到了
        end;
    until h=t;                                                      //当所有点出队时就退出
    for i:=1 to n do
        begin
    for j:=1 to m do
    write(x[i,j],' ');
    writeln;
        end;                                                      //输出
end.
```

如有问题需要修正请私信我，完善之后即可获得一封感谢的（私）信。

<https://www.luogu.org/space/show?uid=11358>
