(大年三十写篇题解纪念一下，还是此题第一个题解呢。)

因为此题中喷泉只有$2$个，所以我们可以直接求出每朵花到$2$个喷泉的距离$p,q$。

接下来对$p,q$进行双关键字排序($p$从大到小，$q$从大到小)。

为什么要排序呢？

因为排序后，我们可以利用贪心。

先假设第一个喷泉浇所有的花，值就是$p[1]$。

接下来假设第一个喷泉浇$1$~$n-1$朵花，第二个喷泉浇第$n$朵花，值就是$p[2]+max$。

当第一个喷泉浇$1$~$i$朵花时，$max$就是$q[i+1]$~$q[n]$的最大值。

那么结果就是这些数中的最小值。

$1$个要注意的地方：

求距离时$p,q$的值可能会达到$10$的$14$次方，所以要用$int64/longlong$。

代码：
```pascal
var n,x,y,max,max1,x1,y1,x2,y2:int64; i:longint;
a,b:array[0..1000001] of int64;
procedure sort(l,r: int64);
      var i,j,x,y,xx: int64;
      begin
         i:=l; j:=r;
         x:=a[(l+r) div 2];
         xx:=b[(l+r) div 2];
         repeat
           while (a[i]>x)or(a[i]=x)and(b[i]>xx) do inc(i);
           while (x>a[j])or(a[j]=x)and(b[j]<xx) do dec(j);                      //双关键字排序，从大到小
           if not(i>j) then
             begin
             y:=a[i];a[i]:=a[j];a[j]:=y;
             y:=b[i];b[i]:=b[j];b[j]:=y;
             inc(i); j:=j-1;
             end;
         until i>j;
         if l<j then sort(l,j);
         if i<r then sort(i,r);
      end;
begin
readln(n,x1,y1,x2,y2);
for i:=1 to n do
  begin
  readln(x,y);
  a[i]:=sqr(x1-x)+sqr(y1-y);
  b[i]:=sqr(x2-x)+sqr(y2-y);
  end;                       //求距离
sort(1,n);                  //排序
a[n+1]:=0; max1:=a[1];     //浇所有花的情况
max:=0;
for i:=1 to n do
  begin
  if b[i]>max then max:=b[i];   //求出最大值
  if max+a[i+1]<max1 then max1:=max+a[i+1];
                                 //更新答案
  end;                         
writeln(max1);
end.
```

