~~**P党的福利啦**~~
### 看到楼下的dalao们的树状数组。实测貌似过不了只有30。。。让我这个蒟蒻来一发满分的树状数组。
#### 归并其它的dalao们已经讲的很清楚了，我就不再赘述了。
## 接下来步入正题啦
方便起见，我们设f[x]为x这个数出现过多少次了。

然后我们**倒序**扫一遍。（正序扫的话就是找i<j且ai<aj的数量）接下来是模拟过程

因为最后一个数是an，所以先把答案加上f[1]+f[2]+f[3]+……+f[an -1]，
然后f[an]的值+1

倒数第二个数是a(n-1)，所以先把答案加上f[1]+f[2]+f[3]+……+f[a(n-1)-1]，
然后f[a(n-1)]的值+1

以此类推。。。。

然后那个前缀和用树状数组优化一下就好了。

举个小栗子：

比如说999 99 9999这三个数

通过离散化之后我们可以把它看作2 1 3

先扫到3，f[3]+1,ans=0;

然后扫1，f[1]+1,ans=0

然后扫到2,f[2]+1,ans加上f[1]的值也就是ans=1

#### ~~然后就大功告成了啦~~

PS：
由于这个数据貌似有点毒瘤，同级[dalao](https://www.luogu.org/space/show?uid=29575)告诉我会有重复的数。。。~~貌似楼下的dalao们都被这个卡了。~~

离散化处理重复的数大家应该都会。毕竟打树状数组的都是巨佬啦~~(我太菜了)~~

~~处理重复的数~~(巨佬请无视)
```
  sort(1,n); //先按原数组大小排序
  for i:=1 to n do
   if a[i]<>a[i-1] then
    c[i]:=c[i-1]+1
   else
    c[i]:=c[i-1];
  a:=c;
  ssort(1,n); //再按下标排序，来自p党的淡淡的忧伤
```
贴一个丑陋的代码%%%[dalao](https://www.luogu.org/space/show?uid=29575)
```
var n,i:longint;
    ans:int64;
    a,b,c,s:array[0..1000001] of int64;
procedure sort(l,r:int64);　　//快排啦。。。
var i,j,x,t:int64;
begin
  i:=l;j:=r;x:=a[(l+r) div 2];
  repeat
   while a[i]<x do
    inc(i);
   while x<a[j] do
    dec(j);
   if i<=j then
    begin
     t:=a[i];a[i]:=a[j];a[j]:=t;
     t:=b[i];b[i]:=b[j];b[j]:=t;
     inc(i);dec(j);
    end;
  until i>j;
  if l<j then
   sort(l,j);
  if i<r then
   sort(i,r);
end;
procedure ssort(l,r:int64);　//快排啦。。。
var i,j,x,t:int64;
begin
  i:=l;j:=r;x:=b[(l+r) div 2];
  repeat
   while b[i]<x do
    inc(i);
   while x<b[j] do
    dec(j);
   if i<=j then
    begin
     t:=a[i];a[i]:=a[j];a[j]:=t;
     t:=b[i];b[i]:=b[j];b[j]:=t;
     inc(i);dec(j);
    end;
  until i>j;
  if l<j then
   ssort(l,j);
  if i<r then
   ssort(i,r);
end;
function lowbit(x:int64):int64;　//树状数组基本操作啦
begin
  exit(x and -x);
end;
procedure add(x,k:int64);　//树状数组基本操作啦
begin
  while x<=n do
   begin
    s[x]:=s[x]+k;
    x:=x+lowbit(x);
   end;
end;
function ss(x:int64):int64;　//树状数组基本操作啦
var now:int64;
begin
  now:=0;
  while x>0 do
   begin
    now:=now+s[x];
    x:=x-lowbit(x);
   end;
  exit(now);
end;
begin
  read(n);
  for i:=1 to n do
   begin
    read(a[i]);
    b[i]:=i;
   end;
  sort(1,n);
  for i:=1 to n do
   if a[i]<>a[i-1] then
    c[i]:=c[i-1]+1
   else
    c[i]:=c[i-1];
  a:=c;
  ssort(1,n);
  for i:=n downto 1 do　//开始统计答案啦
   begin
    add(a[i],1);
    ans:=ans+ss(a[i]-1);
   end;
  writeln(ans);
end.
```
最后再%%%[dalao](https://www.luogu.org/space/show?uid=29575)，感谢dalao的帮助与启发