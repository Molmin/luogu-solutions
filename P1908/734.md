本题一开始我是使用暴搜，结果值得了可怜的20分。然后我决定先快排一边再说。结果一边快排下来，我瞬间醒悟了。我们从大到小排序之后，一定是这样写的（b表示原来在数组中的位置）

```delphi
for i:=2 to n do
  for j:=1 to i-1 do
    if b[j]<b[i] then inc(ans);
```
虽然这还是N方的，但别着急，这已经十分接近正确答案了，我们发现，每一次i都是求1~b[i]中那些在它的前面，相当于在它前面的值为1，后面的值为0，循环之后值有变为了1，于是写出来是这样子的:

```delphi
for i:=2 to n do
   begin
     for j:=1 to b[i] do inc(ans,c[j]);
     c[b[i]]:=1;
   end;
```
我们发现了什么？对，只有加一和求和两种操作，于是~~~~树状数组！！！


详见标程：

```delphi
var
  a,b,c:array[0..100000] of longint;
  i,n,ans:longint;
procedure qsort(r,l:longint);
var
  i,j,t,mid:longint;
begin
  i:=r; j:=l;
  mid:=a[random(j-i+1)+i];
  repeat
    while a[i]>mid do inc(i);
    while a[j]<mid do dec(j);
    if i<=j then
      begin
        t:=a[i]; a[i]:=a[j]; a[j]:=t;
        t:=b[i]; b[i]:=b[j]; b[j]:=t;
        inc(i); dec(j);
      end;
  until i>j;
  if i<l then qsort(i,l);
  if r<j then qsort(r,j);
end;
function lowbit(x:longint):longint;
begin
  exit(x and -x);
end;//求x的lowbit
function getsum(x:longint):longint;
var
  t:longint;
begin
  t:=0;
  while x>0 do
    begin
      inc(t,c[x]);
      x:=x-lowbit(x);
    end;
  exit(t);
end;//求和
procedure insert(x:longint);
begin
  while x<=n do
    begin
      inc(c[x]);//由于只有加一，所以这样子即可
      x:=x+lowbit(x);
    end;
end;//插入操作
begin
  randomize;
  readln(n);
  for i:=1 to n do
    begin
      read(a[i]);
      b[i]:=i;
    end;
  qsort(1,n);//从大到小排序
  insert(b[1]);
  for i:=2 to n do
    begin
      inc(ans,getsum(b[i]));//累加求和
      insert(b[i]);//加1
    end;
  writeln(ans);
end.
```