模拟赛的时候没想到并查集和暴力，打了个 $Tarjan$，luogu上最优解第四？？？

# 开始正题

PS：实际上应该是点双，但这个数据貌似并没有卡掉窝，大家凑活着看吧（

>若一个无向图中的去掉任意一条边都不会改变此图的连通性，即不存在桥，则称作边双连通图。一个无向图中的每一个极大边双连通子图称作此无向图的边双连通分量。

首先我们可以发现，如果 $1$ 和 $n$ 不可能在同一个边双连通分量中，因为如果在同一个边双连通分量中，无论删去哪一个点，$1$ 总能到达 $n$。

那么我们画个图就会发现，答案其实是 $1$ 所在的边双连通分量到 $n$ 所在的边双连通分量的路径上的点。

如样例

![](https://cdn.luogu.com.cn/upload/image_hosting/s2j4vqps.png)

我们可以发现，去掉 $2$ 或 $5$ 可以让 $1$ 无法到达 $6$。

![](https://cdn.luogu.com.cn/upload/image_hosting/k29va59i.png)

我们知道，在一个边双连通分量中，不管去掉其中哪一条边，点点之间总是互相连通，所以我们可以用 $Tarjan$ 将每个边双缩点。

![](https://cdn.luogu.com.cn/upload/image_hosting/hvbajc8p.png)

缩完点后，整个图将变成一个树(样例是链)。

我们发现，从 $1$ 所在的边双到 $6$ 所在的边双需要经过 $1-2$，$5-6$ 两条边。

![](https://cdn.luogu.com.cn/upload/image_hosting/34cf05ww.png)

那么我们将 $1,2,5,6$ 四点提出，去掉 $1,6$ 排序之后输出即可。

值得注意的是，由于一个边双可能只有一个点，所以需要去重。

那么我们就得到了这道题的做法：

1、存图。

2、$Tarjan$ 缩点。

3、从 $1$ 开始 $dfs$ ，得到路径上的点。

4、去重并排序后输出答案。

## 出乎意料的快

![](https://cdn.luogu.com.cn/upload/image_hosting/dz0g9uty.png)

下面是 $Pascal$代码：

```pascal
type edge=record
     t,next,f,yuan:longint;
     end;
var n,m,i,x,y,cnt,cnt1,tp,top,cnt2,ans1,ans2,cnt3,cnt4,cnt5:longint;
    h,a,dfn,low,bs,zhan,h1:array[0..100000]of longint;
    gb:array[0..2000,0..2000]of longint;
    e,e1:array[0..100000]of edge;
    vis,bbl:array[0..100000]of boolean;
function min(x,y:longint):longint;
begin
  if x<y then exit(x) else exit(y);
end;
procedure ad(x,y:longint);
begin
  inc(cnt1);
  e[cnt1].t:=y;
  e[cnt1].next:=h[x];
  e[cnt1].f:=x;
  h[x]:=cnt1;
end;
procedure ad1(x,y,t:longint);
begin
  inc(cnt4);
  e1[cnt4].t:=y;
  e1[cnt4].next:=h1[x];
  e1[cnt4].f:=x;
  e1[cnt4].yuan:=t;
  h1[x]:=cnt4;
end;
procedure sort(l,r: longint);
      var
         i,j,x,y: longint;
      begin
         i:=l;
         j:=r;
         x:=a[(l+r) div 2];
         repeat
           while a[i]<x do
            inc(i);
           while x<a[j] do
            dec(j);
           if not(i>j) then
             begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;

procedure tarjan(x,y:longint);
var t,now:longint;
begin
  inc(tp);
  dfn[x]:=tp;
  low[x]:=tp;
  inc(top);
  zhan[top]:=x;
  t:=h[x];
  while t>0 do
  begin
    if dfn[e[t].t]=0 then
    begin
      tarjan(e[t].t,x);
      low[x]:=min(low[x],low[e[t].t]);
    end else if e[t].t<>y then low[x]:=min(low[x],dfn[e[t].t]);
    t:=e[t].next;
  end;
  if dfn[x]=low[x] then
  begin inc(cnt3);
  while (zhan[top]>0) do
  begin
    bs[zhan[top]]:=cnt3;
    dec(top);
    if zhan[top+1]=x then break;
  end;
  end;
end;
function dfs(x:longint):boolean;
var t:longint;
begin
  dfs:=false;
  vis[x]:=true;
  if x=bs[n] then
    exit(true);
  t:=h1[x];
  while t>0 do
  begin
    if vis[e1[t].t]=false then
    begin
      bbl[t]:=dfs(e1[t].t);
      dfs:=dfs or bbl[t];
    end;
    t:=e1[t].next;
  end;
end;
begin
  readln(n,m); cnt2:=1;
  for i:=1 to m do
  begin
    readln(x,y);
    ad(x,y);
    ad(y,x);
  end;
  tarjan(1,0);
  if bs[1]=bs[n] then begin writeln(0);halt;end;
  for i:=1 to 2*m do
    if bs[e[i].f]<>bs[e[i].t] then
      ad1(bs[e[i].f],bs[e[i].t],i);
  dfs(bs[1]);
  for i:=1 to cnt4 do
    if bbl[i] then
    begin
      if (e[e1[i].yuan].f<>1)and(e[e1[i].yuan].f<>n) then
      begin
        inc(cnt);
        a[cnt]:=e[e1[i].yuan].f;
      end;
      if (e[e1[i].yuan].t<>1)and(e[e1[i].yuan].t<>n) then
      begin
        inc(cnt);
        a[cnt]:=e[e1[i].yuan].t;
      end;
    end;
  sort(1,cnt); for i:=1 to cnt do if a[i]<>a[i-1] then inc(cnt5);
  writeln(cnt5);
  for i:=1 to cnt do if a[i]<>a[i-1] then write(a[i],' ');
end.

```
