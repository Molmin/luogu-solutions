这道题就是寻找最短路径。适合跑广搜，只是加了一个规则：广搜时如果遇到一个字母，则无条件转移到另一个相同字母处，继续广搜


AC代码：

```delphi

type re=record
x:longint;
y:longint;
end;
var map:array[1..300,1..300]of char;//记录原图
var f:array[1..300,1..300]of longint;//记录每个点的步数
var cs:array['A'..'z',1..2]of re;//记录两个传送点
var num:array['A'..'z']of longint;//cs的配套数组
var h:array[1..100000]of re;//队列
var i,j,k,m,n,l,r,xx,yy,xxx,yyy:longint;
var qi,q:re;
var dx,dy:array[1..4]of longint;//四个方向
begin
readln(n,m);

for i:=1 to n do
begin
 for j:=1 to m do
 begin
  read(map[i,j]);
  if map[i,j]='@' then
  begin
   qi.x:=i;
   qi.y:=j;
  end;//读图的时候找到起点
  if (map[i,j]<>'@')and(map[i,j]<>'#')and(map[i,j]<>'.')and(map[i,j]<>'=') then//如果找到了字母，如果是第一次出现这个字母，
                   //那么记录到相应的cs[‘×’，１]数组里,如果是第二次出现，记录在cs[‘ｘ’，２]数组里，可以方便查询传送点信息
  begin
   inc(num[map[i,j]]);//记录了“x”是第几次出现
   cs[map[i,j],num[map[i,j]]].x:=i;
   cs[map[i,j],num[map[i,j]]].y:=j;
  end;
 end;
 readln;
end;

for i:=1 to n do
for j:=1 to m do
f[i,j]:=10000000;//初始化

dx[1]:=0;dx[2]:=0;dx[3]:=1;dx[4]:=-1;
dy[1]:=1;dy[2]:=-1;dy[3]:=0;dy[4]:=0;
l:=0;
r:=1;
h[1].x:=qi.x;
h[1].y:=qi.y;//起点入队
f[qi.x,qi.y]:=0;//初始化
while l<r do
begin
inc(l);
q:=h[l];
for i:=1 to 4 do//四个方向
begin
 xx:=q.x+dx[i];
 yy:=q.y+dy[i];//找到下一步要走的点的坐标

  if (xx>=1)and(xx<=n)and(yy>=1)and(yy<=m)and(map[xx,yy]<>'#') then//如果此点合法
                                                                                                          
  begin//我动用了紫色！这里需要明确begin下面包括几个操作。要不然begin end套死你！
        //操作1：传送点的变换 操作2：更新搜索到的点 操作3：如果走到终点 停止搜索 
//操作1开始
  if (map[xx,yy]<>'.')and(map[xx,yy]<>'@')and(map[xx,yy]<>'=') then//如果是传送点
  begin
  xxx:=xx;//很重要，先把（xx，yy）这个点copy出来为（xxx，yyy）要不然执行红色字的时候，xx
  yyy:=yy;//的值就改变了，然后执行蓝色字的时候，yy的赋值地址就变了
   if (xx=cs[map[xxx,yyy],1].x)and(yy=cs[map[xxx,yyy],1].y) then//如果这个点是传送点1
   begin
   xx:=cs[map[xxx,yyy],2].x;//把它变成传送点2
   yy:=cs[map[xxx,yyy],2].y;
   end
   else//否则的话 他就是传送点2
   //if (xx=cs[map[xx,yy],2].x)and(yy=cs[map[xx,yy],2].y) then
   begin
   xx:=cs[map[xxx,yyy],1].x;//把它变成传送点1
   yy:=cs[map[xxx,yyy],1].y;
   end;
   end;
//操作1结束
//操作2开始
   if (xx>=1)and(xx<=n)and(yy>=1)and(yy<=m)and(map[xx,yy]<>'#')and(f[xx,yy]>f[q.x,q.y]+1) then//如果是地图中的可走待更新点
   begin
   f[xx,yy]:=f[q.x,q.y]+1;//更新
   inc(r);//入队
   h[r].x:=xx;
   h[r].y:=yy;
   end;
//操作2结束
//操作3开始
   if (xx>=1)and(xx<=n)and(yy>=1)and(yy<=m)and(map[xx,yy]='=') then//如果是终点
   begin
   writeln(f[xx,yy]);//输出
   halt;
   end;
//操作3结束
 end;//结束的是 合法点的三个操作
end;//结束的是 四个方向的枚举
end;//结束的是 整个while循环

end.

```