### 这是一篇pascal题解
根据数据范围，本题是要我们在线性时间求1~n的逆元

- ##### 逆元:
 求逆元，首先我们要了解什么是逆元
 1. 取模运算的性质
 
   (a+b) mod n=(a mod n+b mod n)mod n

   a*b mod n=((a mod n)*(b mod n))mod n
   
   我们发现在模域下加减乘都是比较方便的
 1. 除法?
 
   (a/b)mod p?=(a mod p)/(b mod p) mod p
   
   。。。
   
   (12/6)mod 3=2
   
   (12 mod 3)/(6 mod 3)mod 3=0
   
   emmmmmmmm
   
   很明显除法不满足上述的性质~
   
   假若有b使得ab≡1(mod p)
   
   那么除以a就等价于乘b,这样就把除法转换成了乘法啦啦啦~~~
   
   我们把b称作a的逆元,记作a^-1
  
  1. 求逆元
    ab≡1(mod p)
    
    等等，这个形式不正像noip2012的同余方程吗~
    
    那么就可以用拓欧求啦
    
    https://www.luogu.org/problemnew/show/P1082
    
    法二：费马小定理
      
      ![](https://cdn.luogu.com.cn/upload/pic/14741.png)
      
      很明显a^-1=a^(p-2)
      
      嗯呢呢,怎么快速求呢
      
      https://www.luogu.org/problemnew/show/P1226
      
      哈哈

我们回到本题

拓欧和费马小定理一个一个的求，时间都是O(nlogn)

拓欧(83分)

```pascal
 var a,b,x,y,i:longint;
procedure gcd(a,b:longint;var x,y:longint);
begin
  if b=0 then
  begin
    x:=1;
    y:=2;
    exit;
  end;
  gcd(b,a mod b,y,x);
  y:=y-(a div b)*x;
end;
begin
  readln(a,b);
  for i:=1 to a do
  begin
    gcd(i,b,x,y);
    writeln((x mod b +b) mod b);
  end;
end.
```

费马(66分)
```pascal
function mo(b,p,k:longint):longint;
var mid,t:longint;
begin
  if p=1 then exit(b mod k);
  mid:=p div 2;
  t:=mo(b,mid,k);
  if mid=p-mid then mo:=t*t mod k
    else mo:=(t*t mod k)*(b mod k) mod k;
end;
var i,p,n:longint;
begin
  readln(n,p);
  for i:=1 to n do 
  writeln(mo(i,p-2,p));
end.
```

#### 注：以下数均在(mod p)的意义下
根据费马小定理,a的逆元为a^(p-2)，b的逆元为b^(p-2)

ab的逆元为(ab)^(p-2)=a^(p-2)*b^(p-2)

若用f(a)表示a的逆元，那么f(ab)=f(a)*f(b)

换句话说，只用求质数的逆元即可推出其他数的逆元

想到这里，本蒟蒻套上了埃氏筛，在筛质数的倍数时把逆元给乘上去(其实是素数筛那题数据比较水，让我用埃氏筛水过去，所以蒟蒻我一直把埃氏筛当欧拉筛)

埃氏筛(83分）
```pascal
var n,p,y:longint;
    f:array[1..20000528]of longint;
    b:array[1..20000528]of boolean;
    cnt,j,i,k:longint;
procedure gcd(a,b:longint;var x,y:longint);
begin
  if b=0 then
  begin
    x:=1;
    y:=2;
    exit;
  end;
  gcd(b,a mod b,y,x);
  y:=y-(a div b)*x;
end;
begin
  readln(n,p);
  for i:=1 to n do
  begin
    f[i]:=1;
    b[i]:=true;
  end;
  b[1]:=false;
  writeln(1);
   for i:=2 to n do
  begin
    if b[i] then
    begin
      gcd(i,p,f[i],y);
      f[i]:=(f[i] mod p+p)mod p; 
      j:=i+i;
      while j<n do
      begin
        b[j]:=false;
        k:=j;
        while k mod i=0 do
        begin
          f[j]:=f[j]*f[i] mod p;
          k:=k div i;
        end;
        j:=j+i;
      end;
    end;
    writeln(f[i]);
  end;
end.
```
由于时间上任是nlogn的级别，依然过不了最后一个点

既然如此，我们选择~~真正的~~欧拉筛

欧拉筛在用最小质数去清合数的是后可以更新合数的逆元，f(ap)=f(a)*f(p)，因为p<a<ap所以f(a)和f(p)都已经是已知的啦

欧拉筛(100分)
```pascal
var b:array[1..30000000]of boolean;
    a:array[1..1000000]of int64;
    f:array[1..30000000]of longint;
    n,p,k,i,j,y:longint;
procedure gcd(a,b:longint;var x,y:longint);
begin
  if b=0 then
  begin
    x:=1;
    y:=2;
    exit;
  end;
  gcd(b,a mod b,y,x);
  y:=y-(a div b)*x;
end;
begin
  f[1]:=1;
  readln(n,p);
  b[1]:=true;
  for i:=2 to n do
  begin
    if b[i]=false then
    begin
      inc(k);
      a[k]:=i;
      gcd(i,p,f[i],y);//如果是质数就求逆元
      f[i]:=(f[i] mod p+p)mod p;
    end;
    for j:=1 to k do
      if i*a[j]<=n then
      begin
        b[i*a[j]]:=true;
        f[i*a[j]]:=f[a[j]]*f[i]mod p;//用质数更新合数
        if i mod a[j]=0 then break;
      end
      else break;
  end;
  for i:=1 to n do
    writeln(f[i]);
end.
```

完美的在O(n)时间内解决

然而神犇qhy告诉我逆元可以递推求~~~

dalao的手稿
![](https://cdn.luogu.com.cn/upload/pic/14743.png)

嗯，证明很清晰啦

f(a)=(p-p div a)*f(p mod i) mod p

那么通过这个式子递推就好啦

递推版(100分)
```pascal
var n,p,y:longint;
    f:array[1..20000528]of longint;
    b:array[1..20000528]of boolean;
    cnt,j,i,k:longint;

begin
  readln(n,p);
  f[1]:=1;
  writeln(1);
  for i:=2 to n do
  begin
    f[i]:=(p-p div i)*f[p mod i]mod p;
    writeln(f[i]);
  end;
end.
```

是不是简便许多呢(￣▽￣)~*

~~还是要接受dalao的熏陶呢~~