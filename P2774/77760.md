本题的先决是 [$P3410$ 拍照](https://www.luogu.org/problemnew/show/P3410) , [题解](https://www.luogu.org/blog/acking/solution-p3410)。

我只能照着 $P3410$ 的来说喽。

$P3410$ 中我们要求给一些人拍照,一些人给你钱。最后付下属的钱。求最大的纯利润。

这一题我们也转化一下:

- 首先黑白染色 ($(i+j) \mod 2=1$ 为白色),显然一个黑色点周围都是白色,一个白色点周围都是黑色。

- 白色点会给你钱,但是白色点要去除一些黑色点。而一个黑色点被去除 (只要一次或一次以上) 要你钱,它可以被去除多次 (因为它上下左右都是白点)。

这样子就变成了 $P3410$ 的问题了。

所以每一个白色点向源点连边,黑色点向汇点连边,流量为点权。白色点向黑色点连边,流量为 $inf$。

```pascal
// luogu-judger-enable-o2
Uses math;

var
    value,reach,next:array[-1..21000] of longint;
    gap,dis,cnt:array[-1..21000] of longint;
    n,m,l,r,i,j,sum,tot,sink,source,number,maxflow:longint;

procedure add(l,r,sum:longint);
begin
    inc(tot); reach[tot]:=r; value[tot]:=sum; next[tot]:=cnt[l]; cnt[l]:=tot;
    inc(tot); reach[tot]:=l; value[tot]:=8-8; next[tot]:=cnt[r]; cnt[r]:=tot;
end;

function Dfs(now,flow:longint):longint;
var i,k,mindis,ret:longint;
begin
    mindis:=n-1; ret:=flow;
    if now=sink then exit(flow);
    i:=cnt[now];
    while i<>-1 do
    begin
        if value[i]>0 then
        begin
            if dis[now]=dis[reach[i]]+1 then
            begin
                k:=Dfs(reach[i],min(ret,value[i]));
                dec(value[i],k);inc(value[i xor 1],k);
                dec(ret,k);
                if dis[source]>=n then exit(flow-ret);
                if ret=0 then break;
            end;
            mindis:=min(mindis,dis[reach[i]]);
        end;
        i:=next[i];
    end;
    if ret=flow then
    begin
        dec(gap[dis[now]]);
        if gap[dis[now]]=0 then dis[source]:=n;
        dis[now]:=mindis+1;
        inc(gap[dis[now]]);
    end;
    exit(flow-ret);
end;

begin
    filldword(cnt,sizeof(cnt) div 4,maxlongint*2+1);

    read(n,m);
    source:=1; sink:=n*m+2; number:=1; tot:=1;
    for i:=1 to n do
        for j:=1 to m do
        begin
            read(l); inc(sum,l); inc(number);
            if ((i+j) mod 2=1) then
            begin
                add(source,number,l);
                if (j<m) then add(number,number+1,maxlongint);
                if (j>1) then add(number,number-1,maxlongint);
                if (i<n) then add(number,number+m,maxlongint);
                if (i>1) then add(number,number-m,maxlongint);
            end else add(number,sink,l);
        end;

    n:=sink; gap[source]:=n;
    while dis[source]<n do inc(maxflow,Dfs(source,maxlongint));
    writeln(sum-maxflow);
end.
```

至于我一开始为什么 $9$ 分是因为 $n,m$ 搞反了。