发一下pas的

这道题很明显就是分时间的**多源最短路径**

一般都采用Floyd算法，，

解释一下代码里的数组

\* e[][]用来存边

\* hash[]表示该村庄在当前时间段是否重建完成

\* vis[] 来表示k有没有被访问过，是用来优化Floyd的时间复杂度的

\* t[]与题面意义相符

函数：

```cpp
const INF=99999999;
var e:array[0..500,0..500] of longint;
    hash,vis:array[0..500] of boolean;
    t:array[0..500] of longint;
    N,M,Q,i,j,nowt,time,x,y,u,v,w:longint;
    Upd:boolean;
procedure Floyd();
var i,j,k:longint;
begin
    for k:=0 to N-1 do
    if (hash[k])and(vis[k]) then //如果k已经重建完成
    begin
        vis[k]:=false; //优化：每个k都只访问一次
        for i:=0 to N-1 do 
            for j:=0 to N-1 do  
                if (e[i][j]>e[i][k]+e[k][j]) then 
                    e[i][j]:=e[i][k]+e[k][j];
    end;                
end;
function Solve(x,y:longint):longint;
begin
    if not(hash[x] and hash[y]) then exit(-1);//如果有一个没重建完 返回-1
    if Upd then begin Floyd();Upd:=false;end;//如果时间更新了，就要重新求一次最短路径。
    if e[x][y]<>INF then exit(e[x][y]) else exit(-1);//如果两点之间不能到达 返回-1 否则返回最短路径
end;
procedure Update(time:longint);
var i:longint;
begin
    if time=nowt then exit;//如果现在时间和要更新的时间一样，就不用更新了
    for i:=0 to N-1 do 
        if time>=t[i] then hash[i]:=true;
    nowt:=time;Upd:=true;//更新时间
end;
begin
    readln(N,M);
    for i:=0 to N-1 do read(t[i]);
    for i:=0 to N-1 do 
        for j:=0 to N-1 do 
            if i=j then e[i][j]:=0
                else e[i][j]:=INF;
    fillchar(hash,sizeof(hash),0);
    fillchar(vis,sizeof(vis),1);
    for i:=1 to M do 
    begin
        readln(u,v,w);
        e[u][v]:=w;e[v][u]:=w;//邻接矩阵
    end;
    readln(Q);Upd:=false;
    nowt:=-1;Update(0);
    for i:=1 to Q do 
    begin
        readln(x,y,time);
        Update(time);//更新时间
        writeln(Solve(x,y));//求最短路径
    end;
end.
* Floyd() 求多源最短路径

* Solve(x,y) 求在当前时间x到y的最短距离

* Update(time) 更新时间

```