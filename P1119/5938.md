数据规模比较小，所以用矩阵+离线floyd（在线spfa貌似要超时）

floyd算法中枚举的k是中转点，在这道题中就可以按时间顺序把点当作中转点，挨个儿加入图中，并且同时将‘时间恰当的询问’求出来（是指询问的时间<=t[k]的询问）

﹡注意题中所给的数据已经排好了序

```delphi
const
  inf=100010;
var
  e:array[0..200,0..200] of longint;
  t:array[0..200] of longint;
  qs,qt,tq:array[1..50000] of longint;
  n,m,x,y,z,q:longint;
  l,i,j,k:longint;
procedure print(l:longint);
  begin
    if (tq[l]<t[qs[l]]) or (tq[l]<t[qt[l]]) then
      writeln(-1)
    else
      if e[qs[l],qt[l]]=inf then
        writeln(-1)
      else
        writeln(e[qs[l],qt[l]]);
  end;
begin
  read(n,m);
  for i:=0 to n-1 do
    for j:=0 to n-1 do
      e[i,j]:=inf;
  for i:=0 to n-1 do read(t[i]);
  for i:=1 to m do
    begin
      read(x,y,z);
      e[x,y]:=z;
      e[y,x]:=z;
    end;
  for i:=0 to n-1 do e[i,i]:=0;

  read(q);
  for i:=1 to q do read(qs[i],qt[i],tq[i]);
//读入完毕，floyd开始
  l:=1;
  for k:=0 to n-1 do
    begin
      while (tq[l]<t[k]) and (l<=q) do  //询问的时间小于当前时间则输出
        begin
          print(l);
          inc(l);
        end;
      for i:=0 to n-1 do
        for j:=0 to n-1 do
          if e[i,j]>e[i,k]+e[k,j] then
            e[i,j]:=e[i,k]+e[k,j];   //将k点加入图中并更新最短路径
    end;

  while l<=q do
    begin
      print(l);
      inc(l);
    end;
end.
```