由于这里没有P的模板，我就发一个上来

伸展树，或者叫自适应查找树，是一种用于保存有序集合的简单高效的数据结构。伸展树实质上是一个二叉查找树。允许查找，插入，删除，删除最小，删除最大，分割，合并等许多操作，这些操作的时间复杂度为O(logN)。由于伸展树可以适应需求序列，因此他们的性能在实际应用中更优秀。

伸展树支持所有的二叉树操作。伸展树不保证最坏情况下的时间复杂度为O(logN)。伸展树的时间复杂度边界是均摊的。尽管一个单独的操作可能很耗时，但对于一个任意的操作序列，时间复杂度可以保证为O(logN)。

二、自调整和均摊分析：

平衡查找树的一些限制：

1、平衡查找树每个节点都需要保存额外的信息。

2、难于实现，因此插入和删除操作复杂度高，且是潜在的错误点。

3、对于简单的输入，性能并没有什么提高。

平衡查找树可以考虑提高性能的地方：

1、平衡查找树在最差、平均和最坏情况下的时间复杂度在本质上是相同的。

2、对一个节点的访问，如果第二次访问的时间小于第一次访问，将是非常好的事情。

3、90-10法则。在实际情况中，90%的访问发生在10%的数据上。

4、处理好那90%的情况就很好了。

在一颗二叉树中访问一个节点的时间复杂度是这个节点的深度。因此，我们可以重构树的结构，使得被经常访问的节点朝树根的方向移动。尽管这会引入额外的操作，但是经常被访问的节点被移动到了靠近根的位置，因此，对于这部分节点，我们可以很快的访问。根据上面的90-10法则，这样做可以提高性能。

为了达到上面的目的，我们需要使用一种策略──旋转到根(rotate-to-root)。

五、基本伸展树操作：

1、插入：

当一个节点插入时，伸展操作将执行。因此，新插入的节点在根上。

2、查找：

如果查找成功（找到），那么由于伸展操作，被查找的节点成为树的新根。

如果查找失败（没有），那么在查找遇到NULL之前的那个节点成为新的根。也就是，如果查找的节点在树中，那么，此时根上的节点就是距离这个节点最近的节点。

3、查找最大最小：

查找之后执行伸展。

4、删除最大最小：

a)删除最小：

首先执行查找最小的操作。

这时，要删除的节点就在根上。根据二叉查找树的特点，根没有左子节点。

使用根的右子结点作为新的根，删除旧的包含最小值的根。

b)删除最大：

首先执行查找最大的操作。

删除根，并把被删除的根的左子结点作为新的根。

5、删除：

将要删除的节点移至根。

删除根，剩下两个子树L（左子树）和R（右子树）。

使用DeleteMax查找L的最大节点，此时，L的根没有右子树。

使R成为L的根的右子树。



```cpp
const maxn=100007;
type tp=record
          left,right,data,father:longint;
        end;
var
  tree:array[0..maxn]of tp;
  tot,now,ans,min,root,prev,next,same,n:longint;
procedure init;
begin
  readln(n);
  fillchar(tree,sizeof(tree),0);
  read(tree[1].data); root:=1;
  ans:=tree[1].data;
end;
procedure leftrotate(x:longint);
var y:longint;
begin
  y:=tree[x].father;
  tree[y].right:=tree[x].left;
  if tree[x].left<>0 then tree[tree[x].left].father:=y;
  tree[x].father:=tree[y].father;
  if tree[y].father<>0 then
  begin
    if y=tree[tree[y].father].left then tree[tree[y].father].left:=x
                                   else tree[tree[y].father].right:=x;
  end;
  tree[y].father:=x;
  tree[x].left:=y;
end;
procedure rightrotate(x:longint);
var y:longint;
begin
  y:=tree[x].father;
  tree[y].left:=tree[x].right;
  if tree[x].right<>0 then tree[tree[x].right].father:=y;
  tree[x].father:=tree[y].father;
  if tree[x].father<>0 then
  begin
    if tree[tree[y].father].left=y then tree[tree[y].father].left:=x
                                   else tree[tree[y].father].right:=x;
  end;
  tree[y].father:=x;
  tree[x].right:=y;
end;
procedure splay(now:longint);
var t:longint;
begin
  while tree[now].father<>0 do
  begin
    t:=tree[now].father;
    if tree[t].father=0 then
    begin
      if tree[t].left=now then rightrotate(now) else leftrotate(now);
      break;
    end;
    if now=tree[t].left then
        begin
          if t=tree[tree[t].father].left then
            begin rightrotate(t); rightrotate(now); end
          else
            begin rightrotate(now); leftrotate(now); end;
        end
      else
        begin
          if t=tree[tree[t].father].right then
            begin leftrotate(t); leftrotate(now); end
          else
            begin leftrotate(now); rightrotate(now); end;
        end;
  end;
  root:=now;
end;
procedure insert;
var t:longint;
begin
  t:=root;   same:=now;
  while true do
  begin
    if tree[t].data=tree[now].data then
    begin min:=0; same:=t; exit; end;
    if tree[t].data>tree[now].data then
      begin
        if tree[t].left=0 then
        begin tree[now].father:=t; tree[t].left:=now; exit; end
        else t:=tree[t].left;
      end
    else
      begin
        if tree[t].right=0 then
        begin tree[now].father:=t; tree[t].right:=now; exit; end
        else t:=tree[t].right;
      end;
  end;
end;
procedure findprev;
begin
  prev:=tree[now].left;
  while prev<>0 do
  begin
    if tree[prev].right=0 then exit;
    prev:=tree[prev].right;
  end;
end;
procedure findnext;
begin
  next:=tree[now].right;
  while next<>0 do
  begin
    if tree[next].left=0 then exit;
    next:=tree[next].left;
  end;
end;
procedure work;
begin
  read(tree[now].data);
  min:=maxlongint;
  insert;
  splay(same);
  if min>0 then
  begin
    findprev;
    findnext;
    if (next<>0)and((prev=0)or
    (tree[next].data-tree[now].data<tree[now].data-tree[prev].data))
    then min:=tree[next].data-tree[now].data
    else min:=tree[now].data-tree[prev].data;
  end;
  ans:=ans+min;
end;
begin
  init;
  for now:=2 to n do  work;
  writeln(ans);
end.
```