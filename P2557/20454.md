~~芝麻开门.~~

题意.求出$n^k$所有的约数和..

若整数$i\ (1<=i<=p)$能整除$p$.则$i$是$p$的约数..

此题把$n$分解1下...

设$n = 180 = 2*2*3*3*5$.

若只考虑$2$.有3种约数$(1,2,4)$.

同理有约数$(1,3,9)$,$(1,5)$.

可以发现其它的约数是通过组合出来的..

所以神奇的转换成多项式乘法..

约数和就是.$(1+2+4)*(1+3+9)*(1+5) = 546\ (n = 180,k = 1)$.

这里多了个$k$.那么每1种约数的个数变成$k$倍.

最后用上高精就好了..

~~复杂度难以分析..~~

奇怪的参考文献.[....](http://oi.nks.edu.cn/zh/Discussion/Details/1736?entry=prob&pid=1374 "....").

Diu代码..

```cpp
program P2557;
 type
  gg=record
   f:array[0..201] of int64;                   //高精数组..
   s:longint;                                  //数组的位数.
  end;
 var
  a,b:gg;
  i,j,k,n,s:longint;
 procedure hahaincdec(var a:gg);               //进退位处理..
  var
   i:longint;
  begin
   i:=1;
   while i<=a.s do
    begin
     if a.f[i]>=10 then
      begin
       inc(a.f[i+1],a.f[i] div 10);
       a.f[i]:=a.f[i] mod 10;
       if i=a.s then inc(a.s);
      end;
     if a.f[i]<=-1 then
      begin
       inc(a.f[i+1],(a.f[i]-9) div 10);
       a.f[i]:=a.f[i] mod 10+10;
      end;
     inc(i);
    end;
   while (a.s>=2) and (a.f[a.s]=0) do dec(a.s);
  end;
 operator *(a,b:gg)c:gg;                   //高精*高精.
  var
   i,j:longint;
  begin
   fillchar(c,sizeof(c),0);
   for i:=1 to a.s do
    for j:=1 to b.s do
     inc(c.f[i+j-1],a.f[i]*b.f[j]);
   c.s:=a.s+b.s-1;
   hahaincdec(c);
  end;
 operator **(a:gg;b:longint)c:gg;         //高精*低精.
  var
   i,j:longint;
  begin
   for i:=1 to a.s do
    a.f[i]:=a.f[i]*b;
   hahaincdec(a);
   c:=a;
  end;
 operator -(a:gg;b:longint)c:gg;          //高精-低精.
  var
   i:longint;
  begin
   dec(a.f[1],b);
   hahaincdec(a);
   c:=a;
  end;
 operator /(a:gg;b:longint)c:gg;          //高精/低精.
  var
   i:longint;
   p:int64;
  begin
   p:=0;
   for i:=a.s downto 1 do
    begin
     p:=p*10+a.f[i];
     a.f[i]:=p div b;
     p:=p mod b;
    end;
   hahaincdec(a);
   c:=a;
  end;
 begin
  readln(n,k);
  if n=1 then writeln(n)                 //说明n永远是1....
         else
  begin
   a.s:=1;
   a.f[a.s]:=1;                          //用于累计最后结果.
   for i:=2 to n do                      //枚举约数..
    begin
     s:=0;
     fillchar(b,sizeof(b),0);            //用于计算当前约数结果..
     b.s:=1;
     b.f[b.s]:=1;
     while n mod i=0 do
      begin
       inc(s);                           //约数个数..
       n:=n div i;
      end;
     s:=s*k;                             //k倍..
     for j:=1 to s+1 do
      b:=b**i;                           //用等比数列求和公式计算...
     b:=(b-1)/(i-1);
     a:=a*b;                             //累乘..
    end;
   for i:=a.s downto 1 do
    write(a.f[i]);
   writeln;
  end;
 end.
```
(ಡωಡ).
