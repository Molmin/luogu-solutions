引用楼下的一句话：

## 脑子都快被剖炸了

虽然是道模板题，但我觉得树链剖分学习起来还是有一定困难的。

这里给大家推荐一个[入门树链剖分的博文](http://blog.sina.com.cn/s/blog\_6974c8b20100zc61.html)，一些详细的概念可以参照里面。

**但是本蒟蒻表示看完还是不太明白**

于是在这里写一写**我学树链剖分时遇到的问题**：

首先，我从离NOIP2017 100多天时开始学树链剖分，然后在距离比赛第九天时终于学完了。。。（懒癌晚期）

**回归正题**

（这里的概念直接借用了刚刚的博文中的名称）

（问答显示展现）

- 1：
- Q：为什么树链剖分扯到线段树上去了？

- A：通过线段树达到查询，维护这些树上点和边的关系。

- 2：
- Q：为什么要使一条重链各边在线段树中连续分布？

- A：这与我们实现我们的树链剖分所用的线段树有关。我们修改时一般对一个子树或一条链操作，而使当前子树的边在线段树中分布在连续区间里，我们就方便通过线段树进行操作。


以上就是我学习遇到的问题所在了，相信大部分同志也会遇到。

附带一些实践的小细节，在我的代码中展现：

**（温馨提示：注意随时取模，因此作为P选手longint即可（看不下去楼下一堆写long long的））**

```cpp
//Luogu P3384

type
     edgetype=record
      point,next:longint;
     end;

var
    edge:array[0..200005]of edgetype;    //edge邻接表存边；开双倍大小（无向图一条边要存两个方向的）
    tree,lazy:array[0..300005]of longint;    //tree是线段树节点的信息；lazy是每个节点的懒标记
    last,siz,fa,dep,son,top,id,a,real:array[0..100000]of longint;    
    {last[v]表示邻接表中以v为起点的边最后一次出现的位置
     siz[v]表示以节点v为根的子树大小，fa[v]表示节点v的父节点，dep[v]表示节点v的深度，son[v]表示节点v的重儿子（没有则是-1），
     top[v]表示节点v所在重链的顶端节点，id[v]表示v在线段树中的位置，real表示线段树的叶子节点对应实际树中点的编号，
     a则是每个节点的初始值}
    es,ps,n,m,i,r,p,x,y,z,ty:longint;

procedure ready;
 begin
       es:=0; ps:=0;    //es记录存入边时边的编号，ps记录在DFS2过程时点在线段树底端的编号
       filldword(last,sizeof(last) div 4,0);
       filldword(lazy,sizeof(lazy) div 4,0);    //last和lazy初始化
 end;

procedure swap(var x1,x2:longint);
 var temp:longint;
 begin
       temp:=x1; x1:=x2; x2:=temp;
 end;

procedure addedge(u,v:longint);    //加边
 begin
       inc(es);
       edge[es].point:=v;
       edge[es].next:=last[u];
       last[u]:=es;
 end;

//Part. Sugment Tree 线段树基本操作

{
function max(n1,n2:longint):longint;
 begin
       if (n1>n2) then exit(n1) else exit(n2);
 end;}

procedure pushup(rt:longint);
 begin
       tree[rt]:=(tree[rt*2]+tree[rt*2+1]) mod p;
 end;

procedure pushdown(rt,l,r:longint);
 var mid,ls,rs,ll,lr:longint;
 begin
       if (lazy[rt]>0) then
       begin
             mid:=(l+r) div 2; ls:=rt*2; rs:=ls+1; ll:=mid-l+1; lr:=r-mid;
             tree[ls]:=(tree[ls]+lazy[rt]*ll mod p) mod p;
             lazy[ls]:=(lazy[ls]+lazy[rt]) mod p;
             tree[rs]:=(tree[rs]+lazy[rt]*lr mod p) mod p;
             lazy[rs]:=(lazy[rs]+lazy[rt]) mod p;
             lazy[rt]:=0;
       end;
 end;

procedure build(rt,l,r:longint);    //普通的建树
 var mid:longint;
 begin
       if (l=r) then
       begin
             tree[rt]:=a[real[l]] mod p;
             exit;
       end;
       mid:=(l+r) div 2;
       build(rt*2,l,mid);
       build(rt*2+1,mid+1,r);
       pushup(rt);
 end;

procedure updata(rt,l,r,ql,qr,w:longint);    //更新
 var mid:longint;
 begin
       if ((ql<=l) and (r<=qr)) then
       begin
             tree[rt]:=(tree[rt]+(r-l+1)*w mod p) mod p;
             lazy[rt]:=(lazy[rt]+w) mod p;
             exit;
       end;
       mid:=(l+r) div 2;
       pushdown(rt,l,r);
       if (ql<=mid) then updata(rt*2,l,mid,ql,qr,w);
       if (qr>mid) then updata(rt*2+1,mid+1,r,ql,qr,w);
       pushup(rt);
 end;

function query(rt,l,r,ql,qr:longint):longint;    //查询
 var mid:longint;
 begin
       if ((ql<=l) and (r<=qr)) then exit(tree[rt]);
       mid:=(l+r) div 2; query:=0;
       pushdown(rt,l,r);
       if (ql<=mid) then query:=(query+query(rt*2,l,mid,ql,qr)) mod p;
       if (qr>mid) then query:=(query+query(rt*2+1,mid+1,r,ql,qr)) mod p;
       pushup(rt);
 end;

//Part. End

procedure dfs1(u,f:longint);    //得到fa,dep,siz,son的值
 var now,v:longint;
 begin
       fa[u]:=f; dep[u]:=dep[f]+1; siz[u]:=1; son[u]:=-1;
       now:=last[u];
       while (now>0) do
       begin
             v:=edge[now].point;
             if (v<>f) then
             begin
                   dfs1(v,u);
                   inc(siz[u],siz[v]);
                   if ((son[u]=-1) or (siz[v]>siz[son[u]])) then son[u]:=v;
             end;
             now:=edge[now].next;
       end;
 end;

procedure dfs2(u,tp:longint);    //将树放入线段树，同时求出top和id
 var now,v:longint;
 begin
       inc(ps); id[u]:=ps; top[u]:=tp; real[ps]:=u;    //ps就是类似es变量的功能
       if (son[u]=-1) then exit;
       dfs2(son[u],tp);
       now:=last[u];
       while (now>0) do
       begin
             v:=edge[now].point;
             if ((v<>son[u]) and (v<>fa[u])) then dfs2(v,v);
             now:=edge[now].next;
       end;
 end;

procedure add(u,v,w:longint);    //在树上u到v的路径上加上w
 var f1,f2:longint;
 begin
       f1:=top[u]; f2:=top[v];
       while (f1<>f2) do    //基本的树链剖分更新操作，不解释（不明白可以看那份博文里的解释）
       begin
             if (dep[f1]<dep[f2]) then
             begin
                   swap(f1,f2);
                   swap(u,v);
             end;
             updata(1,1,n,id[f1],id[u],w);    //更新树上u到顶端f1节点的路径
             u:=fa[f1];
             f1:=top[u];
       end;
       if (dep[u]>dep[v]) then swap(u,v);
       updata(1,1,n,id[u],id[v],w);    //同一链上更新u到v的路径
 end;

function getsum(u,v:longint):longint;    //树上求和
 var ans,f1,f2:longint;
 begin
       ans:=0;
       f1:=top[u]; f2:=top[v];
       while (f1<>f2) do    //类似add过程的操作
       begin
             if (dep[f1]<dep[f2]) then
             begin
                   swap(f1,f2);
                   swap(u,v);
             end;
             ans:=(ans+query(1,1,n,id[f1],id[u])) mod p;
             u:=fa[f1];
             f1:=top[u];
       end;
       if (dep[u]>dep[v]) then swap(u,v);
       ans:=(ans+query(1,1,n,id[u],id[v])) mod p;
       exit(ans);
 end;

begin
      ready;

      read(n,m,r,p);
      for i:=1 to n do read(a[i]);
      for i:=1 to n-1 do
      begin
            read(x,y);
            addedge(x,y);
            addedge(y,x);
      end;

      dep[0]:=0;
      dfs1(r,0);
      dfs2(r,r);
      build(1,1,n);

      for i:=1 to m do
      begin
            read(ty);
            case ty of
             1:begin    //1操作
                     read(x,y,z);
                     add(x,y,z);
               end;
             2:begin    //2操作
                     read(x,y);
                     writeln(getsum(x,y));
               end;
             3:begin    //3操作
                     read(x,z);
                     updata(1,1,n,id[x],id[x]+siz[x]-1,z);    
                     //因为是子树上操作所以直接用updata即可，id[x]~id[x]+siz[x]-1即以x为根节点的子树在线段树中的区间
               end;
             4:begin    //4操作
                     read(x);
                     writeln(query(1,1,n,id[x],id[x]+siz[x]-1));    //因为是子树上查询所以直接用query即可
               end;
            end;
      end;
end.
```
（有问题的童鞋可以私信我~）
