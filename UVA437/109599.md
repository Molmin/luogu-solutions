- 题意：现在有n种长方体，每种都有无穷多个，现在选择一些立方体摞成一根尽量高的柱子（对于每一个立方体来说可以选择任一条边作为高），要求上方的立方体的底面长宽严格小于它下方立方体的底面长宽。
**
- 思路：在任何时候，只有顶面的尺寸会影响到后续决策，因此可以用二元组(a,b)来表示“顶面尺寸为a×b”这个状态。因为每次增加一个立方体以后顶面的长和宽都会严格减小，所以这个图是DAG，可以套用前面学过的DAG最长路算法。

  可以用(idx, k)这个二元组来“间接”表达这个状态，其中idx为顶面立方体的序号，k是高的序号（输入时把每个立方体的3个维度编号为0～2）。例如，若立方体3的大小为a*b*c，则状态(3,1)就是指顶面为a×c或c×a且高是b。因为idx是0～n－1的整数，k是0～2的整数，所以可以很方便地用二维数组来存取。状态总数是O(3×n)的，每个状态的决策有O(n)个，时间复杂度为 O(n^2)

 - 与 P1034 矩形覆盖 类似用记忆化搜索做（只不过把一个平面改成了长方体，也就是三个平面）
 
 上代码
 ```pascal
var
x,y,z,f:array[1..90]of longint;
n,i,s,tx,ty,tz,temp:longint;
function min(x,y:longint):longint;
 begin
 if x<y then
 exit(x)
 else
 exit(y);
 end;
function max(x,y:longint):longint;
 begin
 if x>y then
 exit(x)
 else
 exit(y);
 end;
function dfs(t:longint):longint;  //开始搜索
 var
 i:longint;
 begin
 if f[t]>0 then
 exit(f[t]);
 f[t]:=z[t];
 for i:=1 to n*3 do
  if (x[t]<x[i]) and (y[t]<y[i]) then //能搭上去则继续搜索
  f[t]:=max(f[t],dfs(i)+z[t]);
 exit(f[t]);
 end;
begin
temp:=0;
while true do
begin
inc(temp);
read(n);
 if n=0 then
 halt;
 for i:=1 to n do
 begin
 read(tx,ty,tz);
 x[i*3-2]:=max(tx,ty);  //就把一个立方体转化成了三个平面
 y[i*3-2]:=min(tx,ty);
 z[i*3-2]:=tz;
 x[i*3-1]:=max(tx,tz);
 y[i*3-1]:=min(tx,tz);
 z[i*3-1]:=ty;
 x[i*3]:=max(ty,tz);
 y[i*3]:=min(ty,tz);
 z[i*3]:=tx;
 end;
fillchar(f,sizeof(f),0);  //每做完一次一定要注意初始化
s:=0;
 for i:=1 to n*3 do
 s:=max(s,dfs(i));
writeln('Case ',temp,': maximum height = ',s);  //注意‘Case ’后面有个很恶心的空格，‘: maximum height = ’冒号后面，等号前后也有空格
end;
end.
```