高中以后把平衡树什么的都忘光了...

大学也开始接触了C语言，但是还是偶尔想写写Pascal，回忆一下从前。（顺便配置了vscode的Pascal环境????）

算法：**快排**、**线段树**

先按时间排序，离散化所需奶牛编号，线段树记录最大值、最大值个数、某个最大值的奶牛编号。按时间轴修改，一旦最大值个数或最大值奶牛编号变化了，答案+1。

```pascal
program luogu4087;
var
    n,g,i,m,_sz,_id,ans:Longint;
    t,a,b,c,u,v:Array[0..1000005]of Longint;
    mx,sz,id:Array[0..4000050]of Longint;

procedure swap(var a,b:Longint);
var c:Longint; begin c:=a; a:=b; b:=c end;

procedure qs(l,r:Longint);
var i,j,m:Longint;
begin
    if l>r then exit;
    i:=l; j:=r; m:=t[(l+r)shr 1];
    repeat
        while t[i]<m do inc(i);
        while t[j]>m do dec(j);
        if i<=j then begin swap(t[i],t[j]); swap(a[i],a[j]); swap(b[i],b[j]); inc(i); dec(j) end
    until i>j;
    if i<r then qs(i,r);
    if l<j then qs(l,j)
end;

procedure qs2(l,r:Longint);
var i,j,m:Longint;
begin
    if l>r then exit;
    i:=l; j:=r; m:=u[(l+r)shr 1];
    repeat
        while u[i]<m do inc(i);
        while u[j]>m do dec(j);
        if i<=j then begin swap(u[i],u[j]); swap(v[i],v[j]); inc(i); dec(j) end
    until i>j;
    if i<r then qs2(i,r);
    if l<j then qs2(l,j)
end;

procedure pu(u:Longint);
begin
    if mx[u+u]=mx[u+u+1] then begin
        mx[u]:=mx[u+u];
        sz[u]:=sz[u+u]+sz[u+u+1];
        id[u]:=id[u+u]
    end 
    else if mx[u+u]>mx[u+u+1] then begin
        mx[u]:=mx[u+u];
        sz[u]:=sz[u+u];
        id[u]:=id[u+u]
    end 
    else begin
        mx[u]:=mx[u+u+1];
        sz[u]:=sz[u+u+1];
        id[u]:=id[u+u+1]
    end
end;

procedure bd(k,l,r:Longint);
var m:Longint;
begin
    if l=r then begin
        mx[k]:=G;
        sz[k]:=1;
        id[k]:=l;
        exit;
    end;
    m:=(l+r)shr 1;
    bd(k+k,l,m);
    bd(k+k+1,m+1,r);
    pu(k);
end;

procedure ct(k,l,r,x,y:Longint);
var m:Longint;
begin
    if l=r then begin
        inc(mx[k],y);
        exit
    end;
    m:=(l+r)shr 1;
    if m>=x then ct(k+k,l,m,x,y)
            else ct(k+k+1,m+1,r,x,y);
    pu(k)
end;

begin
    m:=0;
    ans:=0;
    read(n,G);
    for i:=1 to n do read(t[i],a[i],b[i]);
    qs(1,n);
    for i:=1 to n do begin
        u[i]:=a[i];
        v[i]:=i
    end;
    qs2(1,n);
    for i:=1 to n do begin
        if u[i]<>u[i-1] then begin
            inc(m);
            u[m]:=u[i];
        end;
        c[v[i]]:=m
    end;
    inc(m);
    bd(1,1,m);
    _sz:=sz[1];
    _id:=id[1];
    for i:=1 to n do begin
        ct(1,1,m,c[i],b[i]);
        if (sz[1]<>_sz)or(id[1]<>_id) then inc(ans);
        _sz:=sz[1];
        _id:=id[1]
    end;
    write(ans)
end.
```
