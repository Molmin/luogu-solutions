这题我们使用kdtree，显然的模板题，

我们枚举每个点，都做一遍最小曼哈顿距离，和最大曼哈顿距离。找一个差值最小即可。


代码如下（pascal）

```cpp
type pp=record
     max,min,d:array[0..1]of longint;
     l,r:longint;
end;
var ans,mn,mx,anss,root,i,j,n:longint;
    a:array[0..500005]of pp;
    t:pp;
procedure swap(var x,y:longint);
var z:longint;
begin
  z:=x; x:=y; y:=z;
end;
procedure sort(l,r,k,dd:longint);
var i,j,x:longint;
begin
  i:=l; j:=r; x:=a[(l+r) div 2].d[dd];
  while i<=j do
    begin
    while a[i].d[dd]<x do inc(i);
    while x<a[j].d[dd] do dec(j);
    if i<=j then begin swap(a[i].d[0],a[j].d[0]); swap(a[i].d[1],a[j].d[1]); inc(i); dec(j); end;
    end;
  if (l<j)and(k<=j) then sort(l,j,k,dd);
  if (i<r)and(k>=i) then sort(i,r,k,dd);
end;
function max(x,y:longint):longint;
begin
  if x>y then exit(x) else exit(y);
end;
function min(x,y:longint):longint;
begin
  if x<y then exit(x) else exit(y);
end;
procedure get(k:longint);
var i:longint;
begin
  for i:=0 to 1 do begin a[k].min[i]:=a[k].d[i]; a[k].max[i]:=a[k].d[i]; end;
end;
procedure update(k:longint);
var i:longint;
begin
  if a[k].l<>0 then
    for i:=0 to 1 do
      begin
      a[k].min[i]:=min(a[k].min[i],a[a[k].l].min[i]);
      a[k].max[i]:=max(a[k].max[i],a[a[k].l].max[i]);
      end;
  if a[k].r<>0 then
    for i:=0 to 1 do
      begin
      a[k].min[i]:=min(a[k].min[i],a[a[k].r].min[i]);
      a[k].max[i]:=max(a[k].max[i],a[a[k].r].max[i]);
      end;
end;
function build(l,r,dd:longint):longint;
var mid:longint;
begin
  mid:=(l+r) div 2;
  sort(l,r,mid,dd); get(mid);
  if mid>l then a[mid].l:=build(l,mid-1,dd xor 1);
  if mid<r then a[mid].r:=build(mid+1,r,dd xor 1);
  update(mid);
  exit(mid);
end;
function getdis(k:longint):longint;
begin
  exit(abs(a[k].d[0]-t.d[0])+abs(a[k].d[1]-t.d[1]));
end;
function cal1(k:longint):longint;
var i,res:longint;
begin
  res:=0;
  for i:=0 to 1 do begin inc(res,max(0,a[k].min[i]-t.d[i])); inc(res,max(0,t.d[i]-a[k].max[i])); end;
  exit(res);
end;
procedure querymn(k:longint);
var d0,dl,dr,l,r:longint;
begin
  d0:=getdis(k);
  if d0<>0 then ans:=min(d0,ans);
  l:=a[k].l; r:=a[k].r;
  if l<>0 then dl:=cal1(l) else dl:=100000000;
  if r<>0 then dr:=cal1(r) else dr:=100000000;
  if dl<dr then
    begin
    if dl<ans then querymn(l);
    if dr<ans then querymn(r);
    end
    else
    begin
    if dr<ans then querymn(r);
    if dl<ans then querymn(l);
    end;
end;
function cal2(k:longint):longint;
var i,res:longint;
begin
  res:=0;
  for i:=0 to 1 do inc(res,max(abs(t.d[i]-a[k].max[i]),abs(t.d[i]-a[k].min[i])));
  exit(res);
end;
procedure querymx(k:longint);
var d0,dl,dr,l,r:longint;
begin
  d0:=getdis(k); ans:=max(ans,d0); l:=a[k].l; r:=a[k].r;
  if l<>0 then dl:=cal2(l) else dl:=-100000000;
  if r<>0 then dr:=cal2(r) else dr:=-100000000;
  if dl>dr then
    begin
    if dl>ans then querymx(l);
    if dr>ans then querymx(r);
    end
    else
    begin
    if dr>ans then querymx(r);
    if dl>ans then querymx(l);
    end;
end;
function query(f,x,y:longint):longint;
begin
  t.d[0]:=x; t.d[1]:=y;
  if f=0 then begin ans:=100000000; querymn(root); end
    else begin ans:=-100000000; querymx(root); end;
  exit(ans);
end;
begin
  readln(n);
  for i:=1 to n do readln(a[i].d[0],a[i].d[1]);
  root:=build(1,n,0); anss:=maxlongint;
  for i:=1 to n do
    begin
    mn:=query(0,a[i].d[0],a[i].d[1]); mx:=query(1,a[i].d[0],a[i].d[1]);
    anss:=min(mx-mn,anss);
    end;
  writeln(anss);
end.
```