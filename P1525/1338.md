
```cpp
program prison;
const
  maxn=20000; maxm=100000;
var
  n,m:longint;
  f,s:array[0..maxn+1] of longint;
  a,b,c:array[0..maxm+1] of longint;
function findsets(k:longint):longint;
var
  y:longint;
begin
  if f[k]<>k then
    begin
    y:=f[k]; f[k]:=findsets(y);  {路径的压缩}
    s[k]:=(s[k]+s[y]) mod 2;  {在路径压缩中正确的建立与新的父亲的新的关系}
    end;
  exit(f[k]);
end;
procedure combinesets(a,b:longint);
begin
  s[f[a]]:=(s[a]+1) mod 2;  {建立a的父亲与b的关系，1为敌人，0为朋友}
  f[f[a]]:=b;  {改变a的父亲的对象，为b}
end;
procedure work;
var
  i,sa,sb:longint;
begin
  for i:=m downto 1 do  {枚举每条边，进行二分图的判断}  
    begin
    sa:=findsets(a[i]); sb:=findsets(b[i]);
    if sa=sb then
      begin
      if s[a[i]]=s[b[i]] then  {当二者之前发生过关系，并且现在的关系和之前的关系相矛盾，则说明当前的为输出解}
        begin
        writeln(c[i]);
        exit;
        end;
      end
    else combinesets(a[i],b[i]);  {否则将二者合并，建立关系}
    end;
  writeln(0);{删去所有的边后仍然符合条件，则说明此时应当输出0}
end;
procedure qsort(l,r:longint);  {将读入的边按照大小排序}
var
  i,j,x,y:longint;
begin
  i:=l; j:=r; x:=c[(l+r) div 2];
  repeat
    while c[i]<x do inc(i);
    while c[j]>x do dec(j);
    if not (i>j) then
      begin
      y:=a[i]; a[i]:=a[j]; a[j]:=y;
      y:=b[i]; b[i]:=b[j]; b[j]:=y;
      y:=c[i]; c[i]:=c[j]; c[j]:=y;
      inc(i); dec(j);
      end;
  until i>j;
  if j>l then qsort(l,j);
  if i<r then qsort(i,r);
end;
procedure makesets;  {建立一个新的并查集}
var
  i:longint;
begin
  for i:=1 to n do f[i]:=i;
end;
procedure reads;  {读入数据}
var
  i:longint;
begin
  readln(n,m);
  makesets;
  for i:=1 to m do readln(a[i],b[i],c[i]);
end;
begin
  assign(input,'prison.in');
  reset(input);
  assign(output,'prison.out');
  rewrite(output);
  reads; 
  qsort(1,m);
  work;
  close(input);
  close(output);
end.
就是个图论并查集。。
```