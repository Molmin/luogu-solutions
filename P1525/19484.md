这道题的基本思路是并查集，如果对并查集不熟悉，建议先去做P3367，P1551，P1892。

因为这道题要我们输出的是‘影响力最小的事件’，所以我们只要把影响力从大到小排序（同时排对应的人），然后一件件处理。

如果两个人在同一个并查集里，那么就可以输出并退出程序了。

代码如下：





```cpp
var i,j,k,l,m,n:longint;
    a,b,c,e,f:array[0..100001]of longint;
procedure sort(l,r:longint);//经典快排
var i,j,t,v:longint;
  begin
    i:=l;
    j:=r;
    v:=c[(i+j)div 2];
    while i<j do
      begin
        while c[i]>v do inc(i);
        while c[j]<v do dec(j);
        if i<=j then
          begin
            t:=a[i]; a[i]:=a[j]; a[j]:=t;//记得把人也一起交换
            t:=b[i]; b[i]:=b[j]; b[j]:=t;
            t:=c[i]; c[i]:=c[j]; c[j]:=t;
            inc(i); dec(j);
          end;
      end;
    if l<j then sort(l,j);
    if i<r then sort(i,r);
  end;
function gf(k:longint):longint;//并查集必备：查找一个顶点的根节点
  begin
    if f[k]=k then exit(k);
    f[k]:=gf(f[k]);//这里做了一个优化，源代码是‘exit(gf(f[k]));'
    exit(f[k]);//现在用这种方式可以把当前节点的f[k]直接赋值为它的根节点，使得之后的查找不用再搜一遍
  end;
begin
  readln(n,m);
  for i:=1 to n do f[i]:=i;
  for i:=1 to m do readln(a[i],b[i],c[i]);
  sort(1,m);
  for i:=1 to m do
    begin
      if gf(a[i])=gf(b[i]) then begin writeln(c[i]); halt; end;//如果没法避免了就输出
      if e[a[i]]=0 then e[a[i]]:=b[i] else f[gf(b[i])]:=gf(e[a[i]]);//敌人处理，相应的思路在P1892中有出现，就不介绍了。
      if e[b[i]]=0 then e[b[i]]:=a[i] else f[gf(a[i])]:=gf(e[b[i]]);
      if i=m then writeln(0);//值为0
    end;
end.
```