这道题如何分配的思路非常明显，把尽可能大的抵消（即放在不同的监狱）然后等到实在不能抵消时再输出

Bingo！快排！

那怎么维护两个罪犯之间的关系？

Bingo！并查集！

到此为止，大致的思路已经水落石出：先快排，然后一对对判断，这时的father记录的是两个罪犯是否在同一个监狱

很像非常裸的贪心+并查集

但是，机（wu）智（chi）的我注意到了它的**普及+/提高**

怎么可能把一道裸题摆在这个位置？

其实这道题类似于P1892团伙这道题，有一种“我敌人的敌人是我的朋友”的关系

举个栗子:

罪犯A与罪犯B有仇（仇恨值666），罪犯B与罪犯C有仇（仇恨值233）

但在排序完后，发现罪犯A与罪犯B处理的优先级比罪犯B与罪犯C处理的优先级要高（即先循环A,B后循环B,C）

所以我选择把A放到监狱一号，B放到监狱二号，那么此时C也要放到监狱一号，有因为上文提过father存储的是两个罪犯是否在同一个监狱，所以A的father是C（倒过来也可以）

这时候就能看出来“我敌人的敌人是我的朋友”了吧

下面上代码：
```Pascal
var
	father,enemy,a,b,c:array[0..200010]of longint;
	n,m,i,j,x,y:longint;
	//enemy表示仇人
    
function getfather(p:longint):longint;
begin 	
	if father[p]<>p then father[p]:=getfather(father[p]);
	exit(father[p]);
end; 

procedure sort(l,r: longint);
var
	i,j,x,y: longint;
begin
	i:=l;
	j:=r;
	x:=c[(l+r) div 2];
	repeat
		while c[i]>x do inc(i);
		while x>c[j] do dec(j);
		if not(i>j) then
			begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
				y:=b[i];
				b[i]:=b[j];
				b[j]:=y;
				y:=c[i];
				c[i]:=c[j];
				c[j]:=y;
                inc(i);
				dec(j);
			end;
	until i>j;
	if l<j then sort(l,j);
	if i<r then sort(i,r);
end;

begin 
	readln(n,m);
	for i:= 1 to n do father[i]:=i;
	for i:= 1 to m do readln(a[i],b[i],c[i]);
	sort(1,m);
	for i:= 1 to m do 
		begin 
			x:=getfather(a[i]);
			y:=getfather(b[i]);
			if x=y then 
				begin
					writeln(c[i]);
					halt;
				end; 
			if enemy[a[i]]=0 then enemy[a[i]]:=b[i] else father[y]:=getfather(enemy[a[i]]);
			if enemy[b[i]]=0 then enemy[b[i]]:=a[i] else father[x]:=getfather(enemy[b[i]]);
            //重点体会一下
		end;
	writeln(0);
end. 
```