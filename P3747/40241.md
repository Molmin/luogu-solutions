水一波Pascal题解~~~

这题，解法具体的不说了。另外两个大佬已经说的很清楚了。

此题，显然用扩展欧拉定理降幂。

因为$\phi(\phi(\dots))=1$，所以如果没几项就暴力处理，到了边界就不用管了。

此外，要注意的就是判断是否要加上$\phi(p)$。

一项一项求$\phi(\phi(\dots))$的时候，最后还要加上一个$1$，防止边界出问题。

求解幂的时候，分块拼凑，不然就会像某位大佬一样80分。

代码：

```pas
var
  a:array[1..50000]of longint; //原来的数值
  s,t:array[1..200000]of longint;//维护两颗线段树：s是求和，t是求最小值，t用来检查边界条件
  rp:array[0..100]of longint;  //φ(p),φ(φ(p)),...,1
  p1,p2:array[0..100,0..10000]of int64;//分块拼凑的数值
  b1,b2:array[0..100,0..10000]of boolean;//是否对φ(p)取模过
  n,m,p,c,i,x,l,r,num:longint; //num是φ(φ(...))有几个
  b:boolean;                   //这个储存是否mod过φ(p)
function min(x,y:longint):longint;//不解释
begin
  if x<y then
    exit(x)
  else
    exit(y);
end;
function phi(x:longint):longint;//暴力求φ(i)
var
  i:longint;
begin
  phi:=x;
  for i:=2 to trunc(sqrt(x)) do
    if x mod i=0 then
    begin
      dec(phi,phi div i);
      while x mod i=0 do
        x:=x div i;
    end;
  if x>1 then
    dec(phi,phi div x);
end;
procedure init;                //预处理
var
  i,j:longint;
begin
  num:=0;
  rp[0]:=p;
  while rp[num]>1 do           //处理φ(p),φ(φ(p)),...
  begin
    inc(num);
    rp[num]:=phi(rp[num-1]);
  end;
  inc(num);                    //注意：一定要再加一个1
  rp[num]:=1;
  for i:=0 to num do           //处理分块拼凑，写的很丑（不过应该可以理解）
  begin                        //（强制类型转换这么多当然很丑）
    p1[i,0]:=1;
    b1[i,0]:=false;
    for j:=1 to 10000 do
    begin
      p1[i,j]:=p1[i,j-1]*c;
      if p1[i,j]>=rp[i] then
      begin
        p1[i,j]:=p1[i,j] mod rp[i];
        b1[i,j]:=true;
      end
      else
        b1[i,j]:=b1[i,j-1];
    end;
    p2[i,0]:=1;
    b2[i,0]:=false;
    p2[i,1]:=p1[i,10000];
    b2[i,1]:=b1[i,10000];
    for j:=2 to 10000 do
    begin
      p2[i,j]:=p2[i,j-1]*p2[i,1];
      if p2[i,j]>=rp[i] then
      begin
        p2[i,j]:=p2[i,j] mod rp[i];
        b2[i,j]:=true;
      end
      else
        b2[i,j]:=b2[i,j-1];
    end;
  end;
end;
function power(x,p:longint):int64;//分块拼凑的kasumi，顺便处理b
begin
  b:=false;
  power:=p1[p,x mod 10000]*p2[p,x div 10000];
  if power>=rp[p] then
  begin
    power:=power mod rp[p];
    b:=true;
  end
  else
    b:=b1[p,x mod 10000] or b2[p,x div 10000];
end;
function solve(x,y:longint):longint;//求解问题
var
  i:longint;
begin
  solve:=x;
  if solve>rp[y] then
    solve:=solve mod rp[y]+rp[y];
  for i:=y-1 downto 0 do
  begin
    solve:=power(solve,i);
    if b then
      solve:=solve+rp[i];
  end;
  solve:=solve mod p;
end;
procedure pushup(x:longint);   //线段树没什么可以说的
begin
  s[x]:=(s[x shl 1]+s[x shl 1 or 1]) mod p;
  t[x]:=min(t[x shl 1],t[x shl 1 or 1]);
end;
procedure build(l,r,x:longint);
begin
  if l=r then
  begin
    s[x]:=a[l];
    t[x]:=0;
    exit;
  end;
  build(l,(l+r) shr 1,x shl 1);
  build((l+r) shr 1+1,r,x shl 1 or 1);
  pushup(x);
end;
procedure update(l,r,tl,tr,x:longint);
begin
  if t[x]=num then             //剪枝剪掉不需要处理的区间
    exit;
  if l=r then
  begin
    inc(t[x]);
    s[x]:=solve(a[l],t[x]);    //每次都暴力求解
    exit;
  end;
  if tl<=(l+r) shr 1 then
    update(l,(l+r) shr 1,tl,tr,x shl 1);
  if tr>(l+r) shr 1 then
    update((l+r) shr 1+1,r,tl,tr,x shl 1 or 1);
  pushup(x);
end;
function query(l,r,tl,tr,x:longint):int64;
begin
  if (l>=tl) and (r<=tr) then
    exit(s[x]);
  query:=0;
  if tl<=(l+r) shr 1 then
    query:=query(l,(l+r) shr 1,tl,tr,x shl 1);
  if tr>(l+r) shr 1 then
    query:=(query+query((l+r) shr 1+1,r,tl,tr,x shl 1 or 1)) mod p;
  pushup(x);
end;
begin                          //主程序没什么好说的
  read(n,m,p,c);
  init;
  for i:=1 to n do
    read(a[i]);
  build(1,n,1);
  for i:=1 to m do
  begin
    read(x,l,r);
    if x=0 then
      update(1,n,l,r,1)
    else
      writeln(query(1,n,l,r,1));
  end;
end.
```