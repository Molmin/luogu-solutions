明显的搜索+欧拉图

有点暴力 。。可能优化不够完善

附代码（其实还是比较简单的）

 
 
```cpp
var edge:array[1..500,1..500] of integer;
    t:array[1..500] of integer;
    ans:array[1..1024] of integer;
    f,k,v,max:integer;
function init():integer;
  var i,s,e:integer;
  begin
    fillchar(edge,sizeof(edge),0);
    fillchar(t,sizeof(t),0);
    max:=0;
    readln(f);
    for i:=1 to f do
      begin
        readln(s,e);
        inc(edge[s,e]);
        inc(edge[e,s]);
        inc(t[s]);
        inc(t[e]);
        if s>max then max:=s;
        if e>max then max:=e;
      end;
    for i:=1 to max do
      if odd(t[i]) then exit(i);
    exit(1);
  end;
procedure search(n:integer);
  var i:integer;
  begin
    for i:=1 to max do
      if edge[n,i]>0 then
        begin
          dec(edge[n,i]);
          dec(edge[i,n]);
          search(i);
        end;
    v:=v+1;
    ans[v]:=n;
  end;
procedure print;
  var i:integer;
  begin
    for i:=v downto 1 do
      writeln(ans[i]);
  end;
begin
  k:=init();
  v:=0;
  search(k);
  print;
end.
```