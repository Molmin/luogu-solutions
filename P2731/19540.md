很久没有pascal题解了，来一发。

来做这题的真正原因是半年多以前做过这题竟然没AC。

现在做了才发现当时很多小细节没注意。

本题是一笔画问题的判断构造，整体思路不难，主要是找到度为奇数的点，再按字典序最小输出。

具体思路在代码里看：

```cpp
program p2731;
  var g:array[0..2000,0..2000]of longint;
      d,ans:array[0..2000]of longint;
      a,b,i,n,lo,minv,maxv,start:longint;
procedure find(x:longint);
  var i:longint;
begin
  for i:=minv to maxv do
  if g[x,i]>=1 then
  begin
    dec(g[x,i]);
    dec(g[i,x]);
    find(i);
  end;//将连通的边去掉并递归。
  inc(lo);
  ans[lo]:=x;//这样保证了字典序最小输出。
end;
function max(a,b:longint):longint;
begin
  if a>b then exit(a) else exit(b);
end;
function min(a,b:longint):longint;
begin
  if a<b then exit(a) else exit(b);
end;
begin
  readln(n);
  minv:=1000000;
  for i:=1 to n do
  begin
    readln(a,b);
    inc(g[a,b]);
    inc(g[b,a]);
    inc(d[a]);
    inc(d[b]);
    minv:=min(a,minv);
    minv:=min(b,minv);
    maxv:=max(a,maxv);
    maxv:=max(b,maxv);
  end;//将能连通的边累加，同时累加点的度，将标号最小和标号最大的点找出来。
  start:=1;
  for i:=1 to 500 do
  if (d[i] mod 2=1) then
  begin
    start:=i;
    break;
  end;//找到第一个入度为奇数的点开始一笔画递归。
  lo:=0;
  find(start);//找路线。
  for i:=lo downto 1 do
  writeln(ans[i]);//打印路线。
end.
```