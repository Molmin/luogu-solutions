由于担心奇怪的缩进影响,发图片(不知luogu图床能上题解.?)..

```pas
program P2419;
 type
  dis=record
   n,t:longint;
  end;
 var
  a:array[0..4501,1..2] of dis;
  d,h,f:array[0..101,1..2] of longint;                                         //这里的[1..2]分别(1起)用来做正向和反向SPFA.
  b:array[0..101] of longint;
  i,j,m,n,o,p,p1,p2,p3,s,x,y:longint;
  u:boolean;
 procedure hahai(x,y,z:longint);                                               //链式前向星表示边.
  begin
   a[i,z].n:=h[x,z];
   a[i,z].t:=y;
   h[x,z]:=i;
   inc(f[y,z]);                                                                //f[y]表示有多少条边指向编号为y的点.
  end;
 procedure hahasw(var x,y:longint);                                            //排序交换.
  var
   t:longint;
  begin
   t:=x;
   x:=y;
   y:=t;
  end;
 procedure hahas(l,r:longint);                                                 //快排..
  var
   o,p,m:longint;
  begin
   o:=l;
   p:=r;
   m:=d[(l+r) div 2,1];
   repeat
    while d[o,1]<m do inc(o);
    while d[p,1]>m do dec(p);
    if o<=p then
     begin
      hahasw(d[o,1],d[p,1]);
      hahasw(d[o,2],d[p,2]);
      inc(o);
      dec(p);
     end;
   until o>p;
   if o<r then hahas(o,r);
   if l<p then hahas(l,p);
  end;                                                                         //根据每个点活动范围排序,以便之后的处理.
 begin
  readln(n,m);
  fillchar(h,sizeof(h),0);
  fillchar(f,sizeof(f),0);
  for i:=1 to m do
   begin
    readln(x,y);
    hahai(x,y,1);          
    hahai(y,x,2);                                                              //反向建立边.
   end;                                                                        //存边..
  for j:=1 to 2 do                                                             //正反1起做..
   begin
    o:=1;
    p:=0;
    a[0,j].n:=-1;
    if j=1 then s:=0;
    for i:=1 to n do
     if j=1 then d[i,j]:=0
            else d[i,j]:=s;                                                    //d[i]表示i号点在树形结构上的上下限,意思为这个点可以在d[i,1]~d[i,2]中的任意1层上面.
    for i:=1 to n do
     if h[i,3-j]=0 then
      begin
       inc(p);
       b[p]:=i;                                                                //b数组为SPFA的队列.
      end;                                                                     //用前向星特性找出入度为0的点.
    while o<=p do                                                              //最长路法求上下限.
     begin
      p1:=b[o];                                                                //指定当前的起点(p1).
      i:=h[p1,j];
      while a[i,j].n<>-1 do                                                    //遍历所有与p1相关联的边.
       begin
        p2:=a[i,j].t;                                                          //这条边的终点(p2).
        dec(f[p2,j]);                                                          //删除这条边.
        if j=1 then                                                            //正反分开考虑..
         begin
          p3:=1;
          u:=d[p1,j]+p3>d[p2,j];                                               //如果p2的上限>=p1的上限,不满足条件,加强p2的限制.
         end
               else
         begin
          p3:=-1;
          u:=d[p1,j]+p3<d[p2,j];                                               //如果p2的下限<=p1的下限,不满足条件,加强p2的限制.
         end;
        if u then d[p2,j]:=d[p1,j]+p3;                                         //满足上述变化.
        if (d[p2,j]>s) and (j=1) then s:=d[p2,j];                              //找出最大的层数(s),即确定反向的层数.
        if f[p2,j]=0 then                                                      //如果所有指向p2的边都删完了.p2就可以入队了.
         begin
          inc(p);
          b[p]:=p2;
         end;
        i:=a[i,j].n;                                                           //指向下1条边.
       end;
      inc(o);
     end;
   end;                                                                        //做完后我们得到了每个点在树上的上下限.最后的答案就是那些上限和下限相等(即d[i,1]=d[i,2].),且没有与其他点有公共区域的点.
  hahas(1,n);
  p:=0;
  d[0,1]:=-1;
  for i:=1 to n+1 do
   if (d[i,1]=d[i,2]) or (i=n+1) then                                          //找出可能是答案的点(d[i,1]=d[i,2]).
    begin
     if (d[b[p],1]<>d[i,1]) and (p<>0) or (i=n+1) then                         //i=n+1即最后再做1遍删除.
      begin
       p1:=1;
       while (d[b[p],1]=d[b[p-p1],1]) and (p-p1>0) do inc(p1);                 //p1确定连续重复的位置.
       if d[b[p],1]=d[b[p-1],1] then dec(p,p1);                                //如果有重复的删除.
      end;                                                                     //有些原先就重复的点(即d[i,1]=d[j,1],d[i,2]=d[j,2])要把他们全部删除.
     if i<>n+1 then
      begin
       inc(p);
       b[p]:=i;
      end;                                                                     //新点入列.b数组用来存储可能是答案的点.
    end;
  o:=1;
  s:=0;                                                                        //统计答案个数.
  for i:=1 to n do
   if d[i,1]<>d[i,2] then                                                      //上下限不相等的点已经不可能是答案(如果是答案,说明d[i,1]~d[i,2]在其他点上没有公共的部分,最后得出来的还是d[i,1]=d[i,2]的样子..),但可能与b数组的点有公共区域.这些b数组的点也要删掉.
    begin
     while (d[b[o],1]<d[i,1]) and (o<=p) do                                    //由于已经有序.脱离范围的点就是答案了.
      begin
       inc(s);
       inc(o);
      end;
     while (d[b[o],1]>=d[i,1]) and (d[b[o],1]<=d[i,2]) and (o<=p) do inc(o);   //有相同的区域,不计入答案.
    end;
  writeln(s+p-o+1);                                                            //最后还有p-o+1个点也要算上.
 end.
```
 ![](https://cdn.luogu.com.cn/upload/pic/5737.png) 

 ![](https://cdn.luogu.com.cn/upload/pic/5738.png) 

 ![](https://cdn.luogu.com.cn/upload/pic/5739.png) 

补充说明..

····其实所谓的SPFA就是拓扑了..形式上有点像而已...

····此题解额外的求出了每头牛的可能的位置..作为延伸吧..

(ಡωಡ).
