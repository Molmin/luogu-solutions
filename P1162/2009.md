看了几页题解，貌似都在用**搜索**做这道题。

那么在这里就谈下一个思路新奇的做法:

## 并查集

这个方法的**好处**就在于：**容易写**（重点）+不论是一个还是**几个**闭合区间都可以搞定。


1. 大概就是把outside当成一个node.

2. 然后读入的时候**往左上相同node**（假如在边界上就算上outside-node）union集合.

3. 最后输出检查一下这个**0有没有和outside在一个集合**并改2就好了。

4. 还有就是**1的node不用参与union**。


放个自己写的Pascal代码。(洛谷显示pascal总是有问题不知道为啥,姑且用C的高亮吧,聊胜于无)


```c
const maxn=30;
    maxn2=900;
var n,i,j,cnt:longint;
    mtx:array[1..maxn,1..maxn]of longint;
    fa:array[0..maxn2]of longint;

function find(x:longint):longint;
begin
    if fa[x]=x then exit(x);
    fa[x]:=find(fa[x]);
    exit(fa[x]);
end;

procedure union(x,y:longint);
begin
    fa[find(x)]:=find(y);
end;

begin
    readln(n);
    cnt:=0;
    for i:=1 to n do
    for j:=1 to n do
    begin
        inc(cnt);
        read(mtx[i,j]);
        if mtx[i,j]=1 then continue;
        fa[cnt]:=cnt;
        if (i=1)or(j=1)or(i=n)or(j=n) then
        union(cnt,0);
        if (i>1)and(mtx[i,j]=mtx[i-1,j]) then
        union(cnt,cnt-n);
        if (j>1)and(mtx[i,j]=mtx[i,j-1]) then
        union(cnt,cnt-1);
    end;
    cnt:=0;
    for i:=1 to n do
    begin
        for j:=1 to n do
        begin
            inc(cnt);
            if (mtx[i,j]=0)and(find(cnt)<>find(0)) then
            mtx[i,j]:=2;
            write(mtx[i,j],' ');
        end;
        writeln;
    end;
end.
```