按楼上大神的方法，在开始的时候把0全看成2，再一个个消掉。

其实还可以省掉visited数组，方法如下：

在搜索拓展的时候遇到2就拓展，把拓展得到的点立刻转成0并加入队列

所以，所有值是0的点，不是已经拓展过的，就是在队列中将要拓展的。

这不是和另开一个visited数组一样的效果吗，还天然避开1，做的时候就不用管了

另外还要注意初始化拓展四条边的时候，四个角还是不要重复的好。

下附蒟蒻代码：

[codep]




```cpp
program p1162;
type
  point=record
    x,y:longint;
  end;
const
  dir:array[1..4,1..2] of longint=((0,1),(0,-1),(1,0),(-1,0));
var
  n,i,j,h,t,x,y,newx,newy:longint;
  map:array[1..35,1..35] of longint;
  q:array[1..2000] of point;
procedure checkedge(x,y:longint);
begin
  if map[x,y]=2 then
    begin
      map[x,y]:=0;
      inc(t);
      q[t].x:=x; q[t].y:=y;
    end;
end;
begin
  readln(n);
  for i:=1 to n do
    begin
      for j:=1 to n do
        begin
          read(map[i,j]);
          if map[i,j]=0 then
            map[i,j]:=2;
        end;
      readln;
    end;
  h:=0; t:=0;
  for i:=1 to n-1 do
    begin
      checkedge(i,1); //left
      checkedge(i+1,n); //right
      checkedge(1,i+1); //up
      checkedge(n,i); //down
    end;
  while h<t do
    begin
      inc(h);
      x:=q[h].x; y:=q[h].y;
      for i:=1 to 4 do
        begin
          newx:=x+dir[i,1];
          newy:=y+dir[i,2];
          if (newx>=1) and (newx<=n) and
            (newy>=1) and (newy<=n) and (map[newx,newy]=2) then
              begin
                map[newx,newy]:=0;
                inc(t);
                q[t].x:=newx; q[t].y:=newy;
              end;
        end;
    end;
  for i:=1 to n do
    begin
      for j:=1 to n do
        write(map[i,j],' ');
      writeln;
    end;
end.
[/codep]
```