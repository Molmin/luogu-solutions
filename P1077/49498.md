一上来就想到了区间dp

f[i,j]表示在前i盆花中选择j盆花的方案数

很容易得出状态转移方程 

f[i,j]=f[i-1,j]+f[i-1,j-1]+f[i-1,j-2]+……+f[i-1,j-min(a[i],j)]

这样就可以过了这道题了
```pascal
var
 f:array[0..1005,0..1005]of int64;
 a:array[0..1005]of int64;
 i,j,k:longint;
 n,m:int64;
function min(i,j:longint):longint;
begin
 if i<j then exit(i)
  else exit(j);
end;
begin
 read(n,m);
 for i:=1 to n do 
 begin
  read(a[i]);
  f[i,0]:=1;//一盆花也不选也是一种方案
 end;
 f[0,0]:=1;//同上
 for i:=1 to n do
  for j:=1 to m do
   for k:=0 to min(a[i],j) do
   f[i,j]:=(f[i,j]+f[i-1,j-k]) mod 1000007;
 write(f[n,m]);
end.
```

然后这个算法还可以继续优化

由于f[i-1,j]+f[i-1,j-1]+f[i-1,j-2]+……+f[i-1,j-min(a[i],j)]可以直接用前缀和计算出来，所以k可以被去掉

而且f[i,j]的值只与f[i-1,...]有关，所以可以通过滚动数组来实现对空间的优化

上代码：

```pascal
var
 s:array[0..1,0..1005]of int64;
 //滚动数组
 a,f:array[0..1005]of int64;
 i,j,k,now,pre:longint;
 n,m:int64;
function min(i,j:longint):longint;
begin
 if i<j then exit(i)
  else exit(j);
end;
begin
 read(n,m);
 for i:=1 to n do read(a[i]);
 now:=1;pre:=0;
 for i:=0 to m do
  s[0,i]:=1;//初始化
 for i:=1 to n do
 begin
  s[now,0]:=1;//一盆花也不选也是一种方案
  for j:=1 to m do
  begin
   f[j]:=(s[pre,j]-s[pre,j-min(a[i],j)-1]+1000007) mod 1000007;
   //因为s需要经过取模，所以s[pre,j]-s[pre,j-min(a[i],j)-1]可能小于0，所以加上1000007来保证非负性
   s[now,j]:=(s[now,j-1]+f[j])mod 1000007;
  end;
  now:=1-now;
  pre:=1-pre;
  //滚~
 end;
 write(f[m]);
end.
```