可以说这是一个不太难的堆的问题；（来一发pascal）

这题一定不能用直接循环的堆，会很慢，会爆掉；

其次，避免一个小误区（题目坑我，第一次没ak）：最后一个数只要输出一次，不论后面是否还有相同的

由于题目中的函数满足在正无穷上单调递增，所以1的函数值一定是单个函数中最小的，于是把每个函数的第一个值放进堆里；

然后像我的程序里面s[?,1]用来记录函数值，s[?,2]用来记录是哪个函数，然后问题就简单了，只要每次输出一个小根堆的顶部，然后

把对应函数的下个值加进堆里面，直到输出m个（【注】交换值时记得s的两个值一起换）

下面是我的code：


```cpp
program min;
var
    s:array[0..10000,1..2]of longint;
    a,b,c,d:array[1..10000]of longint;
    i,j,n,m,p,t,x,y,sum,ans:longint;
procedure intt(y:longint);//堆的插入操作
var
    o,temp:longint;
begin
      inc(t);
      s[t,1]:=y;
      s[t,2]:=x;
      o:=t;
      while (o>0)and(s[o shr 1,1]>s[o,1])do
      begin
            temp:=s[o shr 1,1];
            s[o shr 1,1]:=s[o,1];
            s[o,1]:=temp;
            temp:=s[o shr 1,2];
            s[o shr 1,2]:=s[o,2];
            s[o,2]:=temp;
            o:=o shr 1;
      end;
end;
function outt():longint;//找小根堆的顶值
var
    i,j,temp:longint;
    p:boolean;
begin
      outt:=s[1,1];
      s[1,1]:=s[t,1];
      s[1,2]:=s[t,2];
      dec(t);
      i:=1;
      p:=true;
      while (p)and(i shl 1<=t)do
      begin
            if (i shl 1+1>t)or(s[i shl 1,1]<s[i shl 1+1,1])then j:=i shl 1
            else j:=i shl 1+1;
            if s[j,1]<s[i,1] then
            begin
                  temp:=s[j,1];
                  s[j,1]:=s[i,1];
                  s[i,1]:=temp;
                  temp:=s[j,2];
                  s[j,2]:=s[i,2];
                  s[i,2]:=temp;
                  i:=j;
            end
            else p:=false;
      end;
end;
begin
      readln(n,m);
      for i:=1 to n do
      begin
            readln(a[i],b[i],c[i]);
            x:=i; //初始赋值，别忘了
            intt(a[i]+b[i]+c[i]);//把1的函数值放进去
            inc(d[i]);//d用来记录这个函数已经计算到了哪个值
      end;
      for j:=1 to m do
      begin
            x:=s[1,2];//找到该函数的位置
            inc(d[x]);//计算还没算过的值
            ans:=outt();
            write(ans,' ');//输出当前最小值
            if j<m then//尽量减少计算次数0-0
            begin
                  intt(a[x]*d[x]*d[x]+b[x]*d[x]+c[x]);//插入值
            end;
      end;
end.

```