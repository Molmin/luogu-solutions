只是一道非常容易的基础题，程序思路如下：

首先，对所有读入的数据进行排序（为了防卡快排，用了归并排序，归并的思路见程序），之后，会发现对弈的两个人也就是排序后相邻的两个人。因此，我们开一个数组用来表示相邻两个人的等级差，再对这一个数组按从小到大进行排序，再取前k组进行累加，即可得到答案。

这个算法同时也保证了题目中的“在对弈中，等级高的人必须用黑色的棋子，等级低的人必须用白色的棋子。每个人最多只能用一次黑色的棋子和一次白色的棋子”的要求，即每个数仅使用于一次。

具体程序如下：

```cpp
type
  arr=array[0..200000] of longint;
var
  t,s,c:arr;
  ans,i,n,k:longint;
{procedure qsort(r,l:longint;var a:arr);
var i,j,mid,t:longint;
begin
  i:=r;
  j:=l;
  mid:=a[(i+j) shr 1];
  repeat
    while a[i]<mid do inc(i);
    while a[j]>mid do dec(j);
    if i<=j then
      begin
        t:=a[i];
        a[i]:=a[j];
        a[j]:=t;
        inc(i);
        dec(j);
      end;
  until i>j;
  if i<l then qsort(i,l,a);
  if r<j then qsort(r,j,a);
end;}//快排，本题未选用。
procedure msort(r,l:longint;var a:arr);
var
  i,j,k,mid:longint;
begin
  if r>=l then exit;//如果只有1个元素了，就不必排序了
  mid:=(r+l) shr 1;//二分
  msort(r,mid,a);
  msort(mid+1,l,a);//递归，直到使a[r..mid],a[mid+1..l]成为有序数组
  i:=r;
  j:=mid+1;
  k:=i;
  while (i<=mid) and (j<=l) do
    begin
      if a[i]<a[j] then
        begin//如果a[i]<a[j]，则将a[i]接入数组，反之将a[j]接入数组
          c[k]:=a[i];
          inc(k);//c数组下标加1
          inc(i);//因为接入的是a[i]，所以i+1,反之j+1
        end else
        begin
          c[k]:=a[j];
          inc(k);
          inc(j);
        end;
    end;
  while i<=mid do
    begin
      c[k]:=a[i];
      inc(k);
      inc(i);
    end;//将前半部分的数据接进来
  while j<=l do
    begin
      c[k]:=a[j];
      inc(k);
      inc(j);
    end;//将后半部分的数据接进来
  for k:=r to l do a[k]:=c[k];//赋值到原来的数组中；使得a[r..l]成为有序序列
end;
begin
  read(n,k);
  for i:=1 to n do read(t[i]);
  msort(1,n,t);//归并排序
  for i:=1 to n-1 do s[i]:=t[i+1]-t[i];
  msort(1,n-1,s);
  for i:=1 to k do inc(ans,s[i]);
  writeln(ans);
end.
//注：归并排序的算法时间复杂度为地地道道的O（nlogn），而快排被卡的时候为O(N^2)，所以选用归并。
```