# pascal的题解好少……

看到其它的题解都是$O(n^3k)$，下面是$O(n^2k)$算法。

原题题意可以转化一下，把牛安排在$k$个房间里，它们可以逆时针走，使得第$i$个位置有$ri$头牛，求最短总路程。

能想的只有$dp$了。

我们先需要破环为链，这是经典环状$dp$思路。

还有，考虑到最小路程，我们肯定是把一段区间的牛全部集中到区间最左端，这样最优。这样$dp$方程就很好想了。

设$f[i,j]$表示安排$i$个门，前$j$头牛所能产生的最短路程。

则$f[i,j]=min(f[i,j],f[i-1,k]+∑(l-k-1)*r[l])(k+1<=l<=j,0<=k<j)$

把后面的（把$[k+1,j]$的牛集中到第$k+1$个位置）化开，用前缀和，$s1[i]=a[1]+a[2]……a[i]$，$s2[i]=a[2]*(2-1)+……a[i]*(i-1)$。

那么，后面的就变成了$s2[j]-s2[k]-k*(s1[j]-s1[k])$。

忽略$i$，发现这是一个斜率优化的经典形式（如果不知道斜率优化可以去看[浅谈斜率优化](https://www.luogu.org/blog/top-oier/qian-tan-xie-shuai-you-hua)）。

那么，设该一次函数为$y=Kx+b$，则

$K=s1[i]$

$x=j$

$y=s1[j]*j+f[l-1,j]-s2[j]$

$b=f[l,i]-s2[i]$

于是，本题结束，其中状态$O(nk)$，破环为链$O(n)$，状态转移$O(1)$，总的时间复杂度$O(n^2k)$。

代码如下：

```pascal
var f:array[0..7,0..1000] of int64;
    s1,s2,a:array[0..1000] of int64;
    q:array[0..1000] of longint;
    n,k,p,l,r,i,j:longint;
    minn:int64;
function jiao(s,k1,k2:longint):extended; //求斜率
begin
  exit((s1[k1]*k1-s1[k2]*k2+f[s,k1]-f[s,k2]-s2[k1]+s2[k2])/(k1-k2));
end;
procedure dp; //斜率优化dp
var m,i,j:longint;
begin
  for m:=1 to k do
  begin
    f[m,0]:=0;l:=1;r:=1;q[1]:=0;
    for i:=1 to n do
    begin
      while (l<r) and (jiao(m-1,q[l],q[l+1])<=s1[i]) do inc(l);
      j:=q[l];f[m,i]:=f[m-1,j]+s2[i]-s2[j]-j*(s1[i]-s1[j]);
      while (l<r) and (jiao(m-1,i,q[r-1])<=jiao(m-1,q[r-1],q[r])) do dec(r);
      inc(r);q[r]:=i;
    end;
  end;
end;
begin
  minn:=1000000000000000000;
  readln(n,k);
  for i:=1 to n do readln(a[i]);
  for j:=1 to n do //破环为链
  begin
    fillchar(f[0],sizeof(f[0]),127);f[0,0]:=0;
    s1[0]:=0;s2[0]:=0;
    for i:=1 to n do begin s1[i]:=s1[i-1]+a[i];s2[i]:=s2[i-1]+a[i]*(i-1);end;
    //for i:=1 to n do write(s1[i],' ');writeln;
    //for i:=1 to n do write(s2[i],' ');writeln;
    dp;
    if f[k,n]<minn then minn:=f[k,n];
    p:=a[1];for i:=2 to n do a[i-1]:=a[i];a[n]:=p; //转换到下一个链，把第一个放到最后一个
  end;
  writeln(minn);
end.
```