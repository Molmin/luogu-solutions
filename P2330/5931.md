本题的题意很明确，使N个点两两互达

所以如果两个点已经间接或直接相连，你还连他们两个是不是钱太多了

所以只要有N-1条边就可以了

就是裸的最小生成树喽

为了保证总和最小，先排序（从小到大）

然后从前往后扫描，如果这个点的u和v不在一个集合，用并查集加到集合里，如果他们在同一集合，表示已经可以相互到达，什么都不干

↑就是克鲁斯卡尔算法

开始直接输出n-1条连边

每次递减n，当n=1时跳出

因为已经排序，所以只要输出此时点的c就可以了

代码如下

```
Type
    hh=record
        a,x,y:longint;
    end;

Var
    fa:array[1..301]of longint;
    n,i,f1,f2,m:longint;
    a:array[1..50001]of hh;
    
Function getfather(k:longint):longint;//并查集
begin
    if fa[k]<>k then getfather:=getfather(fa[k])
        else getfather:=k;
    fa[k]:=getfather;
end;

Procedure qsort(l,r:longint);
var
    i,j,e:longint;
    f:hh;
begin
    i:=l;
    j:=r;
    e:=a[(l+r) div 2].a;
    repeat
        while a[i].a<e do inc(i);
        while a[j].a>e do dec(j);
        if i<=j then
            begin
                f:=a[i];
                a[i]:=a[j];
                a[j]:=f;
                inc(i);
                dec(j);
            end;
    until i>j;
    if i<r then qsort(i,r);
    if l<j then qsort(l,j);
end;

Begin
    readln(n,m);
    write(n-1,' '); //树有N-1条边相连
    for i:=1 to m do
        read(a[i].x,a[i].y,a[i].a);
    qsort(1,m);
    for i:=1 to n do
        fa[i]:=i;
    i:=0;
    while (n>1) and (i<=m) do
        begin
            inc(i);
            f1:=getfather(a[i].x);
            f2:=getfather(a[i].y);
            if f1<>f2 then //不在同一集合，并
                begin
                    fa[f1]:=f2;
                    dec(n);
                end;
        end;
    writeln(a[i].a); //根据排序性质，当前点的值一定最大
End.
```