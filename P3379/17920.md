怎么没有一个pascal的。。。

我来贴一个好了

这题用tarjan，简单的来说分为以下几个步骤

①指父链（邻接表）存，要存两个，这个后面再解释

②深搜：a  建立并查集

b 若此结点（x）已走过，且需要求解，且与之相关的点（y）也走过，那么答案就是y或x的根节点（不是严格意义上的，至于为什么，后面做解释）

c 回溯的时候合并并查集，切记：不能先合并再往下搜，一定是搜到底再往上合并！！







```cpp
var
n,m,a,b,i,tot,s:longint;
now,child,pre,pre1,child1,now1,f,id,sum,ans:array[0..1000000]of longint;
q:array[0..1000000,1..2]of longint;
can:array[0..1000000]of boolean;
function find(x:longint):longint;//关于并查集不多解释，这里要路径压缩
begin
   if f[x]=x then exit(x);
   find:=find(f[x]);
   f[x]:=find;
end;
procedure hb(p,q:longint);//合并并查集，这个大家应该没问题
var pp,qq:longint;
begin
  pp:=find(p);
  qq:=find(q);
  if pp<>qq then
   f[pp]:=qq;
end;
procedure  tar(faa:longint);//tarjan
var  fa,son:longint;
begin
    fa:=now1[faa];//这一段还不是深搜
    can[faa]:=true;//标记走过
    while  fa<>0  do//while 当前的点被询问 do
       begin
          son:=child1[fa];//son是当前结点的孩子
          if can[son] then//
              ans[id[fa]]:=find(son);//这一句是关键（此时find（son）并不是根节点，因为还没有路径压缩）不可言传，自己体会吧
          fa:=pre1[fa];//找下一个与之有关联的点
       end;
    fa:=now[faa];//深搜
    while fa<>0 do//还有孩子时
       begin
          son:=child[fa];
          if can[son]=false then//如果没有被走过
            begin
             tar(son);//深搜下一层
             hb(son,faa);//回溯的时候合并
            end;
        fa:=pre[fa];
       end;
end;
```
｛主程序｝
```cpp
begin
    readln(n,m,s);
    tot:=0;
    for i:=1 to n-1 do
       begin//建立指父链
          readln(a,b);
          inc(tot);
          child[tot]:=a;
          pre[tot]:=now[b];
          now[b]:=tot;
          inc(tot);
          child[tot]:=b;
          pre[tot]:=now[a];
          now[a]:=tot;
       end;
    tot:=0;
   for i:=1 to m do
     begin//这里有一个问题，就是询问的也要相关联（但不要将其理解为a是b的父亲或儿子，只是他们两个有关系，待会只要搜到a，且a与b有关系，那么就可以求解了
       readln(a,b);
       q[i,1]:=a;q[i,2]:=b;//这里也可以用一位数组存，看个人喜好
       inc(tot);
       child1[tot]:=a;
       pre1[tot]:=now1[b];
       now1[b]:=tot;
       id[tot]:=i;//代表当前这个链表是第几个询问，否则后面不好记录答案，这里大家自己体会没有这句话的后果
       inc(tot);
       child1[tot]:=b;
       pre1[tot]:=now1[a];
       now1[a]:=tot;
       id[tot]:=i;//同上
     end;
  for i:=0 to n do
     f[i]:=i;//并查集初始化
  tar(s);
  for i:=1 to m do//输出
     begin
        writeln(ans[i]);
     end;
end.

```