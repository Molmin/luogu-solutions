Tarjan算法求LCA伪代码

Tarjan(u)//marge和find为并查集合并函数和查找函数

```cpp
{
    for each(u,v)    //访问所有u子节点v
    {
        Tarjan(v);        //继续往下遍历
        marge(u,v);    //合并v到u上
```
标记v被访问过;
    }
    for each(u,e)    //访问所有和u有询问关系的e

    {
如果e被访问过;

u,e的最近公共祖先为find(e);

    }
}伪代码来源：http://www.cnblogs.com/JVxie/p/4854719.html

----------------------------------------------------------------------------------------------------------------------

因为数据范围较大所以用邻接表模拟数组。

并且要用两个一个存要查的点，一个存边。


```cpp
type tr=record
     next,too:longint;//too指他指向的边，next指邻接表的下一个点
     end;
var n,m,k,i,q,w,tot,tot2:longint;
    vis:array[-2..1000009] of boolean;
    data,data2:array[-2..1000009] of tr;//data，data2模拟链表
    c,head,head2,fa,where:array[-2..1000009] of longint;//head记录邻接表起点。C记录结果。//Fa记录父子关系，where下面讲。
function find(x:longint):longint;//并查集，路径压缩
begin
   if fa[x]=x then exit(x);
   find:=find(fa[x]);
   fa[x]:=find;
end;
 procedure hb(p,q:longint);//合并
var pp,qq:longint;
begin
  pp:=find(p);
  qq:=find(q);
  if pp<>qq then
   fa[pp]:=qq;
end;
procedure  dfs(q:longint);//tarjan算法
var  w,v:longint;
begin
 vis[q]:=true;
    w:=head[q];
    while w<>0 do
       begin
          v:=data[w].too;
          if vis[v]=false then//当没访问过就递归搜索。
            begin
             dfs(v);
             hb(v,q);//要先搜索再合并
            end;
        w:=data[w].next;
       end;
     w:=head2[q];
      while w<>0 do
       begin
          v:=data2[w].too;
          if vis[v] then//如果已经访问过就可以求公共祖先
           c[where[w]]:=find(v);
          w:=data2[w].next;
       end;
end;
 begin
 readln(n,m,k);
 tot:=0;
  for i:=1 to (n-1) do//邻接表存边
   begin
    read(q,w);
    tot:=tot+1;
    data[tot].too:=w;
    data[tot].next:=head[q];
    head[q]:=tot;
    tot:=tot+1;//双向都要存
    data[tot].too:=q;
    data[tot].next:=head[w];
    head[w]:=tot;
   end;
  tot2:=0;
  for i:=1 to m do
   begin
    read(q,w);
    tot2:=tot2+1;
    data2[tot2].too:=w;
    data2[tot2].next:=head2[q];
    head2[q]:=tot2;
    where[tot2]:=i;
    tot2:=tot2+1;
    data2[tot2].too:=q;
    data2[tot2].next:=head2[w];
    head2[w]:=tot2;
    where[tot2]:=i;//因为我们存两边把长度扩大了，所用where指回他原来的位置
   end;
  for i:=0 to n do//初始化父节点
   fa[i]:=i;
   dfs(k);
  for i:=1 to m do
   writeln(c[i]);
end.

```