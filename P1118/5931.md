（之前说排版乱没过，重排一遍又发了一次，希望能过）

这题用纯粹的爆搜你会发现你T掉了

然后就要用神奇的办法搞一搞

分析样例

```
3                 1                    2               4

                              ↓
 
            4（3+1）         3（1+2）            6（2+4）

                              ↓

                    7（3+1+1+2）  9（1+2+2+4）

                              ↓

                              16（3+1+1+1+1+2+2+2+4）


```

最后一层16是怎么得到的呢？


```
3*1+1*3+2*3+4*1
```

我们可以变为
1a+3b+3c+1d的形式

看一下每个字母的系数

分别为

1 3 3 1

有没有觉得熟悉

               1
             1   1
           1   2   1
         1   3   3   1
       1   4   6   4   1
       ↑还记得这个三角形吗
       
没错就是杨辉三角的第四层

说明各项系数就是杨辉三角形的各项位置（你还可以手玩一下，结果一样，很简单可以想出来）

就非常的简单了

先求出相应第n层的杨辉三角形

再搜索往里面带

就ok了

搜索的时候i:=1 to n

从小到大

所以不用考虑字典序的问题

by the way

一维求杨辉三角形的方法

```
	for i:=1 to n do
		begin
			a[1]:=1;
			a[i]:=1;
			if i mod 2=1 then
					a[i div 2+1]:=a[i div 2+1]+a[i div 2];
			for j:=i div 2 downto 1 do
				a[j]:=a[j]+a[j-1];
			for j:=i div 2+1 to i-1 do
				a[j]:=a[i-j+1];
		end;
```

（好像有个大佬@ plazum写过了Orz）

-----------------------------------------------------------
代码分割线


```
Var
	a:array[1..12]of longint;
	n,i,ans,j:longint;
	visited:array[1..12]of boolean;
	f:array[1..12]of longint;
	t:boolean;
	
Procedure print;
var
	i:longint;
begin
	for i:=1 to n do
		write(f[i],' ');
end;
	
Procedure dfs(k,sum:longint); //一个正常的搜索模板
var
	i:longint;
begin 
	if sum<0 then exit; //这里是个优化，如果当前的结果已经大于之前的sum了，那这条路径就可以去死了
	if t=false then exit;
	if k>n then //到达条件判断
		if sum=0 then 
			begin 
				print;
				t:=false;
			end;
	for i:=1 to n do
		if visited[i] then   //一个一个带入尝试，因为从小到大搜索，所以不用考虑字典序的问题
			begin
				visited[i]:=false; 
				f[k]:=i;
				dfs(k+1,sum-f[k]*a[k]); //搜索的数*系数
				visited[i]:=true;//回溯
			end;
end;
			
	
Begin
	read(n,ans);//ans是乱写的变量名
	fillchar(visited,sizeof(visited),true);
	t:=true;
	for i:=1 to n do
		begin
			a[1]:=1;
			a[i]:=1;
			if i mod 2=1 then
				a[i div 2+1]:=a[i div 2+1]+a[i div 2];//这里单独判了奇数的情况，中间那个数单独算
			for j:=i div 2 downto 1 do
				a[j]:=a[j]+a[j-1]; //从中间向两边更新没有影响
			for j:=i div 2+1 to i-1 do
				a[j]:=a[i-j+1];
		end;
	dfs(1,ans);
End.
```