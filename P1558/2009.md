首先献给某些迷之0分的童鞋题目说明：**（可能A> B），也就是说假如出现A>B的时候，记得把AB反过来询问或修改！**


##好，这道题是一道明显的**线段树**。


- 因为T很小只有30，完全可以**状态压缩处理颜色**。

- **区间加法**的时候用**按位或运算**。

- **输出的时候循环color有多少个1就好了**，我是用lowbit处理的，（可能比直接for一遍快一点？不过这不重要

###以上是主体思路，以下是我处理的几个优化。

- 这道题的**lazy-tag不需要存颜色**，因为假如有需要用到lazy-tag的change/modify的话，那么一定是一片全部搞成某个颜色的，所以在有tag而且update（下传tag）之前，current区间里面的颜色一定是纯色的，也就是说update的时候不需要tag的信息，直接把child全部搞成与current一样的颜色就好了，所以**tag的唯一存在目的就是一个bool变量而已**（有无tag）。

- 还有就是在询问的时候，不需要像平时一样，下传tag之后继续递归询问区间。因为当你tag不等于false时（就是说需要下传tag），目前区间的孩子里面的颜色和目前区间的颜色一定是一样的而且是纯色（因为假如出现有不是纯色，那么你肯定在此区间孩子中执行过change/modify，也就是说你一定update过），也就是说，其实你**在询问的时候根本就不需要update，见到tag=true直接return 目前颜色就好了**。

- 最后就是一个大家都常忽视的一个点，就是在用堆储存线段树的时候大家习惯开4倍空间，实际上是对空间的极大浪费（而且有的人还在4倍空间的基础上再加了5-10个以防万一。。其实本身就完全不可能达到4倍空间，最坏情况也是比4倍空间小4的23333）而我作为一个强迫症患者，平时总是喜欢卡数据范围（你给我最大多少我就只开多少）的设数组（坏习惯大家别模仿），自然也会先算出来最大的空间是多少了，**本题数组最大个数只用开到262145**。不多不少。

###下面是代码时间，正好没有pascal的题解（虽然过几年pascal连NOIP都上不了了），我就过来放个= =。

[P.S.]我的线段树是继承了我看的数据结构书里面的写法，所以存的区间是[l,r)这样的（这就是为啥建的时候我建的是(1,n+1)），而且在change和query的时候也有点不大一样，希望不要介意。

```cpp
program P1558;
const maxt=262145;
type
    node=record
    left:longint;
    right:longint;
    color:longint;
    delta:boolean;
    end;

var n,tot,m,i,le,ri,col:longint;
    c:char;
    a:array[1..maxt]of node;

function lowbit(x:longint):longint;
begin
    exit(x and (-x));
end;

function count(x:longint):longint;
begin
    count:=0;
    while x<>0 do
    begin
        inc(count);
        dec(x,lowbit(x));
    end;
end;

procedure update(cur:longint);
var left,right:longint;
begin
    left:=cur shl 1;
    right:=left+1;
    a[left].color:=a[cur].color;
    a[right].color:=a[cur].color;
    a[left].delta:=true;
    a[right].delta:=true;
    a[cur].delta:=false;
end;

procedure change(cur,l,r,delta:longint);
var left,right,mid:longint;
begin
    if (l<=a[cur].left)and(a[cur].right<=r) then
    begin
        a[cur].color:=1 shl (delta-1);
        a[cur].delta:=true;
    end
    else
    begin
        left:=cur shl 1;
        right:=left+1;
        mid:=(a[cur].left+a[cur].right)shr 1;
        if a[cur].delta then
        update(cur);
        if l<mid then
        change(left,l,r,delta);
        if r>mid then
        change(right,l,r,delta);
        a[cur].color:=a[left].color or a[right].color;
    end;
end;

function query(cur,l,r:longint):longint;
var left,right,mid:longint;
begin
    if ((l<=a[cur].left)and(a[cur].right<=r))or(a[cur].delta) then
    exit(a[cur].color)
    else
    begin
        left:=cur shl 1;
        right:=left+1;
        mid:=(a[cur].left+a[cur].right)shr 1;
        query:=0;
        if l<mid then
        query:=query or query(left,l,r);
        if r>mid then
        query:=query or query(right,l,r);
    end;
end;

procedure built(cur,l,r:longint);
var left,right,mid:longint;
begin
    a[cur].left:=l;
    a[cur].right:=r;
    a[cur].color:=1;
    a[cur].delta:=false;
    if l+1<r then
    begin
        left:=cur shl 1;
        right:=left+1;
        mid:=(l+r)shr 1;
        built(left,l,mid);
        built(right,mid,r);
    end;
end;

begin
    readln(n,tot,m);
    built(1,1,n+1);
    for i:=1 to m do
    begin
        read(c);
        if c='C' then
        begin
            readln(le,ri,col);
            if le<=ri then
            change(1,le,ri+1,col)
            else
            change(1,ri,le+1,col);
        end
        else
        begin
            readln(le,ri);
            if le>ri then
            writeln(count(query(1,ri,le+1)))
            else
            writeln(count(query(1,le,ri+1)));
        end;
    end;
end.
```