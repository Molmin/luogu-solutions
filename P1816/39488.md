都没有P党的题解呢

表示RMQ写不来

写了一个没有修改功能的线段树嘿嘿

L,R数组是写这个点的管理范围，LC,RC是这个点左右孩子是几。KEY就是关键字，这题目是求最小值

下面亮代码咯

```cpp
var
  len:longint;
  q,i,j,m,n,k,p:longint;
  a,l,r,lc,rc,key:array[0..200005]of longint;
function min(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
procedure built(x,y:longint);
var
  mid,now:longint;
begin
  inc(len); now:=len;
  l[now]:=x; r[now]:=y;
  lc[now]:=-1; rc[now]:=-1;//一开始认为没有左右孩子呢
  if x=y then key[now]:=a[x]
  else
    begin
      mid:=(x+y) div 2;//二分这个点管的范围
      lc[now]:=len+1; built(x,mid);
      rc[now]:=len+1; built(mid+1,y);
      key[now]:=min(key[lc[now]],key[rc[now]]);
    end;
end;
function findmin(now,x,y:longint):longint;
var
  mid,a1,a2:longint;
begin
  if (l[now]=x)and(r[now]=y) then findmin:=key[now]
  else
    begin
      a1:=lc[now]; a2:=rc[now];
      mid:=(l[now]+r[now]) div 2;
      if y<=mid then findmin:=findmin(a1,x,y) //如果要求的区间完全在左孩子管的范围内
      else if mid<x then findmin:=findmin(a2,x,y) //如果要求的区间完全在右孩子管的范围内
      else
        findmin:=min(findmin(a1,x,mid),findmin(a2,mid+1,y));//两边都有就分开
    end;
end;
begin
  readln(n,q);
  for i:=1 to n do
    read(a[i]);
  readln;
  len:=0; built(1,n);//这个就是建树啦
  for i:=1 to q do
    begin
      readln(k,p);
      write(findmin(1,k,p),' ');//查找区间
    end;
end.
```