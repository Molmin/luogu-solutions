对于此题，发一个RMQ的pas版本

倍增思想，预处理出每个点的后2^i个数的最小值，然后每次用O（1） 的时间复杂度查找区间内的最小元素，

查找时，如果正好区间长度为2^i，直接查找

如果不是，那么2个为2^k（k=log2（区间长度）下取整）的长度区间的区间的并集的最小值就是当前区间的最小值。

当然 第一种情况可以看做第二种情况的特例；

```cpp
program luo1816;
const maxn=100000;
      jc:array[0..20] of longint=(1,2,4,8,16,32,64,128,256,512,1024,2048,4096,8192,16384,32768,65536,131072,262144,524288,1048576);//打表，快速幂，或直接幂运算都可以
var f:array[1..maxn,0..20] of longint;
    n,m,i,j,x,y:longint;
function min(a,b:longint):longint;
begin
  if a<b then exit(a);
  exit(b);
end;
procedure init;
var i:longint;
begin
  readln(n,m);
  for i:=1 to n do
   read(f[i,0]);
end;
procedure pre;//预处理RMQ
var i,j:longint;
begin
  for j:=1 to 20 do
   for i:=1 to n do
   begin
     if i+jc[j]-1>n then  break;//别忘了-1
     f[i,j]:=min(f[i,j-1],f[i+jc[j-1],j-1]);//同上
   end;
end;
function find(l,r:longint):longint;//查找
var k:longint;
begin
  k:=trunc(ln(r-l+1)/ln(2));//关键
  exit(min(f[l,k],f[r-jc[k]+1,k]));
end;
begin
  init;
  pre;
  for i:=1 to m do
  begin
    readln(x,y);
    write(find(x,y),' ');
  end;
end.
```