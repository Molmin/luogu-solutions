~~这是树状数组的模板题吗？~~

根据要添加一包新的干草到范围为A..B的干草堆顶，很容易想到用数据结构来维护进行区间修改，然后对每一堆干草进行单点查询，然后排序，求中位数

因为数据范围达到了10^6，树状数组相对线段树来说常数小，还好写，所以采用了树状数组，运用差分思想来实现

上代码：

```pascal
var
 a,tree:array[0..1000005]of longint;
 n,k,i,l,r,id:longint;
function lowbit(x:longint):longint;
//树状数组的核心lowbit
begin
 exit(x and (-x));
end;
procedure addsum(x,y:longint);//修改操作
begin
 while x<=n do
 begin
  tree[x]:=tree[x]+y;
  x:=x+lowbit(x);
 end;
end;
function getsum(x:longint):longint;//查询操作
begin
 getsum:=0;
 while x>0 do
 begin
  getsum:=getsum+tree[x];
  x:=x-lowbit(x);
 end;
 exit(getsum);
end;
procedure qsort(l,r:longint);//快排。。。
var
 t,mid,i,j:longint;
begin
 i:=l;j:=r;
 mid:=a[(l+r)div 2];
 repeat
  while a[i]<mid do inc(i);
  while a[j]>mid do dec(j);
  if i<=j then
  begin
   t:=a[i];a[i]:=a[j];a[j]:=t;
   inc(i);dec(j);
  end;
 until i>j;
 if l<j then qsort(l,j);
 if i<r then qsort(i,r);
end;
begin
 read(n,k);
 for i:=1 to k do
 begin
  read(l,r);//读入
  addsum(l,1);
  addsum(r+1,-1);//通过差分来对[l..r]加上1
 end;
 for i:=1 to n do
  a[i]:=getsum(i);
 //统计每一堆干草的高度
 qsort(1,n);
 id:=(n+1)>>1;//求出中位数的下标
 write(a[id]);
end.
```