附带此题我用各种方法的总时间：

SPFA：689ms

SPFA双优化：627ms

dijkstra堆优化：617ms

裸dijkstra：2194ms（简直了）

以下我就给出SPFA的双优化以及Dijkstra的堆优化两种方法吧



双优化版SPFA——PASCAL党的福利：众所周知SPFA的时间复杂度并不稳定，所以有两个著名的优化：SLF和LLL


SLF优化：将新加入的结点与队首比较，如果比队首更优则加入队首，否则加入队尾，这样能保证尽可能的把每次松弛变得更有效


LLL优化：队首h出队时，假设全队的距离平均值为aver，如果d[q[h]]>aver则把当前队首放到队尾，直到找到一个<=aver的再进行松弛


据网上某一大神所说，SLF能优化10%-20%，双优化能快几乎50%。


不知为何此题我加上优化后虽然确实快了一些，但是效果并非那么明显，可能是此题的数据不是很残暴吧。但是这两种优化的方法还是值得推荐的




```cpp
const max=2147483647;
var d,f,last:array[0..10000] of longint;
    e,next,w:Array[1..600000] of longint;
    q:array[-10000..5000000] of longint;//注意对于我的代码来说，队列要开到负数，不然会RE，具体原因往下看就知道了
    x,y,z,i,j,k,m,n,s,h,t,p,po,poo,tot,now:longint;aver:double;
begin
    assign(input,'P3371.in');reset(input);
    readln(n,m,s);
    for i:=1 to m do
    begin
        readln(x,y,z);
        e[i]:=y;w[i]:=z;next[i]:=last[x];last[x]:=i;
    end;
    h:=0;t:=1;
    for i:=0 to n do
      d[i]:=max;
    d[s]:=0;f[s]:=1;q[1]:=s;aver:=0;tot:=0;
    while h<t do
    begin
        inc(h);
        while d[q[h]]>aver do//LLL优化的比较操作
        begin
            inc(t);
            q[t]:=q[h];
            inc(h);
        end;
        dec(tot,d[q[h]]);
        if t=h then aver:=tot else aver:=tot/(t-h);//如果h=t那么队中只有一个元素，aver=tot/1
        p:=last[q[h]];
        while p<>0 do
        begin
            po:=e[p];
            if d[q[h]]+w[p]<d[po] then
            begin
                d[po]:=d[q[h]]+w[p];
                if f[po]=0 then
                begin
                    f[po]:=1;
                    if (d[po]<=d[q[h+1]])and(h+1<t) then//SLF优化操作
                    begin
                        poo:=q[h];
                        q[h]:=po;now:=h;
                        dec(h);//一不小心可能h就小于0了
                        q[h]:=poo;
                    end else
                    begin
                        inc(t);
                        q[t]:=po;now:=t;
                    end;
                    inc(tot,d[q[now]]);
                    aver:=tot/(t-h);
                end;
            end;
            p:=next[p];
        end;
        f[q[h]]:=0;
    end;
    for i:=1 to n do
      write(d[i],' ');
end.
```
继续pascal福利：写了一遍堆优化的dijkstra算法，毕竟是模板题就当练练各种方法了吧，下面也有某些神犇的堆优化代码，所以我觉得注释写尽量多一点吧....
我们知道dijkstra算法最重要的一点就是每次查找未染色的点中距离最小的点，每次都要枚举的话显然非常耽误时间，这就是为什么裸的dijkstra算法是O（n^2），数据大一点就完蛋。由于是每次都只求最小值，所以考虑用堆来优化（毕竟pascal没有优先队列这玩意儿）。堆最大的优点在于每次求最小值的时候都是log级别的，就要快很多，所以优化后的dijkstra算法复杂度基本保持在O（nlogn），比起SPFA都要稳定很多

```cpp
const max=2147483647;
type node=record
     data,num:longint;//data存距离数值，num存此结点的编号
     end;
var last,f,d:Array[1..10000] of longint;
    e,next,w:array[1..500000] of longint;
    dui:Array[0..2000000] of node;//堆数组
    i,j,k,m,n,nd,x,y,z,p,t,u,s,po:longint;
procedure down(pos:longint);//堆有著名的上传下放过程，不懂的还是先学一下堆结构吧，也不是特别难
var i:longint;k:node;
begin
    i:=pos;k:=dui[pos];
    if (2*pos<=nd)and(k.data>dui[2*pos].data) then//nd为堆的结点数量
    begin
        i:=2*pos;
        k:=dui[2*pos];
    end;
    if (2*pos+1<=nd)and(k.data>dui[2*pos+1].data) then
    begin
        i:=2*pos+1;
        k:=dui[2*pos+1];
    end;
    if i<>pos then
    begin
        dui[i]:=dui[pos];
        dui[pos]:=k;
        down(i);
    end;
end;
procedure up(pos:longint);
var i:longint;k:node;
begin
    if pos=1 then exit;
    if dui[pos].data<dui[pos div 2].data then
    begin
        k:=dui[pos];
        dui[pos]:=dui[pos div 2];
        dui[pos div 2]:=k;
        up(pos div 2);
    end;
end;
function pop:longint;//弹出堆顶，然后再更新一遍堆
begin
    pop:=dui[1].num;
    dui[1]:=dui[nd];
    dec(nd);
    if nd<=1 then exit;
    down(1);
end;
begin
    assign(input,'P3371.in');reset(input);
    readln(n,m,s);
    for i:=1 to m do
    begin
        readln(x,y,z);
        e[i]:=y;
        next[i]:=last[x];
        last[x]:=i;
        w[i]:=z;
    end;
    for i:=1 to n do
      d[i]:=max;
    d[s]:=0;dui[1].data:=0;dui[1].num:=s;nd:=1;
    for i:=1 to n do
    begin
        u:=pop;
        while (f[u]=1)and(nd>0) do u:=pop;//默默地弹出我们需要的结点
        f[u]:=1;
        p:=last[u];
        while p<>0 do
        begin
            po:=e[p];
            if (d[u]+w[p]<d[po])and(f[po]=0) then
            begin
                d[po]:=d[u]+w[p];
                inc(nd);
                dui[nd].num:=po;dui[nd].data:=d[po];
                up(nd);//松弛后入堆
            end;
            p:=next[p];
        end;
    end;
    for i:=1 to n do
      write(d[i],' ');
end.
```