练练堆优dij

pas 福利（没有STL真伤感）







```cpp
program dij;
type
    rec=record
          n,t,d:longint;
        end;
    rec2=record
          num,data:longint;
         end;
var n,m,s,cnt:longint;
    w:array[0..500000] of rec;
    h,d,pl:array[0..10000] of longint;
    v:array[0..10000] of boolean;
    small:array[0..100000] of rec2;
procedure init;
var i,tot:longint;
    x,y,z:longint;
begin                                             //读入，模拟邻接表存图
  readln(n,m,s); tot:=0;
  for i:=1 to m do
  begin
    readln(x,y,z);
    inc(tot);
    w[tot].n:=h[x]; h[x]:=tot; w[tot].t:=y; w[tot].d:=z;
  end;
  for i:=1 to n do d[i]:=maxlongint;
  d[s]:=0; cnt:=0;
  fillchar(small,sizeof(small),127);
  small[0].data:=0;
end;
procedure swap(a,b:longint);      //交换位置
var p:rec2; q:longint;
begin
  q:=pl[small[a].num]; pl[small[a].num]:=pl[small[b].num]; pl[small[b].num]:=q;
  p:=small[a]; small[a]:=small[b]; small[b]:=p;
end;
procedure put(x:longint);            //堆的加入操作
var k:longint;
begin
  if pl[x]=0 then                        //判断是否在堆中已经存在（pl数组标记元素在堆中的位置）
  begin
    inc(cnt);
    pl[x]:=cnt;
    small[cnt].num:=x;
    small[cnt].data:=d[x];
    k:=cnt;
  end
  else
  begin
    small[pl[x]].data:=d[x];          //如果已经存在，直接修改
    k:=pl[x];
  end;
  while small[k].data<small[k div 2].data do   //维护堆的性质
  begin
    swap(k,k div 2);
    k:=k div 2;
  end;
end;
function get:longint;                               //堆的取出操作
var k:longint;
begin
  get:=small[1].num;
  pl[small[cnt].num]:=1;                          //维护堆的性质
  small[1]:=small[cnt];
  small[cnt].data:=maxlongint;
  k:=1;  dec(cnt);
  if cnt>0 then
  while (small[k].data>small[k*2].data) or (small[k].data>small[k*2+1].data) do
  begin
    if small[k*2].data<small[k*2+1].data then
    begin
      swap(k,k*2);
      k:=k*2;
    end
    else
    begin
      swap(k,k*2+1);
      k:=(k*2+1);
    end;
  end;
end;
procedure find(x:longint);                       //拓展某一点的所有出边（当然是没走过的）
var k:longint;
begin
  k:=h[x];
  while k>0 do
  begin
    if not v[w[k].t] then
      if w[k].d+d[x]<d[w[k].t] then
      begin
        d[w[k].t]:=w[k].d+d[x];
        put(w[k].t);
      end;
    k:=w[k].n;
  end;
end;
procedure dijkstra(x:longint);                  //dijkstra
var i:longint;
    p,min:longint;
begin
  v[x]:=true;
  find(x);
  for i:=1 to n-1 do
  begin
    if cnt>0 then
     p:=get                                               //这里将循环取得最小值改为在堆中判断最小值
    else
     break;
    v[p]:=true;
    find(p);
  end;
end;
procedure print;
var i:longint;
begin
  for i:=1 to n do
   write(d[i],' ');
end;
begin
  init;
  dijkstra(s);
  print;
end.
```