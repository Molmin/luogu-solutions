用并查集 但并查集只能并不能删 所以我们可以逆向思维反着做

从最后一个星球开始恢复 每恢复一个星球同时恢复相关的道路 记录下此时的连通块个数 最后逆序输出即可

另外要注意的是存储 用记录类型记录下边集 再记录下以这个星球为关键字时的前一条边的序号 就像链表一样 只用知道以这个星球为关键字时最后一条边的编号 就可以一个一个往前面找 一直找到第一个 所以我们还需要一个tail数组记录以这个星球为关键字时最后一条边的编号

而一条路连接两个星球 为了方便查找 我们需要把两个星球分别作为关键字存储边


记录连通块个数的方法：每恢复一个星球 视该星球为独立的 连通块加一 再对相关道路进行处理 每合并一次 连通块个数减一


```delphi

var n,m,i,x,y,k,j,crt,lb:longint;
    b,flag,f,tail:array[0..800000]of longint;
    a:array[0..800000]of record
                           p1,p2,front:longint;
                         end;
function gfa(u:longint):longint;//寻找根节点
begin
    if f[u]=u then exit(u);     //f为父节点
    f[u]:=gfa(f[u]);
    exit(f[u]);
end;
procedure union(u,v:longint);   //合并
var fu,fv:longint;
begin
    fu:=gfa(u);fv:=gfa(v);
    if fu<>fv then begin f[fu]:=fv;dec(crt);end;
end;
begin
    readln(n,m);
    for i:=0 to n-1 do f[i]:=i;//将每个的根节点初始化为自身
    for i:=1 to m do
    begin
        readln(x,y);   
        a[2*i-1].p1:=x;a[2*i-1].p2:=y;a[2*i-1].front:=tail[x];tail[x]:=2*i-1;
        //2*i-1记录以x为关键字的边
        a[2*i].p1:=y;a[2*i].p2:=x;a[2*i].front:=tail[y];tail[y]:=2*i;
        //2*i记录以y为关键字的边
    end;
    readln(k);
    for i:=1 to k do
    begin
        readln(b[i]);//将要摧毁的星球序号存入b数组
        flag[b[i]]:=1;//最后该星球为被占领状态
    end;
    crt:=n-k;//连通块个数
    for i:=1 to m do //所有最后仍未被占领的星球合并
        if (flag[a[i*2-1].p1]=0)and(flag[a[i*2-1].p2]=0) then
            union(a[i*2-1].p1,a[i*2-1].p2);
    b[k+1]:=crt;
    for i:=k downto 1 do  //对每一个被占领的星球依次恢复
    begin
        inc(crt);
        lb:=tail[b[i]];    
        flag[b[i]]:=0;  //恢复b[i]
        repeat  //以b[i]为关键字往前找
            if flag[a[lb].p2]=0 then union(a[lb].p1,a[lb].p2);
            lb:=a[lb].front;
        until lb=0;
        b[i]:=crt;  //此时b[i]没有用处了 可以将结果存入b[i] 
    end;
    for i:=1 to k+1 do
        writeln(b[i]);
end.

```