**莫队+treap**

算是对之前学的一个复习吧

不会莫队或平衡树的同学可以先去看看[洛谷P3369](https://www.luogu.org/problemnew/show/P3369)和[洛谷P3901](https://www.luogu.org/problemnew/show/P3901)

**这里写一下对于莫队的自己的一些理解**

就是莫队(while)的时候，4个指针移动的顺序，应该是先写从里向外的扩展，然后再写从外向里的

#### 什么意思呢？
**就是先默认你现在处理的范围比你要求的范围小，扩展再写缩小的范围**

这对后面的问题处理没有影响，但是对第1个问题的求解有影响

因为我们一开始l=1 r=0;我们的第一个问题是[2,4],如果我们先缩小范围，再扩大范围的话，会误把1这个位置的元素删去

### 应该没有想错吧，如果写错了dalao私信指导我一下吧

然后就是平衡树的三种基本操作了：插入，删除，找第k大

**Code:(Pascal)：**
```
var
  root,sizen,ll,rr:longint;
  block,i,j,m,n,k,p:longint;
  a,rnd,v,w,l,r,size:array[0..1000005]of longint;
  x,y,num,ans,z:array[0..50005]of longint;
procedure qs(l,r:longint);
var
  i,j,mid1,mid2,a1:longint;
begin
  i:=l; j:=r;
  mid1:=x[(l+r) div 2]; mid2:=y[(l+r) div 2];
  repeat
    if i div block=j div block then
      begin
        while x[i]<mid1 do inc(i);
        while x[j]>mid1 do dec(j);
        if not(i>j) then
          begin
            a1:=x[i]; x[i]:=x[j]; x[j]:=a1;
            a1:=y[i]; y[i]:=y[j]; y[j]:=a1;
            a1:=z[i]; z[i]:=z[j]; z[j]:=a1;
            a1:=num[i]; num[i]:=num[j]; num[j]:=a1;
            inc(i); dec(j);
          end;
      end
    else
      begin
        while y[i]<mid2 do inc(i);
        while y[j]>mid2 do dec(j);
        if not(i>j) then
          begin
            a1:=x[i]; x[i]:=x[j]; x[j]:=a1;
            a1:=y[i]; y[i]:=y[j]; y[j]:=a1;
            a1:=z[i]; z[i]:=z[j]; z[j]:=a1;
            a1:=num[i]; num[i]:=num[j]; num[j]:=a1;
            inc(i); dec(j);
          end;
      end;
  until i>j;
  if i<r then qs(i,r);
  if l<j then qs(l,j);
end;
procedure update(k:longint);
begin
  size[k]:=size[l[k]]+size[r[k]]+w[k];
end;
procedure rturn(var k:longint);
var
  child:longint;
begin
  child:=l[k]; l[k]:=r[child]; r[child]:=k;
  size[child]:=size[k]; update(k); k:=child;
end;
procedure lturn(var k:longint);
var
  child:longint;
begin
  child:=r[k]; r[k]:=l[child]; l[child]:=k;
  size[child]:=size[k]; update(k); k:=child;
end;
procedure put(var k:longint; x:longint);
begin
  if k=0 then
    begin
      inc(sizen); k:=sizen;
      rnd[k]:=random(20*n+5); w[k]:=1;
      v[k]:=x; size[k]:=1;
      exit;
    end;
  inc(size[k]);
  if x=v[k] then inc(w[k])
  else
    if x>v[k] then
      begin
        put(r[k],x);
        if rnd[r[k]]<rnd[k] then lturn(k);
      end
    else
      begin
        put(l[k],x);
        if rnd[l[k]]<rnd[k] then rturn(k);
      end;
end;
procedure delete(var k:longint; x:longint);
begin
  if k=0 then exit;
  if v[k]=x then
    begin
      if w[k]>1 then
        begin dec(size[k]); dec(w[k]); exit; end;
      if l[k]*r[k]=0 then
        k:=l[k]+r[k]
      else
        begin
          if rnd[l[k]]<rnd[r[k]] then
            begin rturn(k); delete(k,x); end
          else
            begin lturn(k); delete(k,x); end;
        end;
    end
  else
    begin
      if x>v[k] then
        begin dec(size[k]); delete(r[k],x); end
      else
        begin dec(size[k]); delete(l[k],x); end;
    end;
end;
function get(k,x:longint):longint;
begin
  if x<=size[l[k]] then
    exit(get(l[k],x))
  else
    if x>size[l[k]]+w[k] then
      exit(get(r[k],x-size[l[k]]-w[k]))
  else
    exit(v[k]);
end;
begin
  randomize;
  readln(n,m);
  block:=trunc(sqrt(n));
  for i:=1 to n do
    read(a[i]);
  readln;
  for i:=1 to m do
    begin
      readln(x[i],y[i],z[i]);
      num[i]:=i;
    end;
  qs(1,m);
  ll:=1; rr:=0;
  for i:=1 to m do
    begin
      while ll>x[i] do
        begin
          dec(ll); put(root,a[ll]);
        end;
      while rr<y[i] do
        begin
          inc(rr); put(root,a[rr]);
        end;
      while ll<x[i] do
        begin
         delete(root,a[ll]); inc(ll);
        end;
      while rr>y[i] do
        begin
          delete(root,a[rr]); dec(rr);
        end;
      ans[num[i]]:=get(root,z[i]);
    end;
  for i:=1 to m do
    writeln(ans[i]);
end.

```