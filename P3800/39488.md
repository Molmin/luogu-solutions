一道很好的用单调队列优化DP的题目

首先你要先推出DP的公式，这道题和那种数塔很像(刚学DP都要做的题目)

f[i,j]表示走到(i,j)这个位置得到的最大power

那么显而易见对于这个状态f[i,j]可以由**上一行的 j-t到j+t 的状态中推过来**。
t是灵梦能左右移动的速度

**那么状态转移方程就是f[i,j]=max(f[i-1,k]); k∈[j-t,j+t]**，注意一下有没有超边界的话就可以用DP做出来了

最大的答案应该在f[n,j]，j∈[1,m]中找一个最大值

但是这样时间是会超的，如果我没有算错的话，计算机最差情况要计算64000000000次，超过一亿次的话不能在1秒钟出答案的

**优化**

我们可以从方程中发现，对于f[i,j]，我们每次都是在f[i-1,k]，k∈[j-t,j+t]里找一个最大值，对于下一个j，我们要找最大值的范围基本上也就是向有平移一格，这样的话有很多冗余的查找，如果可以更快的找出最大值就可以过这题了

这里就可以应用到我们的单调队列了，维护一个单调递减的队列，那么每次找最大值我就只需要把队列的头元素拿出来就行了，时间复杂度O(1);

**那么怎么维护呢，我的代码中dp[i],表示上一行的各列的最大power(怕超空间)
s[i]是我要推出来的这一行各列的最大power，其实就是滚动数组**

那么推s[i]就要在dp[i]中找在范围内的最大值
定义一个单调队列f，**如果新加进去的dp[i]连上去能保证单调递减，那就接上去，如果不能，那就不停从队列尾部弹出元素，直到dp[i]接在上面能保证单调**

拿出最大值的时候还需要判断一下，如果这个最大值不在我们的范围内，是一个过时的最大值，那我们就head=head+1;直到找到一个最大值在范围内。处理这个的话只需要加一个num数组记一下这个值是在哪里找到的就可以解决了

说这么多好累啊，看代码吧(**P党福利！！**)
```
var
  ans:int64;
  point,head,tail,a1,a2,a3:longint;
  len,i,j,m,n,k,p:longint;
  map:array[0..4005,0..4005]of longint;
  dp,f,s,num:array[0..4005]of int64;
function min(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
begin
  readln(n,m,k,len);
  for i:=1 to k do
    begin
      readln(a1,a2,a3);
      map[a1,a2]:=a3;
    end;
  for i:=1 to m do
    dp[i]:=map[1,i];
  for i:=2 to n do
    begin
      a1:=min(m,1+len);
      head:=1; tail:=1; f[1]:=dp[1]; num[1]:=1;
      for j:=2 to a1 do
        begin
          if f[tail]>dp[j] then
            begin
              inc(tail); f[tail]:=dp[j]; num[tail]:=j;
            end
          else
            begin
              while (f[tail]<dp[j])and(tail>=head) do dec(tail);
              inc(tail); f[tail]:=dp[j]; num[tail]:=j;
            end;
        end;
      s[1]:=f[head]+map[i,1]; point:=1+len;
      for j:=2 to m do
        begin
          if point+1<=m then
            begin
              inc(point); a2:=dp[point];
              if f[tail]>a2 then
                begin
                  inc(tail); f[tail]:=a2; num[tail]:=point;
                end
              else
                begin
                  while (f[tail]<a2)and(tail>=head) do dec(tail);
                  inc(tail); f[tail]:=a2; num[tail]:=point;
                end;
            end;
          while num[head]<j-len do inc(head);
          s[j]:=f[head]+map[i,j];
        end;
      for j:=1 to m do
        dp[j]:=s[j];
    end;
  for j:=1 to m do
    if ans<dp[j] then
      ans:=dp[j];
  writeln(ans);
end.
```