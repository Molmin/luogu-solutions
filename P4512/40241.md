多项式Pascal题解·第三弹

把[多项式求逆](https://www.luogu.org/problemnew/show/P4512)的板子复制下来改一改就好了（我的[多项式求逆题解](qwq

这题 $ Q_R(x) = F_R(x)G_R^{-1}(x) $，$ R(x) $ 直接求就好了。

代码写的超丑，凑合着看吧

~~吐槽一句这题题解怎么那么少啊~~

```pas
type
  arr = array[0..524287] of longint;//定义多项式
const
  p  = 998244353;              //模数
  g  = 3;                      //原根
  gi = 332748118;
var
  a, b, q, rr, pow, r: arr;
  n, m, i: longint;

  procedure swap(var x, y: longint);//交换
  var
    t: longint;
  begin
    t := x;
    x := y;
    y := t;
  end;

  function power(x, y: longint): longint;//快速幂
  begin
    power := 1;
    while y > 0 do
    begin
      if odd(y) then
        power := int64(power) * x mod p;
      x := sqr(int64(x)) mod p;
      y := y shr 1;
    end;
  end;

  function inv(x: longint): longint;//逆元
  begin
    exit(power(x, p - 2));
  end;

  procedure reverse(var a: arr; n: longint);//翻转
  var
    i: longint;
  begin
    for i := 0 to n div 2 do
      swap(a[i], a[n - i]);
  end;

  procedure ntt(var a: arr; n, s: longint);//NTT，不详细写了
  var
    i, j, k, x, y, w: longint;
  begin
    for i := 0 to n - 1 do
      if i < r[i] then
        swap(a[i], a[r[i]]);
    i := 1;
    while i < n do
    begin
      j := 0;
      if s = 1 then
        w := power(g, (p - 1) div (i shl 1))
      else
        w := power(gi, (p - 1) div (i shl 1));
      pow[0] := 1;
      for k := 1 to i - 1 do
        pow[k] := int64(pow[k - 1]) * w mod p;
      while j < n do
      begin
        for k := 0 to i - 1 do
        begin
          x := a[j + k];
          y := int64(a[i + j + k]) * pow[k] mod p;
          a[j + k] := (x + y) mod p;
          a[i + j + k] := (x - y) mod p;
        end;
        Inc(j, i shl 1);
      end;
      i := i shl 1;
    end;
    if s = -1 then
    begin
      x := inv(n);
      for i := 0 to n - 1 do
        a[i] := (int64(a[i]) * x mod p + p) mod p;
    end;
  end;

  function mul(a, b: arr; n, m: longint): arr;//多项式乘法，不详细写了
  var
    i, l, num: longint;
  begin
    num := 1;
    l := 0;
    while num <= n + m do
    begin
      num := num shl 1;
      Inc(l);
    end;
    for i := 0 to num - 1 do
      r[i] := (r[i shr 1] shr 1) or ((i and 1) shl (l - 1));
    ntt(a, num, 1);
    ntt(b, num, 1);
    for i := 0 to num - 1 do
      mul[i] := int64(a[i]) * b[i] mod p;
    ntt(mul, num, -1);
  end;

  function inv(x: arr; n: longint): arr;//求逆元，不详细写了
  var
    t: arr;
    i, l, num: longint;
  begin
    num := 2;
    l := 1;
    inv[0] := inv(x[0]);
    while num <= n shl 1 do
    begin
      num := num shl 1;
      Inc(l);
      for i := 0 to num - 1 do
        r[i] := (r[i shr 1] shr 1) or ((i and 1) shl (l - 1));
      for i := 0 to num shr 1 - 1 do
        t[i] := x[i];
      ntt(inv, num, 1);
      ntt(t, num, 1);
      for i := 0 to num - 1 do
        inv[i] := int64((p + 2 - int64(inv[i]) * t[i] mod p) mod p) * inv[i] mod p;
      ntt(inv, num, -1);
      for i := num shr 1 to num - 1 do
        inv[i] := 0;
    end;
    for i := n + 1 to num shr 1 - 1 do
      inv[i] := 0;
  end;

  function dv(a, b: arr; n, m: longint): arr;//多项式除法啦啦啦
  var
    i: longint;
  begin
    reverse(a, n);              //翻转a
    reverse(b, m);              //翻转b
    dv := mul(a, inv(b, n - m), n, n - m);//a*b^-1
    reverse(dv, n - m);         //答案再翻转
    for i := n - m + 1 to 2 * n do//清零
      dv[i] := 0;
  end;

begin
  Read(n, m);
  for i := 0 to n do
    Read(a[i]);
  for i := 0 to m do
    Read(b[i]);
  q := dv(a, b, n, m);
  for i := 0 to n - m do
    Write(q[i], ' ');
  writeln;
  rr := mul(q, b, n - m, m);
  for i := 0 to m - 1 do        //边算边输出
    Write((p + a[i] - rr[i]) mod p, ' ');
  writeln;
end.
```