# 题意：

- 在一个 $n\times m$ 的网格中，每一个格子里都有一个数字，表示到达这个格子后可以移动的步数（方向为上下左右）。求从左上角到右下角的最短路径，若无解，输出`-1`。

# $\mathcal{Sol:}$

- 对于 $n,m\le 500$ 的数据，直接暴力bfs，不过较bfs模板有下面的改变：

- 在bfs模板中的方向数组 $(dx[i],\ dy[i])$ 不再是简单的移动一格。在本题中需要**每访问一个格子就更新**一下它。

实现代码：
```pascal
procedure make (x:longint);
  begin
   dx[1]:=x; dx[2]:=0; dx[3]:=-x; dx[4]:=0; //dx表示纵坐标
	dy[1]:=0; dy[2]:=x; dy[3]:=0; dy[4]:=-x; //dy表示横坐标
    //x，-x和0交替出现，是为了满足上下左右的方向要求
  end; 

```

- 还有个坑点，就是bfs队列数组要开到 $250000 (500*500)$ ，否则会RE。

# $\mathcal{Code:}$

```pascal
uses math;
var
  n,m,l,r,x,y,i,j:longint;
  dx,dy,a,b,c:array[0..250001] of longint;
  map:array[0..501] of ansistring;
  v:array[0..501,0..501] of boolean;
procedure make (t:char);
  var x:longint;
  begin
    val(t,x); //将t字符转换为数字并将值放入x
    dx[1]:=x; dx[2]:=0; dx[3]:=-x; dx[4]:=0;
	dy[1]:=0; dy[2]:=x; dy[3]:=0; dy[4]:=-x;
  end; 
begin
  readln(n,m);
  for i:=1 to n do readln(map[i]);
  //以下bfs模板
  l:=1; r:=1; a[1]:=1; b[1]:=1; c[1]:=0;
  v[1,1]:=true;
  while l<=r do begin
    x:=a[l]; y:=b[l];
	if (x=n)and(y=m) then begin write(c[l]); exit; end;
	make(map[x,y]); //本题核心
	for i:=1 to 4 do 
	  if (x+dx[i]>0)and(x+dx[i]<=n)and(y+dy[i]>0)and(y+dy[i]<=m) then 
	    if not v[x+dx[i],y+dy[i]] then 
	      begin
 	        inc(r); a[r]:=x+dx[i]; b[r]:=y+dy[i]; c[r]:=c[l]+1;
			v[a[r],b[r]]:=true;
	      end;
	inc(l);
  end;  
  write(-1); //无解
end.
```