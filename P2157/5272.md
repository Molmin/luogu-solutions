这是数据范围

对于30%的数据，满足1 ≤ N ≤ 20。

对于100%的数据，满足1 ≤ N ≤ 1,000，0 ≤ Ti ≤ 1,000，[color=red]0 ≤ Bi ≤ 7[/color]，1 ≤ C ≤ 5。

存在30%的数据，满足0 ≤ Bi ≤ 1。

存在65%的数据，满足0 ≤ Bi ≤ 5。

存在45%的数据，满足0 ≤ Ti ≤ 130。

所以说，这些同学们的气量才是两点，由于bi很小，所以说我们的边界情况就很明显了，所以说我们猜测这道题是不是一个状压dp，答案是肯定的。

如何确定我们的动态转移方程呢？

1.显然动态转移方程中一定含有i，即已经处理了前i-1个，正在处理i时的情况（循环更新的就不要计较了）

2.边界情况是吃没吃饭，所以既然是状态压缩，有一维变量表示i及i后7个人吃没吃饭

3.由于做菜时间与前一个拿到饭菜的人有关，还有一维变量存储的是上一个拿到饭菜的人相对于目前i的位置，根据数据范围，这一维的范围是-8~7

综上所述，我们可以猜到动态转移方程了

f[i,j,k]：前i-1个人已经吃完了，j:i及后面7个人吃饭情况，k:最后一个吃饭的人是i+k

若j=(*******1)(2进制下)说明i已经吃了，那么f[i+1,j shr 1,k-1]:=min(f[i+1,j shr 1,k-1],f[i,j,k])注意：i->i+1时，最后一个吃饭的人的相对位置也发生变化，吃饭情况为整体向后移一位

若j=(*******0)i没吃，i~i+7随便找一个没吃且合适的人，更新

```delphi

program p2157;
var
  f:array[1..1005,0..255,-8..7]of longint;
    co,an:array[1..1005]of longint;
    t2:array[0..7]of longint;
    i,j,k,l,r,ans,cc,ccc,inf,n:longint;
function cal(x,y:longint):longint;
begin
  if x=0 then exit(0)
      else exit(co[x]xor co[y]);
end;
function min(x,y:longint):longint;
begin
  if x>y then exit(y) else exit(x);
end;
begin
 
    readln(ccc);
    t2[0]:=1;
    for i:=1 to 7 do t2[i]:=t2[i-1]*2;
    for cc:=1 to ccc do begin
      readln(n);
        for i:=1 to n do readln(co[i],an[i]);
        fillchar(f,sizeof(f),$7f);
        inf:=f[1,0,-1];
        f[1,0,-1]:=0;
        for i:=1 to n do
          for j:=0 to 255 do
              for k:=-8 to 7 do
                  if f[i,j,k]<inf then begin
                    if (j and 1<>0) then
                          f[i+1,j shr 1,k-1]:=min(f[i+1,j shr 1,k-1],f[i,j,k])
                        else begin
                          r:=inf;
                            for l:=0 to 7 do
                            if j and t2[l]=0 then begin
                                if i+l>r then break;
                                  r:=min(r,i+l+an[i+l]);
                                f[i,j+t2[l],l]:=min(f[i,j+t2[l],l],f[i,j,k]+cal(i+k,i+l));
                              end;
                        end;
                    end;
        ans:=maxlongint;
        for i:=-8 to -1 do
          ans:=min(ans,f[n+1,0,i]);
        writeln(ans);
  end;
end.

```