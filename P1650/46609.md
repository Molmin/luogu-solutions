## 一道经典的题目 $ QWQ $
### 如果做完这道题，推荐大佬顺便A一下[P2587 [ZJOI2008]泡泡堂](https://www.luogu.org/problemnew/show/P2587)，只是在这道题的基础上反着做一遍就可以了。
### 要想做这道题，千万别被描述中那最后一段话吓到了...（本蒟蒻直接瘫坐在地上）
### 相对于泡泡堂，这道题就显得较为简单了。
### 首先排序qsort，必须滴-----
### 然后做的时候分下面三种情况：
- #### 先用自己队最强的和对方队最强的互怼一下，如果能赢，那么就把分数+2,指针各往后移动一格，进入下一次循环；（比如说下图）
![](https://i.loli.net/2019/02/02/5c55580249db5.png)

- #### 如果怼不过，那就再拿自己队最弱的和对方队最强的怼，能怼赢，同样的，加上分数，指针往前移一格（比如说下图）；
![](https://i.loli.net/2019/02/02/5c555882e64e1.png)

- #### 若是还怼不过，只能硬刚了，就拿自己队最弱的耗掉对方最强的，移动指针（比如下图）
![](https://i.loli.net/2019/02/02/5c5559584306d.png)

### 特别注释：这几张~~也许~~跟泡泡堂某篇题解里的图一样，那篇题解也是我写的，请管理员巨佬不要怀疑$ QAQ $
### 下面就来瞧一瞧丑陋的代码吧：
```
type arr=array[0..2000] of longint;  //让下面排序尽可能地方便一些，也可以排两次序
var
  n,i,tjhead,qwhead,tjtail,qwtail,ans:longint;
  tj,qw:array[0..2000] of longint;
procedure qsort(l,r:longint;var a:arr);  //瞧，这边'a:arr'用到了吧
var
  i,j,mid,t:longint;
begin
  i:=l;j:=r;
  mid:=a[(l+r)>>1];
  repeat
    while a[i]<mid do inc(i);
    while a[j]>mid do dec(j);
    if i<=j then
     begin
       t:=a[i];a[i]:=a[j];a[j]:=t;
       inc(i);dec(j);
     end;
  until i>j;
  if l<j then qsort(l,j,a);
  if i<r then qsort(i,r,a);
end;                          //快排...
begin
  readln(n);
  for i:=1 to n do read(tj[i]);
  readln;
  for i:=1 to n do read(qw[i]);
  qsort(1,n,tj);  //排田忌的马
  qsort(1,n,qw);  //排齐王的马
  tjhead:=1;qwhead:=1;tjtail:=n;qwtail:=n;  //初始化，不说了
  ans:=0;
  for i:=1 to n do
   begin
     if tj[tjhead]>qw[qwhead] then  //见上文第一种情况
      begin
        inc(ans,200);
        inc(tjhead);
        inc(qwhead);
        continue;
      end;
     if tj[tjtail]>qw[qwtail] then  //见上文第二种情况
      begin
        inc(ans,200);
        dec(tjtail);
        dec(qwtail);
        continue;
      end;
     if tj[tjhead]<qw[qwtail] then  //见上文第三种情况，硬刚
      begin
        dec(ans,200);
        inc(tjhead);
        dec(qwtail);
      end
       else
     if tj[tjhead]=qw[qwtail] then  //硬刚还平局！！！QAQ
      begin
        inc(tjhead);
        dec(qwtail);
      end;
   end;
  write(ans);  //输出田忌能拿到的money￥￥
end.
```