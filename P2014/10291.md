乍一看就是树结构。啥？指针？还是用模拟指针吧（神犇请无视本文）……树结构嘛，组织自然费力我说了多少遍这是坑 ，啥？你说这是森林？老办法，还得变二叉树。

森林嘛，自然是这么组织的：一个节点的父辈代表它的先修课，因为一门课只有一个先修课，正好符合树的要求（否则还是有向图……）。

变二叉树，还是孩子-兄弟表示法：一个节点的左孩子代表它的第一个孩子（或称大孩子），右孩子代表它的第一个兄弟，右孩子的右孩子，右孩子的右孩子的右孩子……组成它的所有兄弟们。啥？你说它的其他孩子？不就是它左孩子的兄弟（右孩子）们嘛！

现在要说模拟指针了。这个好理解，a[i]代表第i课的学分，l[i]代表i的左孩子，r[i]代表i的右孩子。

言归正传，回到难点上来（刚刚都是浮云……）动规题的核心：动规方程。

假设在以i为根节点的子树上选j个节点的最大价值为f[i,j],那么考虑两种情况：Ⅰ如果选根节点，那就左右子树中的节点都可以选,即f[i,j]=max(f[l[i],k]+f[r[i],j-1-k]+a[i]|0<=k<=j；Ⅱ如果不选根节点，那么只能选右子树上的节点（为什么？根节点是左孩子的先修课啊！）即f[i,j]=f[r[i],j]。总而言之，动规方程为：

f[i,j]=max(max(f[l[i],k]+f[r[i],j-1-k]+a[i]|0<=k<=j，f[r[i],j])

至此，即有两种解法。

①树状动规

方程已出，多说无益然而我就喜欢废话，但这里的重点是动规的次序。

不难看出，要从叶节点向上动规，所以就得以该节点为根的子树的节点个数为标准排序，但万万不可直接调a数组，否则树就倒了……所以要再用一个数组作为指针（当然是模拟指针），存储排序结果。标程略。


②记忆化搜索

跟动规一比，毫无技术含量可言……递归+存储，OK。标称附上：

```cpp
const maxn=500;
type arr1=array[1..maxn] of longint;
     arr2=array[-1..maxn,0..maxn] of longint;
var a,l,r:arr1;
    f:arr2;
    n,m,i,j,k,root:longint;
function max(x,y:longint):longint;
begin
   if x>y then exit(x);
   exit(y);
end;
function work(i,j:longint):longint;
var tmp,k:longint;
begin
  if (i=-1)or(j=0) then f[i,j]:=0;
  if f[i,j]>=0 then exit(f[i,j]);
  tmp:=work(r[i],j);
  for k:=0 to j-1 do
    tmp:=max(tmp,work(l[i],k)+work(r[i],j-1-k)+a[i]);
  f[i,j]:=tmp; 
  exit(f[i,j]);
end;
begin
  readln(n,m);
  for i:=1 to n do begin l[i]:=-1; r[i]:=-1;end;
  root:=-1;
  for i:=1 to n do 
    begin
      readln(j,a[i]);
      if j=0 then begin
                    if root=-1 then root:=i
                               else begin
                                      k:=root;
                                      while r[k]<>-1 do k:=r[k];
                                      r[k]:=i;
                                    end
                  end
             else if l[j]=-1 then l[j]:=i
                             else begin
                                    k:=l[j];
                                    while r[k]<>-1 do k:=r[k];
                                    r[k]:=i;
                                  end; 
    end;
  fillchar(f,sizeof(f),255);
  f[root,m]:=work(root,m);
  writeln(f[root,m]);
end.

```