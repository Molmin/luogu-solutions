每一门课都只有一个先修课，所以这其实是一个无向无环图（也就是森林啦）；所以，整个问题就变成了：已知一个森林，每个节点都有一个权，选择一个节点必须选择它的根节点，求选择m个节点的最优解。

树状DP？是的！那么，唯一的问题就是如何把一个森林转化成一个二叉树。

首先，增加一个总根（0节点），然后对于每个节点，记录两个指针，son和brother（当然也可以用left和right）；它的最左儿子和右兄弟。然后做树状DP即可。但是要注意一个特殊情况：因为brother是它的兄弟，所以可以不选它本身，只选brother。

接下来就很简单啦~~~

pascal代码如下：

```pascal
type
  node=record
    s,b,x:integer
  end;
var
  tree:array[0..300] of node;  //记录整棵树
  f:array[0..300,0..301] of integer;  //记录结果
  n,m,i,a,b:integer;
function max(a,b:integer):integer;
  begin
    if a>b then exit(a)
    else exit(b)
  end;
function dp(root,q:integer):integer;  //root是当前节点，q是选的节点数
  var
    i,s:integer;
  begin
    if f[root,q]>0 then exit(f[root,q]);
    if q=0 then exit(0);
    if tree[root].s=0 then if tree[root].b=0 then f[root,q]:=tree[root].x
    else f[root,q]:=max(dp(tree[root].b,q-1)+tree[root].x,dp(tree[root].b,q))
    else if tree[root].b=0 then f[root,q]:=dp(tree[root].s,q-1)+tree[root].x  //特判
    else begin
      s:=0;
      for i:=0 to q-1 do s:=max(s,dp(tree[root].s,i)+dp(tree[root].b,q-i-1));
      inc(s,tree[root].x);
      s:=max(s,dp(tree[root].b,q));  //特殊情况
      f[root,q]:=s
    end;
    exit(f[root,q])
  end;
//树状DP（其实是记忆化搜索实现）
begin
  readln(n,m);
  fillchar(tree,sizeof(tree),0);
  for i:=1 to n do begin
    readln(a,b);
    if tree[a].s=0 then tree[a].s:=i
    else begin
      tree[i].b:=tree[a].s;
      tree[a].s:=i
    end;  //这里并没有记录最左儿子，不过节约了效率，而且树的形态无关紧要
    tree[i].x:=b
  end;  //初始化
  fillchar(f,sizeof(f),0);
  write(dp(0,m+1))
end.
```