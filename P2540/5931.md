## P版本的w_32768大佬的题解，以及加了一条规则使之前被hack的题解可以过cz加的第48个点

### 首先，用暴力搜出顺子，然后暴力的搜索回溯，不赘述

### 其次最重要的，是对于零散的牌如何优秀的用最少的次数打完

先统计一个cnt数组，范围为0-4，表示牌的数量分别为1-4的牌的个数（因为我们不需要知道具体的点数，我们只需要知道这种点数的牌的个数）

我们来具体分析接下来如何出牌以及如何拆牌

首先是几个放在前面特特判，几种比较特殊的情况（举例中相同颜色的笔表示每次出的牌），代码中的qwq表示这组牌出的此数

1
```
while (cnt[2]=0) and (cnt[1]=1) and (cnt[4]=1) and (cnt[3]>1) do
        begin
            dec(cnt[3],2);
            dec(cnt[1]);
            dec(cnt[4]);
            inc(qwq,2);
        end;
```
![](https://cdn.luogu.com.cn/upload/pic/42418.png) qwq=2

2
```
while (cnt[3]=0) and (cnt[1]=1) and (cnt[2]=1) and (cnt[4]>1) do
        begin
            dec(cnt[4],2);
            dec(cnt[1]);
            dec(cnt[2]);
            inc(qwq,2);
        end;
```
![](https://cdn.luogu.com.cn/upload/pic/42419.png) qwq=2

↑第44组数据

3
```
if cnt[3]+cnt[4]>cnt[1]+cnt[2] then
        while (cnt[4]<>0) and (cnt[3]<>0) and (cnt[2]<>0) do
            begin
                dec(cnt[2]);
                dec(cnt[3]);
                dec(cnt[4]);
                inc(cnt[1]);
                inc(qwq);
            end;
```
三张和四张比一张和两张大很多的话，我们就要考虑把三张和四张拆成一张和两张

这里是把三张拆成了一张和两张，然后四带两对（因为三张和四张的总数大于一张和两张）

4
```
if cnt[3]+cnt[4]>cnt[1]+cnt[2] then
        while (cnt[3]<>0) and (cnt[4]<>0) and (cnt[1]<>0) do
            begin
                dec(cnt[1]);
                inc(cnt[2]);
                dec(cnt[3]);
                dec(cnt[4]);
                inc(qwq);
            end;
```
如果仍然大于的话把三拆成一张和两张，然后四带两张

5
特殊情况处理完了，我们考虑先出四带二对，后出四带两张,最后出四带二，因为三只能带一，不能带二，你先把四带一出完了三和二就慢慢单打吧（不过好像没卡这个点。。。）

```
while (cnt[4]>0) and (cnt[2]>1) do
        begin
            dec(cnt[4]);
            dec(cnt[2],2);
            inc(qwq);
        end;
    while (cnt[4]>0) and (cnt[1]>1) do
        begin
            dec(cnt[4]);
            dec(cnt[1],2);
            inc(qwq);
        end;
    while (cnt[4]>0) and (cnt[2]>0) do
        begin
            dec(cnt[4]);
            dec(cnt[2]);
            inc(qwq);
        end;
```

6
三张的剩很多但是一张和两张的很少（总数加起来少于一），就把三个三张拆成两个三张和一个一张一个一对
```
if (cnt[3] mod 3=0) and (cnt[1]+cnt[2]<=1) then
        while cnt[3]>2 do
            begin
                dec(cnt[3],3);
                inc(qwq,2);
            end;
```

7
继续三带二和三带一
```
	while (cnt[3]<>0) and (cnt[2]<>0) do 
        begin
            dec(cnt[3]);
            dec(cnt[2]);
            inc(qwq);
        end;
	while (cnt[3]<>0) and (cnt[1]<>0) do
        begin
            dec(cnt[3]);
            dec(cnt[1]);
            inc(qwq);
        end;
```

接下来就是四和三在出完三带一三带二后还有结余了

### 8原题解没有考虑的地方

把两个四张和两个三张的两个三张拆成两个两张和两个一对，四带两对和四带两张
```
while (cnt[3]>1) and (cnt[4]>1) do
        begin
            inc(qwq,2);
            dec(cnt[4],2);
            dec(cnt[3],2);
        end;
```
![](https://cdn.luogu.com.cn/upload/pic/42424.png)

↑第48组cz加的数据


9 两个四张和一个三张，三张拆成一张和两张
```
while (cnt[4]>1) and (cnt[3]>0) do
        begin
            dec(cnt[3]);
            dec(cnt[4],2);
            inc(qwq,2);
        end;
```

10 两个三张和一个四张，四张拆成两个两张，两个三带二
```
while (cnt[3]>1) and (cnt[4]>0) do
        begin
            dec(cnt[4]);
            dec(cnt[3],2);
            inc(qwq,2);
        end;
```

11 三张有三个的情况下，把一个拆成一张和两张，三带一三带二
```
while cnt[3]>2 do
        begin
            inc(qwq,2);
            dec(cnt[3],3);
        end;
```

12 四张有两个的情况下，把一个拆成两张和两张，两个四带二
```
while cnt[4]>1 do
        begin
            inc(qwq);
            dec(cnt[4],2);
        end;
```

13 大小王
如果最后剩余两张一且有大小王就出火箭

好了暴力拆牌结束，接下来就是代码了

```
Var
    t,n,ans,ii,kk,x,y:longint;
    num:array[1..15]of longint;

Function min(xx,yy:longint):longint;
begin 
    if xx<yy then exit(xx)
        else exit(yy);
end;

Procedure dfs(c:longint);
var
    i,qwq,j,k:longint;
    cnt:array[0..4]of longint;
    boom:boolean;
begin
    for i:=1 to 4 do
        cnt[i]:=0;
    boom:=false;
    qwq:=0;
    if (num[14]=1) and (num[15]=1) then boom:=true;
    for i:=1 to 15 do
        inc(cnt[num[i]]);
    while (cnt[2]=0) and (cnt[1]=1) and (cnt[4]=1) and (cnt[3]>1) do
        begin
            dec(cnt[3],2);
            dec(cnt[1]);
            dec(cnt[4]);
            inc(qwq,2);
        end;
    while (cnt[3]=0) and (cnt[1]=1) and (cnt[2]=1) and (cnt[4]>1) do
        begin
            dec(cnt[4],2);
            dec(cnt[1]);
            dec(cnt[2]);
            inc(qwq,2);
        end;
    if cnt[3]+cnt[4]>cnt[1]+cnt[2] then
        while (cnt[4]<>0) and (cnt[3]<>0) and (cnt[2]<>0) do
            begin
                dec(cnt[2]);
                dec(cnt[3]);
                dec(cnt[4]);
                inc(cnt[1]);
                inc(qwq);
            end;
    if cnt[3]+cnt[4]>cnt[1]+cnt[2] then
        while (cnt[3]<>0) and (cnt[4]<>0) and (cnt[1]<>0) do
            begin
                dec(cnt[1]);
                inc(cnt[2]);
                dec(cnt[3]);
                dec(cnt[4]);
                inc(qwq);
            end;
    while (cnt[4]>0) and (cnt[2]>1) do
        begin
            dec(cnt[4]);
            dec(cnt[2],2);
            inc(qwq);
        end;
    while (cnt[4]>0) and (cnt[1]>1) do
        begin
            dec(cnt[4]);
            dec(cnt[1],2);
            inc(qwq);
        end;
    while (cnt[4]>0) and (cnt[2]>0) do
        begin
            dec(cnt[4]);
            dec(cnt[2]);
            inc(qwq);
        end;
    if (cnt[3] mod 3=0) and (cnt[1]+cnt[2]<=1) then
        while cnt[3]>2 do
            begin
                dec(cnt[3],3);
                inc(qwq,2);
            end;
    while (cnt[3]<>0) and (cnt[2]<>0) do 
        begin
            dec(cnt[3]);
            dec(cnt[2]);
            inc(qwq);
        end;
    while (cnt[3]<>0) and (cnt[1]<>0) do
        begin
            dec(cnt[3]);
            dec(cnt[1]);
            inc(qwq);
        end;
	while (cnt[3]>1) and (cnt[4]>1) do
			begin
				inc(qwq,2);
				dec(cnt[4],2);
				dec(cnt[3],2);
			end;
    while (cnt[4]>1) and (cnt[3]>0) do
        begin
            dec(cnt[3]);
            dec(cnt[4],2);
            inc(qwq,2);
        end;
    while (cnt[3]>1) and (cnt[4]>0) do
        begin
            dec(cnt[4]);
            dec(cnt[3],2);
            inc(qwq,2);
        end;
    while cnt[3]>2 do
        begin
            inc(qwq,2);
            dec(cnt[3],3);
        end;
    while cnt[4]>1 do
        begin
            inc(qwq);
            dec(cnt[4],2);
        end;
    qwq:=qwq+cnt[3]+cnt[4]+cnt[2]+cnt[1]; //如果还有剩余接着往上加
    if (boom) and (cnt[1]>=2) then dec(qwq);//如果有火箭只需要一次
    ans:=min(ans,qwq+c);
//PS:两点不能参加顺子但是一点可以参加
    for i:=1 to 12 do
        begin
            j:=i;
            while (j<=12) and (num[j]>2) do
                begin
                    if j-i+1>=2 then
                        begin
                            for k:=i to j do
                                num[k]:=num[k]-3;
                            dfs(c+1);
                            for k:=i to j do
                                num[k]:=num[k]+3;
                        end;
                    inc(j);
                end;
        end;//三顺
    for i:=1 to 12 do
        begin
            j:=i;
            while (j<=12) and (num[j]>=2) do
                begin
                    if (j-i+1>=3) then
                        begin
                            for k:=i to j do
                                num[k]:=num[k]-2;
                            dfs(c+1);
                            for k:=i to j do
                                num[k]:=num[k]+2;
                        end;
                    inc(j);
                end;
        end;//两顺
    for i:=1 to 12 do
        begin
            j:=i;
            while (j<=12) and (num[j]<>0) do
                begin
                    if (j-i+1>=5) then
                        begin
                            for k:=i to j do
                                dec(num[k]);
                            dfs(c+1);
                            for k:=i to j do
                                inc(num[k]);
                        end;
                    inc(j);
                end;
        end;//一顺
end;

Begin
    readln(t,n);
    for kk:=1 to t do
        begin
            fillchar(num,sizeof(num),0);
            for ii:=1 to n do
                begin
                    read(x,y);
                    if x=0 then
                        begin
                            if y=1 then x:=14
                                else x:=15;
                        end
                    else if (x=1) or (x=2) then x:=x+11
                        else x:=x-2;
                    inc(num[x]);
                end;
            ans:=n;
            dfs(0);
            writeln(ans);
        end;
End.
```