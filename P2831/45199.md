# P2831 愤怒的小鸟


------------


题意:给定一群猪的坐标，问至少用几条抛物线可以将这些猪的坐标点全部经过(抛物线以ax^2+bx,a<0的形式呈现)


------------
### 1.玄学搜索


------------
暴力枚举每只猪与哪一头猪组成一条抛物线,dfs。
然后......~~玄学剪枝~~,在这里就不多说了(~~其实是因为我也不会......QwQ~~)


------------
### 2.状压DP(“~~真正的~~”正解)
#### - 85pts:O(Tn^2*2^n)

f[s]表示已经死了的猪的集合状态为S时最少要发射的鸟数。

明显有
- dp[0]:=0
- dp[s or line[i][j]]:=min(f[s]+1,f[s or line[i[j]])
- dp[s or 1 shl (i-1)]=min(dp[s]+1,dp[s or (1 shl (i−1)])

其中 line[i][j]表示经过i,j两点的抛物线能经过的所有点的集合。
这就是网上大多流传的O(Tn^2*2^n),实测85分(~~当然开O2就能过~~)


------------
#### - 100pts:O(Tn2^n)
延续85分的作法,在之基础上再进行改进


------------
- 对于状态S,设x为(S and (1 shl (x-1)))=0的最小整数。
  每次在S的基础上拓展能打下x的抛物线。
- 证明：假设x=3,那你不打第3个而去打第4个,那之后的转移还会去打第3个,存在多余的转移,所以先打最小的。

#### 最后,上代码
```pascal
var i,j,k,n,m,t:longint; a,b,a1,b1,c1,d1,m1,n1,eps:double;
    f,lowbit:array[0..270000] of longint; x,y:array[0..405] of double;
    line:array[0..25,0..25] of longint;
function min(x,y:longint):longint;
begin
  if x<y then exit(x);
  exit(y);
end;
begin
  readln(t);
  for i:=0 to (1 shl 18)-1 do
  begin
    j:=1;
    while (j<=18) and ((i and (1 shl (j-1)))<>0) do inc(j);
    lowbit[i]:=j;
  end;//预处理对于每个状态S的最小x
  while t>0 do
  begin
    dec(t); eps:=1e-8;//防止精度误差造成抛物线不同
    readln(n,m);
    for i:=1 to (1 shl n)-1 do f[i]:=n+1;
    fillchar(line,sizeof(line),0);
    for i:=1 to n do readln(x[i],y[i]);
    for i:=1 to n do
      for j:=1 to n do
      begin
        if abs(x[i]-x[j])<eps then continue;
        a1:=x[i]*x[i]; c1:=x[j]*x[j];
        b1:=x[i]; d1:=x[j];
        m1:=y[i]; n1:=y[j];
        b:=(a1*n1-c1*m1)/(a1*d1-b1*c1);
        a:=(m1-b1*b)/a1;//计算打掉i,j两点的抛物线的a和b
        if a>-eps then continue;
        for k:=1 to n do
        if (abs(a*x[k]*x[k]+b*x[k]-y[k])<eps) then line[i,j]:=line[i,j] or (1 shl (k-1));
      end;
    for i:=0 to (1 shl n)-1 do
    begin
      j:=lowbit[i];
      f[i or (1 shl (j-1))]:=min(f[i or (1 shl (j-1))],f[i]+1);//直接转移
      for k:=1 to n do
      f[i or line[j][k]]:=min(f[i or line[j][k]],f[i]+1);//抛物线转移
    end;
    writeln(f[(1 shl n)-1]);
  end;
end.

```



