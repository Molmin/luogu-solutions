由∑(ai-bi)^2可以得到当每个(ai-bi)^2最小，∑(ai-bi)^2

当(ai-bi)^2最小，abs(ai-bi)需要最小，其实是贪心的思路（简单证明）

对于两个序列a，b，也就是对于每个序列第i小值对应第i小值时abs(ai-bi)最小

而题目不要求关于a，b序列中值的若干问题，由此想到对a,b离散处理：

举例来看：对于a[]=1 123 7 966 1208;b[]=21 34 36 25 111

离散后 a[]=1 3 2 4 5; b[]=1 3 4 2 5

接下来b怎么在最短的步数到a？

映射处理。

对于每个a[i]的值对应一个i，b[i]映射到新序列的d[i]中

for i:=1 to n do d[a[i].id]:=b[i].id;

答案就是d的逆序对

这里由于不会打线段树和归并所以打个树状数组

```cpp
const maxn=100000;
type rec=record
     id,x:longint;
end;
arr=array[1..maxn]of rec;
var a,b:arr;
    c,d:array[1..maxn]of longint;
    n,ans,i:longint;
procedure qsort(l,r:longint; var a:arr);
var i,j,mid:longint; t:rec;
begin
i:=l; j:=r;
mid:=a[(l+r)div 2].x;
while i<j do
begin
 while a[i].x<mid do inc(i);
 while a[j].x>mid do dec(j);
 if i<=j then begin
   t:=a[i]; a[i]:=a[j]; a[j]:=t;
   inc(i);dec(j);
 end;
end;
if l<j then qsort(l,j,a);
if r>i then qsort(i,r,a);
end;
procedure update(x,y:longint);
begin
 while x<=n do begin
  c[x]:=c[x]+y;
  x:=x+x and (-x);
 end;
end;
function query(x:longint):longint;
var  cnt:longint;
begin
 cnt:=0;
 while x>0 do begin
  cnt:=cnt+c[x];
  x:=x-x and (-x);
 end;
 exit(cnt);
end;
begin
 readln(n);
 for i:=1 to n do begin read(a[i].x); a[i].id:=i; end;readln;
 for i:=1 to n do begin read(b[i].x); b[i].id:=i; end;readln;
 qsort(1,n,a);
 qsort(1,n,b);
 for i:=1 to n do d[a[i].id]:=b[i].id;
 for i:=1 to n do begin
  update(d[i],1);
  ans:=(ans+i-query(d[i]))mod 99999997;
 end;
 writeln(ans mod 99999997);
end.

```