#没有人发代码，我来补一发

#为什么要用dp，这不应该是题数论题吗？？？真的是假的吧。。。

#而且dp慢到爆炸，而数论0ms。。。

对于x^x mod 1000，我们用快速幂就行了。

对于把这x^x mod 1000分成为k个数，蒟蒻的我推出的公式是C(x^x mod 1000-1,k-1)（似乎是杨辉三角的通向公式）

然后分解质因数（1000以内的素数就行了，因为x^x模了1000），约分，再用高精度相乘计算

##妖怪吧

丑陋的代码，非丑陋之人不得欣（fu）赏（zhi）：

```cpp
const
  p=1000;
var
  b:array[0..1001]of boolean;
  a,ans,c:array[0..1001]of longint;
  n,m,x,xx,i,j:longint;
function power(x,y:longint):longint;
var
  t:longint;
begin
  if y=1 then exit(x);
  t:=power(x,y div 2) mod p;
  t:=(t*t) mod p;
  if odd(y) then t:=(t*x) mod p;
  exit(t); 
end; //快速幂
procedure zz(x,y:longint);
var
  i:longint;
begin
  while x<>1 do 
    begin
      if not b[x] then 
        begin
          inc(ans[x],y);
          exit;
        end;
      for i:=1 to m do 
        if x mod a[i]=0 then begin inc(ans[a[i]],y);x:=x div a[i];break; end;
    end;
end;//分解质因数
procedure mul(tt:longint);
var
  i,x:longint;
begin
  x:=0;
  for i:=1 to 1000 do 
    begin
      c[i]:=c[i]*tt+x;
      x:=c[i] div 10;
      c[i]:=c[i] mod 10;
    end;
end;//高精度乘
begin
  readln(n,x);
  xx:=power(x mod p,x);
  for i:=2 to p do
    if not b[i] then  
      for j:=2 to p div i do 
        b[i*j]:=true;
  for i:=2 to 1000 do 
    if not b[i] then 
      begin
        inc(m);
        a[m]:=i;
      end;//筛法求素数
  dec(xx); dec(n);
  for i:=xx-n+1 to xx do zz(i,1);
  for i:=2 to n do zz(i,-1);//分解质因数再约分
  c[1]:=1;
  for i:=2 to p do 
    while ans[i]>0 do 
      begin
        dec(ans[i]);
        mul(i);
      end;//最后把约分剩下的乘起来
  j:=1000;
  while (c[j]=0)and(j>1) do dec(j);
  for i:=j downto 1 do write(c[i]);//输出
end.
```