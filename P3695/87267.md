这不是重复的做法。洛咕审题解好像判错了。

思路：CyaRon 转 Perl 后再运行。

这个题，好像是要我们设计一个新语言。所以我们可以写 23 行 Perl 程序完成这个任务。因为正则用的不是很熟练，所以不确定是是不是更短的实现。

```perl
$/ = undef;
my $code = <>;
$code = $code =~
s/#.*?\n/\n/gsr =~ s/{[^}]*vars.*?}//gsr =~
s/:yosoro(.*?)\n/print $1\nprint ' '\n/gsr =~ s/:set(.*?),(.*?)\n/$1 = $2\n/gsr;
$code = $code =~
s/([^a-z])lt([^a-z])/$1<$2/gsr =~ s/([^a-z])gt([^a-z])/$1>$2/gsr =~
s/([^a-z])le([^a-z])/$1<=$2/gsr =~ s/([^a-z])ge([^a-z])/$1>=$2/gsr =~
s/([^a-z])eq([^a-z])/$1==$2/gsr =~ s/([^a-z])neq([^a-z])/$1!=$2/gsr;
my $magic = '([^,\{\}]*),([^,\{\}]*),([^,\{\}\n]*)\n([^\}\{]*?)\}';
while ($code =~ m/ihu/ or $code =~ m/hor/ or $code =~ /while/) {
	$code =~ s/\{[^\{\}]*?ihu$magic/IF ($2 $1 $3) <%\n $4 %>\n/gs;
	$code =~ s/\{[^\{\}]*?hor$magic/FOR $1 ( $2 .. $3 ) <%\n $4 %>\n/gs;
	$code =~ s/\{[^\{\}]*?while$magic/WHILE ($2 $1 $3) <%\n $4 %>\n/gs;}
$code = $code =~ s/<%/{/gsr =~ s/%>/}/gsr;
for my $keyword (qw/print/) {
	$code =~ s/([^a-z])${keyword}([^a-z])/$1\U$keyword\E$2/gs; }
$code = $code =~
s/([^a-z\$])([a-z])/$1\$$2/gsr =~ s/([^0-9\'])([0-9]+)/$1'$2'/gsr;
for my $keyword (qw/if for while print while if for/) {
	$code =~ s/([^a-z])\U$keyword\E([^a-z])/$1$keyword$2/gs; }
$code = $code =~ s/\[/{/gsr =~ s/\]/}/gsr =~ s/\n/;\n/gsr;
eval $code;
```

这一坨代码看不得。把代码打散之后还是有一点可读性的。

基本思路是把 CYaRon 语言转成 Perl，然后再 eval 一下。

代码逻辑写在注释里了。如果有疑问，欢迎私信。

```perl

#!/usr/bin/perl

$/ = undef;
my $code = <>;

# 去掉注释
$code =~ s/#.*?\n/\n/gs;

# 去掉 vars 标签
$code =~ s/{[^}]*vars.*?}//gs;

# 处理 yosoro
$code =~ s/:yosoro(.*?)\n/print $1\nprint ' '\n/gs;

# 处理 set
$code =~ s/:set(.*?),(.*?)\n/$1 = $2\n/g;

# 转换逻辑操作符
$code =~ s/([^a-z])lt([^a-z])/$1<$2/gs;
$code =~ s/([^a-z])gt([^a-z])/$1>$2/gs;
$code =~ s/([^a-z])le([^a-z])/$1<=$2/gs;
$code =~ s/([^a-z])ge([^a-z])/$1>=$2/gs;
$code =~ s/([^a-z])eq([^a-z])/$1==$2/gs;
$code =~ s/([^a-z])neq([^a-z])/$1!=$2/gs;

# 代码块嵌套
while ($code =~ m/ihu/ or $code =~ m/hor/ or $code =~ /while/) {
	$code =~ s/\{[^\{\}]*?ihu([^,\{\}]*),([^,\{\}]*),([^,\{\}\n]*)\n([^\}\{]*?)\}/IF ($2 $1 $3) <%\n $4 %>\n/gs;
	$code =~ s/\{[^\{\}]*?hor([^,\{\}]*),([^,\{\}]*),([^,\{\}\n]*)\n([^\}\{]*?)\}/FOR $1 ( $2 .. $3 ) <%\n $4 %>\n/gs;
	$code =~ s/\{[^\{\}]*?while([^,\{\}]*),([^,\{\}]*),([^,\{\}\n]*)\n([^\}\{]*?)\}/WHILE ($2 $1 $3) <%\n $4 %>\n/gs;
}

$code =~ s/<%/{/g;
$code =~ s/%>/}/g;

# 保护关键字
for my $keyword (qw/print/) {
	$code =~ s/([^a-z])${keyword}([^a-z])/$1\U$keyword\E$2/gs;
}

# 给变量加上魔法
$code =~ s/([^a-z\$])([a-z])/$1\$$2/gs;

# 给数字加上魔法
$code =~ s/([^0-9\'])([0-9]+)/$1'$2'/gs;

# 转回关键字
for my $keyword (qw/if for while print while if for/) {
	$code =~ s/([^a-z])\U$keyword\E([^a-z])/$1$keyword$2/gs;
}

# 使用哈希，防止数组越界
$code =~ s/\[/{/gs;
$code =~ s/\]/}/gs;

# 加上分号
$code =~ s/\n/;\n/gs;

# --L-i-v-e--（bushi） 运行！
eval $code;

```