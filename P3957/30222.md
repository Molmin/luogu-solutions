首先看到题目很容易想到二分答案。

让f[i]表示到i点的最大分数，很容易想到f[i]=max(f[j])+s[i],j是在i前能到达i的点。

而我们发现，能到达i点的其实是一个区间，只需找到最左和最右能到达i的点就能知道j的范围。我们可以令left[i]为能到达i的最左边的点，right[i]为能到达i的最右边的点，我们很容易推出left和right是递增的，即left[1]<=left[2]<=......<=left[n]，right同理。所以我们可以根据left[i-1]和right[i-1]来往后推出left[i]和right[i]，时间复杂度为O(n)

看到求区间最大值，我们很容易能想到用st表，st[i,j]表示i~i+j^2-1的最大值，动态转移方程为

St[i,j]=max(st[i,j-1],st[i+2^(j-1),j-1]),建立st表的时间复杂度为O（n log n）,怎样总时间复杂度就为O（log（10^9）\*n log n）,大概为3亿，时长有2秒，貌似可以,但我们还可以节省一下时间。

因为我们只需要求left~right这段区间的最大值，所以算1~left-1开头这段是没必要的，怎样我们就可以把时间复杂度从每轮的O（log(i)）降到O(log(i-left))。

最后补充Pascal里求log(n)的表达式为ln(n)/ln(2)。

```cpp
const
  spt=-1 shl 60;
var
  n,d,k,l,r,m,min,max,szy,left,right:int64;
  i,j:longint;
  x,s,f:array[0..500000] of int64;
  st:array[0..500000,0..20] of int64;
  two:array[0..20] of longint;
  bool:boolean;
function fmax(a,b:int64):int64;
  begin
    if a>b then exit(a)
         else exit(b);
  end;
begin
  readln(n,d,k);max:=0;
  for i:=1 to n do
    begin
      readln(x[i],s[i]);
      if s[i]>0 then max:=max+s[i];
    end;
  if max<k then begin
               write(-1);
               exit;
             end;
  l:=0;r:=fmax(x[n]-d,d-1);x[0]:=0;f[0]:=0;two[0]:=1;
  for i:=1 to trunc(ln(n)/ln(2)) do
    two[i]:=two[i-1]*2;
  while l<r do
    begin
      m:=(l+r)div 2;
      if m<d then min:=d-m
           else min:=1;
      max:=m+d;left:=0;right:=-1;
      for i:=1 to n do
        if f[i-1]<k
          then begin
                 bool:=true;
                 for j:=left to i-1 do
                   if x[j]+max>=x[i] then begin
                                       left:=j;
                                       bool:=false;
                                       break;
                                     end;
                 if bool then break;
                 for j:=right+1 to i do
                   if x[j]+min>x[i] then begin
                                      right:=j-1;
                                      break;
                                    end;
                 st[i-1,0]:=f[i-1];
                 for j:=1 to trunc(ln(i-left)/ln(2)) do
                   st[i-two[j],j]:=fmax(st[i-two[j],j-1],st[i-two[j-1],j-1]);
                 if left>right then begin
                                 f[i]:=spt;
                                 continue;
                               end;
                 szy:=trunc(ln(right-left+1)/ln(2));
                 f[i]:=fmax(st[left,szy],st[right-two[szy]+1,szy])+s[i];
               end
           else break;
      if (f[i-1]>=k)or((not bool)and(f[i]>=k)) then r:=m
                                    else l:=m+1;
    end;
  write(r);
end.

```