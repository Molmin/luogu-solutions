
```cpp
uses math;
var 
    a,h,t:array[0..202]of longint;//h头，t尾
    f:array[0..202,0..202]of longint;//用来运算
    l,i,j,k,n,max1:longint;
begin
  readln(n);
  for i:=1 to n do
  begin
    read(a[i]);
    a[i+n]:=a[i];//将数组延展为之前的两倍，顺序不变，变环为线
    h[i]:=a[i];//头
    h[i+n]:=a[i+n];//头
  end;
  for i:=2*n-1 downto 1 do t[i]:=h[i+1];//尾
  t[2*n]:=h[1];//题目所述
  for l:=2 to n do//l为区间长度，相当于在一条线上从左往右滑的滑块（滑一遍后长度+1），计算区间内的能量大小
   for i:=1 to 2*n-l+1 do//滑块每次开始的起点
   begin
     j:=i+l-1;//滑块终点
     f[i,j]:=-maxlongint;//初始化
     for k:=i to j-1 do//k在区间中移动，将区间分为两份
     begin
       f[i,j]:=max(f[i,j],f[i,k]+f[k+1,j]+h[i]*t[k]*t[j]);//比较原f[i,j]与f[i,k]+f[k+1,j]+h[i]*t[k]*t[j]的大小，取大。f[i,k]+f[k+1,j]为两个被k隔开的小区间的之前积累的能量和，h[i]*t[k]*t[j]为此次的能量和
     end;
   end;
  max1:=-maxlongint;//初始化
  for i:=1 to n do
  begin
    max1:=max(max1,f[i,i+n-1]);//原为4 3 2 1的环，会有4 3 2 1,3 2 1 4,2 1 4 3,1 4 3 2四种排列，取最大。
  end;
  writeln(max1);
end.
```