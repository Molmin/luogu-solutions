```delphi

program P1063;  
type  
  tBall=record  
    first,last:integer;  
  end;  
var  
  balls:array[1..200] of tBall;  
  answer:array[1..200,1..200] of longint;  
  i,j,k:longint;  
  size:integer;  
function max(a,b:longint):longint;  
begin  
  if a>b then  
  begin  
    max:=a;  
  end  
  else  
  begin  
    max:=b;  
  end;  
end;  
begin  
  readln(size);  
  for i:=1 to size do  
  begin  
    read(balls[i].first);  
  end;  
  readln;  
  for i:=1 to size-1 do  
  begin  
    balls[i].last:=balls[i+1].first;  
  end;  
  balls[size].last:=balls[1].first;  
  for i:=1 to size-1 do  
  begin  
    balls[size+i]:=balls[i];  
  end;  
  for i:=1 to size*2-1 do  
  begin  
    answer[i,i]:=0;  
  end;  
  for i:=2 to size do  
  begin  
    for j:=1 to size*2-i do  
    begin  
      answer[j,j+i-1]:=0;  
      for k:=j to i+j-2 do  
      begin  
        answer[j,j+i-1]:=max(answer[j,j+i-1],answer[j,k]+answer[k+1,i+j-1]+balls[j].first*balls[k].last*balls[i+j-1].last);  
      end;  
    end;  
  end;  
  j:=0;  
  for i:=1 to size do  
  begin  
    j:=max(j,answer[i,i+size-1]);  
  end;  
  writeln(j);  
end.   

```
环形动态规划，需要把除了最右边的珠子之外的所有珠子复制一遍，放到原来序列的右边。

对于一段珠子的序列，可以在序列中任意选取一个点，先把这个点左边的所有珠子合并成一个，再把这个点右边的所有珠子合并成一个，最后再合并这两个珠子。左边珠子、右边珠子的头和尾都可以依据选择的点计算出来，然合并这段序列上的珠子的问题久变成了合并两条更短序列的更小子问题。

下限是当只有一个珠子的时候，规定它的能量值为0。

当有两个珠子的时候，可以照常计算，只不过点只有一种情况。

把所有的情况都试一遍，求出能量最大的。
