求最大流(源点(s):0,汇点(t):n+f+dd+n+1,食品编号:1--f,饮料编号:f+1--f+dd,奶牛编号:f+dd+1--f+dd+n,奶牛‘编号:f+dd+n+1--f+dd+n+n。

s向所有食品连边，权为1

所有饮料向t连边，权为1

拆点，把奶牛i拆成i和i+n，i向i+1连边，权为1

奶牛i对应的食品向i连边，权为1，奶牛’i+n向对应的饮料连边，权为1

最后做一边最大流






```cpp
var
  s,t,tot,i,x,k,m,n,j,ans,f,dd,aa,bb,a1:longint;
  deep,d,pre,now,son,h,opp:array[0..100000] of longint;
procedure insert(a,b,c,p:longint);
begin
  inc(tot);
  pre[tot]:=now[a]; now[a]:=tot; son[tot]:=b;
  d[tot]:=c; opp[tot]:=p;
end;
procedure doit(a,b,c:longint);
begin
  insert(a,b,c,tot+2);
  insert(b,a,0,tot);
end;
function bfs:boolean;
var
  head,tail,p:longint;
begin
  fillchar(deep,sizeof(deep),$ff);
  deep[s]:=0;
  head:=1; tail:=1;
  h[1]:=s;
  repeat
    p:=now[h[head]];
    while p>0 do
    begin
      if (d[p]>0) and (deep[son[p]]=-1) then
      begin
        deep[son[p]]:=deep[h[head]]+1;
        if son[p]=t then exit(true);
        inc(tail);
        h[tail]:=son[p];
      end;
      p:=pre[p];
    end;
    inc(head);
  until head>tail;
  exit(false);
end;
function find(x,tmpflow:longint):longint;
var
  temp,sum,p:longint;
begin
  if x=t then exit(tmpflow);
  sum:=0;
  p:=now[x];
  while p>0 do
  begin
    if (d[p]>0) and (deep[x]+1=deep[son[p]]) then
    begin
      if tmpflow<d[p] then temp:=find(son[p],tmpflow)
      else temp:=find(son[p],d[p]);
      dec(d[p],temp);
      inc(d[opp[p]],temp);
      inc(sum,temp);
      dec(tmpflow,temp);
      if tmpflow=0 then break;
    end;
    p:=pre[p];
  end;
  deep[x]:=-1;
  exit(sum);
end;
begin
  readln(n,f,dd);
  s:=0;
  t:=n+f+dd+n+1;
  for i:=1 to f do
    doit(s,i,1);
  for i:=f+1 to f+dd do
    doit(i,t,1);
  for i:=f+dd+n+1 to f+dd+n+n do
    doit(i-n,i,1);
  for i:=f+dd+1 to f+dd+n do
  begin
    read(aa,bb);
    for j:=1 to aa do
    begin
      read(a1);
      doit(a1,i,1);
    end;
    for j:=1 to bb do
    begin
      read(a1);
      doit(i+n,a1+f,1);
    end;
  end;
  ans:=0;
  while bfs do
    inc(ans,find(s,maxlongint));
  writeln(ans);
end.
```