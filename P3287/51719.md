我来贡献本题第一篇题解

初始的思路十分重要

**每次拔高玉米的右端点总是n**

为什么？

如果对中间一段进行操作，对前面的是更优的，但对后面的会劣，所以如果右端点为n，则对后面不会有影响

**朴素DP：**

dp[i][j]表示前i个，拔j次，最优解

     **dp[i][j]=max(dp[k][l])+1(k<i,l<=j,a[i]+j>=a[k]+l）**

CODE:

```cpp
uses math;
var
    a:Array[0..10000] of int64;
    dp:array[0..10000,0..500] of int64;
    n,m,i,j,k,l:longint;
begin
    readln(n,m);
    for i:=1 to n do
        read(a[i]);
    for i:=1 to n do
        for j:=m downto 0 do
            for k:=0 to i-1 do
                for l:=m downto 0 do
                    if a[k]+l<=a[i]+j then
                        dp[i][j]:=max(dp[i][j],dp[k][l]+1);
    writeln(dp[n][m]);
end.
```
时间复杂度O(n^2\*k^2)全超
**想到用二维树状数组优化**

把dp数组变成一个树状数组

定义不变，时间复杂度O(nlogn\*klogk)








```cpp
uses math;
var
    a:Array[0..10000] of int64;
    c:array[0..10000,0..500] of int64;
    n,m,i,j,k,l,maxa,ans,sum:longint;
function lowbit(x:int64):int64;
begin
    exit(x and -x);
end;
procedure change(x,y,z:int64);
var
    yy:int64;
begin
    yy:=y;
    while x<=maxa+m do
        begin
            y:=yy;
            while y<=m+1 do
                begin
                    c[x][y]:=max(c[x][y],z);
                    inc(y,lowbit(y));
                end;
            inc(x,lowbit(x));
        end;
end;
function getsum(x,y:int64):int64;
var
    yy:int64;
begin
    getsum:=0;
    yy:=y;
    while x>0 do
        begin
            y:=yy;
            while y>0 do
                begin
                    getsum:=max(getsum,c[x][y]);
                    dec(y,lowbit(y));
                end;
            dec(x,lowbit(x));
        end;
end;
begin
    readln(n,m);
    for i:=1 to n do
        begin
            read(a[i]);
            maxa:=max(maxa,a[i]);
        end;
    for i:=1 to n do
        for j:=m downto 0 do
            begin
                sum:=getsum(a[i]+j,j+1)+1;
                change(a[i]+j,j+1,sum);
                ans:=max(ans,sum);
            end;
    writeln(ans);
end.
```