# 前言
此题是一道裸的搜索题，正确的做法很多，启发式搜索，双向搜索都可以，这里想介绍一下双向搜索的做法
# 最最差的方法（死暴力）
代码见20分 $Code$

暴力枚举变化的旋钮编号，再判断是否达到目标

时间：$O(12^{step})$

当step小于6时可以通过
# 优化
可以考虑状态优化，如果一个状态到达过了就不用搜索了，由于状态最多只有
$4^{12}$=$2^{24}$，
所以可以大幅度优化时间，一般可以得到40分，但如果搜索技术好也能多拿一点分

### 注意
此题内存花费较大，请尽量减少开大数组，尤其不要随便在子程序里开大数组
### 但我们还有一个问题：怎么表示和储存状态
我们可以考虑采用四进制储存，因为旋钮的状态只有4种，所以可以把十二个旋钮组合起来看做一个4进制数，再把它转化成10进制再表示和储存就好了（把1看作0,2看作1,3看作2,4看作1）
# 主角：双向搜索
由于我们知道了最开始的状态和最后的目标状态，所以我们可以从这两个状态分别开始搜索，得到的搜索树大概是这样的
![](https://cdn.luogu.com.cn/upload/pic/72817.png)
而之前的搜索方法所形成的搜索树大概是这样的
![](https://cdn.luogu.com.cn/upload/pic/72819.png)
......表示省略号，很显然双向搜索的搜索树更小
~~（虽然作者美术不好，看不出来）~~

所以我们采用双向搜索，可以大幅度降低时间，将时间降低到搜索两次9步（由于最多的步数只有17步），最慢的点是650+$ms$，虽然比启发式搜索慢，但也是可以接受的做法

具体的实现过程以及注释可以见代码：$AC$ $Code$

其实我还有一个脑抽提出来的的问题
### 搜到的第一个答案步数一定最少吗
由于作者太蒟蒻了，在做这道题之前从来都没有打过双向搜索，只是有所耳闻，所以做着做着就脑抽提出来这个脑抽问题。

其实只要好好想一想，就会发现双向搜索的本质其实和单向搜索是一样的，由于宽搜是一层一层宽搜的，步数保证是单调上升的，所以搜到的提一个答案肯定是正解。

举个栗子（假设搜索上限是9步）

单向搜索:5步

反向搜索:6步 

相遇了

单向搜索:5步

反向搜索:5步 

相遇了

如果前面的一种情况先搜到，那么答案就不是最优的了，~~但其实两种情况都不可能最想搜到~~

因为

第一种情况其实最先搜到的是这种情况（因为正向反向的搜索搜出来的方案是一样的）

单向搜索:9步

反向搜索:2步 

相遇了

第二种情况其实最先搜到的是这种情况、

单向搜索:9步

反向搜索:1步 

相遇了

由于答案单调上升，所以第二种情况必然在第一种情况出现之前，保证答案更优，证毕。

~~作者终于治好了他的脑抽~~$QAQ$
# $Code$
## 20分 $Code$
```pascal
const n=12;
type arr1=array[0..13] of longint;
var
  s,d,c:arr1;
  a:array[0..30,0..5] of longint;
  i,t:longint;
procedure next(var x:longint);
begin
  inc(x);
  if x>4 then x:=1;
end;//求下一个状态
procedure sc(x:longint;b:arr1);
var
  c:arr1;
  i,j:longint;
begin
  if x>t then//每一步都枚举完了，判断是否符合要求
  begin
    for i:=1 to n do
    if b[i]<>1 then exit;//如果又一个没有变成1，就不行，返回
    writeln(t);
    for i:=1 to t do
    write(d[i],' ');
    halt;//输出答案，第一个搜到的肯定是正解并且最小的，结束程序
  end;
  for i:=1 to n do
  begin
    for j:=1 to n do c[j]:=b[j];//copy原序列
    next(c[a[i,c[i]]]);
    next(c[i]);//这两个的顺序不能变，否则会WA
    d[x]:=i;//记录方案
    sc(x+1,c);//传递序列和当前步数
  end;
end;//暴力搜索
begin
  for i:=1 to n do
  readln(s[i],a[i,1],a[i,2],a[i,3],a[i,4]);
  for t:=1 to 6 do
  sc(1,s);//暴力枚举步数
end.
```

## $AC$ $Code$
### $Pascal$ $AC$ $Code$
```pascal
{
  数组说明
  bfs数组表示旋钮状态对应的10进制
  change数组表示变化的旋钮编号
  sum数组表示是第几步
  getway数组表示状态k在队列中的编号
  last数组表示是从那一个状态转移过来的（在队列中的标号）
  getway1,bfs1,change1,sum1,last1这些数组的作用和去掉1之后的数组作用是一样的，用在反向搜索中
}
type arr1=array[0..13] of longint;
const n=12;//定义一个常量方便一点
var
  a:array[0..n+1,0..5]  of longint;
  s,c,b:arr1;
  bfs,change,sum,getway,last,getway1,bfs1,change1,sum1,last1:array[0..17000000] of longint;
  //大数组一定要开在主程序里
  ans,i,k,l,r,j:longint;
function get:longint;
var
  anss,yu,i:longint;
begin
  yu:=1; anss:=c[1]-1;//因为输入是1,2,3,4，所以要减一
  for i:=2 to n do
  begin
    yu:=yu*4;
    anss:=anss+yu*(c[i]-1);
  end;
  exit(anss);
end;//对于指定的c数组4进制转10进制
procedure chai(x:longint);
var
  i,t:longint;
begin
  t:=x;
  for i:=1 to n do
  begin
    b[i]:=t mod 4+1;//加1与上同理
    t:=t div 4;
  end;
end;//将转为10进制的数重新拆成12位4进制数
procedure printf(x:longint);
begin
  if x<>1 then printf(last[x])//正向搜索的过程记录是反的链表，要先输出再递归
  else exit;
  write(change[x],' ');
end;//打印正向搜索的过程
procedure printf1(x:longint);
begin
  if x=1 then exit;//1的情况就直接返回，否则就炸了
  write(change1[x],' ');//反向搜索的过程记录是正的链表，要先输出再递归
  printf1(last1[x]);
end;//打印反向搜索的过程
procedure next(var k:longint);
//相当于c++中&了一下，是一个变参，在lass过程中同理
begin
  inc(k);
  if k>4 then k:=1;
end;//求旋钮状态的后继
procedure lass(var k:longint);
begin
  dec(k);
  if k<1 then k:=4;
end;//求旋钮状态的前继
begin
  for i:=1 to n do readln(c[i],a[i,1],a[i,2],a[i,3],a[i,4]);//读入
  ans:=0;
  k:=get;//储存最开始的状态
  getway[k]:=1;//标记该状态出现在bfs队列的哪个下标中
  l:=1; r:=1; bfs[1]:=k;//bfs初始化,bfs数组中储存的是转化完的10进制数
  while l<=r do
  begin
    if (sum[l]=9) then break;//到第9步了就退出
    chai(bfs[l]);//将10进制的数重新拆成12位4进制数
    for j:=1 to 12 do
      c[j]:=b[j];//放到c数组里，方便后面操作
    for i:=1 to 12 do
    begin
      next(c[a[i,c[i]]]);//先处理自己引起的旋转
      next(c[i]);//再处理自己的旋转
      //注意顺序不能换
      k:=get;
      if (getway[k]=0) and (sum[l]<9) then/
      /如果没有到达过(用来去重，保存最先搜到的最优解)并且没有到步数上限
      begin
        inc(r);//尾指针加1
        bfs[r]:=k;
        change[r]:=i;
        sum[r]:=sum[l]+1;
        last[r]:=l;
        getway[k]:=r;//新的状态入队
      end;
      if getway[ans]<>0 then
      //ans就是所谓的目标状态，就是全是1，对应在10进制中表示就是0，所以把ans换成0也是可以的
      begin
       writeln(sum[getway[ans]]);//输出最少的步数
       printf(getway[ans]);//打印方案
       halt;
      end;
      lass(c[i]);//退回到初始状态，顺序也不能动
      lass(c[a[i,c[i]]]);
    end;
    inc(l);
  end;
  //以下代码其实和上面同理，只是换了一下初始状态，相似的内容就不注释了
  l:=1; r:=1; bfs1[1]:=0;
  while l<=r do
  begin
    if (sum1[l]=9) then break;
    chai(bfs1[l]);
    for j:=1 to 12 do
      c[j]:=b[j];
    for i:=1 to 12 do
    begin
      lass(c[i]);
      lass(c[a[i,c[i]]]);//注意是反向搜索，所以使用前继
      k:=get;
      if (getway1[k]=0) and (sum1[l]<9) then
      begin
        inc(r);
        bfs1[r]:=k;
        change1[r]:=i;
        sum1[r]:=sum1[l]+1;
        last1[r]:=l;
        getway1[k]:=r;
        if (getway[k]<>0) then//如果和正向搜索搜到了相同的结果，说明找到了方案
        begin
          writeln(sum[getway[k]]+sum1[getway1[k]]);//步数是两个方向搜索的步数总和
          printf(getway[k]);//打印正向搜索的过程
          printf1(getway1[k]);//打印反向搜索的过程
          halt;
        end;
      end;
      next(c[a[i,c[i]]]);
      next(c[i]);
    end;
    inc(l);
  end;
end.
```
### $C++$ $AC$ $Code$
作者是一个正在转$C++$的$P$党，就不献丑了
~~（其实是懒得打）~~
，我会在$Pascal$的$AC$ $Code$的里把注释写的尽可能详细，方便大家理解，力争让$C/C++$党都看懂
~~（其实应该不写注释也能看懂吧）~~

如果一定想要看$C++$的$AC$ $Code$，请到后记中研究一下[我的天空](https://www.luogu.org/space/show?uid=94712)这位巨佬的代码

最后提供一下$Check$，方便调试
## $Check$
$Check$说明：
读入机关的状态后再读入变化步数和依次旋转的旋钮编号，最后会输出各个机关的状态

有了说明就不在代码里写注释了，相信都能看懂的
### 示例　
$Input$
```
3 3 7 2 6
3 1 4 5 3
3 1 2 6 4
3 1 10 3 5
3 2 8 3 6
3 7 9 2 1
1 1 2 3 4
1 3 11 10 12
1 8 6 7 4
1 9 9 8 8
1 12 10 12 12
1 7 8 9 10
6
1 2 3 4 5 6
```
$Output$
```
1 1 1 1 1 1 1 1 1 1 1 1
```

### $Pascal$ $Check$
```pascal
const n=12;
var
  i,x,step:longint;
  s:array[0..13] of longint;
  a:array[0..13,0..10] of longint;
procedure next(var k:longint);
begin
  inc(k);
  if k>4 then k:=1;
end;
begin
  for i:=1 to n do readln(s[i],a[i,1],a[i,2],a[i,3],a[i,4]);
  readln(step);
  for i:=1 to step do
  begin
    read(x);
    next(s[a[x,s[x]]]);
    next(s[x]);
  end;
  for i:=1 to n do write(s[i],' ');
  writeln;
end.

```
### $C++$ $Check$
```cpp
#include<bits/stdc++.h>
using namespace std;
const int n=12;
int i,x,step,s[10],a[13][10];
void nxt(int &k)
{
	k++;
	if (k>4) k=1;
}
int main()
{
	for (i=1;i<=n;i++) 
	cin>>s[i]>>a[i][1]>>a[i][2]>>a[i][3]>>a[i][4];
	cin>>step;
	for (i=1;i<=step;i++)
	{
		cin>>x;
		nxt(s[a[x][s[x]]]);
		nxt(s[x]);
	}
	for (i=1;i<=n;i++) cout<<s[i]<<' ';
	cout<<endl;
}
```

# 总结
这道题比较经典，对搜索的优化考察比较丰富（包括状态记录，启发式搜索或双向搜索），需要思路和一定的代码实现能力，适合提高选手练练手。

思维难度：中

代码难度：中
# 后记
翻了一下$AC$的提交记录，发现 [我的天空](https://www.luogu.org/space/show?uid=94712) 巨佬和我的思路相近，但他的代码跑得比我快很多，最慢的点也只接近400$ms$，有兴趣的同学可以研究一下这位[巨佬的代码](https://www.luogu.org/record/22702524)，或者和这位巨佬交流。
# 鸣谢
[ i_m_a_](https://www.luogu.org/space/show?uid=86649)
这位出题人在我遇上困难时无私地帮助了我（提供了一个数据），使得我找到了原来代码的问题，特此感谢