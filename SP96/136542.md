# 前言
此题是宽搜裸题，适合刚学bfs的新手练练手（新手也可以考虑保存一下当模板）
# 思路
暴力宽搜，开辟一个f数组表示到达i行j列的最短时间，然后用类似于SPFA的更新操作就可以无压力一发Ac，可以看我的代码注释辅助理解
# 时空
#### 时间 
约O(knm)级别(k为较小的常数)
#### 空间
O(nm)级别
# Ac Code (pascal)
```pascal
const
  dx:array[1..4] of longint=(0,0,-1,1);
  dy:array[1..4] of longint=(1,-1,0,0);//dx,dy为四个方向
var
  ch:char;
  n,m,i,j,xx,yy,l,r,zz,startx,starty,overx,overy:longint;
  a,f:array[0..500,0..500] of longint;
  x,y,z:array[0..1000000] of longint;
function pd(xxx,yyy:longint):boolean;
begin
  if (xxx>0) and (yyy>0) and (xxx<=n) and (yyy<=m) and (a[xxx,yyy]<>10) then exit(true)
  else exit(false);
end;//判断是否越界，是否有障碍，如果有就返回false（不能走），否则返回true（能走）

begin
  while true do
  begin

  readln(m,n);
  fillchar(a,sizeof(a),0);//初始化
  if (m=0) and (n=0) then break;//结束
  for i:=1 to n do
  begin
   for j:=1 to m do
   begin
     read(ch);
     if ch='S' then begin startx:=i; starty:=j; end
     else if ch='D' then begin overx:=i; overy:=j; end
     else if ch='X' then a[i,j]:=10//障碍标记为10
     else a[i,j]:=ord(ch)-48;//读入并处理起点，终点与障碍，时间
     f[i,j]:=maxlongint;//f数组初始值为一个极大值
   end;
   readln;
  end;
  x[1]:=startx; y[1]:=starty; z[1]:=0;  l:=1; r:=1;//从起点开始宽搜
  while l<=r do
  begin
    for i:=1 to 4 do//向四个方向探查
    begin
      xx:=dx[i]+x[l];
      yy:=dy[i]+y[l];
      if pd(xx,yy) then//如果可以走
      begin
        zz:=z[l]+a[xx,yy];//累加时间
        if zz<f[xx,yy] then//判断是否更优，更优就更新，否则就不动
        begin
          f[xx,yy]:=zz;//更新
          inc(r);
          x[r]:=xx;
          y[r]:=yy;
          z[r]:=zz;//更优值入队列进行宽搜
        end;
      end;
    end;
    inc(l);
  end;//宽搜主体框架
  writeln(f[overx,overy]);//输出答案
  end;
end.

```
# 后记
如果还无法理解，建议去询问老师或其他dalao了解一下bfs(我自认为我已经在代码里写的够明白了)，或者如果发现本题解有问题也可以私信我