## Problems
给出若干组数据，每组数据有两个字符串（中间由一个空格隔开），这两个字符串分别为一棵树的前序遍历和中序遍历，现在要你求这棵树的后序遍历。
## Answer
这道题，其实与 $P1030$ 很像，只不过在细节上有一些差别
### Deformation
$P1030$ 这道题要我们求得是一棵树的前序遍历，而现在这道题要我们求得是一棵树的后序遍历。

但是做法还是一样的，只不过求前序遍历需要先把目前搜索的这棵数的根节点找到，然后直接输出；而求后序遍历，则是在目前搜索的这棵找到根节点后，先把它的左右子树搜完，然后再把根节点输出。

在搜前序遍历时，我们的主要搜索程序是这样的
```pascal
procedure qianxu(l,m:ansistring);
var
	k:longint;
begin
	if length(l)=1 then write(l)
		else
		begin
			k:=pos(l[1],m);
        		write(l[1]);//找到根节点后直接输出
			if k>1 then qianxu(copy(l,2,k-1),copy(m,1,k-1));
			if k<length(m) then qianxu(copy(l,k+1,255),copy(m,k+1,255));
		end;
end;
```

而在搜后序遍历时，我们的主要搜索程序是这样的
```pascal
procedure houxu(l,m:ansistring);
var
	k:longint;
begin
	if length(l)=1 then write(l)
		else
		begin
			k:=pos(l[1],m);
			if k>1 then houxu(copy(l,2,k-1),copy(m,1,k-1));
			if k<length(m) then houxu(copy(l,k+1,255),copy(m,k+1,255));//在找到根节点后，先搜索左右子树，搜完了再把根节点输出
			write(l[1]);
		end;
end;
```

当把怎么搜后序遍历理解明白后，这道题就变得十分简单了

# Code
```pascal
var
	s,l,m:ansistring;

procedure houxu(l,m:ansistring);//这块其实就是上面解释的部分
var
	k:longint;
begin
	if length(l)=1 then write(l)
		else
		begin
			k:=pos(l[1],m);
			if k>1 then houxu(copy(l,2,k-1),copy(m,1,k-1));
			if k<length(m) then houxu(copy(l,k+1,255),copy(m,k+1,255));
			write(l[1]);
		end;
end;

begin
        while not eof do
        begin
	        readln(s);
	        l:=copy(s,1,pos(' ',s)-1);
                delete(s,1,pos(' ',s));m:=s;//因为前序遍历和中序遍历是一起读入的，所以需要利用copy函数和delete函数来把前序遍历和中序遍历提取出来
	        houxu(l,m);
                writeln;
        end;
end.

```