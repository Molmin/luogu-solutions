pascal题解好少……

这道题目在考场上的时候，我第一眼看到就觉得是动态规划，但是就是没有想出DP方程，最后NOIP崩了……

不过，考完后，我发现了一个问题：

对于50%的数据，n≤500，m≤100，0≤ti≤10^4。

对于100%的数据，n≤500，m≤100，0≤ti≤4*10^6。

只有ti发生了变化，所以，如果这道题的时间复杂度和ti无关，那么打出了50分代码就相当于100分……

这是不可能的，所以时间复杂度一定和ti有关。

于是，我们可以很快得出这样一个DP方程：

f[k]:=min(f[k-j]+s)

其中，f[k]表示在k时间内，在第k时间发车，所产生的最小等待时间。j在m到2m之间，因为如果我们在之前超过2m时间发车，不妨在k-m到k-2m之间发车，这样ti在k-2m以外的人坐那次车，等待时间明显更短。而一次车要m时间才能够回来，所以是m到2m。那么，s就是ti在k-j+1到k之间的所有人坐这次车造成的等待时间。我们可以先排序再查找。

时间复杂度为O(max(ti)nm)，明显是超时的，以下有几个优化：

1、前缀和

既然是处理k-j+1到k之间的等待时间，对于每个ti，等待时间为k-ti，s就是：k*(r-l+1)-l到r之间所有ti的和。其中在[l,r]区间内所有ti都是在k-j+1到k之间的。这样，我们用前缀和处理ti（需要排序，这样才能使区间连续），O(1)即可处理出s。

2、二分

如果按照普通方法找到l，r，时间复杂度为O(n)，太慢，但是，我们已经对ti进行了排序，此时数据呈现单调性，进行二分查找即可找到l，r的位置。时间复杂度O(log n)。

3、离散化

这是最重要的一点，虽然有前面这么多的优化，但是max(ti)太大，即使是2s也会超时，但是，在排序以后，我们考虑相邻的两个人，如果他们的ti相差超过2m，假设有一班次车在他们俩的等待时间之间出发，因为要有最短等待时间，所以这班车肯定会在前一个人到达后m时间之前出发（不然，就像上面分析的那样，不是最短等待时间），也就会后一个人来等车之前回来。此时，接下来的这段时间只有车在，我们可以忽略这段时间不去处理，将他们俩等待时间差值设为2m，是没有影响的。这样，就可以将max(ti)变为2nm（有n个人，每两个人时间相差不超过2m）。

优化过后，时间复杂度为O(n lognm^2)。

以下是代码：

```pascal
uses math;
var a,s:array[-1..500] of longint;
    f:array[0..200000] of longint;
    n,m,i,j,t,minn,r:longint;
function erfen(opoi,opoj:longint):longint;
var l,r,mid,tot1,tot2:longint;
begin
  l:=1;r:=n;tot1:=0;tot2:=0;
  while l<=r do
  begin
    mid:=(l+r) div 2;
    if a[mid]>=opoi then begin tot1:=mid;r:=mid-1;end
    else l:=mid+1;
  end;
  l:=1;r:=n;
  while l<=r do
  begin
    mid:=(l+r) div 2;
    if a[mid]<=opoj then begin tot2:=mid;l:=mid+1;end
    else r:=mid-1;
  end;
  exit(opoj*(tot2-tot1+1)-(s[tot2]-s[tot1-1]));
end;
begin
  minn:=maxlongint;
  readln(n,m);
  for i:=1 to n do read(a[i]);
  for i:=1 to n-1 do
    for j:=i+1 to n do
      if a[i]>a[j] then
      begin
        t:=a[i];a[i]:=a[j];a[j]:=t;
      end;
  for i:=1 to n do
    if a[i]-a[i-1]>2*m then
    begin
      for j:=i+1 to n do
        a[j]:=a[j]-a[i]+a[i-1]+2*m;
      a[i]:=a[i-1]+2*m;
    end;
  fillchar(f,sizeof(f),$7f);
  for i:=1 to n do s[i]:=s[i-1]+a[i];
  for i:=0 to m-1 do
  begin
    for j:=1 to n do
      if (a[j-1]<=i) and (a[j]>i) then
      begin
        r:=j;
        break;
      end;
    dec(r);
    f[i]:=r*i-s[r];
  end;
  for i:=m to a[n]+m do
    for j:=m to 2*m-1 do
      if i>=j then
        f[i]:=min(f[i],f[i-j]+erfen(i-j+1,i));
  //for i:=0 to a[n]+m do write(f[i],' ');writeln;
  for i:=a[n] to a[n]+m do
    if f[i]<minn then minn:=f[i];
  writeln(minn);
end.
```

后来，我翻了一下别人的题解，发现他们的复杂度都是O(nm)、O(nm^2)的，我感到自己的题解好弱……

经@[CMinusMinus](https://www.luogu.org/space/show?uid=136721)提醒，我发现找l、r无需二分查找，可以O(nm)预处理一下每一个时间点对应的l、r，dp时直接用就可以了。

next[i]表示来等待时间小于i的所有人中时间最大的那一个。

pre[i]表示来等待时间不大于i的所有人中时间最大的那一个。

那么，上述tot1就是next[opoi]+1，tot2就是pre[opoj]。

于是，时间复杂度为O(nm^2)。

代码如下：
```pascal
uses math;
var a,s:array[-1..500] of longint;
    f,pre,next:array[0..100000] of longint;
    n,m,i,j,t,minn,r:longint;
function erfen(opoi,opoj:longint):longint;
var tot1,tot2:longint;
begin
  tot1:=next[opoi]+1;
  tot2:=pre[opoj];
  exit(opoj*(tot2-tot1+1)-(s[tot2]-s[tot1-1]));
end;
begin
  minn:=maxlongint;
  readln(n,m);
  if m=1 then begin writeln(0);exit;end;
  for i:=1 to n do read(a[i]);
  for i:=1 to n-1 do //排序，由于n只有500，不想打快排。
    for j:=i+1 to n do
      if a[i]>a[j] then
      begin
        t:=a[i];a[i]:=a[j];a[j]:=t;
      end;
  for i:=1 to n do //离散化
    if a[i]-a[i-1]>2*m then
    begin
      for j:=i+1 to n do
        a[j]:=a[j]-a[i]+a[i-1]+2*m;
      a[i]:=a[i-1]+2*m;
    end;
  fillchar(f,sizeof(f),$7f);
  for i:=1 to n do s[i]:=s[i-1]+a[i];
  pre[0]:=1;
  for i:=1 to a[n]+m do //处理pre数组
  begin
    pre[i]:=pre[i-1];
    while (pre[i]<n) and (i>=a[pre[i]+1]) do inc(pre[i]);
  end;
  //for i:=0 to a[n]+m do write(pre[i],' ');writeln;
  next[0]:=0;
  for i:=1 to a[n]+m do //处理next数组
  begin
    next[i]:=next[i-1];
    while (next[i]<n) and (i>a[next[i]+1]) do inc(next[i]);
  end;
  //for i:=0 to a[n]+m do write(next[i],' ');writeln;
  for i:=0 to m-1 do
  begin
    r:=pre[i];
    f[i]:=r*i-s[r];
  end;
  for i:=m to a[n]+m do //dp
    for j:=m to 2*m-1 do
      if i>=j then
        f[i]:=min(f[i],f[i-j]+erfen(i-j+1,i));
  //for i:=0 to a[n]+m do write(f[i],' ');writeln;
  for i:=a[n] to a[n]+m do
    if f[i]<minn then minn:=f[i];
  writeln(minn);
end.
```

时间很快，合计236ms，最上面的要1314ms。