本题可以用离散化优化到O(N^3)，也可以用wanglichao1121的方法优化到O(N(Y[I]-X[I])，但是，如果把两者结合在一起，可以写出O(N^2)的程序，虽然对于本题的数据，简直就是杀鸡用牛刀，但是普及一下还是很不错的。


首先给出O(N(Y[I]-X[I])的程序，思路详见wanglichao1121的题解（虽然说思路是我想出来的）：

```delphi
var
  x1,x2,x,y1,y2,y:array[0..100] of longint;
  min,max,n,i,j,k,tail,ans:longint;
procedure swap(var a,b:longint);
var t:longint;
begin
  t:=a; a:=b; b:=t;
end;
procedure qsort(r,l:longint);
var
  i,j,mid:longint;
begin
  i:=r; j:=l;
  mid:=y1[(i+j) shr 1];
  repeat
    while y1[i]<mid do inc(i);
    while y1[j]>mid do dec(j);
    if i<=j then
      begin
        swap(x1[i],x1[j]);
        swap(x2[i],x2[j]);
        swap(y1[i],y1[j]);
        swap(y2[i],y2[j]);
        inc(i); dec(j);
      end;
  until i>j;
  if i<l then qsort(i,l);
  if r<j then qsort(r,j);
end;//根据y坐标从小到大排序，方便后面的O(N)求线段覆盖
begin
  readln(n);
  for i:=1 to n do
    begin
      readln(y1[i],x1[i],y2[i],x2[i]);
      x1[i]:=-x1[i];//因为是数字越大，就越上面，所以横坐标需要取反，适合人的思路
      x2[i]:=-x2[i]-1;//注意是点边，所以要减去1
      //y2[i]:=y2[i];
    end;//读入
  qsort(1,n);//排序
  min:=maxlongint; max:=-min;
  //min:=x1[1]; max:=x2[n];
  for i:=1 to n do
    begin
      if x1[i]<min then min:=x1[i];
      if x2[i]>max then max:=x2[i];
    end;//寻找最上面的边和最下面的边
  //writeln(min); writeln(max);
  for i:=min to max do//从上到下枚举每条边
    begin
      k:=0;
      for j:=1 to n do
        if (x1[j]<=i) and (x2[j]>=i) then
          begin
            inc(k);
            x[k]:=y1[j];
            y[k]:=y2[j];
          end;//统计矩形内含有这条边的矩形
      if k=0 then continue;
      //head:=x[1];
      tail:=y[1];
      ans:=ans+tail-x[1];
      for j:=2 to k do
        if y[j]>tail then
          if x[j]<tail then
            begin ans:=ans+y[j]-tail; tail:=y[j]; end else
            begin ans:=ans+y[j]-x[j]; tail:=y[j]; end;//O(N)线段覆盖，详见洛谷online judge的火烧赤壁题解
    end;
  writeln(ans);
end.
```

我们画出图形之后，可以发现有许多线段是属于同一组矩形的，这就给了我们优化的思路。因为上述算法枚举线段明显存在可以合并的地方。因此我们先把所有的横坐标记录下来，用一维普通离散化的思路将横坐标排序，然后判断任意两个坐标之间的矩形，用O(N)扫描完成统计，最后×坐标差并累加，就可以完成了。

```delphi
var
  a,x1,x2,x,y1,y2,y:array[0..100] of longint;
  min,max,n,i,j,k,tail,ans,sum,m:longint;
procedure swap(var a,b:longint);
var t:longint;
begin
  t:=a; a:=b; b:=t;
end;
procedure qsort(r,l:longint);
var
  i,j,mid:longint;
begin
  i:=r; j:=l;
  mid:=y1[(i+j) shr 1];
  repeat
    while y1[i]<mid do inc(i);
    while y1[j]>mid do dec(j);
    if i<=j then
      begin
        swap(x1[i],x1[j]);
        swap(x2[i],x2[j]);
        swap(y1[i],y1[j]);
        swap(y2[i],y2[j]);
        inc(i); dec(j);
      end;
  until i>j;
  if i<l then qsort(i,l);
  if r<j then qsort(r,j);
end;
procedure sort(r,l:longint);
var
  i,j,mid,t:longint;
begin
  i:=r; j:=l;
  mid:=a[(i+j) shr 1];
  repeat
    while a[i]<mid do inc(i);
    while a[j]>mid do dec(j);
    if i<=j then
      begin
        t:=a[i]; a[i]:=a[j]; a[j]:=t;
        inc(i); dec(j);
      end;
  until i>j;
  if i<l then sort(i,l);
  if r<j then sort(r,j);
end;
begin
  readln(n);
  for i:=1 to n do
    begin
      readln(y1[i],x1[i],y2[i],x2[i]);
      x1[i]:=-x1[i];
      x2[i]:=-x2[i];//不需要减一，切记切记！！！
      a[i shl 1-1]:=x1[i];
      a[i shl 1]:=x2[i];
    end;
  m:=n shl 1;
  qsort(1,n);  sort(1,m);
  for i:=1 to m-1 do//枚举每个横坐标之间的空间，M个横坐标，M-1个区间
    begin
      k:=0;
      for j:=1 to n do
        if (x1[j]<=a[i]) and (x2[j]>=a[i+1]) then
          begin
            inc(k);
            x[k]:=y1[j];
            y[k]:=y2[j];
          end;//包含此区间的矩形
      if k=0 then continue;
      tail:=y[1];
      sum:=tail-x[1];
      for j:=2 to k do
        if y[j]>tail then
          if x[j]<tail then
            begin sum:=sum+y[j]-tail; tail:=y[j]; end else
            begin sum:=sum+y[j]-x[j]; tail:=y[j]; end;//O(N)扫描
      sum:=sum*(a[i+1]-a[i]);//乘以区间的宽
      ans:=ans+sum;//累加
    end;
  writeln(ans);
end.
```