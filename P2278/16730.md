看到没有pas的so来一发


**赤裸裸**的模拟啊，为了不**TLE**所以要加个堆优化


维护一个**最大堆**


代码如下

```cpp
type prog=
    record
        pid,start,time,level:longint;
    end;
var 
    h:array[1..15001] of prog;
    now,size,pid,start,time,level:longint;
    t:prog;
function cmp(x,y:prog):boolean;//比较函数
begin
    if x.level>y.level then exit(true);//优先级高的在前
    if (x.level=y.level)and(x.start<y.start) then exit(true);//如果优先级相等先到先来
    exit(false);
end;
procedure swap(x,y:longint);//交换函数
var t:prog;
begin
    t:=h[x];
    h[x]:=h[y];
    h[y]:=t;
end;
procedure up(i:longint);//堆-向上调整
var flag:boolean;
begin
    if i=1 then exit();
    flag:=true;
    while (i<>1)and(flag) do
    begin
        if cmp(h[i],h[i div 2]) then 
            swap(i,i div 2)
        else
            flag:=false;
        i:=i div 2;
    end;
end;
procedure down(i:longint);//堆-向下调整
var t:longint;
    flag:boolean;
begin
    flag:=true;
    while ((i*2)<=size)and(flag) do
    begin
        t:=i;
        if cmp(h[i*2],h[i]) then t:=i*2;
        if ((i*2+1)<=size) then
            if cmp(h[i*2+1],h[t]) then t:=i*2+1;
        if t<>i then
        begin
            swap(i,t);
            i:=t;
        end else
            flag:=false;
    end;
end;
function pop():prog;//取堆顶函数，也就是取优先级最高先到的那个进程
var t:prog;
begin
    t:=h[1];
    h[1]:=h[size];
    size:=size-1;
    down(1);
    exit(t);
end;
begin
    now:=0;//现在时间
    while not eof do //读到文件结束 "eof": End Of File
    begin
        readln(pid,start,time,level);
        //优化-如果下一个来的时间之前当前进程能处理完
        while (size>0)and(h[1].time<=start-now) do
        begin
            t:=pop();
            inc(now,t.time);
            writeln(t.pid,' ',now);
        end;
        //优化 处理完时 如果至下一个进程到来还有时间则跳过
        if (size>0)and((start-now)>0) then 
            dec(h[1].time,start-now);
        now:=start;//更新现在时间
        //优化-堆
        inc(size);
        h[size].time:=time;
        h[size].start:=start;
        h[size].pid:=pid;
        h[size].level:=level;
        up(size);
    end;
    //执行最后几个进程
    while (size>0) do 
    begin
        t:=pop();
        inc(now,t.time);
        writeln(t.pid,' ',now);
    end;
end.
```