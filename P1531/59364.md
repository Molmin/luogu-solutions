线段树的基础模板

解法：单点更新&区间查询


------------


# 此题虽然和杭电的那题一样，却有些不同，注意洛谷上说的是成绩比原来大的时候做修改，我和好多同学一样被坑了好多次。。。



------------


看下面P党的好少，贴上~~个人认为简洁优美的~~Pas代码，希望你们且行且珍惜！


```pascal
uses math;
var a:array[1..200000]of longint;
    sum:array[1..800000]of longint;
    n,m,l,p,q:longint;
    c:char;
procedure pushup(rt:longint);
begin
 sum[rt]:=max(sum[(rt shl 1)],sum[(rt shl 1) or 1]);
end;
procedure build(l,r,rt:longint);
var mid:longint;
begin
 if l=r then
  begin
   sum[rt]:=a[l];
   exit;
  end;
 mid:=(l+r) shr 1;
 build(l,mid,rt shl 1);
 build(mid+1,r,(rt shl 1)or 1);
 pushup(rt);
end;
function query(x,y,l,r,rt:longint):longint;
var mid,ansl,ansr:longint;
begin
 if (x<=l)and(r<=y) then exit(sum[rt]);
 mid:=(l+r) shr 1;
 ansl:=0;ansr:=0;
 if x<=mid then ansl:=query(x,y,l,mid,rt shl 1);
 if y>mid then ansr:=query(x,y,mid+1,r,(rt shl 1) or 1);
 exit(max(ansr,ansl));
end;
procedure update(p,c,l,r,rt:longint);
var mid:longint;
begin
 if l=r then
  begin
   sum[rt]:=max(sum[rt],c);
   exit;
  end;
 mid:=(l+r) shr 1;
 if p<=mid then update(p,c,l,mid,rt shl 1)
 else update(p,c,mid+1,r,(rt shl 1)or 1);
 pushup(rt);
end;
begin
 readln(n,m);
 for l:=1 to n do
  read(a[l]);
 readln;
 build(1,n,1);
 for l:=1 to m do
  begin
   readln(c,p,q);
   if c='Q' then writeln(query(p,q,1,n,1))
   else update(p,q,1,n,1);
  end;
end.
```

~~帅气的管理员大大给个通过吧~~