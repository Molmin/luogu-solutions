显然，这道题是求一个以‘Z'为根节点单源最短路中的路径最短的大写字母节点

很自然地会想到用dijkstra嘛

为什么都用floyd呢。实验表明dijkstra比floyd确实要快那么一些

其实对于这种稠密图最好应用邻接矩阵（删去一些没用的边，比如几条连接两个相同节点的边只取其中最短的一条）

我这里只是想研究一下邻接表的优化。

还有，其实pascal对字符的运用能力很强，没必要转ascall码

二话不说上代码






```cpp
type
  adj=record
    last,wei:longint;  //last存储这个节点在链表中的上一条边的地址，wei存储权值
    too:char; //too存储这条边指向那个节点
  end;
const caps=['A'..'Z'];
var
  all:set of 'A'..'z'; 
  a:array['A'..'z','A'..'z'] of longint; //a存储这条边在链表中的地址，如果不在链表中则为0
  edge:array[1..20000] of adj; //邻接表
  head,min:array['A'..'z'] of longint; //head存储节点在链表中最后一条边的地址
  nedge,v,e,z,k,minw:longint;
  x,xx,y,yy,i,j,minv:char;
procedure add(a,b:char; w:longint); //将边插入
begin
  inc(nedge);
  edge[nedge].last:=head[a];
  edge[nedge].too:=b;
  edge[nedge].wei:=w;
  head[a]:=nedge;
end;
procedure find; //找出最小值
begin
  for i:='A' to 'z' do
  if (i in all) and (min[i]<minw) then
    begin
      minw:=min[i];
      minv:=i;
    end;
end;
procedure readin;
begin
  readln(e);
  nedge:=0;
  fillchar(head,sizeof(head),0);
  for k:=1 to e do
  begin
    readln(x,xx,y,yy,z);
    if (a[x,y]<>0) and (edge[a[x,y]].wei>z) then
    begin
      edge[a[x,y]].wei:=z;
      edge[a[y,x]].wei:=z;
      continue;
    end; //如果这条边已经读过而且新读入的权值更小则替换原来的权值，相当于给他升级
    if a[x,y]<>0 then continue; //不然不处理
    if x=y then continue; //自己指自己无意义
    all:=all+[y];
    all:=all+[x]; //加入顶点集合all
    add(x,y,z);
    a[x,y]:=nedge;//这条边是新读入的要记录地址
    add(y,x,z);
    a[y,x]:=nedge;
  end;
end;
begin
  readin;
  all:=all-['Z']; //预处理第一个点'Z'，现在all用来存储待处理的节点
  k:=head['Z']; 
  for i:='A' to 'z' do
  begin
    min[i]:=maxlongint;
  end;
  min['Z']:=0;
  while k>0 do
  if min[edge[k].too]>edge[k].wei then
  begin
    min[edge[k].too]:=edge[k].wei;
    k:=edge[k].last;
  end;  
  while all<>[] do //dijkstra
  begin
    minw:=maxlongint;
    find;
    if minv in caps then //dijkstra的最短路径长度是递增的，所以找到的第一个大写字母最短路径长度就是最小的
    begin
      write(minv,' ',minw);
      halt;
    end;
    k:=head[minv];
    while (k>0) do  //松弛
    begin
      if min[edge[k].too]>edge[k].wei+minw then
      min[edge[k].too]:=edge[k].wei+minw;
      k:=edge[k].last;
    end;
    all:=all-[minv];
  end;
end.
```