本人略显蒟蒻，代码有些冗长，但长长一页题解竟没有一个pascal实在让人痛心，网上也都c++，鄙人感到尴尬，分享一波：

其实懂了思想还是挺简单的，朴素的高精度乘法和高精度除法，但难想到的还是如何一开始给他们排序（像我，略加分析。。直接按右手排序，结果你懂得。。。），于是冥思苦想，终于发现了如何排序（。。好吧，借鉴了别人的想法），便是按a\*b排序。

为什么呢？我简单说明一下，假设前面几个人都排好了，我们要如何证明最后两个人谁排前面会对答案影响小一点呢？也就是贡献得尽量小一点，设前几个人a的乘积为x，那么这两个人（a1,b1）和（a2,b2）谁排前一个结果就为x\*a1/b2和x\*a2/b1，将他们做下比较，易得出当a1\*b1<a2\*b2时(a1,b1)排在前面更好【可以在草稿纸上列下不等式算下，不难得出】，然后就简单了！

多余的话请自行省略，下面是我的code：

program king;//还有很重要的一点，大家一定要分清以下各数组是从低位到高位还是从高位到低位

```cpp
var
        a,b,c:array[0..1000]of longint;
        r,max,ans:array[0..4000]of longint;
        i,j,n,k,sum,ss:longint;
procedure qsort(l,r:longint);//这个不用说吧。。。
var
        i,j,x,temp:longint;
begin
        i:=l;
        j:=r;
        x:=c[(i+j)div 2];
        repeat
                while c[i]<x do inc(i);
                while x<c[j] do dec(j);
                if not(i>j)then
                begin
                        temp:=a[i];a[i]:=a[j];a[j]:=temp;//这个地方可能可以缩短代码长度，请读者自行决定
                        temp:=b[i];b[i]:=b[j];b[j]:=temp;
                        temp:=c[i];c[i]:=c[j];c[j]:=temp;
                        inc(i);dec(j);
                end;
        until i>j;
        if i<r then qsort(i,r);
        if l<j then qsort(l,j);
end;
procedure sheng(x:longint);//高精度乘法（拼音拼错，cheng拼成sheng尴尬），不懂的请翻阅下资料书，这个都有
var
        kk,i,j,y:longint;
        e1,e2:array[1..10]of longint;
        h:array[0..4000]of longint;
begin
        kk:=0;
        fillchar(h,sizeof(h),0);
        while x<>0 do//把要乘的数也拆为数组
        begin
                inc(kk);
                e1[kk]:=x mod 10;
                x:=x div 10;
        end;
        for i:=1 to k do//高精度乘法具体实现
        begin
              y:=0;
              for j:=1 to kk do
              begin
                    h[i+j-1]:=r[i]*e1[j]+y+h[i+j-1];
                    y:=h[i+j-1]div 10;
                    h[i+j-1]:=h[i+j-1] mod 10;
              end;
              h[i+j]:=y;
        end;
        k:=k+kk; i:=k;//统计位数，我也不清楚是不是多此一举
        while h[i]=0 do dec(i);
        k:=i;
        for i:=k downto 1 do r[i]:=h[i];
end;
function chu(x:longint):longint;//这里我要说明一下，因为我的资料书没说这个，我是从今年初赛的大整数除法学到的（用于高精度除于低精度），尴尬
var
        i,j,p:longint;
begin
        fillchar(ans,sizeof(ans),0);
        i:=k;p:=0;
        while (p<x)and(i>0)do//这里不太好说，只要能理解了mod的应用都不难理解这个除法是什么意思
        begin
                p:=p*10+r[i];
                dec(i);
        end;
        if p<x then exit(0);//被除数小于除数（称呼没有搞乱吧，意思一下）
        ans[1]:=p div x;j:=1;
        p:=p mod x;
        while i>0 do
        begin
                p:=p*10+r[i];
                inc(j);dec(i);
                ans[j]:=p div x;
                p:=p mod x;
        end;
        sum:=j;//记录商的位数
end;
procedure judge;//判断，寻找当前答案
var
        i,j:longint;
begin
        j:=0;
        if sum<ss then exit
        else if sum>ss then
             begin
                        ss:=sum;
                        for i:=1 to ss do
                        max[i]:=ans[i];
             end
             else  begin
                        for i:=1 to sum do
                        if max[i]>ans[i] then begin j:=0; break; end
                        else if ans[i]>max[i] then  begin j:=1; break; end;
                        if j=1 then for i:=1 to sum do max[i]:=ans[i];
                end;
end;
begin
        assign(input,'king.in'); reset(input);
        read(n);
        for i:=0 to n do
        begin
                read(a[i],b[i]);
                c[i]:=a[i]*b[i];
        end;
        qsort(1,n);
        k:=1;
        r[1]:=1; ss:=1;
        for  i:=1 to n do
        begin
                sheng(a[i-1]);//累乘
                chu(b[i]);//除的时候不改变累乘的数值
                judge;//寻找答案
        end;
        for i:=1 to ss do write(max[i]);//输出》~《
end.

```