广告一下：<http://zhoushengjie.pw>

看了一下，这道题Pascal题解还挺多，然而可能很多人不知道，Pascal和C++一样，也有**运算符重载**的功能。

运算符重载后，打代码会方便许多。

~~不过由于Pascal的输入输出不是运算符，特别恶心啊~~

具体用法会在代码里介绍，其实跟C++没啥两样。

因为主要是讲运算符重载，所以高精度就不解释了哈。~~看其他题解去~~

```pas
{P1009}
//operator可能是少数Pascal和C++通用的关键词
//这并不是标准语法，然而好像在第一个Pascal编译器出现的时候就能用了
type
bigint=array[0..200]of longint;
operator :=(a:longint)b:bigint;
//这是:=赋值运算符的重载，将a赋值给b
//重载后就可以进行隐性转换了，比如a=0，这里编译器会自动将0转换成bigint类型
 begin
 fillchar(b,sizeof(b),0);
 b[0]:=0;
 repeat
 inc(b[0]);
 b[b[0]]:=a mod 10;
 a:=a div 10;
 until a=0;
 end;
operator =(a,b:bigint)c:boolean;//=等号重载，重载后<>不等号也可以用了
 var
 i:longint;
 begin
 c:=false;
 if a[0]<>b[0] then
 exit;
 for i:=a[0] downto 1 do
  if a[i]<>b[i] then
  exit;
 c:=true;
 end;
function max(x,y:longint):longint;//无需解释
 begin
 if x>y then
 exit(x)
 else
 exit(y);
 end;
operator +(a,b:bigint)c:bigint;//+加号重载
 var
 i:longint;
 begin
 c:=0;
 c[0]:=max(a[0],b[0]);
 for i:=1 to c[0] do
 begin
 inc(c[i],a[i]+b[i]);
 inc(c[i+1],c[i] div 10);
 c[i]:=c[i] mod 10;
 end;
 if c[c[0]+1]>0 then
 inc(c[0]);
 end;
operator *(a,b:bigint)c:bigint;//*乘号重载
 var
 i,j:longint;
 begin
 c:=0;
 if (a=0) or (b=0) then
 exit;
 c[0]:=a[0]+b[0]-1;
 for i:=1 to a[0] do
  for j:=1 to b[0] do
  begin
  inc(c[i+j-1],a[i]*b[j]);
  inc(c[i+j],c[i+j-1] div 10);
  c[i+j-1]:=c[i+j-1] mod 10;
  end;
 if c[c[0]+1]>0 then
 inc(c[0]);
 end;
procedure print(a:bigint);//输出
 var
 i:longint;
 begin
 for i:=a[0] downto 1 do
 write(a[i]);
 writeln;
 end;
//以下没啥好看的
var
n,i:longint;
s,x:bigint;
begin
read(n);
s:=0;
x:=1;
for i:=1 to n do
begin
x:=x*i;
s:=s+x;
end;
print(s);
end.
```