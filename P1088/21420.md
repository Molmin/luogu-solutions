应该是一道全排列吧

因为m<=100，所以我们可以一次次模拟它的进位情况。

我的启发是行驶里程表，末尾数到“9”后，末尾的“9”全部清零，它的上一位进位。

本题也一样，只要找到以最后一个元素为尾的最长不升连续子序列，它的前一位必须要“进位”。

既然要进位，就必须在后面找一个大于它的最小的数，将他们两个交换。

由于以上交换，不升连续子序列的性质并没有被破坏（可以自己模拟下）。

接下来就是清零了，就要找出子序列里字典序最小的（就是子序列的不降排列）。

个人认为不需要重新排序，由于有不升子序列的性质，翻转后就可得到不降排列。

然后，附代码：

[codep ]




```cpp
program p1088;
var
  a:array[0..10500] of longint;
  n,m,i:longint;
procedure swap(var a,b:longint); //用于翻转、交换
var
  t:longint;
begin
  t:=a;
  a:=b;
  b:=t;
end;
procedure succ;  //求下一个排列
var
  i,p:longint;
begin
  p:=n;
  while (p>1) and (a[p]<a[p-1]) do dec(p);  //找最长不升连续子序列
  dec(p);  //交换位
  for i:=n downto p+1 do  //找大于它的最小值
    if a[i]>a[p] then
      break;
  swap(a[i],a[p]);
  for i:=p+1 to (n+p) div 2 do  //p+(n-p) div 2=(n+p) div 2  //翻转
    swap(a[i],a[n+p-i+1]);
end;
begin
  readln(n);
  readln(m);
  for i:=1 to n do
    read(a[i]);
  for i:=1 to m do
    succ;
  for i:=1 to n do
    write(a[i],' ');
  writeln; 
end.
[/codep ]
```