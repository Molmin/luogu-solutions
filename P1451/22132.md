##(Pascal居然只有一个用BFS的题解，那我来写一个)思路：用DFS，找到一个未被标记的细胞后，先累积数量，然后标记它，再对其上下左右进行深搜，若发现未被标记的细胞，则递归...

```cpp
const
  e:array[1..4,1..2] of -1..1=((1,0),(-1,0),(0,1),(0,-1)); //存储上下左右的坐标
var
  n,m,i,j,s:longint;
  st:string;
  b:array[0..81,0..81] of boolean; //存储细胞是否被标记过，true为未标记，false为已标记
procedure f(i,j:longint); //深搜过程
  var
    k,x,y:longint;
  begin
    b[i,j]:=false; //标记这个细胞
    for k:=1 to 4 do
      begin
        x:=i+e[k,1]; //存储上下左右的x坐标
        y:=j+e[k,2]; //存储上下左右的y坐标
        if b[x,y] then //如果未被标记过
          f(x,y); //DFS这个坐标
      end;
  end;
begin
  readln(n,m); //输入n,m
  for i:=1 to n do
    begin
      readln(st); //读入一个字符串，这样可以避免读入回车
      for j:=1 to m do
        begin
          if st[j]<>'0' then
            b[i,j]:=true; //如果它是细胞，就去除标记
        end;
    end;
  for i:=1 to n do
    for j:=1 to m do
      if b[i,j] then //如果未被标记
        begin
          f(i,j); //DFS该细胞
          inc(s); //增加一个细胞数量
        end;
  writeln(s); //输出细胞数量
end.
```