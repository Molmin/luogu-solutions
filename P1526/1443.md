初步分析：

A国炸弹I可以炸到B国武器J的条件: (u[i]-x[j])2+(v[i]-y[j])2<=r2

结论：很难找到求最优解的多项式算法。

面对此类问题，一般只有搜索策略。

进一步分析：

每一颗炸弹必定炸掉B国武器中编号连续的一段。

5分钟只是表明每一颗炸弹可以炸掉任意多个编号连续的B国武器。

部分搜索：

此题使用部分搜索的算法需要一些转化：如果已经将B国武器根据编号分为x

段，[Si,Ti] (S1=1,Ti>=Si,Ti+1=Si+1)。然后判断是否可以从A国的N颗炸弹中选出x

颗，分别可以炸掉其中的一段。

其实我们把搜索分为了两部分，先通过搜索将B国武器根据编号分为x段，再

通过搜索判断是否可以从A国的N颗炸弹中选出x颗，分别可以炸掉其中的一段。

其实第二部分可以用匹配来解决。

C[T] 表示A国炸弹I是否可以炸到B国武器S,S+1..T-1,T。

C=((u-x)2+(v-y)2<=R2)

C[T]=C[T-1] & C[T][T] (S<T)

求C的时间复杂度为O(N3)。

建图：左边x个点，表示B国武器根据编号分为的x段，右边N个点，表示A国

的N颗炸弹。左边第i个点到右边第j个点有边的条件即：C[Si][Ti][j]。

搜索的任务就是将B国武器根据编号划分为若干段+二分图匹配判断。

性能分析（1）：

搜索的基本框架已经建立，虽然数据是随机生成的，但是M个B国武器的划分

方案还是非常多的，有时可能高达2m。时间上很难承受，如果使用卡时，正确性

受到影响，效果不会很好。

只有4个数据可以在时限内出解，另外6个如果卡时，有1个也可以得到最优

解。
优化：

优化可以通过可行性和最优性两方面分析。

优化一（最优性）：如果A国炸弹可以重复使用，设：

Dist[i]=炸掉B国武器i－m的最少使用炸弹数。可以用动态规划计算Dist值，

状态转移方程如下：

Dist[m+1]=0。

Dist[i]=Min(Dist[j]+1 | Can[i][j-1][k](1<=k<=n)) (1<=i<=n)

(i<j<=n+1)

求Dist的时间复杂度为O(N3)。

从而产生了一个最优性剪枝条件：

if 当前已经使用的炸弹数+Dist[当前已经炸掉的B国武器数+1]>=当前找到

的最优解 then 剪枝；

优化二（可行性）：

此搜索方法一般都可以用两个效果很好的可行性优化：

(1)提前判断是否可以匹配成功，避免多余的搜索。

(2)每次匹配可以从以前的匹配开始扩展，不需要重新开始。

如果当前的划分方法已经无法匹配成功，就没有搜索下去的必要了，只要每

搜索新的一段时立即通过匹配判断即可。

每次求匹配只要从原来的基础上扩展就可以了。

通过上述两个优化，程序的效率有了很大的提高。

性能分析（2）：

虽然通过上述两个优化，程序的效率较原来的搜索有了很大的提高。10个测

试数据中有8个可以在时限内出解，另外2个如果卡时，有1个也可以得到最优解。

进一步优化：

优化二虽然排除了许多不必要的划分，但是在判断时浪费了不少时间。

因此，在枚举划分长度时，可以通过以前的划分和匹配情况（被匹配的边），

用O(n2)的时间复杂度的宽度优先搜索计算出下一个划分的最大长度maxL，显然

下一个划分的长度在[1，maxL]都一定可以找到可行的匹配。这样既节省了判断

的时间，又可以使每次划分长度从长到短枚举，使程序尽快逼近最优解，同时增

强了剪枝条件一的效果。

这一部分的实现，首先需要求MaxT。

MaxT=炸弹i，从S开始炸，可以炸到的最大编号。

如果，炸弹i炸不到S，则MaxT=S-1。

求MaxT可以用动态规划的方法解决。状态转移方程为：

MaxT= 炸弹i炸不到S S-1

炸弹i炸得到S MaxT[i]

MaxT[i][m+1]=m

求MaxT的时间复杂度为O(N2)。

具体实现方法，考虑二分图右边的n个结点（n颗炸弹），如果结点i没有匹配，

i被认为可以使用。对于一个已经匹配的结点i，如果从任何一个没有匹配的结点

出发存在一条到达i，而且i为外点的交错路，i也被认为可以使用。

计算所有从没有匹配点出发的交错路（没有匹配点i出发的交错路没有被匹

配点i一定为外点）所能到达的匹配的结点，只要从每一个没有匹配的结点出发，

宽度优先搜索，只要O(N2)的时间。注意判断重复（如果一个已经匹配的结点已

经被确定为可以使用，那么不需要对它再扩展一次，因为当把这个已经匹配的结

点确定为可以使用的结点的时候，已经从这个结点扩展过，如果再扩展必将产生

无谓的重复）

所以MaxL=Max(MaxT | i可以使用)；

性能分析（3）：

通过以上的优化，所有数据都是瞬间出解，并且所有结果都是最优解。

甚至对n=200的随机数据，也可以在瞬间出解，可见程序的效率有了很大的

提高。

精益求精：

另外，还有两个优化，但是有时效果不好，但也值得一提。

优化三：分支定界。这样可以增强剪枝条件一的效果，但是当最优解与

Dist[1]相差比较远的时候，会浪费一定的时间。

优化四：优化一中Dist[i]的值有时并不是最优的，通过测试，发现如果

Dist[i]的值与最优值相差1，特别是当i 小的时候，程序的速度都会有明显的

影响。所以，可以通过同样的搜索来计算Dist[i]，（本题的答案就是Dist[1]）。

这样做可以增强剪枝条件一的效果，但是同时对每个i都要搜索也浪费了一定的

时间。

程序结果比较：

1 2 3 4 5 6 7 8 9 10

最简单的

搜索
0.00 0.00 0.50

Time
Over
0.65
Time
Over
Time
Over
Time
Over
Time
Over
Time
Over
优化的搜

索
0.01 0.01 0.10

Time
Over
0.50 0.80

Time
Over
0.55 0.30 0.80

进一步优

化的搜索

0.01 0.01 0.02 0.03 0.00 0.02 0.02 0.01 0.02 0.02

此搜索方法一般都可以用两个效果很好的可行性优化：

(1)提前判断是否可以成功，避免多余的搜索。

(2)每次判断尽量多利用以前的判断结果。

楼教主的论文原文

  
  
  
  
```cpp
var  
  i,ans,n,m,d:longint;  
  x1,x2,y1,y2:array[0..105] of longint;  
  can:array[0..105,0..105,0..105] of boolean;  
  res,from,reach,dis:array[0..105] of longint;  
  ta,tb,max:array[0..105,0..105] of longint;  
  v:Array[0..105] of boolean;  
  q:array[0..105*105] of longint;  
  g,bobo:array[0..105,0..105] of boolean;  
procedure init;  
var i,j,k,t:longint;  
begin  
  readln(m,n,d);  
  for i:=1 to m do readln(x1[i],y1[i]);  
  for i:=1 to n do readln(x2[i],y2[i]);  
  for i:=1 to m do  
   for j:=1 to n do  
    if d*d>=sqr(x1[i]-x2[j])+sqr(y1[i]-y2[j]) then  
     bobo[i,j]:=true;  
  for i:=1 to m do  
   begin  
     for j:=1 to n do can[i,i,j]:=bobo[i,j];  
     for j:=i+1 to m do  
      for k:=1 to n do  
       can[i,j,k]:=can[i,j-1,k]and bobo[j,k];  
     for j:=1 to n do  
     begin  
      max[i,j]:=i-1;  
      for k:=i to m do  
      if can[i,k,j] then max[i,j]:=k;  
     end;  
   end;  
  dis[m+1]:=0;  
  for i:=m downto 1 do  
   begin  
   t:=i-1;  
   for j:=1 to n do  
    if max[i,j]>t then t:=max[i,j];  
   dis[i]:=dis[t+1]+1;  
   end;  
end;  
function change(x:longint):boolean;//匹配下  
var i:longint;  
begin  
 for i:=1 to n do if (not v[i] )and(g[x,i])then  
 begin  
   v[i]:=true;  
   if (from[i]=0)or(change(from[i])) then  
   begin  
     from[i]:=x;  
     reach[x]:=i;  
     exit(true);  
   end;  
 end;  
 exit(false);  
end;  
procedure dfs(used,s:longint);  
var x,maxl,i,h,l:longint;  
begin  
  if  used+dis[s]>=ans then exit;  
  if s=m+1 then  
  begin  
   ans:=used;  
   res:=reach;  
  end;  
  fillchar(v,sizeof(v),false);  
  maxl:=s-1;  
  l:=0;  
  for i:=1 to n do if from[i]=0 then  
  begin  
   v[i]:=true;inc(l);q[l]:=i;  
  end;  
  h:=0;  
  repeat//bfs求出下一段最远到哪里  
    inc(h);  
    x:=q[h];  
    if max[s,x]>maxl then maxl:=max[s,x];  
    for i:=1 to used do  
     if (g[i,x]) and(v[reach[i]]=false) then  
     begin  
      inc(l);  
      q[l]:=reach[i];  
      v[q[l]]:=true;  
     end;  
  until h>=l;  
  if maxl=s-1 then exit;  
  inc(used);  
  ta[s]:=reach;tb[s]:=from;  
  g[used]:=can[s,maxl];  
  fillchar(v,sizeof(v),false);  
  change(used);  
  for i:=maxl downto s do  
  begin  
   g[used]:=can[s,i];  
   dfs(used,i+1);  
  end;  
  reach:=ta[s];from:=tb[s];  
end;  
begin  
 init;  
 ans:=35535;  
 dfs(0,1);  
 writeln(ans);  
 //for i:=1 to ans do  write(res[i],' ');  
end.
```