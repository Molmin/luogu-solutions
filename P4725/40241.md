多项式 Pascal 题解·第四弹

我们来推一波式子：

$$ B(x) \equiv ln(A(x)) $$

$ ln(x) $ 的导数是 $ \frac{1}{x} $，似乎很好处理，我们可以先求导再积分求回来：

$$ B'(x) \equiv ln'(A(x)) A'(x) $$

$$ B'(x) \equiv \frac{1}{A(x)} A'(x) $$

$$ B'(x) \equiv \frac{A'(x)}{A(x)} $$

诶，这里好像出现了多项式除法，我不会怎么办！

然而问题不大，这里只要求余数即可，只需要多项式求逆即可，~~不像多项式除法一样需要翻转。~~

不过由于求导再积分的时候会损失函数的一些信息，我们再看题目：$ a_0 = 1 $，那么结果里面的 $ a_0 $ 也必为 $ 1 $。这个比较显然，可以自己推一下。

所以就好了！~~再次把多项式求逆的板子粘贴一下~~关于多项式求逆，可以看[窝的这篇题解](/blog/dblark/solution-p4238)


```pas
type
  arr = array[0..524287] of longint; //有很多定义都在窝多项式除法的那篇题解里解释过啦，窝在这里就不重复了，可以去看那篇题解（多项式求逆部分代码是完全一样的）
const
  p  = 998244353;
  g  = 3;
  gi = 332748118;
var
  a, b, pow, r: arr;
  n, i: longint;

  procedure swap(var x, y: longint); //日常
  var
    t: longint;
  begin
    t := x;
    x := y;
    y := t;
  end;

  function power(x, y: longint): longint; //日常
  begin
    power := 1;
    while y > 0 do
    begin
      if odd(y) then
        power := int64(power) * x mod p;
      x := sqr(int64(x)) mod p;
      y := y shr 1;
    end;
  end;

  function inv(x: longint): longint; //日常
  begin
    exit(power(x, p - 2));
  end;

  function dev(a: arr; n: longint): arr; //求导
  var
    i: longint;
  begin
    for i := 1 to n do
      a[i - 1] := int64(a[i]) * i mod p;
    a[n] := 0;
    exit(a);
  end;

  function int(a: arr; n: longint): arr; //积分
  var
    i: longint;
  begin
    for i := n + 1 downto 1 do
      a[i] := int64(a[i - 1]) * inv(i) mod p;
    a[0] := 0;
    exit(a);
  end;

  procedure ntt(var a: arr; n, s: longint); //日常
  var
    i, j, k, x, y, w: longint;
  begin
    for i := 0 to n - 1 do
      if i < r[i] then
        swap(a[i], a[r[i]]);
    i := 1;
    while i < n do
    begin
      j := 0;
      if s = 1 then
        w := power(g, (p - 1) div (i shl 1))
      else
        w := power(gi, (p - 1) div (i shl 1));
      pow[0] := 1;
      for k := 1 to i - 1 do
        pow[k] := int64(pow[k - 1]) * w mod p;
      while j < n do
      begin
        for k := 0 to i - 1 do
        begin
          x := a[j + k];
          y := int64(a[i + j + k]) * pow[k] mod p;
          a[j + k] := (x + y) mod p;
          a[i + j + k] := (x - y) mod p;
        end;
        Inc(j, i shl 1);
      end;
      i := i shl 1;
    end;
    if s = -1 then
    begin
      x := inv(n);
      for i := 0 to n - 1 do
        a[i] := (int64(a[i]) * x mod p + p) mod p;
    end;
  end;

  function mul(a, b: arr; n, m: longint): arr; //多项式乘法
  var
    i, l, num: longint;
  begin
    num := 1;
    l := 0;
    while num <= n + m do
    begin
      num := num shl 1;
      Inc(l);
    end;
    for i := 0 to num - 1 do
      r[i] := (r[i shr 1] shr 1) or ((i and 1) shl (l - 1));
    ntt(a, num, 1);
    ntt(b, num, 1);
    for i := 0 to num - 1 do
      mul[i] := int64(a[i]) * b[i] mod p;
    ntt(mul, num, -1);
  end;

  function inv(x: arr; n: longint): arr; //多项式求逆
  var
    t: arr;
    i, l, num: longint;
  begin
    num := 2;
    l := 1;
    inv[0] := inv(x[0]);
    while num <= n shl 1 do
    begin
      num := num shl 1;
      Inc(l);
      for i := 0 to num - 1 do
        r[i] := (r[i shr 1] shr 1) or ((i and 1) shl (l - 1));
      for i := 0 to num shr 1 - 1 do
        t[i] := x[i];
      ntt(inv, num, 1);
      ntt(t, num, 1);
      for i := 0 to num - 1 do
        inv[i] := int64((p + 2 - int64(inv[i]) * t[i] mod p) mod p) * inv[i] mod p;
      ntt(inv, num, -1);
      for i := num shr 1 to num - 1 do
        inv[i] := 0;
    end;
    for i := n + 1 to num shr 1 - 1 do
      inv[i] := 0;
  end;

  function ln(a: arr; n: longint): arr; //多项式对数函数，这才是重点
  var
    i: longint;
  begin
    ln := mul(dev(a, n), inv(a, n), n - 1, n); //A'(i) * (1 / A(i))
    for i := n to 2 * n do //清零数组（误差）
      ln[i] := 0;
    ln := int(ln, n - 1);  //积分回去
  end;                     //然后……就没了

begin                      //主程序，非常简单
  Read(n);
  for i := 0 to n - 1 do
    Read(a[i]);
  b := ln(a, n - 1);
  for i := 0 to n - 1 do
    Write(b[i], ' ');
  writeln;
end.
```