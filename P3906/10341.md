感谢逝星DS的指正

今天早上刷图论，正好做到这道题，来写一波题解。。。

刚看题目，看到是最短路，有多组询问，显然是多源最短路。Floyd！！！

又想到了NOIP2012文化之旅的Floyd写法，突然有了灵感，一把AC了。。。

让我们来讲一讲解法：

首先，题目让我们输出的是从u到v的最短路集合，所以Floyd需要另开一个g数组，数组开三维，用Bool类型储存，记录I（u，v）。

让我们再次定义一下变量：

**f[i][j]表示从i到j的最短路径。**

**g[i][j][k]表示从i到j的最短路集合，g[i][j][k]=true表示k属于这个集合中,g[i][j][k]=false表示k不属于这个集合。**

现在开始循环：-------->

**当f[i][j]>f[i][k]+f[k][j]时，说明g数组需要被完全重新更改（因为存在更优解）枚举t从1到n，g[i][j][t]=g[i][k][t] or （|） g[j][k][t]（之要两个集合中任意一个存在t，那么g[i][j]集合也存在t）。**

当f[i][j]<f[i][k]+f[k][j]时，不用更改（~~这岂不是废话~~）。

**当f[i][j]=f[i][k]+f[k][j]时，说明g数组需要在原来的基础上新加入两个集合（因为原来的集合和现在的集合都属于当前的最短路集合），那么转移方程很容易出来了，g[i][j][t]=g[i][j][t] or g[i][k][t] or g[k][j][t]（~~
显然~~）。**

时间复杂度：O（n^4）

运行时间：4ms

上Pascal代码：（~~好像忘记跟你们说了我是P党~~）：

```cpp

// luogu-judger-enable-o2//O2优化。。。
var
  f:array[0..50,0..50]of int64;//f[i][j]表示从i到j的最短路径。
  g:array[0..50,0..50,0..50]of boolean;//g[i][j][k]表示从i到j的最短路集合，g[i][j][k]=true表示k属于这个集合中,g[i][j][k]=false表示k不属于这个集合。
  n,m,i,j,k,t,q,x,y:longint;
  inf:int64;
begin
  readln(n,m);
  inf:=100000000007;
  for i:=0 to n do
    for j:=0 to n do
      f[i][j]:=inf;//赋初值
  for i:=1 to m do
  begin
    readln(x,y);
    f[x][y]:=1;//更新
    f[y][x]:=1;//更新
    g[x][y][x]:=true;//在x到y的路径集合中一定存在x和y，下同
    g[x][y][y]:=true;
    g[y][x][x]:=true;
    g[y][x][y]:=true;
  end;
  for k:=1 to n do//Floyd必须先枚举中转站
    for i:=1 to n do
      for j:=1 to n do
        if (i<>j) and (j<>k) and (i<>k) then//我好想还是不清楚为什么要写这句话，求大佬的解释。
        begin
          if f[i][k]+f[k][j]<f[i][j] then//完全更新
          begin
            f[i][j]:=f[i][k]+f[k][j];//更新最短路
            for t:=1 to n do//枚举t
              g[i][j][t]:=g[i][k][t] or g[k][j][t];
          end else
          if f[i][k]+f[k][j]=f[i][j] then//出现更多的最短路
          begin
            for t:=1 to n do//枚举t
              g[i][j][t]:=g[i][j][t] or g[i][k][t] or g[k][j][t];
          end;
        end;
  readln(q);
  for i:=1 to q do
  begin
    readln(x,y);
    for j:=1 to n do
      if g[x][y][j] then//直接输出该集合
        write(j,' ');
    writeln;
  end;
end.
```