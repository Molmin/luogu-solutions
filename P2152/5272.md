好吧，这道题用了一种很掉节操的方法进行查错

说一下这道题要注意的几个地方：

1.一定要用数位压缩

2.辗转相减

3.使用函数和过程注意局部变量与全局变量（ToT）

4.字符串读入比字符读入快


```delphi

program p2152;
type
  arr=array[0..2000]of longint;
var
  s:ansistring;
  i,t,two:longint;
    a,b:arr;
procedure div2(var a:arr);
var i:longint;
begin
  for i:=a[0] downto 1 do begin
      if a[i]mod 2=1 then inc(a[i-1],1000000);
      a[i]:=a[i]shr 1;
    end;
  for i:=a[0]downto 1 do
      if a[i]<>0 then break;
  a[0]:=i;
end;
procedure minus(var a:arr;b:arr);
var i:longint;
begin
  for i:=1 to b[0] do begin
      a[i]:=a[i]-b[i];
    if a[i]<0 then begin
      inc(a[i],1000000);
          dec(a[i+1]);
    end;
  end;
  for i:=a[0]downto 1 do
      if a[i]<>0 then break;a[0]:=i;
end;
function compare:integer;
var i:longint;
begin
  if a[0]>b[0] then exit(1);
    if a[0]<b[0] then exit(-1);
    for i:=a[0] downto 1 do begin
      if a[i]>b[i] then exit(1);
        if a[i]<b[i] then exit(-1);
    end;
    exit(0);
end;
procedure change(var a:arr);
var p,l,i,t:longint;
begin
  l:=length(s);
    p:=l div 6;
    if l mod 6<>0 then inc(p);
    fillchar(a,sizeof(a),0);
    for i:=l downto 1 do begin
      t:=ord(s[i])-ord('0');
        case (l-i)mod 6 of
          0:inc(a[(l-i)div 6+1],t);
          1:inc(a[(l-i)div 6+1],t*10);
          2:inc(a[(l-i)div 6+1],t*100);
            3:inc(a[(l-i)div 6+1],t*1000);
            4:inc(a[(l-i)div 6+1],t*10000);
            5:inc(a[(l-i)div 6+1],t*100000);
        end;
  end;
  a[0]:=p;
end;
procedure double;
var i,g:longint;
begin
  g:=0;
  for i:=1 to a[0] do begin
        a[i]:=a[i]*2+g;
    g:=a[i]div 1000000;
    a[i]:=a[i]mod 1000000;
  end;
    if g<>0 then begin inc(a[0]);a[a[0]]:=g;end;
end;
procedure print(a:arr);
var i:longint;
begin
  write(a[a[0]]);
    for i:=a[0]-1 downto 1 do begin
      if a[i]<100000 then write('0');
        if a[i]<10000 then write('0');
        if a[i]<1000 then write('0');
        if a[i]<100 then write('0');
        if a[i]<10 then write('0');
        write(a[i]);
  end;writeln;
end;
begin
    readln(s);
  change(a);
    readln(s);
    change(b);
    two:=0;
  while not(odd(a[1])or odd(b[1])) do begin
      div2(a);div2(b);inc(two);
  end;
    while true do begin
    while not odd(a[1]) do div2(a);
        while not odd(b[1]) do div2(b);
    t:=compare;
    if t=0 then break;
        if t=1 then minus(a,b) else minus(b,a);
    end;
  for i:=1 to two do double;
    print(a);
end.

```