我是~~P党~~的。。。

这道题的数据范围很小，好多方法都能AC，如暴力地进行DP，但这道题的正解应该是贪心。

①枚举小V（VIP999）在前i个湖钓鱼（每个湖不一定需要钓鱼）。

②我们记录一下当前还剩多少时间来钓鱼（由于时间都是5的倍数，我们可以都对时间/5后计算）。

③对于我们枚举的每一个方案，易得小V不会走回头路（否则不是最优解），让时间减去从第一个湖到最后一个湖所花费的时间，这样小V现在在前i个湖间移动就不需要时间了（已经提前花了）。

④然后就变成简单的堆操作啦！！！首先将前i个湖初始的鱼数丢入堆中，取出并弹出堆头，将下一次还能钓到的鱼数丢入堆中，直至堆为空或时间到了停止。

//你可以~~忽略下一部分~~

然而我用的不是堆。。。

我用了左偏树，左偏树可以代替所有的堆操作，还可以合并堆；左偏树只有一个操作，那就是合并！！！

删除操作：将该点左右儿子合并

插入操作：将该点与大的数合并

详细操作请学习左偏树P3377。

上代码

```pascal

```uses math;//数学库
type ZAI=record//节点信息
  l,r,val,dep:longint;
end;
var
  p:array[0..1005]of ZAI;
  fa,f,d,t:array[0..1005]of longint;
  n,i,j,realh,h,id,gone,now:longint;
  ans,nowans:int64; 
  kong:ZAI;
function father(x:longint):longint;//并查集
begin
  if fa[x]=0 then
   exit(x);
  exit(father(fa[x]));
end;
function merge(x,y:longint):longint;//合并操作
var
  t:longint;
begin
  if (x=0) or (y=0) then
    exit(x+y);
  if p[x].val<p[y].val then//维护堆顶元素最大
  begin
    t:=x;
    x:=y;
    y:=t;
  end;
  p[x].r:=merge(p[x].r,y);//将y与x右子树合并
  fa[p[x].r]:=x;
  if p[p[x].l].dep<p[p[x].r].dep then
  begin
    t:=p[x].l;
    p[x].l:=p[x].r;
    p[x].r:=t;
  end;
  p[x].dep:=p[p[x].r].dep+1;//更新距离
  exit(x);
end;
procedure pop(x:longint);//删除操作
begin
  p[x].val:=-1;
  fa[p[x].l]:=0;
  fa[p[x].r]:=0;//将儿子节点的父亲置为0
  id:=merge(p[x].l,p[x].r);//合并！！！
end;
procedure ins(x,y:longint);//插入操作
begin
  fa[x]:=0;
  p[x].val:=y;
  p[x].l:=0;
  p[x].r:=0;
  p[x].dep:=0;//新建一个点，可以直接用刚删去的那个点
  id:=merge(id,x);//合并！！！
end;
begin
  readln(n);
  readln(realh);
  realh:=realh*12;
  for i:=1 to n do
    read(f[i]);
  readln;
  for i:=1 to n do
    read(d[i]);
  readln;
  for i:=1 to n-1 do
    read(t[i]);
  readln;//读入
  for i:=1 to n do//枚举小V（VIP999）在前i个湖钓鱼
  begin
    nowans:=0;
    h:=realh;
    if h<=0 then//若没时间钓鱼了，则结束
      break;
    p[0].dep:=-1;
    for j:=1 to i do//置空
    begin
      fa[j]:=0;
      p[j]:=kong;
    end;
    id:=0;
    for j:=1 to i do
    begin
      p[j].val:=f[j];
      if j=1 then
        id:=1 else
        id:=merge(id,j);
    end;
    while (id<>0) and (h>0) do//开始计算
    begin
      dec(h);
      nowans:=nowans+p[id].val;//堆顶元素
      gone:=id;//记录
      now:=p[id].val;//记录
      pop(id);//弹出堆顶元素
      if now-d[gone]>0 then
        ins(gone,now-d[gone]);//新建节点
    end;
    ans:=max(ans,nowans);//取max
    realh:=realh-t[i];//减去所花时间
    if realh<=0 then
      break;
  end;
  writeln(ans);//输出
end.
```pascal

```