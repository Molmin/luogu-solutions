如楼下各位网友所说，采用贪心算法（如有漏洞请指出）：

1. 输入数据是杂乱的，先进行排序，n＜10000，所以用快速排序。

2. 尽量使当前留下的线段的尾坐标最小。

3. 对于一条线段，有三种情况

上AC代码：

```cpp
var
  n,i,x,t,s:longint;
  a:array[0..10000,1..2]of longint;
procedure qsort(l,r:longint);
var
  i,j:longint;
  mid:array[1..2]of longint;
begin
  i:=l; j:=r; mid:=a[(l+r) shr 1];
  repeat
    while (a[i,1]<mid[1]) or ((a[i,1]=mid[1]) and (a[i,2]<mid[2])) do inc(i);
    while (a[j,1]>mid[1]) or ((a[j,1]=mid[1]) and (a[j,2]>mid[2])) do dec(j);
    if i<=j then 
      begin
        a[0]:=a[i]; a[i]:=a[j]; a[j]:=a[0];
        inc(i); dec(j);
      end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;
begin
  readln(n);
  for i:=1 to n do 
    begin
      readln(a[i,1],a[i,2]);
      if a[i,1]>a[i,2] then 
        begin
          t:=a[i,1]; a[i,1]:=a[i,2]; a[i,2]:=t;
        end;
    end;
  qsort(1,n);
  x:=a[1,2]; s:=0;
  for i:=2 to n do 
    if a[i,2]<x then //整条包含（尾坐标比当前尾坐标小）
      begin
        x:=a[i,2]; s:=s+1;
      end
    else
      if a[i,1]<x then //部分包含（头坐标比当前尾坐标小，尾坐标比当前尾坐标大或等于）
        s:=s+1
      else //不包含
        x:=a[i,2];
  write(n-s);
end.
```
程序中贪心部分的三种情况分别是：
1. 整条线段都包含在前面留下的线段中，那么可以删去i-1线段或i线段，那么前面说了，尽量使留下线段的尾坐标小，所以删掉尾坐标大的那条——i-1线段，留下部分尾坐标变为i线段的尾坐标。

2. 部分包含在前面留下的线段中，那么“尽量使留下线段的尾坐标小”，还是删去“尾坐标大的那条”——i线段。

3. 不包含在前面留下的线段中，那么留下，留下部分尾坐标变为i线段的尾坐标。

代码不太简洁，神犇勿喷！
