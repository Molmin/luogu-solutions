**P党的福利**

【算法分析】

当n=1时

显而易见，答案为0.

当n>1时

我们将这个正方形分为三部分，

显而易见，一定有3个点（1，0）（1，1）（0，1）满足条件。

我们把这三个点所在的直线先放在一旁，

我们通过观察可以发现，

从第三行开始，

**第i行中当前直角三角形范围内能观察到的点数等于(i-1)的欧拉函数值**  //**核心思想**

将得出的答案\*2+3即是最终答案。

当然，我们还需要一些欧拉函数（我们假定n的欧拉函数值为phi[n]）的性质：

1、当n为质数时, phi[n]=n-1。

2、若m,n互质，phi[m\*n]=phi[m]\*phi[n]。

-----------------------分割线-----------------------

【代码】


```cpp
    var n,i,j,sp,ans:longint;
    p:array[1..40000] of boolean;
    f,phi:array[1..40000] of longint;
begin
  read(n);
  if n=1 then             //特判
   begin
    writeln(0);
    halt;
   end;
  for i:=2 to n-1 do
   begin
    if not p[i] then
     begin
      inc(sp);
      f[sp]:=i;
      phi[i]:=i-1;                 //当n为质数时, phi[n]=n-1。
     end;
    for j:=1 to sp do
     if f[j]*i<=n then
      begin
       p[f[j]*i]:=true;
       if i mod f[j]=0 then
        begin
         phi[f[j]*i]:=f[j]*phi[i];           //积性函数性质：若m,n互质，phi[m*n]=phi[m]*phi[n]。
         break;                                //防止重复筛
        end
       else
        phi[f[j]*i]:=(f[j]-1)*phi[i];     //同理
      end
     else
      break;
   end;
  for i:=2 to n-1 do      //累加答案
   ans:=ans+phi[i];
  writeln(ans*2+3);
end.
```
P.S. 代码风格较丑，望谅解。