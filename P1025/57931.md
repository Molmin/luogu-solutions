递归算法：

本程序用递归的方法进行计算，首先建立递推关系式：将一整数num按题意分成k个整数之和的分法个数M满足如下关系：（简写为M(num,k,large)，其中large为将num分成k分之后的最大整数，在这里large=num-k+1）

①当large=1，num=k时，M(num,k,large)=1。

②当large=1，num≠k时，M(num,k,large)=0。

③当k=1，num<=large时，M(num,k,large)=1。

④当k=1，num>k时，M(num,k,large)=0。

⑤当k>1，large>1时，M(num,k,large)=M(num-large,k-1,MAX(num-k-large+2,large))+M(num,k,large-1)。

优化：用递推关系式递归可以进行大幅度优化，而计数法递归却做不到这一点。在本题中当k值较大时(如k>5)，有些M值被计算上千遍甚至上万遍，如果每个需要计算的M值只需算一遍，那么其速度会成倍提高。建立数组存储是最好的方法，但由于内存有限，本程序只建立了部分M值的数组。尽管如此，优化后程序的效率最大可为原先的960倍(num=200，k=6)。

程序如下：

```cpp
const
  mn=200;mk=6;ml=110;sk=2;   { mn：数组中num的上限，ml：数组中large的上限}
  opendata=true;     {opendata：优化开关(true为打开)，mk：数组中k的上限，sk：数组中k的下限}
type
  data=array[1..mk,1..ml] of longint;   {建立数组}
var
  n,m:integer;
  da:array[1..mn] of ^data;           {建立数组}
function work(num,k,large:integer):longint;               {递归函数}
var
  temp:longint;
  max:integer;
  f:boolean;
begin
  f:=(num<=mn) and (num>0) and (k<=mk) and (k>=sk-1) and (large<=ml) and (large>0) and opendata;
```
{判断num，k，large的值是否在存储范围之内和是否打开优化功能}
if f and (da[num]^[k][large]>-1) then                              {判断该值是否已经计算过}


```cpp
    temp:=da[num]^[k][large]
  else                                                 {以下是算法的核心部分}
    if large<1 then temp:=0
    else
      if large=1 then
        if num=k then temp:=1 else temp:=0
      else
        if k=1 then
          if large>=num then temp:=1 else temp:=0
        else begin
          max:=num-k-large+2;
          if max>large then max:=large;
          temp:=work(num-large,k-1,max)+work(num,k,large-1);
        end;
  work:=temp;
  if f then da[num]^[k][large]:=temp;                       {将算好的值储存}
end;
begin                                                 {主程序}
  if opendata then                                       {判断是否打开优化功能}
    for n:=1 to mn do begin
      new(da[n]);                                      {数组初始化}
      FillChar(da[n]^, SizeOf(da[n]^), 255);                 {将每一个数初始化为-1}
    end;
  readln(n,m);
  writeln(work(n,m,n-m+1));                              {计算并打印}
end.
```
总结：该程序是单纯的递归+优化，结构简单。这种优化方法适用于每一个以递推关系式为基础递归，其效果十分明显，且容易编写，但是所需内存巨大，甚至超过了动态规划，并且在空间上无法优化。如果内存足够大，优化后的递归计算次数要小与动态规划，但由于递归本身的效率不高，如果本题中的num，k值不是非常大，动态规划的效率仍然大于优化后的递归。

动态规划算法：

设N(num,k,large)为将一整数num按题意分成最大数为large的k个整数之和的分法个数，由刚才的递推

关系式⑤可得M(num,k,large)=N(num,k,large)+M(num,k,large-1)=∑N(num,k,i)  (1≦i≦large)，N(num,k,large)=M(num-large,k-1,MAX(num-k-large+2,large))。

不难看出M(num,k,large)=∑M(num-i,k-1,MAX(num-k-i+2,i))  (1≦i≦large)

所以由M(1~num,k,1~large)即可算出M(1~num,k+1,1~large)。

但是这种算法的效率并不是最高的，为了进一步减少运算量，可以把对最大数(large)的讨论改为对最小数(small)的讨论，因为把num分为k份的k个整数中最大数的范围是num div k +1 ~ num-k+1，最小数的范围是1 ~ num div k，当num不变时，随着k的增大，最小数的范围将远小于最大数。这样可以进一步减少运算量。

设S(num,k,small)为将一整数num按题意分成最小数不小于small的k个整数之和的分法个数。

设T(num,k,small)为将一整数num按题意分成最小数为small的k个整数之和的分法个数。

S(num,k,small)=T(num,k,small)+S(num,k,small+1)= ∑T(num,k,i)  (small≦i≦num div k ，1≦small≦num div k) ，T(num,k,small)=S(num-small,k-1,small)

S(num,k,small)= ∑S(num-i,k-1,i)  (small≦i≦num div k)

将一整数num按题意分成k个整数之和的分法个数为S(num,k,1)

程序如下：

```cpp
const
  max=200;                            {max：num的最大值}
type
  long200=array[1..max] of longint;         {建立数组}
var
  data:array[1..2,1..max] of ^long200;        {建立数组data[num,k]^[small]};
  a,b,c,d,e,f,g,h,i,j,k,n:integer;              {n：num  k：k}
begin
  readln(n,k);
if (n<=max) and (k<=n) then               {判断输入数据是否正确}
begin
  for a:=1 to 2 do
    for b:=1 to 200 do
      new(data[a][b]);                   {数组初始化}
  for a:=1 to 200 do
    for b:=1 to a do
      data[1][a]^[b]:=1;               {S(a,1,b)=1 ,(b≦a)}
  i:=1;j:=2;         {以下是算法的核心部分，在计算时两个数组将交替使用，用i，j的交换来实现}
  for a:=2 to k do                    {计算S(1~num,2,1~small) ~ S(1~num,k,1~small)}
  begin
  i:=(a mod 2)+1;                                     {交换i，j }
    j:=((a+1) mod 2)+1;
    for b:=a to n do
    begin
      h:=b div a;                            {h：把b分成a份后的最小数的最大值}
      for c:=1 to h do 
        data[j][b]^[c]:=data[i][b-c]^[c];                  {计算T(b,a,c)}
      for c:=h-1 downto 1 do
        data[j][b]^[c]:=data[j][b]^[c]+data[j][b]^[c+1];     {计算S(b,a,c)}
    end;
  end;
  writeln(data[j][n]^[1]);      {输出结果}
end;
  writeln;
end.
```