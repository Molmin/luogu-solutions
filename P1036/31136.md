思路：深度搜索。

解释：深搜dfs函数中，sum指当前选的数的和，step指上一步选的数为a[step]，num指已经选了num个数。

出口：当num=k时，已经选中k个数，判断sum的值，并退出函数。

注意：此处不需要控制step>n的情况，因为在后面for循环中i的取值会控制。

函数后部的for循环穷举下一个选中的数，并每一次都进行下一级的搜索。

注意：

1）为了使最后的值不重复，需要将a[]数组排序（这里用快排纯粹是吃饱了撑的，其实就20个数...）

2）在判断是否为质数时，检验因数中i只需要试到trunc(sqrt(X))

3）切记，dfs函数的出口中，无论sum的值是否满足，都要退出，不能写成




 

 
 
```cpp
  if num=k then if prime(num) then begin inc(ans);exit;end;.
var
 a:array[1..20] of longint;
 n,k,ans,i:longint;
procedure sort(l,r:longint);
var
 i,j,x,t:longint;
begin
 i:=l;
 j:=r;
 x:=a[(l+r) div 2];
 repeat
  while a[i]<x do inc(i);
  while x<a[j] do dec(j);
  if i<=j then begin
   t:=a[i];a[i]:=a[j];a[j]:=t;
   inc(i);
   dec(j);
  end;
 until i>j;
 if l<j then sort(l,j);
 if i<r then sort(i,r);
end;
function prime(x:longint):boolean;
var
 i:longint;
begin
 for i:=2 to trunc(sqrt(x)) do if x mod i=0 then exit(false);
 exit(true);
end;
procedure dfs(sum,step,num:longint);
var
 i:longint;
begin
 if num=k then begin
  if prime(sum) then inc(ans);
  exit;
 end;
 for i:=step+1 to n-(k-num)+1 do dfs(sum+a[i],i,num+1);
end;
begin
 readln(n,k);
 for i:=1 to n do read(a[i]);
 ans:=0;
 sort(1,n);
 dfs(0,0,0);
 write(ans);
end.
```