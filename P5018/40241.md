考场上，为了写T3牺牲了很多时间……

最后10min，看了一眼T4，直觉告诉我正解是$O(n)$或$O(nlogn)$，但明显来不及了，随手打个暴力好了。

5min写完。测一下样例。诶，大样例过了？？？

然后就水过去了（听说今年的评测姬还跑得特别快）。

献上Pascal代码：

```
{P5018}
var
  a,l,r:array[1..1000000]of longint;
  n,i,x,max:longint;
function dfs(x,y:longint):longint;//比较两颗子树是否对称并累计节点数量
var
  t:longint;
begin
  if (a[x]<>a[y]) or ((l[x]>0)<>(r[y]>0)) or ((l[y]>0)<>(r[x]>0)) then//如果两颗子树的根的权值不一样或者结构不一样
    exit(0);                   //不对，退出
  dfs:=2;
  if l[x]>0 then               //如果有左子树的左子树的话（也就是有右子树的右子树）
  begin
    t:=dfs(l[x],r[y]);         //比较左子树的左子树和右子树的右子树
    if t=0 then                //如果不对
      exit(0);                 //不对，退出
    inc(dfs,t);                //累计节点数量
  end;
  if l[y]>0 then               //基本同上
  begin
    t:=dfs(l[y],r[x]);         //比较左子树的右子树和右子树的左子树
    if t=0 then
      exit(0);
    inc(dfs,t);
  end;
end;
function f(x:longint):longint; //计算以x为根的对称二叉树的节点数量
begin
  if (l[x]=-1) and (r[x]=-1) then//如果是叶子节点
    exit(1)                    //不用搜了，反正就一个根节点
  else
    if (l[x]=-1) or (r[x]=-1) then//如果有左子树没右子树或者有右子树没左子树
      exit(0)                  //不对称，返回0
    else
      exit(dfs(l[x],r[x])+1);  //比较左子树和右子树，再加上根节点（如果不对称就返回1，不过并没有什么关系）
end;
begin
  read(n);
  for i:=1 to n do             //读入
    read(a[i]);
  for i:=1 to n do
    read(l[i],r[i]);
  max:=0;
  for i:=1 to n do             //枚举对称二叉树的根节点
  begin
    x:=f(i);
    if x>max then
      max:=x;
  end;
  writeln(max);
end.
```