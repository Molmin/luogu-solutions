# 暴力出奇迹！你信么

#### 考场上看到这题，我脑子：@#￥%……&￥**&

默默啃了一口点心面包，开始看着题目发愣......

然后突然发现：按照题意模拟每个根节点并不难。

#### ~~像我这种大蒟蒻，T4反正不可能A的，随便写个暴力算了吧~~

思路瞬间清晰，于是开码（……）

15分钟后……打完了

测了前两个样例，调试了5分钟，过了。

接着怀着忐忑不安的心情打开第三个样例……

？？？%￥#@#！！！这么大的数据，肯定凉凉了555。于是怀着必死的信念开始测试……

1秒后……发觉自己的程序竟然秒过？？？！！什么神操作？

#### 我开始怀疑 ~~CCF是不是把比赛用机偷偷换成了神威太湖之光微型版~~

然后瞟了一眼输出，就是那个神奇的 
# 7

### 我差点开心得笑出声来，不过这还没完

今天一测发觉自己竟然A了，最慢的点只跑了0.2s？？啧啧，神奇啊

发片题解庆祝一下

----------------------
我的思路：

题意无非就是要找一个点，使得以这个点为根节点的二叉树为**对称二叉树**并且该二叉树结点总数最多。

### 暴力枚举每一个点，判断以其作为根节点能否形成对称二叉树，如果可以则更新最优值。

当然，暴力也要有方法，绝不能死做。

由题意可知：

1.只有一个根节点的树为对称二叉树

2.只有一个左（右）儿子的二叉树不是对称二叉树

定义check(k)用于判断以k号结点作为根是否能形成对称二叉树。

则我们可以先把上面两条特判写进去。

如果不满足以上两点，那么开始dfs暴搜。

我们可以把一棵二叉树分成左子树和右子树分别往下深搜。

定义dfs(x,y)为当前搜到了左子树的x号结点和右子树的y号结点

仔细观察对称二叉树性质可知：

1.首先这棵二叉树的左右结构必须对称，如下图：（图虽丑，看懂就行）
![](https://s1.ax1x.com/2018/11/11/iqWmVS.png)

  也就是说，对于该二叉树左子树的任意结点x和右子树的任意结点y，如果x有左儿子，那么y必须有右儿子；反之，如果x有右儿子，那么y必须有左儿子。或者他们都没有儿子。
  
  如果不满足上述条件，终止搜索并返回false。否则继续往下搜，如果x结点往左跑，那么y结点往右跑；反之亦然。
  
  按照上述规则往下搜，可以判断该二叉树的结构是否对称。
  
  2.其次就是权值也必须对称，这个很简单，每次搜到两边的各一个点，只需判断x和y的权值是否相等即可（理由：根据性质1的搜索原则，每次搜到的两个结点必定是分别位于左右子树的两个对称点），如果不相等，该二叉树不对称，可直接终止搜索，相等则继续。
  
主程序怎么写？很简单，开一个队列，从根节点开始依次扫描，把有的儿子放入队列，一个个判断过来即可。
  
综上所述，一个华丽丽的暴力程序就显露雏形啦。

函数check部分：

```pascal
  function check(k:longint):boolean;  //判断以k结点为根节点是否能形成对称二叉树
  begin
    s:=1; f:=true;  //计数器初始为1，并默认当前可形成对称二叉树
    if (a[k,1]=-1)and(a[k,2]=-1) then exit(true);  //如果当前根节点无儿子则返回真
    if (a[k,1]<>-1)and(a[k,2]<>-1) then  //如果左右儿子都有
    begin
      dfs(a[k,1],a[k,2]); exit(f);  //开始深搜，并返回最终结果
    end else exit(false);  //如果只有一个儿子，返回假
  end;
```

函数dfs部分：

```pascal
  procedure dfs(x,y:longint);  //搜到了左子树的x号结点和右子树的y号结点
  begin
    if f=false then exit;  //如果已经不是对称二叉树，跳出
    if v[x]<>v[y] then begin f:=false; exit; end;  //如果对应的结点权值不相等，跳出
    inc(s,2);  //计数器记得加2（两遍各一个）
    if a[x,1]<>-1 then  //如果x有左儿子
    begin
      if a[y,2]<>-1 then dfs(a[x,1],a[y,2])  //如果y有右儿子，搜
      else begin f:=false; exit; end;  //y没有对应的右儿子，跳出
    end
    else if a[y,2]<>-1 then begin f:=false; exit; end;  //x没有左儿子但是y有右儿子，跳出
    if a[x,2]<>-1 then  //同上
    begin
      if a[y,1]<>-1 then dfs(a[x,2],a[y,1])
      else begin f:=false; exit; end;
    end
    else if a[y,1]<>-1 then begin f:=false; exit; end;
  end;
```

主程序：
```pascal
  readln(n);
  for i:=1 to n do read(v[i]);  //读入权值
  for i:=1 to n do
    readln(a[i,1],a[i,2]);  //读入左右儿子
  t:=1; w:=1; sq[1]:=1;  //队列初始化，根节点入队
  while t<=w do
  begin
    if check(sq[t]) then  //如果以当前根节点能构成对称二叉树
    begin
      if s>ans then ans:=s;  //更新结点数最大值
    end;
    if a[sq[t],1]<>-1 then begin inc(w); sq[w]:=a[sq[t],1]; end;  //如果头结点有左儿子，进队列
    if a[sq[t],2]<>-1 then begin inc(w); sq[w]:=a[sq[t],2]; end;  //如果头结点有右儿子，进队列
    inc(t);
  end;
  writeln(ans);
```

至此，暴力码完。

注意到有四个测试点输入数据为满二叉树，则我们可以对以上算法进行优化。

由于满二叉树独有的性质，越是靠上层的结点，它为根节点的树中总结点数越大。

因此从根节点开始扫，只要找到满足题意的，即可直接输出答案。

优化后的完整代码（别怪我是P党）：
```pascal
program tree;
var i,n,t,w,s,ans:longint; f,f1:boolean;
    v:array[1..1000000] of longint;
    a:array[1..1000000,1..2] of longint;
    sq:array[1..1000000] of longint;
  procedure dfs(x,y:longint);  //搜到了左子树的x号结点和右子树的y号结点
  begin
    if f=false then exit;  //如果已经不是对称二叉树，跳出
    if v[x]<>v[y] then begin f:=false; exit; end;  //如果对应的结点权值不相等，跳出
    inc(s,2);  //计数器记得加2（两遍各一个）
    if a[x,1]<>-1 then  //如果x有左儿子
    begin
      if a[y,2]<>-1 then dfs(a[x,1],a[y,2])  //如果y有右儿子，搜
      else begin f:=false; exit; end;  //y没有对应的右儿子，跳出
    end
    else if a[y,2]<>-1 then begin f:=false; exit; end;  //x没有左儿子但是y有右儿子，跳出
    if a[x,2]<>-1 then  //同上
    begin
      if a[y,1]<>-1 then dfs(a[x,2],a[y,1])
      else begin f:=false; exit; end;
    end
    else if a[y,1]<>-1 then begin f:=false; exit; end;
  end;
  function check(k:longint):boolean;  //判断以k结点为根节点是否能形成对称二叉树
  begin
    s:=1; f:=true;  //计数器初始为1，并默认当前可形成对称二叉树
    if (a[k,1]=-1)and(a[k,2]=-1) then exit(true);  //如果当前根节点无儿子则返回真
    if (a[k,1]<>-1)and(a[k,2]<>-1) then  //如果左右儿子都有
    begin
      dfs(a[k,1],a[k,2]); exit(f);  //开始深搜，并返回最终结果
    end else exit(false);  //如果只有一个儿子，返回假
  end;
begin
  readln(n);
  for i:=1 to n do read(v[i]);  //读入权值
  f1:=true;
  for i:=1 to n do
  begin
    readln(a[i,1],a[i,2]);  //读入左右儿子
    if (a[i,1]=-1)or(a[i,2]=-1) then f1:=false;  //此处为特判，如果该结点无左或右儿子，那么这不是满二叉树
  end;
  t:=1; w:=1; sq[1]:=1;  //队列初始化，根节点入队
  while t<=w do
  begin
    if check(sq[t]) then  //如果以当前根节点能构成对称二叉树
    begin
      if f1 then begin writeln(s); close(input); close(output); halt; end;  //如果这是一棵满二叉树，那么第一次找到的对称二

叉树必然为最优解，直接输出
      if s>ans then ans:=s;  //更新结点数最大值
    end;
    if a[sq[t],1]<>-1 then begin inc(w); sq[w]:=a[sq[t],1]; end;  //如果头结点有左儿子，进队列
    if a[sq[t],2]<>-1 then begin inc(w); sq[w]:=a[sq[t],2]; end;  //如果头结点有右儿子，进队列
    inc(t);
  end;
  writeln(ans);
end.
```

附评测结果（2018/11/11）：
![](https://s1.ax1x.com/2018/11/11/iqfYQI.png)

事实证明不加优化也可以跑得很快。