这个题就是考了个求逆序对。

所以原题实际意义为求N个数据排序成从小到大有几个逆序对(调换几次)

O（n^2）的算法最好想到，直接一个冒泡/插入  AC

```cpp
var n,i,j,ans,t:longint;
    a:array[1..10000]of longint;
begin
 readln(n);
 for i:=1 to n do read(a[i]);
 for i:=1 to n-1 do
  for j:=i+1 to n do
   if a[i]>a[j] then inc(ans);
 writeln(ans);
end.
```
AC效率：（Greatest）
10ms /  8.25MB

代码：0.2KB Pas 车厢重组

有没有O（n log n）的算法呢？

要用分治思想，即归并排序求逆序对。。

```cpp
var n,i,ans:longint;
a,b:array[0..10000] of longint;
procedure merge(left,mid,right:longint);
var le,mi,k,i:longint;
begin
le:=left;mi:=mid+1;k:=left;
while (le<=mid) and (mi<=right) do
begin
if a[le]<=a[mi] then
begin
b[k]:=a[le];
inc(le);
inc(k);
end else
begin
b[k]:=a[mi];
inc(mi);
inc(k);
ans:=ans+mid-le+1;//删去就是纯归并排序，加上就是求逆序对
end;
end;
while le<=mid do begin b[k]:=a[le];inc(le);inc(k); end;
while mi<=right do begin b[k]:=a[mi];inc(mi);inc(k); end;
for i:=left to right do a[i]:=b[i];
end;
procedure msort(left,right:longint);
var mid:longint;
begin
if left=right then exit;
mid:=(left+right) div 2;
msort(left,mid);
msort(mid+1,right);
merge(left,mid,right);
end;
begin
readln(n);
for i:=1 to n do read(a[i]);
msort(1,n);
writeln(ans);
end.
```
AC效率：（fastest）
6ms /  8.29MB

代码：0.97KB Pas
