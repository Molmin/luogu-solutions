看见大家齐刷刷的冒泡排序，我果断码了一段树状数组。

本题其实是一个求逆序对的问题，有几个逆序对，就需要操作几次。

这里c[i]表示的是数字i出现的次数，比i小的数就有(c[1]+c[2]+...+c[i-1])个，这就关系到一个区间的和，可以用树状数组解决。

- 由于此题数据范围较小（也就10000），不需要离散化了。

附上pascal代码：

```cpp
var
  n,i,ans:longint;  //ans存答案
  a,c:array[0..10000] of longint;  //a数组即原数
function lowbit(x:longint):longint;  //低位
  begin
    exit(x and (-x));
  end;
procedure modify(k:longint);  //插入(修改)操作
  begin
    while k<=n do
      begin
        inc(c[k]);  //次数+1
        inc(k,lowbit(k));  //往根节点上溯
      end;
  end;
function sum(k:longint):longint;  //求和操作
  begin
    sum:=0;
    while k>0 do
      begin
        inc(sum,c[k]);  //累加
        dec(k,lowbit(k));
      end;
  end;
begin
  readln(n);
  for i:=1 to n do read(a[i]);
  for i:=n downto 1 do  //倒推，求比a[i]小的数（保证之前已经插入的数的序号一定是大于i的）
    begin
      inc(ans,sum(a[i]-1));  //累加 比a[i]小的数(即1~a[i]-1)的次数
      modify(a[i]);  //加入a[i]（a[i]出现的次数+1，此处变量省略1）
    end;
  write(ans);
end.
```