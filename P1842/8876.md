一个加法版非高精度版本的P1080，证明同理。假设第n头牛与第n+1头牛互换位置，设第n头牛以上的牛总重量为w,第n和第n+1头牛的重量为w1,w2,力气分别为s1,s2。所以若n在上则有压扁值n=w-s1,压扁值n+1=w+w1-s2。若n在下则有n=w+w2-s1，n+1=w-s2。因为w-s2肯定比w+w1-s2要小，而w-s1肯定比w+w2-s2要小，因此交换之后答案更优的条件就是w+w1-s2>w+w2-s1，移项之后得w1+s1>w2+s2。故以w+s为关键字对所有的数据排序，将w+s更大的放在下面。最后扫描整个数组得出答案即可。代码如下

```pascal
Type
    cow=record
        w,s:int64;
    end;
Var
    cows:array[1..50010] of cow;
    i,n:longint;
    temp,ans:int64;
Function max(a,b:int64):int64;
    begin
            if a>b then exit(a);
            exit(b);
    end;
procedure qsort(l,r:longint);
    var
        i,j,x:int64;
        y:cow;
    begin
        i:=l;
        j:=r;
        x:=cows[(l+r) div 2].w+cows[(l+r) div 2].s;
        repeat
            while cows[i].w+cows[i].s<x do inc(i);
            while x<cows[j].w+cows[j].s do dec(j);
            if not(i>j) then begin
                y:=cows[i];
                cows[i]:=cows[j];
                cows[j]:=y;
                inc(i);
                j:=j-1;
            end;
        until i>j;
        if l<j then qsort(l,j);
        if i<r then qsort(i,r);
     end;
Begin
    readln(n);
    for i:=1 to n do begin
        readln(cows[i].w,cows[i].s);
    end;
    qsort(1,n);
    ans:=-maxlongint;
    for i:=1 to n do begin
        ans:=max(temp-cows[i].s,ans);
        temp:=temp+cows[i].w;
    end;
    writeln(ans);
End.
```