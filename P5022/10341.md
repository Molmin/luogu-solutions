退役前先写一波题解。。。

考场第一眼瞄过去，好像是挺裸的一个基环树~~（但是放T1不太好吧。。。）~~，思路也挺明确的。

写了整整40min过了大样例，后来检查好几发幸好A了。。。

但是还是因为400~410的分数预备退役了QAQ。。。

~~（以上都是废话）~~

------------

## 先考虑60分的情况：

因为是一棵树，所以你一旦访问了某个子节点，那么你就必须把这颗子树访问完才能访问其他子节点，否则剩余的节点将无法访问到。

要求字典序最小，那么我们肯定先从1出发，然后对于每一个节点，我们肯定是按照字典序的大小依次访问，看一眼~~良心~~的数据范围，我就暴力地把所有子节点存下来，排序一遍依次访问（数组尽量不要开在递归里，~~爆栈我不负责~~）。

然后记录一下输出就好了。

## 满分：

现在树中多了一个环。

相信你一定学过**基环树**。

可以看到，对于一个环，假设有m条边，那么无论如何旅行，有且仅有m-1条边能被访问，也就是说，旅行的路径仍旧是一棵树。

也就是每次暴力的割断环上的每一条边，然后跑一遍，找一个字典序最小的就好了。

找环可以用tarjan，我把所有的边存了一遍，然后两端点的点属于同一个点双的边就在环上了。

Pascal Code：

```cpp
// luogu-judger-enable-o2//手抖开的
uses math;
var
  e:array[0..400005,0..2]of longint;
  a:array[0..5005,0..5005]of longint;
  head,next,d,dfn,low,cut,id,sta,nowans,ans:array[0..20005]of longint;
  //nowans记录当前最小遍历
  //ans为总答案
  c:array[0..400005]of boolean;
  n,m,i,j,fl,num,top,s:longint;
  flag:boolean;
procedure add(x,y:longint);
begin
  inc(num);
  d[num]:=y;
  next[num]:=head[x];
  head[x]:=num;
end;
procedure sort(u,l,r:longint);//排序（from Pascal库）
      var
         i,j,x,y:longint;
      begin
         i:=l;
         j:=r;
         x:=a[u][(l+r) div 2];
         repeat
           while a[u][i]<x do
            inc(i);
           while x<a[u][j] do
            dec(j);
           if not(i>j) then
             begin
                y:=a[u][i];
                a[u][i]:=a[u][j];
                a[u][j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(u,l,j);
         if i<r then
           sort(u,i,r);
      end;
procedure dfs(u,fa:longint);
var
  i,v,ll:longint;
begin
  inc(nowans[0]);
  nowans[nowans[0]]:=u;
  a[u][0]:=0;//不存在dfs中，可以开一个二维数组搞定
  ll:=head[u];
  while ll<>0 do//记录子节点信息
  begin
    v:=d[ll];
    if (v=fa) or c[ll] then//除去割断的边
    begin
      ll:=next[ll];
      continue;
    end;
    inc(a[u][0]);
    a[u][a[u][0]]:=v;
    ll:=next[ll];
  end;
  sort(u,1,a[u][0]);
  //排序
  for i:=1 to a[u][0] do//依次访问
    dfs(a[u][i],u);
end;
procedure tarjan(u,fa:longint);//tarjan求点双
var
  v,ll:longint;
begin
  inc(s);
  dfn[u]:=s;
  low[u]:=s;
  inc(top);
  sta[top]:=u;
  ll:=head[u];
  while ll<>0 do
  begin
    v:=d[ll];
    if dfn[v]=0 then
    begin
      tarjan(v,u);
      low[u]:=min(low[u],low[v]);
    end else
      if v<>fa then
        low[u]:=min(low[u],dfn[v]);
    ll:=next[ll];
  end;
  if dfn[u]=low[u] then//虽然不是标准的点双求法，但是在这道题中只有一个环，没事。。。
  begin
    inc(fl);
    repeat
      id[sta[top]]:=fl;
      dec(top);
    until sta[top+1]=u;
  end;
end;
begin
  readln(n,m);
  for i:=1 to m do
  begin
    readln(e[i][1],e[i][2]);
    add(e[i][1],e[i][2]);
    add(e[i][2],e[i][1]);
  end;
  if m=n-1 then
  begin
    dfs(1,0);
    for i:=1 to n-1 do
      write(nowans[i],' ');
    writeln(nowans[n]);
  end else
  begin
    tarjan(1,0);
    for i:=1 to m do
      if id[e[i][1]]=id[e[i][2]] then//找环上的边
      begin
        inc(cut[0]);
        cut[cut[0]]:=i;
      end;
    for i:=1 to n do
      ans[i]:=n;
    for i:=1 to cut[0] do//枚举每条边
    begin
      c[cut[i]*2-1]:=true;//表示割断
      c[cut[i]*2]:=true;
      nowans[0]:=0;//清空
      dfs(1,0);
      flag:=false;//是否替换的flag
      for j:=1 to n do//比较答案
        if nowans[j]<>ans[j] then
        begin
          if nowans[j]<ans[j] then
            flag:=true;
          break;
        end;
      if flag then//更换答案
      begin
        for j:=1 to n do
          ans[j]:=nowans[j];
      end;
      c[cut[i]*2-1]:=false;//去掉标记
      c[cut[i]*2]:=false;
    end;
    for i:=1 to n-1 do//输出
      write(ans[i],' ');
    writeln(ans[n]);
  end;
end.
```