~~其实这题不难啊......可是蒟蒻我考场上还是没做出来.~~


可以发现这题边数只有两种:m=n-1 or m=n.

那么当m=n-1时,我们可以贪心深搜一下.以为字典序比较时肯定是越前面越优先,所以当dfs到u时,把儿子v从小到大继续深搜即可,时间复杂度O($n^2$)(邻接矩阵......) or ($nlogn$)

当m=n时,我考场上想的非常麻烦,但是我们其实可以暴力一点......可以发现我们走到环上的时候只会回去一次,那么不就相当于删去了一条环上的边然后跑第一部分的搜索?(但是我们太懒了不知道哪条边在环上于是我们更暴力地随便删除一条边......)于是我们就开心的AC了,时间复杂度O($n^2$+$nlogn$)(你要是再用邻接矩阵我也没办法)

下附代码
```pascal
var
        n,m,mm,i,sp,tot:longint;
        x,y,l,r,a,b,id:array[1..10005]of longint;
        flag:array[1..5005]of boolean;
procedure dfs(u:longint);//当m=n是的贪心搜索,非常简单
var
        i:longint;
begin
        for i:=l[u] to r[u] do
        if flag[y[i]] then
        begin
                flag[y[i]]:=false;
                write(y[i],' ');
                dfs(y[i]);
        end;
end;
procedure swap(var a,b:longint);
var
        c:longint;
begin
        c:=a;
        a:=b;
        b:=c;
end;
procedure sort(l,r:longint);//把边按照x为第一关键,y为第二关键排序
var
        i,j,mid1,mid2:longint;
begin
        i:=l;
        j:=r;
        mid1:=x[(l+r)>>1];
        mid2:=y[(l+r)>>1];
        repeat
                while (x[i]<mid1)or((x[i]=mid1)and(y[i]<mid2)) do inc(i);
                while (x[j]>mid1)or((x[j]=mid1)and(y[j]>mid2)) do dec(j);
                if i<=j then
                begin
                        swap(x[i],x[j]);
                        swap(y[i],y[j]);
                        swap(id[i],id[j]);
                        inc(i);
                        dec(j);
                end;
        until i>j;
        if i<r then sort(i,r);
        if l<j then sort(l,j);
end;
procedure dfs2(u:longint);
var
        i:longint;
begin
        //注意判断当前边能走否(sp会变成2条,排序后打乱,所以用id数组判断)
        for i:=l[u] to r[u] do
        if (flag[y[i]])and(id[i]<>sp) then
        begin
                flag[y[i]]:=false;
                inc(tot);
                b[tot]:=y[i];
                dfs2(y[i]);
        end;
end;
procedure pd;//判断当前方案字典序是否更优
var
        i:longint;
begin
        for i:=1 to n do//若是不小心删除了不是环上的边那么不行
        if flag[i] then exit;
        if a[1]=0 then//之前没有方案直接选择当前方案
        begin
                for i:=1 to n do
                a[i]:=b[i];
                exit;
        end;
        for i:=1 to n do//比较字典序
        if a[i]<>b[i] then
        begin
                if b[i]>a[i] then exit//当前方案不优
                else break;
        end;
        for i:=1 to n do//当前方案更优,选择
        a[i]:=b[i];
end;
begin
        read(n,m);
        mm:=m;
        for i:=1 to m do//边要复制一遍因为
        begin
                read(x[i],y[i]);
                id[i]:=i;
                inc(mm);
                y[mm]:=x[i];
                x[mm]:=y[i];
                id[mm]:=i;
        end;
        sort(1,mm);
        l[1]:=1;//处理出和i点相邻的边的开头(l[i])与结尾(r[i])
        r[n]:=mm;
        for i:=2 to mm do
        if x[i-1]<>x[i] then
        begin
                r[x[i-1]]:=i-1;
                l[x[i]]:=i;
        end;
        if m=n-1 then//判断特殊情况
        begin
                for i:=1 to n do
                flag[i]:=true;
                flag[1]:=false;
                write(1,' ');
                dfs(1);
                exit;
        end;
        for sp:=1 to m do//随便枚举删掉哪条边
        begin
                for i:=1 to n do//预处理
                flag[i]:=true;
                flag[1]:=false;
                tot:=1;
                b[1]:=1;
                dfs2(1);//贪心搜索
                pd;
        end;
        for i:=1 to n do
        write(a[i],' ');
end.
```