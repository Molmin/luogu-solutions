三维的BFS，看起来难，~~其实很水~~，注意一些细节，套上BFS的模板即可，附上代码~~（P党福音）~~：

```pascal
const //六个移动的方向，上下前后左右
  dx:array[1..6] of longint=(1,-1,0,0,0,0);
  dy:array[1..6] of longint=(0,0,1,-1,0,0);
  dz:array[1..6] of longint=(0,0,0,0,-1,1);
var
  a:array[0..270000,0..5] of longint; 
  map:array[0..50,0..50,0..50] of char; 
  c:array[0..50,0..50,0..50] of boolean; 
  i,j,k,x,y,z,x1,y1,z1,x2,y2,z2:longint;
  d:array[0..100,0..100,0..100] of longint;

procedure bfs;
var
  h,t,nx,ny,nz,i:longint;
begin
  h:=1; t:=1;
  fillchar(c,sizeof(c),true); //地图重置
  c[x1,y1,z1]:=false; //走过的点标记
  a[t,4]:=0; //步数
  a[1,1]:=x1; a[1,2]:=y1; a[1,3]:=z1;
  while t>=h do begin
    for i:=1 to 6 do
      begin
        nx:=a[h,1]+dx[i]; ny:=a[h,2]+dy[i]; nz:=a[h,3]+dz[i]; //移动
        if (nx>=1) and (nx<=x) and (ny>=1) and (ny<=y) and 
           (nz>=1) and (nz<=z) and (c[nx,ny,nz]) and (map[nx,ny,nz]<>'#') then //是否能走
             begin
               inc(t);
               a[t,1]:=nx; a[t,2]:=ny; a[t,3]:=nz; a[t,4]:=a[h,4]+1; //入队列，加步数
               c[nx,ny,nz]:=false;
               if (map[nx,ny,nz]='E') then //如果达到终点，输出
                 begin
                   writeln('Escaped in ',a[t,4],' minute(s).');
                   exit;
                 end;
             end;
      end;
    inc(h);
  end; 
  writeln('Trapped!'); //结束了也没到终点
end;
begin
  readln(x,y,z);  //读入
  while (x<>0) and (y<>0) and (z<>0) do begin
    for i:=1 to x do begin
      for j:=1 to y do begin
        for k:=1 to z do begin 
          read(map[i,j,k]);
          if map[i,j,k]='S' then begin x1:=i; y1:=j; z1:=k; end; //标记起点
        end;
        readln;
      end;
    end;
    bfs; //开始BFS
    readln(x,y,z); 
  end;
end.
```