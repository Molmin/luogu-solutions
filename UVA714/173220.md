## $Problems$
要抄 $m$ 本书，编号为 $1,2,3...m$, 每本书有 $1\le x \le10^7$ 页，把这些书分配给 $k$ 个抄写员，要求分配给某个抄写员的那些书的编号必须是连续的。

每个抄写员的速度是相同的，求让每个人被分配的书的总页数的最大值最小的方案。
## $Answer$
这是一道典型的贪心+二分的题，我们需要用'最大值尽量小'问题来解决。

首先我们要先了解什么是最大值尽量小问题。
![](https://img-my.csdn.net/uploads/201208/14/1344957229_9248.jpg)
此图片来自于[$\color{orange}shuangde800's-blog$](https://blog.csdn.net/shuangde800/article/details/7828695)

然后我们直接用二分，去寻找某个地方是否需要划分，并用 $f$ 数组把这 $k$ 个位置记录号，因为输出时要用。。。

具体就是二分先确定 $mid$ ，即为划分后每个区域的数量最多的书目，然后在从 $m$ 不断往前推，一个个将区域划分好。（就是不断的累积，当前累加的没超过 $mid$，则继续累加，否则就算是判断好了当前状态下划分的情况）
## $Code$
```pascal
var
	a:array[0..1010]of longint;
	f:array[0..1010]of boolean;
	t,m,k,ans,i,min,sum,cnt:longint;

function search(mm:longint):longint;
var
	bool:boolean;
	cnt,p,ss:longint;
begin
	fillchar(f,sizeof(f),false);
	cnt:=0;p:=m;
	while p>0 do
	begin
		ss:=0;bool:=true;
		while (p>0)and(ss+a[p]<=mm) do
		begin
			bool:=false;
			ss:=ss+a[p];
			dec(p);
		end;
		if bool then exit(k+1);
		if p>0 then f[p]:=true;
		inc(cnt);
	end;
	exit(cnt);
end;

function solve:longint;
var
	l,r,mid:longint;
begin
	l:=min;r:=sum;
	while l<r do
	begin
		mid:=(l+r)>>1;
		if (search(mid)<=k) then r:=mid
			else l:=mid+1;
	end;
	exit(r);
end;

begin
	readln(t);
	while t>0 do
	begin
		dec(t);sum:=0;min:=0;
		readln(m,k);
		for i:=1 to m do
		begin
			read(a[i]);
			inc(sum,a[i]);
			if a[i]>min then min:=a[i];
		end;
		ans:=solve;
		cnt:=search(ans);
		for i:=1 to m-1 do
			if cnt<k then
			begin
				if f[i]=false then
				begin
					f[i]:=true;
					inc(cnt);
				end;
			end
			else break;
		for i:=1 to m do
		begin
			if i<>1 then write(' ',a[i])
				else write(a[i]);
			if f[i]then write(' /');
		end;
                writeln;
	end;
end.
```