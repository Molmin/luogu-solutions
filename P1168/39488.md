题目说明很简单，能用上的数据结构也挺多，我看到楼下大佬有用单调队列的，我也没看懂怎么操作，还有平衡树啊，都写不来(你们都太强了)

但是可以写平衡树的退化版二叉排序树。
中位数就是求第（i+1） div 2 大的数，所以很显然就想到了这个数据结构。

找第K大的数就是要累计左右子树节点的个数

如果k<=左子树个数就说明在左子树里

如果k<=左子树个数加该节点个数，说明答案就是该节点

否则就在右子树里找（简单粗暴）

二叉排序树好像不能处理有相同的数，但只要没有删除操作就没有问题

有些人会说二叉排序树会退化成一个线性结构，不过根据我的亲身试验不会炸，数据还是有点水

下面看
```
var
  len:longint;
  i,j,m,n,k:longint;
  f,numl,numr,l,r,num:array[0..1000005]of longint;
procedure put(now,p:longint);
begin
  if f[now]=p then inc(num[now])
  else if f[now]>p then
    begin
      inc(numl[now]);
      if l[now]<>0 then put(l[now],p)
      else
        begin
          inc(len); l[now]:=len; f[len]:=p;
          num[len]:=1;
        end;
    end
  else
    begin
      inc(numr[now]);
      if r[now]<>0 then put(r[now],p)
      else
        begin
          inc(len); r[now]:=len; f[len]:=p;
          num[len]:=1;
        end;
    end;
end;
function get(now,tot:longint):longint;
begin
  if tot<=numl[now] then exit(get(l[now],tot))
  else if tot<=numl[now]+num[now] then exit(f[now])
  else
    exit(get(r[now],tot-numl[now]-num[now]));
end;
begin
  readln(n);
  read(k); writeln(k);
  len:=1; f[1]:=k; num[1]:=1;
  for i:=2 to n do
    begin
      read(k);
      put(1,k);
      if i mod 2=1 then
        writeln(get(1,(i+1) div 2));
    end;
end.
```


