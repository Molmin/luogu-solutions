此题方法层出不穷，这里讲解一下树状数组的

首先ai<=10^9，想到离散化

不过离散好以后，原数与新数需要对应，因为最后还要输出原数

接下来是树状数组的区间查询

怎么查询？

我们令c[i]为当前序列中i为第几小

比如序列：1 3 5 7 9

c数组就为：1 1 2 2 3 3 4 4 5

没有的数跟前面一致，所以每次问题转化为：区间查找第(i+1)/2小数

即为查找：c[i]=(i+1)/2，i最小

方法？

二分。














```cpp
var
    a,b,c,number,print:array[0..1000000] of int64;
    p,n,i:longint;
function lowbit(x:int64):int64;
begin
    exit(x and -x);
end;
procedure add(x:int64);
begin
    while x<=n do
        begin
            inc(c[x]);
            inc(x,lowbit(x));
        end;
end;
function getsum(x:int64):int64;
begin
    getsum:=0;
    while x>0 do
        begin
            inc(getsum,c[x]);
            dec(x,lowbit(x));
        end;
end;
function find(x:int64):int64;//二分区间查询
var
    l,r,mid:int64;
begin
    l:=0;
    r:=n;
    while l<=r do
        begin
            mid:=(l+r) div 2;
            if getsum(mid)>=x then
                r:=mid-1 else
                begin
                    find:=mid+1;
                    l:=mid+1;
                end;
        end;
end;
procedure swap(var x,y:int64);
var
    t:int64;
begin
    t:=x;
    x:=y;
    y:=t;
end;
procedure sort(l,r:longint);
var
    i,j,mid:longint;
begin
    i:=l;
    j:=r;
    mid:=a[(l+r) div 2];
    repeat
        while a[i]<mid do
            inc(i);
        while a[j]>mid do
            dec(j);
        if i<=j then
            begin
                swap(a[i],a[j]);
                swap(number[i],number[j]);
                inc(i);
                dec(j);
            end;
    until i>j;
    if l<j then
        sort(l,j);
    if i<r then
        sort(i,r);
end;
begin
    readln(n);
    for i:=1 to n do
        begin
            read(a[i]);
            number[i]:=i;
        end;
    sort(1,n);
    a[0]:=a[i]-1;
    for i:=1 to n do
        if a[i]<>a[i-1] then //离散
            begin
                inc(p);
                print[p]:=a[i]; //对应，用于输出
                b[number[i]]:=p;
            end else
            b[number[i]]:=p;
    for i:=1 to n do
        begin
            add(b[i]); //更改，在所有比自己大的数中+1
            if odd(i) then //i为奇数时，要输出了
                writeln(print[find((i+1) div 2)]); //find查找的下标
        end;
end.

```