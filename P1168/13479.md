pascal版——线段树（加快排，加二分查找）；

由于数据为10^9；故不能用线段树表示1到10^9内的数的有无（毕竟求中位数不满足裸的线段树条件——区间加法）；

可以先保留原数列，复制一遍进行快排（记得要去重）；

在从原数列中1->n中分别想线段树插入数字（此时线段树底层（该数字在快排中的位置）记录该数字出现次数）；

由此满足区间加法；



```cpp
type qwe=record
    l,r,num:longint;
end;
var tree:array[1..400000] of qwe;//线段树；
    line1,line2,line3:array[1..100000] of longint;//快排数组，去重数组（line2为最终数组）（空间有点浪费，但简单易懂）；
    n,m,j,k,l,i,b,c,l1,l2,l3:longint;
procedure qsort(l,r:longint);//快排；
var i,j,k,mid:longint;
begin
  i:=l;
  j:=r;
  mid:=line2[(l+r) div 2];
  repeat
    while line2[i]<mid do inc(i);
    while line2[j]>mid do j:=j-1;
    if i<=j then
       begin
         k:=line2[i];
         line2[i]:=line2[j];
         line2[j]:=k;
         inc(i);
         j:=j-1;
       end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;
procedure bt(p,l,r:longint);//建树；
      var mid:longint;
begin
   tree[p].l:=l;
   tree[p].r:=r;
   if tree[p].l=tree[p].r then exit;
   mid:=(l+r) div 2;
   bt(2*p,l,mid);
   bt(2*p+1,mid+1,r);
end;
function find(x:longint):longint;//二分查找；
     var r,i,j,mid,l:longint;
begin
  r:=l2;
  l:=1;
  while l<r do
  begin
    mid:=(l+r)div 2;
    if line2[mid]>=x then r:=mid else l:=mid+1;
  end;
  exit(l);
end;
procedure change(p,x:longint);//插入数字；
      var i,j,mid,l:longint;
begin
  if tree[p].l=tree[p].r then
    begin
      inc(tree[p].num);
      exit;
    end;
    inc(tree[p].num);
  mid:=(tree[p].l+tree[p].r) div 2;
  if mid>=x then change(2*p,x)
  else change(2*p+1,x);
end;
function sort(p,x:longint):longint;//查找中位数；
     var mid,i,j:longint;
begin
  if tree[p].l=tree[p].r then exit(tree[p].l);
  if tree[2*p].num>=x then exit(sort(2*p,x))
  else exit(sort(2*p+1,x-tree[2*p].num));
end;
begin
    readln(n);
    for i:=1 to n do read(line1[i]);
    for i:=1 to n do line2[i]:=line1[i];
    qsort(1,n);
    line3[1]:=line2[1];
    k:=1;
    for i:=2 to n do//去重；
        if line2[i]<>line2[i-1] then
               begin
                 inc(k);
                 line3[k]:=line2[i];
               end;
    l2:=k;
    for i:=1 to n do line2[i]:=line3[i];
    bt(1,1,l2);
    for i:=1 to n do
    begin
     k:=find(line1[i]);
     change(1,k);
     if i mod 2=1 then
       begin
         k:=sort(1,(i div 2)+1);
         writeln(line2[k]);
       end;
    end;
end.
```