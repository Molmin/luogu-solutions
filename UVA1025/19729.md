### 题目描述：

一个间谍，他要从第一个车站开始恰好在用s时刻到达第n个车站。

给出车站之间的距离和两端火车的速度。求这个间谍在车站等候的时间的最小值。
        
### 分析：
经典动态规划题，f[i][j]表示恰好在第i个时刻，间谍在第j个车站。

动规DP是f[i][j]=min(f[i][j],f[i-1][j]+1);

### AC代码：
```pascal
var n,num,m1,m2,i,j,k,time,sx:longint;
    f:array[0..205,0..205] of longint;
    d,e,t:array[0..205] of longint;
    p:array[0..205,0..205,0..1] of 0..1;
function min(o1,o2:longint):longint;//求min
begin
  if o1<o2 then exit(o1) else exit(o2);
end;
begin
  read(n);
  num:=1;//把序号初始化为0
  while (n<>0) do
  begin
    for i:=0 to 200 do
      for j:=0 to 200 do
        f[i,j]:=maxlongint div 3;//把f[i][j]初始化成最大值
    fillchar(p,sizeof(p),0);//把p[i][j][]初始化成0
    fillchar(t,sizeof(t),0);//把车站之间的距离更新为0
    fillchar(d,sizeof(d),0);//把从出发点出来的列车速度更新为0
    fillchar(e,sizeof(e),0);//把从终点出来的列车速度更新为0
    read(time);//读入time
    for i:=1 to n-1 do//读入车站之间的距离
      read(t[i]);
    read(m1);//读入从出发点出来的列车的数量
    for i:=1 to m1 do//读入从出发点出来的列车的速度
      read(d[i]);
    read(m2);//读入从终点出来的列车的数量
    for i:=1 to m2 do//读入从终点出来的列车的速度
      read(e[i]);
    for i:=1 to m1 do//初始化p[i][j][1]
    begin
      k:=1;
      j:=d[i];
      while (j<=time) do
      begin
        p[j,k,1]:=1;
        inc(k);
        if (k>n) then break;
        j:=j+t[k-1];
      end;
    end;
    for i:=1 to m2 do//初始化p[i][j][0]
    begin
      k:=n;
      j:=e[i];
      while (j<=time) do
      begin
        p[j,k,0]:=1;
        dec(k);
        if (k=0) then break;
        j:=j+t[k];
      end;
    end;
    f[0,1]:=0;//动态规划初始化
    for i:=1 to time do//开始DP
      for j:=1 to n do
      begin
        f[i,j]:=min(f[i,j],f[i-1,j]+1);//动态规划方程
        if (p[i,j,1]=1) then f[i,j]:=min(f[i,j],f[i-t[j-1],j-1]);//动态规划条件方程
        if (p[i,j,0]=1) then f[i,j]:=min(f[i,j],f[i-t[j],j+1]);//动态规划条件方程
      end;
    if f[time,n]=maxlongint div 3 then writeln('Case Number ',num,': impossible') else//判断impossible情况
    writeln('Case Number ',num,': ',f[time,n]);//输出
    read(n); inc(num);//继续读入n，并且增加num
  end;
end.
```