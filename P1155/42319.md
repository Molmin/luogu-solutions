首先有必要概述一下题目。本题就是对一个给定的1~n序列通过两个栈的操作（a,b,c,d四种）能否得到一个升序序列。能则输出字典序最小的方案，否则输出0.

显然我们第一感觉会想到贪心，能用栈1就用栈1.这样看起来十分正确，但是会造成误判的情况（把有解判成无解，原因下面会解释），如以下数据：

10
10 2 8 1 7 9 3 4 5 6

这里存在一个元素间相斥（不能放同一个栈）的情况（以下元素均不是当前能弹出的元素）：

一个栈：显然a[i]>a[j],i<j时，需要一个栈“暂存”元素a[i]；

两个栈：考虑三个元素a[i],a[j],a[k],i<j<k，当a[k]<a[i]<a[j]时，a[i]和a[j]不可能在同个栈中，其他情况均可以使它们共存。

三个栈：a[p]<a[i]<a[j]<a[k],i<j<k<p……同理n栈一样。

刚才的误判就是把两个相斥的元素放到了同个栈中。因此我们需要判断两个元素的相容性，将相斥的两个元素间连一条边，构成一个无向图。

这个比较简单，通过n2的枚举加后缀min（后n个元素中的最小值）的操作可以完成。

接下来就是判断这个图是否是二分图并每个元素的归宿。完成了这一步，接下来就是简单的模拟。

由于这里只有两个栈，因此就是黑白染色。**注意**如果用找奇环的方式判断会超时，因为这样还要再染色。我们可以直接通过染色的方法判断（如果一条边的两个端点同色则无解）。具体bfs可以实现。

**P.S.:**佩服那位纯模拟的dalao





```cpp
var
  a,min,s1,s2,mat:array[0..1000] of longint;
  g:array[1..1000,1..1000] of boolean;
  f:array[0..2000] of longint;
  n,i,j,p,top1,top2,t:longint;
  flag:boolean;
procedure doit;
var find:boolean;
begin
  find:=false;
  if t<=n then
    if (mat[t]=1) and (a[t]<s1[top1]) then
    begin
      inc(top1);
      s1[top1]:=a[t];
      inc(t);
      find:=true;
      inc(f[0]);
      f[f[0]]:=1;
    end;
if find then exit;
  if s1[top1]=p then begin dec(top1);inc(f[0]);f[f[0]]:=2;find:=true;inc(p);end;
if find then exit;
  if t<=n then
    if (mat[t]=2) and (a[t]<s2[top2]) then
    begin
      inc(top2);
      s2[top2]:=a[t];
      inc(t);
      find:=true;
      inc(f[0]);
      f[f[0]]:=3;
    end;
if find then exit;
  if s2[top2]=p then begin dec(top2);inc(f[0]);f[f[0]]:=4;find:=true;inc(p);end;
end;
procedure bfs;
var
  list:array[1..10000] of longint;
  b:array[1..1000] of boolean;
  head,tail,x,i,j,p:longint;
begin
  fillchar(list,sizeof(list),0);
  fillchar(b,sizeof(b),0);
  for i:=1 to n do
  begin
    if b[i] then continue;
    b[i]:=true;
    mat[i]:=1;
    begin head:=0;tail:=1;fillchar(list,sizeof(list),0);end;
    list[1]:=i;
    while head<tail do
    begin
      inc(head);
      x:=list[head];
      for j:=1 to n do if g[x,j] then
      begin
        if not b[j] then
        begin
          b[j]:=true;
          inc(tail);
          list[tail]:=j;
          mat[j]:=3-mat[x];
        end else if mat[x]=mat[j] then begin writeln(0);halt;end;
      end;
    end;
  end;
end;
begin
  readln(n);
  for i:=1 to n do read(a[i]);
  min[n]:=a[n];
  for i:=n-1 downto 1 do if min[i+1]<a[i] then min[i]:=min[i+1] else min[i]:=a[i];
  for i:=1 to n-2 do
    for j:=i+1 to n-1 do if (min[j+1]<a[i]) and (a[i]<a[j]) then
    begin
      g[i,j]:=true;
      g[j,i]:=true;
    end;
  fillchar(mat,sizeof(mat),0);
  bfs;
  for i:=1 to n do if mat[i]=0 then mat[i]:=1;
  s1[0]:=2000;
  s2[0]:=2000;
  top1:=0;
  top2:=0;
  p:=1;
  t:=1;
  while p<=n do doit;
  for i:=1 to f[0]-1 do write(chr(f[i]+96),' ');
  writeln(chr(f[f[0]]+96));
end.

```