**这道题本身比较水，只是细节有点多。**

看到没有Pascal题解，我就来一发吧。同时也纪念下自己2个多小时调出来的代码。

## 正解：离散化+bfs
题意为求“孔”的个数，即求图中被切割线段包围的独立的块个数。

不难想到在这些“孔”的周围跑一遍bfs，将能跑到的“外围”的面积全部标记。后再扫一遍全图，若发现未被标记的面积，则表示该面积在线段包围范围内。再分别做bfs标记后，统计答案即可。由于 $-1000≤x1,y1,x2,y2≤1000$ ，存在负数，故考虑离散化。

思路简单，实现细节比较复杂。

一个坑点在于：搜孔内部时，**可以从线段的一端穿到另一端**。举个栗子：
![eXdpl9.png](https://s2.ax1x.com/2019/08/10/eXdpl9.png)

在遇到如图这种情况时，我们可以直接穿过。若考虑线段的阻隔，程序将会把一整个“孔”分成3块，从而导致答案错误。

关于其余细节，dalao们应该说的很详细辣。这里不再赘述。

具体实现时特别注意“点”与“面积”的关系！详见代码。

```c
program Luogu_P1299;
const d:array[0..3,1..2] of longint=((-1,0),(1,0),(0,-1),(0,1));
var n,i,j,p,x1,y1,x2,y2,ans,cnt:longint;
    lx,ly:array[-1010..1010] of longint;
    x,y,xx,yy:array[0..1000] of longint;
    vis:array[-10..205,-10..205] of boolean;
    sq:array[1..1000000,1..2] of longint;
    cango:array[-10..205,-10..205,0..3] of boolean;
  procedure swap(var x,y:longint);
  var t:longint;
  begin
    t:=x; x:=y; y:=t;
  end;
  procedure bfs(x,y,t:longint);  //t是临时变量，t=1代表此时搜外围，t=2代表此时搜“孔”的内部
  var l,r,x1,y1:longint;
  begin
    l:=1; r:=1;
    sq[1,1]:=x; sq[1,2]:=y;
    while l<=r do
    begin
      for i:=0 to 3 do
      begin
        x1:=sq[l,1]+d[i,1]; y1:=sq[l,2]+d[i,2];
        if (x1<0)or(x1>200)or(y1<0)or(y1>200) then continue;
        if vis[x1,y1] then continue;
        if (t=1)and(cango[sq[l,1],sq[l,2],i]=false) then continue;  
        //注意：搜孔的内部时，须忽略线段的阻隔效果
        vis[x1,y1]:=true;
        inc(r); sq[r,1]:=x1; sq[r,2]:=y1;
      end;
      inc(l);
    end;
  end;
begin
  readln(n);
  for i:=1 to n do
  begin
    readln(x[i],y[i],xx[i],yy[i]);
    lx[x[i]]:=1; lx[xx[i]]:=1;  ly[y[i]]:=1; ly[yy[i]]:=1;
  end;
  for i:=-1010 to 1010 do  //x,y数据较小，离散化可以直接记录，无需快排
    if lx[i]>0 then
    begin
      inc(cnt); lx[i]:=cnt;
    end;
  cnt:=0;
  for i:=-1010 to 1010 do
    if ly[i]>0 then
    begin
      inc(cnt); ly[i]:=cnt;
    end;
  fillchar(cango,sizeof(cango),true);  //cango数组和楼上几篇题解一样，记录该点往各方向能否走
  for p:=1 to n do
  begin
    x1:=lx[x[p]]; y1:=ly[y[p]];
    x2:=lx[xx[p]]; y2:=ly[yy[p]];
    if (x1>x2)or((x1=x2)and(y1>y2)) then  //注意两个坐标大小顺序
    begin
      swap(x1,x2); swap(y1,y2);
    end;
    for i:=y1+1 to y2 do  //“点”和“单位面积”是不一样的，此处循环需从y1+1到y2
    begin
      cango[x1,i,1]:=false;
      cango[x1+1,i,0]:=false;
    end;
    for i:=x1+1 to x2 do
    begin
      cango[i,y1,3]:=false;
      cango[i,y1+1,2]:=false;
    end;
  end;
  bfs(0,0,1);  //搜外围面积
  for i:=0 to 200 do
    for j:=0 to 200 do
      if vis[i,j]=false then
      begin
        inc(ans); bfs(i,j,2);  //累计答案，标记
      end;
  writeln(ans);
end.
```


