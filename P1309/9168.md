1 每轮比赛后根据 (1)分数(2)编号 排序：

1:快排

n<100000,r<=50 

O(2n\*log2(2n)\*r)<2\*10^5\*18\*50=1.8\*10^8

2:归并排序(分数+1的n个数为有序,分数不变的n个数也有序)


O(2n\*r)<2\*10^5\*50=10^7

当然，一开始得用一次快排


O(2n\*log2(2n))<2\*10^5\*18=3.6\*10^6

2 每轮比赛根据实力大小,分数:(1)+1 (2)不变

```delphi

var
  n,r,q,i,j:longint;
  a,w,num,z:array[1..200000] of longint;
  x,y:array[1..100000]of longint;

procedure swap(var x,y:longint);
var t:longint;
begin
  t:=x;
  x:=y;
  y:=t;
end;

procedure qsort(l,r:longint);
var
  i,j,mid,mida,midnum:longint;
begin
  i:=l; j:=r; mid:=(i+j)div 2;
  mida:=a[mid]; midnum:=num[mid];
  repeat
    while (a[i]>mida)or(num[i]<midnum)and(a[i]=mida) do inc(i);
    while (a[j]<mida)or(a[j]=mida)and(num[j]>midnum) do dec(j);
    if i<=j then
    begin
      if i<j then
      begin
        swap(a[i],a[j]);
        swap(num[i],num[j]);
        swap(w[i],w[j]);
      end;
      inc(i); dec(j);
    end;
  until i>j;
  if i<r then qsort(i,r);
  if l<j then qsort(l,j);
end;

procedure sort;
var i,j,k:longint;
begin
 i:=1; j:=1;
 repeat
  while (j<=n)and((a[x[i]]<a[y[j]])or(a[x[i]]=a[y[j]])and(num[x[i]]>num[y[j]])) do
  begin
   z[i+j-1]:=y[j];
   inc(j);
  end;
  while (i<=n)and((a[x[i]]>a[y[j]])or(a[x[i]]=a[y[j]])and(num[x[i]]<num[y[j]])) do
  begin
   z[i+j-1]:=x[i];
   inc(i);
  end;
 until (i=n+1)or(j=n+1);
 for k:=i to n do z[n+k]:=x[k];
 for k:=j to n do z[n+k]:=y[k];
end;

begin
  readln(n,r,q);
  for i:=1 to 2*n do
   read(a[i]);
  for i:=1 to 2*n do
   read(w[i]);
  for i:=1 to 2*n do
   num[i]:=i;

  qsort(1,2*n);

  for i:=1 to 2*n do z[i]:=i;//用z存储当前1..2n的排名能节约些赋值的时间

  for i:=1 to r do
  begin
   for j:=1 to n do
   if w[z[2*j-1]]>w[z[2*j]] then
   begin
    inc(a[z[2*j-1]]);
    x[j]:=z[2*j-1];
    y[j]:=z[2*j];
   end
   else
   begin
    inc(a[z[2*j]]);
    x[j]:=z[2*j];
    y[j]:=z[2*j-1];
   end;
   sort;
  end;

  writeln(num[z[q]]);
end.

```


O(n\*r)<5\*10^6

