这里首先说一下，被数据范围坑了，一开始只开到20000，后来加了好几次发现要加到200005……


这题说难不难，说简单也不简单，这里面一个是考归并排序的归并部分，一个是考排序时对编号与大小间关系的处理。。

【正题】

来分析下题目：首先，这个一看就知道直接快排分分钟挂掉……然后呢，我们发现，赢得人和输的人排名都没变。。归并排序正好在归并部分是把两个有序数组合并成一个，思想如下：

【归并思想】

指针i,j（下面是i1，j1）分别代表数组的中间位置和开头，m,n（下面是m，s）分别代表数组的中界和末尾，换句话说把一个数组分割成左右两半，而这两半各自有序。

接着比较左右两半的第一个，大（或小）的指针进一，继续比，直到一个数组比到这一段的结尾。

再把剩下的数组复制进成品结尾即可。

【归并举例】

对下面进行顺序归并

2 3 1 5

比较 2 1 得：1

比较 2 3 得：1 2

比较 5 3 得：1 2 3

复制 5 到末尾：1 2 3 5

【总结】

本题需先快排，然后模拟出每次的胜负两组，进行归并即可。顺便说下，这里的归并是最简单的二路归并


【标程】

```delphi

var i,j,n,k,p,q,c:longint;
c1,c2,a:array[1..200005,1..3]of longint; //1:得分 2:序号 3:实力;
procedure qsort(l:longint;r:longint);
var i,j,tp,mid1,mid2:longint;
begin
  i:=l;j:=r;
  mid1:=a[(l+r) div 2][1];
  mid2:=a[(l+r) div 2][2];
  repeat
    while(a[i][1]>mid1)or((a[i][1]=mid1)and(a[i][2]<mid2))do inc(i);//注意处理分数相等的序号大小处理
    while(a[j][1]<mid1)or((a[j][1]=mid1)and(a[j][2]>mid2))do dec(j);
    if(i<=j)then begin
      tp:=a[j][1]; a[j][1]:=a[i][1]; a[i][1]:=tp;
      tp:=a[j][2]; a[j][2]:=a[i][2]; a[i][2]:=tp;
      tp:=a[j][3]; a[j][3]:=a[i][3]; a[i][3]:=tp;
      inc(i);
      dec(j);
    end;
  until(i>j);
  if l<j then
    qsort(l,j);
  if i<r then
    qsort(i,r);
end;

procedure msort(s:longint;m:longint;t:longint);
var i1,j1,p,k1:longint;
begin
  i1:=t;j1:=m+1;
  k1:=t-1;
//比较并复制
  while(i1<=m)and(j1<=s)do begin
  inc(k1);
  if(c1[i1][1]>c1[j1][1])or(c1[i1][1]=c1[j1][1])and(c1[i1][2]<c1[j1][2])then
  begin
    c2[k1][3]:=c1[i1][3];
    c2[k1][2]:=c1[i1][2];
    c2[k1][1]:=c1[i1][1];
    inc(i1);
  end else if(c1[i1][1]<c1[j1][1])or(c1[i1][1]=c1[j1][1])and(c1[i1][2]>c1[j1][2])then
  begin
    c2[k1][3]:=c1[j1][3];
    c2[k1][2]:=c1[j1][2];
    c2[k1][1]:=c1[j1][1];
    inc(j1);
  end;end;
//复制余下的数组
  if(i1<=m)then
    for p:=i1 to m do begin
      inc(k1);
      c2[k1][3]:=c1[p][3];
      c2[k1][2]:=c1[p][2];
      c2[k1][1]:=c1[p][1];
    end;

  if(j1<=s)then
    for p:=j1 to s do begin
      inc(k1);
      c2[k1][3]:=c1[p][3];
      c2[k1][2]:=c1[p][2];
      c2[k1][1]:=c1[p][1];
    end;
end;
begin
  readln(n,p,q);
  n:=n*2;

  for i:=1 to n do
  begin
  read(a[i][1]);
  a[i][2]:=i;
  end;

  for i:=1 to n do
  read(a[i][3]);

  qsort(1,n);//快排不解释

  for i:=1 to p do begin //有这么多轮的比赛
  fillchar(c1,sizeof(c1),0);c:=0;
  fillchar(c2,sizeof(c2),0);
  for j:=1 to (n div 2) do begin
     inc(c);
     if(a[j*2-1][3]>a[j*2][3])then//模拟比赛，并把结果存入左右两边
     begin
        inc(a[j*2-1][1]);
        c1[c][1]:=a[j*2-1][1];
        c1[c][2]:=a[j*2-1][2];
        c1[c][3]:=a[j*2-1][3];

        c1[(n div 2)+c][1]:=a[j*2][1];
        c1[(n div 2)+c][2]:=a[j*2][2];
        c1[(n div 2)+c][3]:=a[j*2][3];
     end else
     if(a[j*2-1][3]<a[j*2][3])then
     begin
        inc(a[j*2][1]);
        c1[c][1]:=a[j*2][1];
        c1[c][2]:=a[j*2][2];
        c1[c][3]:=a[j*2][3];

        c1[(n div 2)+c][1]:=a[j*2-1][1];
        c1[(n div 2)+c][2]:=a[j*2-1][2];
        c1[(n div 2)+c][3]:=a[j*2-1][3];
       end;
  end;
   msort(n,n div 2,1);//进行归并
   a:=c2;
  end;
  writeln(a[q][2]);
end.

```