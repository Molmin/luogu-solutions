【算法分析1】

首先最容易想到的一个算法是，直接按照程序意思每次比赛后进行排序，最后输出排名为q的人的编号。时间复杂度O(2nlog(2n)r)，预计得分50分。

【参考程序1】

```cpp
type
  sss=array[1..3]of longint; //1表示编号，2表示实力值，3表示当前分数
var
  n,r,q,i,j:longint;
  a:array[0..200001]of sss; //要定义2*n
procedure qsort(l,r:longint);
var
  i,j,mid1,mid3:longint;
begin
  i:=l; j:=r; mid3:=a[(l+r) shr 1,3]; mid1:=a[(l+r) shr 1,1];
  repeat
    while (a[i,3]>mid3) or (a[i,3]=mid3) and (a[i,1]<mid1) do inc(i);
while (a[j,3]<mid3) or (a[j,3]=mid3) and (a[j,1]>mid1) do dec(j);
//先按照分数从大到小，如果分数相等，则按照编号从小到大
    if i<=j then 
      begin
        a[0]:=a[i]; a[i]:=a[j]; a[j]:=a[0];
        inc(i); dec(j);
      end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;
begin
  {assign(input, 'swiss.in');
  reset(input);
  assign(output, 'swiss.out');
  rewrite(output);}
  readln(n,r,q);
  for i:=1 to 2*n do a[i,1]:=i;
  for i:=1 to 2*n do read(a[i,3]); readln;
  for i:=1 to 2*n do read(a[i,2]); readln;
  qsort(1,2*n); //最开始比赛之前也需要进行一次快排
  for i:=1 to r do 
    begin
      for j:=1 to n do 
        if a[2*j-1,2]>a[2*j,2] then inc(a[2*j-1,3])
          else inc(a[2*j,3]); //进行N轮比赛
      qsort(1,2*n);
    end;
  write(a[q,1]); //输出排名为Q的选手的编号
 {close(input);
  close(output);}
end.
```
【算法分析2】
接下来观察，发现每次赢的N个人的分数都加了一，保持有序；输的人的分数都没加，还是保持有序。这会让我们想到归并排序——对两个有序表进行合并。那么我们排序可以直接把输的人和赢的人的序列进行合并，时间复杂度优化到O(2nr)。实际得分100分。

【参考程序2】

```cpp
type
  sss=array[1..3]of longint; //1表示编号，2表示实力值，3表示当前分数
var
  n,r,q,i,j:longint;
  win,out:array[0..100001]of sss; //Win表示赢的人，Out表示输的人
  a:array[0..200001]of sss; //要定义2*n
procedure merge; //对Win和Out进行合并
var
  i,j,k:longint;
begin
  i:=1; j:=1; k:=1;
  while (i<=n) and (j<=n) do 
    begin
      if (win[i,3]>out[j,3]) or 
(win[i,3]=out[j,3]) and (win[i,1]<out[j,1]) then 
//先按照分数从大到小，如果分数相等，则按照编号从小到大
        begin a[k]:=win[i]; inc(i); inc(k); end
      else
        begin a[k]:=out[j]; inc(j); inc(k); end;
    end;
  while i<=n do 
    begin a[k]:=win[i]; inc(i); inc(k); end;
  while j<=n do 
    begin a[k]:=out[j]; inc(j); inc(k); end;
end;
procedure qsort(l,r:longint);
var
  i,j,mid1,mid3:longint;
begin
  i:=l; j:=r; mid3:=a[(l+r) shr 1,3]; mid1:=a[(l+r) shr 1,1];
  repeat
    while (a[i,3]>mid3) or (a[i,3]=mid3) and (a[i,1]<mid1) do inc(i);
while (a[j,3]<mid3) or (a[j,3]=mid3) and (a[j,1]>mid1) do dec(j);
//先按照分数从大到小，如果分数相等，则按照编号从小到大
    if i<=j then 
      begin
        a[0]:=a[i]; a[i]:=a[j]; a[j]:=a[0];
        inc(i); dec(j);
      end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;
begin
 { assign(input, 'swiss.in');
  reset(input);
  assign(output, 'swiss.out');
  rewrite(output);}
  readln(n,r,q);
  for i:=1 to 2*n do a[i,1]:=i;
  for i:=1 to 2*n do read(a[i,3]); readln;
  for i:=1 to 2*n do read(a[i,2]); readln;
  qsort(1,2*n);
  for i:=1 to r do 
    begin
      for j:=1 to n do 
        if a[2*j-1,2]>a[2*j,2] then 
          begin
            inc(a[2*j-1,3]);
            win[j]:=a[2*j-1]; out[j]:=a[2*j]; //分别记录下赢的和输的
          end
        else
          begin
            inc(a[2*j,3]);
            win[j]:=a[2*j]; out[j]:=a[2*j-1];
          end;
      merge;  //直接归并即可
    end;
  write(a[q,1]); //输出排名为Q的人的编号
 { close(input);
  close(output);}
end.

```