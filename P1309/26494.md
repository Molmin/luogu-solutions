对于此题，最先想到的算法应该是每轮比赛前都用快排排一次，然后再根据选手的实力值决定胜负。

但是——时间复杂度非常非常非常······高！达到了O(R\*(N\*logN+N)) {实际上还要更多些} 。很明显，超时了。


再进一步想——————————

模拟后可以知道，每轮比赛结束后，胜利者和失败者两个集合的内部顺序是不会被打乱的。

因此，很容易就想到之前那个算法为什么不够高效了，因为所谓的快排效率高，是针对随机的数列的，

而这里每一轮比赛下来，有一半人的相对顺序已经确定了，所以快排就没有了优势。

因此，我们就想到了新方法——归并排序。

这样，每一轮比赛后用O(N)的时间归并一次，时间就够了。

空间不要忘了开两倍！！！

***值得注意的是，在第一轮比赛前，并不知道待参赛选手的排名，而这时又不可能得到任何其他的信息，

因为本来直接给出的初始值s就是无序的，所以要用快排；还要注意初始值相同的情况（这些都是细节，不再赘述）······

最后，下面给出pascal代码，仅供参考：

```cpp
var
  n,r,q,i,j:longint;
  s,w,id,a1,a2,a3,b1,b2,b3:array[0..200005]of longint;
procedure qsort(l,r:longint);
var
  i,j,mid,mid1,t:longint;
begin
  i:=l; j:=r; mid:=s[(l+r)div 2];
  mid1:=id[(l+r)div 2];
  while i<=j do
  begin
    while (s[i]>mid)or(s[i]=mid)and(id[i]<mid1) do inc(i);
    while (s[j]<mid)or(s[j]=mid)and(id[j]>mid1) do dec(j);
    if i<=j then
    begin
      t:=s[i]; s[i]:=s[j]; s[j]:=t;
      t:=w[i]; w[i]:=w[j]; w[j]:=t;
      t:=id[i];id[i]:=id[j];id[j]:=t;
      inc(i); dec(j);
    end;
  end;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;
procedure Merge;       //归并
var
  i,j,k,p:longint;
begin
  k:=1; i:=1; j:=1;
  while(i<=n)and(j<=n)do
  begin
    if (a1[i]>b1[j])or(a1[i]=b1[j])and(a3[i]<b3[j]) then
    begin
      s[k]:=a1[i]; w[k]:=a2[i]; id[k]:=a3[i];
      inc(i); inc(k);
    end
    else
    begin
      s[k]:=b1[j]; w[k]:=b2[j]; id[k]:=b3[j];
      inc(j); inc(k);
    end;
  end;
  for p:=i to n do
  begin
    s[k]:=a1[i]; w[k]:=a1[i]; id[k]:=a3[i];
    inc(i); inc(k);
  end;
  for p:=j to n do
  begin
    s[k]:=b1[j]; w[k]:=b2[j]; id[k]:=b3[j];
    inc(j); inc(k);
  end;
end;
begin
  readln(n,r,q);
  for i:=1 to 2*n do
    read(s[i]);
  for i:=1 to 2*n do
  begin
    id[i]:=i;
    read(w[i]);
  end;
  qsort(1,2*n);
  for i:=1 to r do
  begin
    for j:=1 to n do
    begin
      if (w[j*2-1]>w[j*2]) then
      begin
        a1[j]:=s[j*2-1]+1;
        a2[j]:=w[j*2-1];
        a3[j]:=id[j*2-1];
        b1[j]:=s[j*2];
        b2[j]:=w[j*2];
        b3[j]:=id[j*2];
      end
      else
      begin
        a1[j]:=s[j*2]+1;
        a2[j]:=w[j*2];
        a3[j]:=id[j*2];
        b1[j]:=s[j*2-1];
        b2[j]:=w[j*2-1];
        b3[j]:=id[j*2-1];
      end;
    end;
    Merge;
  end;
  writeln(id[q]);
end.
```