感觉今天一整天都在为pascal党做贡献，打了一整天的历年第三题，面对一百多行的代码已经完全无感了....

我用的是kruskal求最大生成森林，然后倍增LCA求最小值（其实也可以不用求lca）

步骤：

1.读入，按边的长度排序

2.kruskal的经典做法，只不过是从大到小把边加入，每条边都遍历一遍，这样生成的就不一定是一棵树而可能是一片森林

3.对于读入的每个询问，并查集判断是否在同一个集合，如果不在则走不到，答案记录为-1（并查集早在kruskal的时候就做好了对吧）

4.遍历每个点，为根节点的则建树，把深度什么的一遍dfs全搞定吧，同时在这个时候还可以记录每个点与父节点的边的编号

5.再次枚举每个询问，已经为-1的跳过，剩下的只要求路径上最短的边就好，树上ST，当做RMQ来做什么的真的很没必要，完全可以一步步往上爬。假设要从x到y，p为lca(x,y)，则分别从x、y往上爬到p为止，记录最小的那条边即是答案


注意的地方

1.知道kruskal的都清楚kruskal的边存储方式，但这种存储方式在后面的深搜建树中很不好用（因为每一次找连接的点都要遍历所有边一次，妥妥的超时），所以在kruskal的时候还要重建一次邻接表存储的边方便建树

2.可以不用倍增求LCA，不知道怎么求lca的同学们就用最基本的方法好了。对于要求的x、y来说（假设d[x]>d[y]，d为深度），先让x一步步往上爬到深度跟y相等，在这个过程中也存下最短边，与后面比较。深度相同之后，x和y一起一步步往上爬到各自的父节点，中途记录最短边，直到x与y相等，这样做也并不会超时

```cpp
type node=record
     u,v,w,en:longint;
     end;
var fa,d,last,las:array[0..11000] of longint;//fa记录并查集的父节点，d为深度，las[i]为i与其父节点的边的编号
    x,y,ans:array[1..50000] of longint;
    e:array[0..100000] of node;//e是用于kruskal的边集数组
    e2,w2,next:array[1..110000] of longint;//e2、w2、next是用于邻接表的存储方法
    f:array[1..1000000,0..20] of longint;//倍增数组
    i,j,k,m,n,p,q,t,aa,ne,bb,cc,min,now:longint;
function gf(u:longint):longint;//并查集求祖先结点
begin
    if fa[u]=u then exit(u);
    fa[u]:=gf(fa[u]);
    exit(fa[u]);
end;
procedure hb(u,v:longint);//合并
begin
    fa[gf(u)]:=gf(v);
end;
procedure sort(l,r: longint);//kruskal对边进行排序
      var
         i,j,x: longint;y:node;
      begin
         i:=l;
         j:=r;
         x:=e[(l+r) div 2].w;
         repeat
           while e[i].w<x do
            inc(i);
           while x<e[j].w do
            dec(j);
           if not(i>j) then
             begin
                y:=e[i];
                e[i]:=e[j];
                e[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
procedure bt(c:longint);//深搜建树
var i,j,k,p,po:longint;
begin
    p:=last[c];
    while p<>0 do
    begin
        po:=e2[p];
        if po<>f[c,0] then
        begin
            las[po]:=p;//注意这里，把po与c（po的父节点）的边的编号记录下来，方便后面一步步爬的时候好找边
            d[po]:=d[c]+1;
            f[po,0]:=c;
            bt(po);
        end;
        p:=next[p];
    end;
end;
function lca(u,v:longint):longint;//倍增求lca
var i,j,k:longint;
begin
    if d[u]<d[v] then
    begin
        k:=u;u:=v;v:=k;
    end;
    if d[u]>d[v] then
      for k:=20 downto 0 do
        if d[f[u,k]]>=d[v] then u:=f[u,k];
    if u=v then exit(u);
    for k:=20 downto 0 do
      if f[u,k]<>f[v,k] then
      begin
          u:=f[u,k];
          v:=f[v,k];
      end;
    exit(f[u,0]);
end;
begin
    assign(input,'P1967.in');reset(input);
    e[0].w:=maxlongint;
    readln(n,m);
    for i:=1 to m do
    begin
        readln(aa,bb,cc);
        e[i].u:=aa;e[i].v:=bb;e[i].w:=cc;
    end;
    sort(1,m);
    for i:=1 to n do
      fa[i]:=i;
    readln(q);
    for i:=m downto 1 do//kruskal，从大到小遍历
      if gf(e[i].u)<>gf(e[i].v) then
      begin
          hb(e[i].u,e[i].v);
          inc(ne);//重建一遍图，用邻接表存储筛选出来的边，方便后面的深搜建树
          e2[ne]:=e[i].v;
          next[ne]:=last[e[i].u];
          last[e[i].u]:=ne;
          w2[ne]:=e[i].w;
          inc(ne);
          e2[ne]:=e[i].u;
          next[ne]:=last[e[i].v];
          last[e[i].v]:=ne;
          w2[ne]:=e[i].w;
      end;
    for i:=1 to q do
    begin
        readln(x[i],y[i]);
        if gf(x[i])<>gf(y[i]) then ans[i]:=-1;
    end;
    for i:=1 to n do
      if fa[i]=i then begin d[i]:=1;bt(i);end;//深搜建树
    for j:=1 to 20 do
      for i:=1 to n do
        f[i,j]:=f[f[i,j-1],j-1];
    for i:=1 to q do
    begin
        if ans[i]=-1 then continue;
        p:=lca(x[i],y[i]);
        min:=maxlongint;
        now:=x[i];
        while now<>p do//慢慢往上爬吧
        begin
            k:=las[now];
            if w2[k]<min then min:=w2[k];
            now:=f[now,0];
        end;
        now:=y[i];
        while now<>p do
        begin
            k:=las[now];
            if w2[k]<min then min:=w2[k];
            now:=f[now,0];
        end;
        ans[i]:=min;
    end;
    for i:=1 to q do
      writeln(ans[i]);
end.

```