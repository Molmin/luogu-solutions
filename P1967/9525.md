倍增lca+最大生成树

```cpp
program transport;
const maxn=10000; maxm=200000;
      jc:array[0..14] of longint=(1,2,4,8,16,32,64,128,256,512,1024,2048,4096,8192,16384);
type rec=record
           f,t,d:longint;
         end;
     rec2=record
            n,t,d:longint;
          end;
var fa:array[1..maxn] of longint;
    h,d:array[1..maxn] of longint;
    w:array[1..2*maxm] of rec;
    w2:array[0..2*maxn] of rec2;
    f,mi:array[1..maxn,0..14] of longint;
    v:array[1..maxn] of boolean;
    n,m,q,i,x,y:longint;
function min(a,b:longint):longint;  //最小值，不解释
begin
  if a<b then exit(a);
  exit(b);
end;
procedure init;                           //读入存图
var i,t:longint;
    x,y,z:longint;
begin
  t:=0;
  readln(n,m);
  for i:=1 to m do
  begin
    readln(x,y,z);
    inc(t); w[t].f:=x; w[t].t:=y; w[t].d:=z;
    inc(t); w[t].f:=y; w[t].t:=x; w[t].d:=z;
  end;
  for i:=1 to n do
   fa[i]:=i;
  w2[0].n:=-1;
end;
procedure qsort(l,r:longint);                   //kuscal用到
var i,j,mid:longint;
    p:rec;
begin
  i:=l; j:=r; mid:=w[(i+j) div 2].d;
  repeat
    while w[i].d>mid do inc(i);
    while w[j].d<mid do dec(j);
    if i<=j then
    begin
      p:=w[i]; w[i]:=w[j]; w[j]:=p;
      inc(i); dec(j);
    end;
  until i>j;
  if i<r then qsort(i,r);
  if l<j then qsort(l,j);
end;
function find(x:longint):longint;      //判环，并查集+路径压缩
begin
  if fa[x]=x then exit(x);
  fa[x]:=find(fa[x]);
  exit(fa[x]);
end;
procedure kuscal;                  //kuscal 求最大生成树
var i,j,t,f1,f2:longint;
begin
  t:=0;
  for i:=1 to m*2 do
  begin
    f1:=find(w[i].f);                     //并查集部分
    f2:=find(w[i].t);
    if f1<>f2 then
    begin
      fa[f2]:=f1;                                                                                     //数组模拟邻接表存图（注意双向）
      inc(t); w2[t].n:=h[w[i].t]; h[w[i].t]:=t; w2[t].t:=w[i].f; w2[t].d:=w[i].d;
      inc(t); w2[t].n:=h[w[i].f]; h[w[i].f]:=t; w2[t].t:=w[i].t; w2[t].d:=w[i].d;
    end;
  end;
end;
procedure dfs(x:longint);                                                                     //深搜，预处理深度，父亲节点
var k:longint;
begin
  k:=h[x];
  while w2[k].n<>-1 do
  begin
    if w2[k].t<>f[x,0] then
    begin
     f[w2[k].t,0]:=x;
     d[w2[k].t]:=d[x]+1;
     mi[w2[k].t,0]:=w2[k].d;
     v[w2[k].t]:=true;
     dfs(w2[k].t);
    end;
    k:=w2[k].n;
  end;
end;
procedure pre;                                             //倍增预处理，处理跳2^i步最小值，父亲节点
var i,j:longint;
begin
  for j:=1 to 14 do
   for i:=1 to n do
   begin
     if d[i]-jc[j]>=0 then
     begin
       mi[i,j]:=min(mi[i,j-1],mi[f[i,j-1],j-1]);
       f[i,j]:=f[f[i,j-1],j-1];
     end;
   end;
end;
procedure tiao(var x:longint; d:longint; var ans:longint);        //跳到同一深度
var t:longint;
begin
  t:=0;
  while d>0 do
  begin
   if d and 1=1 then
    begin
     ans:=min(ans,mi[x,t]);
     x:=f[x,t];
    end;
   d:=d>>1; inc(t);
  end;
end;
function lca(x,y:longint):longint;         //LCA主要代码
var k,l,ans,j:longint;
begin
  ans:=maxlongint;
  k:=x; l:=y;
  if d[x]<d[y] then tiao(l,abs(d[x]-d[y]),ans);
  if d[x]>d[y] then tiao(k,abs(d[x]-d[y]),ans);
  if k=l then exit(ans);
  for j:=14 downto 0 do                       //注意，一定要dwonto到0！！！
   if f[k,j]<>f[l,j] then
    begin
      ans:=min(ans,min(mi[k,j],mi[l,j]));
      k:=f[k,j]; l:=f[l,j];
    end;
  ans:=min(ans,min(mi[k,0],mi[l,0]));
  exit(ans);
end;
begin
  assign(input,'in.txt'); reset(input);
  assign(output,'out.txt'); rewrite(output);
  init;
  qsort(1,2*m);
  kuscal;
  for i:=1 to n do if (not v[i]) and (h[i]<>0) then dfs(i);
  pre;
  readln(q);
  for i:=1 to q do
  begin
   readln(x,y);
   if find(x)<>find(y) then begin writeln(-1); continue; end;
   writeln(lca(x,y));
  end;
  close(input);
  close(output);
end.

```