
首先构造最大生成树，然后在这个过程中如果某2个点联通了，那么那个边就是答案，所以需要把这条边


作为那两个节点所在搜索树的根节点，这是一颗二叉树，而且是由叶子开始构建，使用并查集每回和并2


个集合的时候，把那条连接2个集合的边作为father合并2个集合然后一边构图，还要记录data,接下来就


采用LCA求出最近公共祖先就是答案了，如果没有在同一个集合那么就说明是不相通，输出-1

```delphi

const inf='truck.in'; outf='truck.out'; maxn=100000;
type
  jss=record
    x,y,data:longint;
  end;
var
  n,m,i,dian,bian,fx,fy,j,time,q,k,tot,x,y:longint;
  temp,next:array[0..2*maxn]of longint;
  list,father,pos,oula,depth,w:array[0..maxn]of longint;
  le:array[0..maxn]of boolean;
  edge:array[0..50001]of jss;
  f:array[0..maxn,0..20]of longint;
Procedure add(x,y:longint);
 begin
   inc(tot);
   temp[tot]:=y;
   next[tot]:=list[x];
   list[x]:=tot;
 end;
procedure qsort(l,r:longint);
 var
   i,j,x:longint;  y:jss;
 begin
   i:=l; j:=r; x:=edge[(2*l+r)div 3].data;
   repeat
     while edge[i].data>x do inc(i);
     while edge[j].data<x do dec(j);
     if i<=j then begin
        y:=edge[i]; edge[i]:=edge[j]; edge[j]:=y;
        inc(i); dec(j);
     end;
   until i>j;
   if i<r then qsort(i,r);
   if l<j then qsort(l,j);
 end;
Function getf(u:longint):longint;
 var
   z:longint;
 begin
   if father[u]=u then exit(u);
   z:=getf(father[u]);
   father[u]:=z;
   exit(z);
 end;
Procedure union(x,y,dian,data:longint);
 var
   fx,fy:longint;
 begin
   father[x]:=dian;
   father[y]:=dian;
   father[dian]:=dian;
   w[dian]:=data;
 end;
Procedure dfs(u,h:longint);
 var
   k:longint;
 begin
   le[u]:=true;
   inc(time); oula[time]:=u; depth[time]:=h; pos[u]:=time;
   k:=list[u];
   while k<>0 do begin
     if not le[temp[k]] then begin
       dfs(temp[k],h+1);
       inc(time); oula[time]:=u; depth[time]:=h;
     end;
     k:=next[k];
   end;
 end;
begin
  assign(input,inf); reset(input);
  assign(output,outf); rewrite(output);
    readln(n,m);
    for i:=1 to m do readln(edge[i].x,edge[i].y,edge[i].data);
    dian:=n;   qsort(1,m);
    bian:=0;

    for i:=1 to n do father[i]:=i;
    i:=0;

    while (bian<n-1)and(i<m) do begin   //!!i<m
      inc(i);
      with edge[i] do begin
        if getf(x)<>getf(y) then begin
          inc(dian); inc(bian);
          fx:=getf(x); fy:=getf(y);
          add(fx,dian); add(dian,fx);
          add(fy,dian); add(dian,fy);
          union(fx,fy,dian,data);
        end;
      end;
    end;
    time:=0;
    fillchar(le,sizeof(le),false);

    for i:=dian downto 1 do if not le[i] then dfs(i,0);
    for i:=1 to time do f[i,0]:=i;
    for j:=1 to trunc(ln(time)/ln(2)) do
     for i:=1 to time do if i+1 shl (j-1)<=time then begin
       f[i,j]:=f[i+1 shl (j-1),j-1];
       if depth[f[i,j-1]]<depth[f[i,j]] then f[i,j]:=f[i,j-1];
     end;
    readln(q);
    for i:=1 to q do begin
      readln(x,y);
      if getf(x)<>getf(y) then writeln(-1)
      else begin
        if pos[x]>pos[y] then begin j:=x; x:=y; y:=j; end;
        k:=trunc(ln(pos[y]-pos[x]+1)/ln(2));
        if depth[f[pos[x],k]]<depth[f[pos[y]-(1 shl k)+1,k]]
         then writeln(w[oula[f[pos[x],k]]])
         else writeln(w[oula[f[pos[y]-(1 shl k)+1,k]]]);
      end;
    end;
  close(input); close(output);
end.

```