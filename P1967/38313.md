pascal题解太难找，是时候来一波了（做了两天的题，做出来只想来一波题解）；【注：鄙人刚学lca，认为我的这份题解适合新手】

本人蒟蒻，一开始只知道思想：

（好像都知道，这是要用最大生成树构成一幅新图，然后再求两点之间的路径所有路中的最小值【由题意可得：两个点之间联通，又要货车运输的东西尽可能重，不难想到用kruskal算法构一颗最大生成树，这样两点之间的大边会被包括<可以想一想为什么>】；）

（然后就是如何求两点之间的路径最小值了【lca最容易，它可以根据递推处理，在找f[i,j]，即节点i的第2^j个父亲时，可以顺带把dis[i,j]即节点i到i的第2^j个父亲的距离最小值处理出来】，刚学lca模板的人只要知道，怎么把 **边的权值** 和 **点** 结合起来就行）

下面是我的code（有详细注释）：

===========================================================================================================


















```cpp
program lca2;
const kk:array[0..14]of longint=(1,2,4,8,16,32,64,128,256,512,1024,2048,4096,8192,16384);
      max1=50000;
      max2=10000;
var
        last,first,next,w:array[0..30000]of longint;//last,first,next建lca的树必备，w[i]记录准备加入第i个节点（不是节点i,区分一下）的边权值
        f,dis:array[0..max2,0..14]of longint;//上面有作说明，为f记录父亲，dis记录距离，求答案用
        fa,h:array[0..max2]of longint;//kruskal算法并查集用到fa数组，h记录dfs（lca必备：对我来说）建树中节点高度
        l,r,c:array[0..max1]of longint;//读入的东东
        flag:array[0..max2]of boolean;//记录有无加入dfs的树
        i,j,n,m,tot,aa,bb,q:longint;
function getfa(x:longint):longint;//并查集，不说了
begin
        if fa[x]=x then exit(x);
        fa[x]:=getfa(fa[x]);
        exit(fa[x]);
end;
procedure swap(var s,t:longint);
var
        temp:longint;
begin
                temp:=s;
                s:=t;
                t:=temp;
end;
procedure qsort(le,ri: longint);
      var
         i,j,x: longint;
      begin
         i:=le;
         j:=ri;
         x:=c[(le+ri) div 2];
         repeat
           while c[i]>x do
            inc(i);
           while x>c[j] do
            dec(j);
           if not(i>j) then
             begin
                swap(l[i],l[j]);
                swap(r[i],r[j]);
                swap(c[i],c[j]);
                inc(i);
                dec(j);
             end;
         until i>j;
         if i<ri then
           qsort(i,ri);
         if le<j then
           qsort(le,j);
      end;
procedure add(x,y,v:longint);//记录要用到的点和边权值
begin
                inc(tot);//第几个
                w[tot]:=v;//实现边权值和点的联系
                last[tot]:=y;//这三句自己理解一下吧，和下面建树过程一起理解，必须明白怎么来的
                next[tot]:=first[x];
                first[x]:=tot;
end;
//好吧，我用了好一会才理解的，还是说一下。。。【last记录的是该次的终点，first[i]记录的是i节点对应的序号，这样就可以找到last[]，next数组深搜方便】
procedure dfs(x,hh:longint);//此处用的是dfs建树
var
        i:longint;
begin
        flag[x]:=true;//已加入树，防止循环次数过多
        h[x]:=hh;//记录高度，lca用
        i:=first[x];//这里的i就是x对应的序号，下面的last[i]就是x对应的终点啦
        while i<>0 do//就是找不到下一个点了呗
        begin
                if flag[last[i]]=false then
                begin
                        f[last[i],0]:=x;//记住papa
                        dis[last[i],0]:=w[i];//等下预处理和求答案要用
                        dfs(last[i],hh+1);//以当前终点为根节点继续找
                end;
                i:=next[i];
        end;
end;
function min(x,y:longint):longint;
begin
        if x<y then exit(x) else exit(y);
end;
function lca(x,y:longint):longint;//模板题是父亲节点的编号，这里被我改掉了，求答案了
var
        k:longint;
begin
                lca:=maxlongint;
                if h[x]<h[y] then swap(x,y);//默认x的深度比y要大
                while h[x]>h[y]  do//要把x提到和y一样浅
                begin
                        k:=0;
                        while h[x]-h[y]>=kk[k+1] do//这里看不懂的话。。。你翻下lca的课件吧
                                inc(k);
                        lca:=min(lca,dis[x,k]);
                        x:=f[x,k];
                end;
                while x<>y do//一样的时候，x,y就重合了，到papa的地方了
                begin
                        if f[x,0]=f[y,0]then
                        begin
                                lca:=min(lca,min(dis[x,0],dis[y,0]));
                                break;
                        end;
                        k:=0;
                        while (f[x,k+1]<>f[y,k+1]) do
                                inc(k);
                        lca:=min(lca,min(dis[x,k],dis[y,k]));
                        x:=f[x,k];
                        y:=f[y,k];
                end;
end;
//上面的min函数还是挺好理解的
begin
        //assign(input,'truck.in'); reset(input);
        read(n,m);
        for i:=1 to m do//很多人这些地方开始很复杂，我也是，但后来我想到，其实没什么关系，建树的时候只会选到两点之间最大的那条边（题目说两个点不一定只有一条路），因为排过序了
        begin
                read(l[i],r[i],c[i]);
        end;
        qsort(1,m);
        for i:=1 to n do fa[i]:=i;
        for i:=1 to m do//kruskal构建最大生成树，顺便记录点和边
              if getfa(l[i])<>getfa(r[i]) then
              begin
                    fa[fa[l[i]]]:=fa[r[i]];
                    add(l[i],r[i],c[i]);//由于双向，必须要两个方向都add一遍
                    add(r[i],l[i],c[i]);
              end;
        dfs(1,0);//数据太水，我这算投机取巧吗？
        i:=1;
        while kk[i]<n do//预处理过程
        begin
                for j:=1 to n do
                begin
                     f[j,i]:=f[f[j,i-1],i-1];
                     dis[j,i]:=min(dis[j,i-1],dis[f[j,i-1],i-1]);
                end;
                inc(i);
        end;
        read(q);
        for i:=1 to q do
        begin
                   read(aa,bb);
                   if getfa(aa)<>getfa(bb) then writeln(-1)//不在同一个并查集，说明他们不连通
                   else writeln(lca(aa,bb));
        end;
end.

```