  这题居然没有p的题解，那我来发一发
  
  题意：求l-r这个区间最大值与最小值的差
  
  于是我们联想到这是一个~~(线段树裸题)~~区间求最值的问题。
  
  区间求最值我用的是线段树。 
  
  如果不会线段树请自行学习
  
  维护两颗线段树，用tree维护区间内的最小值
  
  同时用tree1维护区间内的最大值，在求值时分别求出最大与最小值相减就行了，时间O(m lg n)
  
  上代码(代码里有注释)
```pascal
uses math;
//math库是个好东西，但注意在正式比赛里千万不要用，加assign后会217错误
var
  i,j,n,k,m,oo,ans,p,mid,x,y,id:longint;
  a,tree,tree1:array[0..4000009] of longint;//线段树注意开4倍空间
procedure build(p,l,r:longint);//建树
var
  mid:longint;
begin
  if (l=r) then begin tree[p]:=a[l]; tree1[p]:=a[l]; exit; end;
  mid:=(l+r) div 2;
  build(p*2,l,mid);
  build(p*2+1,mid+1,r);
  tree[p]:=min(tree[p+p],tree[p+p+1]);
   tree1[p]:=max(tree1[p+p],tree1[p+p+1]);
end;
//我们可以用teee维护区间最小，用tree1维护区间最大
function query(p,l,r,x,y:longint):longint;
var
  mid:longint;
begin
  mid:=(l+r) div 2;
  if (l=x)and(r=y) then exit(tree[p])
  else if y<=mid then exit(query(p*2,l,mid,x,y))
  else if x>=mid+1 then exit(query(p*2+1,mid+1,r,x,y))
  else exit(min(query(p*2,l,mid,x,mid),query(p*2+1,mid+1,r,mid+1,y)));
end;
//线段树求最小值
function query1(p,l,r,x,y:longint):longint;
var
  mid:longint;
begin
  mid:=(l+r) div 2;
  if (l=x)and(r=y) then exit(tree1[p])
  else if y<=mid then exit(query1(p*2,l,mid,x,y))
  else if x>=mid+1 then exit(query1(p*2+1,mid+1,r,x,y))
  else exit(max(query1(p*2,l,mid,x,mid),query1(p*2+1,mid+1,r,mid+1,y)));
end;
//线段树求最大值(可以把求最小值的代码复制下来改一改(逃)
begin
  oo:=100000000;
  readln(n,m);
  for i:=1 to n do readln(a[i]);
  build(1,1,n);//建树
  for i:=1 to m do
  begin
    readln(x,y);
    writeln(query1(1,1,n,x,y)-query(1,1,n,x,y));//求区间最值
  end;
end.
```

