来为pascal党贡献一波。方法的话很多题解讲的很清楚了，我就再梳理一遍吧（我用的是倍增求的LCA加二分加差分）

1.建树，顺便把深度以及到根节点的距离(dis数组）给求了

2.读入起始点，把原始路径长度一起求了(i到j的距离就是dis[i]+dis[j]-2\*dis[lca(i,j)]，其中dis是到根节点的距离），然后按路径长度排序

3.二分答案，初始的设置的话l=0，r=300000001就好，也并不会做多少次

4.check，求出mid之后，标记所有大于mid的路径（如果没有的话显然mid可以再缩小，r:=mid就好），若从i到j的路径大于mid，那么就可以inc(cf[i]),inc(cf[j]),dec(cf[lca(i,j)],2),这就是差分的一般做法，用来统计某条边被经过的次数

5.统计边经过的次数，一遍dfs即可。cf[i]即是i到它的父节点那条路所经过的次数，统计完之后看最大的cf是否等于“大于mid的边数量“，如果等于的话，只要最长的原始路径减去经过次数最多的边里面最长的那条之后小于等于mid就好（有点绕不过打起来应该还是懂的），满足上述情况的话这个mid就是合法的，那么就考虑更小的mid二分下去，否则就把mid放大

var last,cf,d,st,ed,len,dis:array[0..300000] of longint;//cf是差分数组，len记录起始点的原始路径长，dis是到根节点的路径，d是深度

```cpp
    w,next,e:array[0..600000] of longint;
    f:Array[0..1000000,0..20] of longint;//倍增数组
    i,j,k,m,n,p,nq,num,t,l,r,mid,a,b,c,max,maxl:longint;ff:boolean;
procedure sort(l,r: longint);
      var
         i,j,x,y: longint;
      begin
         i:=l;
         j:=r;
         x:=len[(l+r) div 2];
         repeat
           while len[i]<x do
            inc(i);
           while x<len[j] do
            dec(j);
           if not(i>j) then
             begin
                y:=len[i];
                len[i]:=len[j];
                len[j]:=y;
                y:=st[i];
                st[i]:=st[j];
                st[j]:=y;
                y:=ed[i];
                ed[i]:=ed[j];
                ed[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
procedure bt(c:longint);
var p,po,i,j,k:longint;
begin
    p:=last[c];
    while p<>0 do
    begin
        po:=e[p];
        if po<>f[c,0] then
        begin
            d[po]:=d[c]+1;//求深度
            f[po,0]:=c;
            dis[po]:=dis[c]+w[p];//求到根节点的距离
            bt(po);
        end;
        p:=next[p];
    end;
end;
function lca(u,v:longint):longint;
var i,j,k:longint;
begin
    if d[u]<d[v] then
    begin
        k:=u;u:=v;v:=k;
    end;
    if d[u]>d[v] then
      for k:=20 downto 0 do
        if d[f[u,k]]>=d[v] then u:=f[u,k];
    if u=v then exit(u);
    for k:=20 downto 0 do
      if f[u,k]<>f[v,k] then
      begin
          u:=f[u,k];
          v:=f[v,k];
      end;
    exit(f[u,0]);
end;
procedure dfs(c:longint);//统计差分数组
var i,j,k,p,po:longint;
begin
    p:=last[c];
    while p<>0 do
    begin
        po:=e[p];
        if po<>f[c,0] then
        begin
            dfs(po);
            inc(cf[c],cf[po]);
        end;
        p:=next[p];
    end;
    if cf[c]>max then
    begin
        max:=cf[c];
        maxl:=dis[c]-dis[f[c,0]];
    end else
    if cf[c]=max then
      if dis[c]-dis[f[c,0]]>maxl then maxl:=dis[c]-dis[f[c,0]]
end;
begin
    readln(n,m);
    for i:=1 to n-1 do
    begin
        readln(a,b,c);
        e[2*i]:=b;next[2*i]:=last[a];
        last[a]:=2*i;w[2*i]:=c;
        e[2*i-1]:=a;next[2*i-1]:=last[b];
        last[b]:=2*i-1;w[2*i-1]:=c;
    end;
    bt(1);
    for j:=1 to 20 do
      for i:=1 to n do
        f[i,j]:=f[f[i,j-1],j-1];
    for i:=1 to m do
    begin
        readln(st[i],ed[i]);
        p:=lca(st[i],ed[i]);
        len[i]:=dis[st[i]]+dis[ed[i]]-2*dis[p];
    end;
    sort(1,m);
    l:=0;r:=300000001;
    repeat
        fillchar(cf,sizeof(cf),0);
        mid:=(l+r) div 2;
        nq:=0;num:=0;
        if len[m]>mid then
        begin
            for i:=m downto 1 do
              if len[i]>mid then
              begin
                  inc(num);
                  inc(cf[st[i]]);
                  inc(cf[ed[i]]);
                  p:=lca(st[i],ed[i]);
                  cf[p]:=cf[p]-2;
              end else break;
            max:=0;
            dfs(1);
            ff:=false;
            if max=num then
              if len[m]-maxl<=mid then ff:=true;
            if ff then r:=mid else l:=mid+1;
        end else r:=mid;
    until l=r;
    writeln(l);
end.
```