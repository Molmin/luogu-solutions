本题是NOIP2005普及组第二题——校门外的树 的数据强化版，

但如果用那题的算法来做的话本题会爆时间和空间，所以果断使用[color=purple]离散化[/color]（思想）

做法如下：

先把每一个着火船只的起点从小到大排序一遍，

接着设l为当前的最前面的线段（已连接）起点，r为这条线段的终点，

因为已经排过序，第i条线段的起点一定是大于等于l的，则对于第i条线段只剩两种情况：

①第i条线段的起点小于等于r，并大于等于l，则令r=max(r,第i条线段的终点）；

②第i条线段的起点大于r，则先在总距离上加上r-l（这条线段的长度），再将l更新为第i条线段的起点，r更新为第i条线段的终点。

[color=saddlebrown]最后别忘了在计算的总距离上加上r-l（因为最后一条线段没有在循环中处理到），以及储存总距离的变量要开int64！[/color]

输出总距离，结束！

代码如下：

```delphi

program ex1496;
var
  a:array[1..20000,1..2] of longint;
  i,j,k,m,n,s,l,r:longint;
  t:int64;

procedure qs(l,r:longint);
var
  i,j,s,mid:longint;
begin
  i:=l;
  j:=r;
  mid:=a[(l+r) div 2,1];
  repeat
    while a[i,1]<mid do inc(i);
    while a[j,1]>mid do dec(j);
    if i<=j then
    begin
      s:=a[i,1];
      a[i,1]:=a[j,1];
      a[j,1]:=s;
      s:=a[i,2];
      a[i,2]:=a[j,2];
      a[j,2]:=s;
      inc(i);
      dec(j);
    end;
  until i>j;
  if l<j then qs(l,j);
  if i<r then qs(i,r);
end;

begin
  readln(n);
  for i:=1 to n do
    readln(a[i,1],a[i,2]);
  qs(1,n);
  l:=a[1,1];
  r:=a[1,2];
  for i:=2 to n do
    if a[i,1]<=r then
    begin
      if a[i,2]>r then r:=a[i,2];
    end else
    begin
      t:=t+r-l;
      l:=a[i,1];
      r:=a[i,2];
    end;
  writeln(t+r-l);
end.

```
时间复杂度为O(n)
