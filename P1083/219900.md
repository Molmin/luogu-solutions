因为本题要求，租借者的请求是要保证先到先得的，如果前面一个人无法满足，那么后面的所有人都是无法满足的，保证了答案的单调、唯一。所以我们可以通过二分能够满足的人，来查找第一个没有满足的人

剩下的就是judge部分，本题的数据![7](https://s2.ax1x.com/2019/07/30/eGDpbF.png)，我们不能通过暴力的O(n^2)求解。因此，差分就是一种很好的方法，它可以求出部分累加的数组，而且是O(n)的

本题难点：差分

例如，我们要将数组a的一部分累加固定的数字，那么，我们就可以用数组b[i]表示a[i]-a[i-1]。举例，现在想要把数组a[5]~a[9]全都+3，就可以先inc(b[5],3);dec(b[10],3)。

```pascal
  for i:=1 to x do
  begin
    inc(b[s[i]],d[i]);
    dec(b[t[i]+1],d[i]);
  end;
```

![8](https://s2.ax1x.com/2019/07/30/eGDtr8.png)

做完所有操作后，在用O(n)扫一遍就可以得出数组a了。

```pascal
  for i:=1 to n do
  begin
    a[i]:=a[i-1]+b[i];
```

代码如下：

```pascal
var
  n,m,i,z,y,mid,ans:longint;
  r,d,s,t,b,a:array[0..1000001] of longint;
function pd(x:longint):boolean;//判断人数是否满足
var i:longint;
begin
  for i:=1 to n do
    b[i]:=0;
  for i:=1 to x do//求出数组a的差
  begin
    inc(b[s[i]],d[i]);
    dec(b[t[i]+1],d[i]);
  end;
  a[0]:=0;
  for i:=1 to n do//求出数组a
  begin
    a[i]:=a[i-1]+b[i];
    if a[i]>r[i] then
      exit(false);
  end;
  exit(true);
end;
begin
  read(n,m);
  for i:=1 to n do
    read(r[i]);
  for i:=1 to m do
    read(d[i],s[i],t[i]);
  if pd(m) then//如果都能满足，输出‘0’
  begin
    writeln('0');
    halt;
  end;
  z:=0;y:=m-1;
  while (z<=y) do//二分最大满足人数
  begin
    mid:=(z+y) shr 1;
    if pd(mid) then
    begin
      ans:=mid;
      z:=mid+1;
    end
    else
      y:=mid-1;
  end;
  writeln('-1');//依据题意
  writeln(ans+1);
end.
```

