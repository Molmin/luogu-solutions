看见这道题，首先想到的是搜索，然后看见数据范围，只能用n^2以下的

又想到它的单调性，可以用二分

因为每一天都要比，所以我们只存第i天需求与前一天相比，变了多少，用sum[i]表示

枚举到第i个订单，只要inc(sum[s[i]],d[i]);dec(sum[t[i]+1],d[i]);就行了

总的时间复杂度为nlogn

代码：

```delphi

var
  n,m,i,l,r,now,k,ssum:longint;
  room,d,s,t,sum:array[0..1000001]of longint;
  flag:boolean;
procedure init;
begin
  read(n,m);
  for i:=1 to n do
    read(room[i]);
  for i:=1 to m do
    read(d[i],s[i],t[i]);
end;
procedure print;
begin
  write(0);
  halt;
end;
procedure work;
begin
  now:=0;
  l:=0;
  r:=m+1;
  repeat
    flag:=true;
    k:=(l+r)shr 1;
    if k>now then
    for i:=now+1 to k do
      begin
        inc(sum[s[i]],d[i]);
        dec(sum[t[i]+1],d[i]);
      end
    else
    for i:=k+1 to now do
      begin
        dec(sum[s[i]],d[i]);
        inc(sum[t[i]+1],d[i]);
      end;
    now:=k;
    ssum:=0;
    for i:=1 to n do
      begin
        inc(ssum,sum[i]);
        if ssum>room[i] then begin flag:=false; break; end;
      end;
    if flag then l:=k+1
    else r:=k;
  until l=r;
  if (k=m)and(flag) then print;
  writeln(-1);
  write(l);
end;
begin
  init;
  work;
end.

```