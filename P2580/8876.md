维护一个trie树，在每个单词的末端节点用count数组记录这个节点当前的状态。效率O(m\*maxlength).

```pascal
Var
    n,i,m,cnt,tt:longint;
    t:ansistring;
    trie:array[0..500010,'a'..'z'] of longint;
    count:array[0..500010] of longint; //0代表不是终点，1代表是终点，2代表已经被点到一次
Procedure add(t:ansistring);
    var
        now,i:longint;
    begin
        now:=0;
        for i:=1 to length(t) do begin
            if trie[now,t[i]]<>0 then now:=trie[now,t[i]] else begin
                inc(cnt);
                trie[now,t[i]]:=cnt;
                now:=cnt;
            end;
        end;
        inc(count[now]);
    end;
Function search(t:ansistring):longint;
    var
        now,i:longint;
    begin
        now:=0;
        for i:=1 to length(t) do begin
            if trie[now,t[i]]<>0 then now:=trie[now,t[i]] else exit(0);
        end;
        if count[now]=1 then begin
            inc(count[now]);
            exit(1);
        end else exit(count[now]);
    end;
Begin
    readln(n);
    fillchar(count,sizeof(count),0);
    for i:=1 to n do begin
        readln(t);
        add(t);
    end;
        readln(m);
    for i:=1 to m do begin
        readln(t);
        tt:=search(t);
        if tt=1 then writeln('OK');
        if tt=2 then writeln('REPEAT');
        if tt=0 then writeln('WRONG');
    end;
End.
```