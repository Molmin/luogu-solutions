分析

这道题就是暴力搜索,方法是很简单的：枚举所有的可能解，注意剪枝。


但是在编程细节上要注意，很多时候你的程序复杂度没有问题，但常数过大就决定了你的超时（比如说，你比别人多赋值一次，这在小数据时根本没有区别，但对于1个运行5s的大数据，你可能就要用10s甚至更多）。


具体来说，预处理把所有的bisquare算出来，用一个布尔数组bene[i]记录i是否是bisquare 另外为了加速，用list记录所有的bisquare（除去中间的空位置，这在对付大数据时很有用），list中的数据要有序。


然后枚举list中的数，把较小的作为起点，两数的差作为公差，接下来就是用bene判断是否存在n个等差数，存在的话就存入path中，最后排序输出。 此时缺少重要剪枝，会超时


思考程序发现，费时最多的地方是枚举list中的数，所以对这个地方的代码加一些小修改，情况就会不一样：

1.在判断是否存在n个等差数时，从末尾向前判断（这个不是主要的）。

2.在枚举list中的数时，假设为i,j，那么如果list[i]+(list[j]-list[i])×(n-1)>lim（lim是最大可能的bisquare），那么对于之后的j肯定也是大于lim的，所以直接break掉。（这个非常有效）


AC，最大数据0.464s(如果是PASCAL的话应该还不止。请看C++的GPF的程序)。


其实输出时可以不用排序，用一个指针b[i]存公差为i的a的链表，由于搜索时a是有序的，存到b[i]中也应是有序的，这样就可以直接输出。对极限数据b的范围应该不超过m^2/n=2500,即b:array[1..2500]of point; 而如果qsort的话，复杂度为n\*logn,n<=10,000


枚举a,b也可以过的 。。要加几个小优化

```cpp
type ac=record
       a,b:longint;
     end;
var b:array[0..125000] of boolean;
    a:array[1..125000] of longint;
    r:array[1..125000] of ac;
    n,m,lim,i,j,k,p,q,la,d,v,lr:longint;
procedure qsort1(low,high:longint);
  var i,j,k:longint;
      tmp:ac;
  begin
    i:=low;
    j:=high;
    while low<high do
      begin
        while (low<high) and (r[low].b<=r[high].b) do dec(high);
        if low<high then begin tmp:=r[low];r[low]:=r[high];r[high]:=tmp;end;
        while (low<high) and (r[low].b<=r[high].b) do inc(low);
        if low<high then begin tmp:=r[low];r[low]:=r[high];r[high]:=tmp;end;
      end;
    if i<low-1 then qsort1(i,low-1);
    if high+1<j then qsort1(high+1,j);
  end;
procedure qsort2(low,high:longint);
  var i,j,k:longint;
      tmp:ac;
  begin
    i:=low;
    j:=high;
    while low<high do
      begin
        while (low<high) and (r[low].a<=r[high].a) do dec(high);
        if low<high then begin tmp:=r[low];r[low]:=r[high];r[high]:=tmp;end;
        while (low<high) and (r[low].a<=r[high].a) do inc(low);
        if low<high then begin tmp:=r[low];r[low]:=r[high];r[high]:=tmp;end;
      end;
    if i<low-1 then qsort2(i,low-1);
    if high+1<j then qsort2(high+1,j);
  end;
procedure save;
  begin
    inc(lr);
    r[lr].a:=a[i];
    r[lr].b:=d;
  end;
begin
  readln(n);
  readln(m);
  for i:=0 to 2*m*m do
    b[i]:=false;
  for p:=0 to m do
    for q:=p to m do
      begin
        k:=p*p+q*q;
        b[k]:=true;
      end;
  la:=0;
  for i:=0 to 2*m*m do
    if b[i] then
      begin
        inc(la);
        a[la]:=i;
      end;
  lim:=a[la];
  lr:=0;
  {for i:=1 to la do
    write(a[i],' ');
  writeln;}
  for i:=1 to la-n+1 do
    for j:=i+1 to la do
      begin
        d:=a[j]-a[i];
        if a[i]+(n-1)*d>lim then break;
        k:=a[j];
        for v:=3 to n do
          if b[k+d] then k:=k+d
            else break;
        if k-a[i]=(n-1)*d then save;
      end;
  if lr=0 then writeln('NONE')
    else
      begin
        qsort1(1,lr);
        j:=1;
        k:=0;
        while j<=lr do
          begin
            v:=r[j].b;
            while (j<=lr) and (r[j].b=v) do j:=j+1;
            dec(j);
            if j>k+1 then qsort2(k+1,j);
            k:=j;
            inc(j);
          end;
        for i:=1 to lr do
          writeln(r[i].a,' ',r[i].b);
      end;
end.
```