难点在于对于操作的处理（一开始操作一直接暴力没想到T了...）

操作1：改变0到目标节点的值为1，同时记录修改前值为0的节点数。

操作2：改变目标节点子树的值为0，同时记录修改前值为1的节点数。

操作2很好处理利用，先查询后处理，dfs序就行了

操作1弄了半天，发现只需要记录这条链上值为1的节点数目，再用 深度差+1-值为1的节点数目 就可以了

操作1的复杂度是O(log²n)，操作2的复杂度是O(logn)

p党代码如下


```cpp
uses math;
const maxn=100010;
type node=record
       left,right,lch,rch,data,op:longint;
     end;
var i,j,k,l,m,n,q,tot,ans:longint;
    top,w,son,size,depth,fa,head,last,first,next:array[0..maxn]of longint;
    a:array[0..maxn*4]of node;
procedure adds(x,y:longint);
begin
    inc(tot);
    next[tot]:=head[x];
    head[x]:=tot;
    last[tot]:=y;
end;
procedure dfs1(i,d:longint);
var j,k,p:longint;
begin
    depth[i]:=d;size[i]:=1;
    j:=head[i];p:=0;
    while j<>0 do
    begin
      k:=last[j];
      fa[k]:=i;
      dfs1(k,d+1);
      if size[k]>p then
      begin
        son[i]:=k;
        p:=size[k];
      end;
      inc(size[i],size[k]);
      j:=next[j];
    end;
end;
procedure dfs2(i:longint);
var j,k:longint;
begin
    inc(tot);w[i]:=tot;
    if son[fa[i]]=i then top[i]:=top[fa[i]]
    else top[i]:=i;
    j:=head[i];
    if son[i]<>0 then dfs2(son[i]);
    while j<>0 do
    begin
      k:=last[j];
      if son[i]<>k then dfs2(k);
      j:=next[j];
    end;
end;
procedure built(i,le,ri:longint);
var mid:longint;
begin
    if(le=ri)then
    begin
      a[i].lch:=le;a[i].rch:=ri;
      a[i].data:=0;a[i].op:=-1;
      exit;
    end;
    a[i].op:=-1;
    mid:=(le+ri)shr 1;
    a[i].lch:=le;
    a[i].rch:=ri;
    inc(tot);a[i].left:=tot;
    inc(tot);a[i].right:=tot;
    built(a[i].left,le,mid);
    built(a[i].right,mid+1,ri);
end;
procedure down(i:longint);
var l,r:longint;
begin
    l:=a[i].left;r:=a[i].right;
    if a[i].op<>-1 then
    begin
      a[l].op:=a[i].op;
      a[r].op:=a[i].op;
      a[l].data:=a[i].op*(a[l].rch-a[l].lch+1);
      a[r].data:=a[i].op*(a[r].rch-a[r].lch+1);
      a[i].op:=-1;
    end;
end;
procedure query(i,le,ri:longint);
var mid:longint;
begin
    if(a[i].lch>=le)and(a[i].rch<=ri)then
    begin
      inc(ans,a[i].data);
      exit;
    end;
    down(i);
    mid:=(a[i].lch+a[i].rch)shr 1;
    if(le<=mid)then query(a[i].left,le,ri);
    if(ri>mid)then query(a[i].right,le,ri);
end;
procedure change(i,le,ri,num:longint);
var mid:longint;
begin
    if(a[i].lch>=le)and(a[i].rch<=ri)then
    begin
      a[i].data:=num*(a[i].rch-a[i].lch+1);
      a[i].op:=num;
      exit;
    end;
    down(i);
    mid:=(a[i].lch+a[i].rch)shr 1;
    if(le<=mid)then change(a[i].left,le,ri,num);
    if(ri>mid)then change(a[i].right,le,ri,num);
    a[i].data:=a[a[i].left].data+a[a[i].right].data;
end;
procedure init;
var i,x,y:longint;
begin
    readln(n);
    for i:=1 to n-1 do
    begin
      read(y);
      adds(y,i);
    end;
    readln(q);
end;
procedure updata(i:longint);
var j,k,x,y,ansp:longint;
begin
    x:=i;y:=0;ansp:=0;
    while top[x]<>top[y] do
    begin
      j:=top[x];ans:=0;
      query(0,w[top[x]],w[x]);
      inc(ansp,depth[x]-depth[top[x]]+1-ans);
      change(0,w[top[x]],w[x],1);
      x:=fa[top[x]];
    end;
    ans:=0;
    query(0,w[y],w[x]);
    inc(ansp,depth[x]-depth[top[x]]+1-ans);
    change(0,w[y],w[x],1);
    writeln(ansp);
end;
procedure delete(i:longint);
var j,k:longint;
begin
    ans:=0;
    query(0,w[i],w[i]+size[i]-1);
    writeln(ans);
    change(0,w[i],w[i]+size[i]-1,0);
end;
procedure work;
var i,j,k:longint;
    ch:char;
begin
    for i:=1 to q do
    begin
      read(ch);
      if ch='i' then
      begin
        for j:=1 to 6 do read(ch);
        readln(k);updata(k);
      end;
      if ch='u' then
      begin
        for j:=1 to 8 do read(ch);
        readln(k);delete(k);
      end;
    end;
end;
begin
    init;
    tot:=0;
    dfs1(0,1);
    dfs2(0);
    tot:=0;
    fa[0]:=-1;
    built(0,1,n);
    work;
end.
```