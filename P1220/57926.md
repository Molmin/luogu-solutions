这题是一个明显的区间型动态规划。

对于一个已关区间（i，j），最后老张一定停在i或者j上，因为在从i到j的过程中，其它所有点一定碰到过了（当然也可以是从j到i，原理同）。

那么就可以写出状态转移方程了（f[i，j，1]表示关了i到j的灯并停在i，f[i，j，2]表示停在j,dist(i,j)表示从i到j的距离,sum(i,j)表示从i到j的灯的功率和）：

f[i，j，1]=min（f[i+1，j，1]+dist（i，i+1）\*sum（i+1，j），f[i+1，j，2]+dist（i，j）\*sum（i+1，j））

f[i，j，2]=min（f[i，j-1，1]+dist（i，j）\*sum（i，j-1），f[i，j-1，2]+dist（j-1，j）\*sum（i，j-1））

然后就很简单了。

```pascal
const
  inf=1000000;  //大点的初始值
var
  a,b,w:array[0..50] of longint;  //w是前缀和数组
  f:array[1..50,1..50,1..2] of longint;  //f数组用途同上
  n,p,i,j,k,t:longint;
function min(a,b:longint):longint;
  begin
    if a<b then exit(a)
    else exit(b)
  end;
begin
  readln(n,p);
  for i:=1 to n do readln(a[i],b[i]);
  w[0]:=0;
  for i:=1 to n do w[i]:=w[i-1]+b[i];  //前缀和
  for i:=1 to n do begin
    f[i,i,1]:=inf;
    f[i,i,2]:=inf
  end;  //设初始值（其实没必要）
  f[p,p,1]:=0;
  f[p,p,2]:=0;  //初始化搞定！
  for i:=2 to n do
    for j:=1 to n-i+1 do begin
      k:=i+j-1;
      t:=w[n]-w[k]+w[j];
      f[j,k,1]:=min(f[j+1,k,1]+(a[j+1]-a[j])*t,f[j+1,k,2]+(a[k]-a[j])*t);
      t:=w[n]-w[k-1]+w[j-1];
      f[j,k,2]:=min(f[j,k-1,1]+(a[k]-a[j])*t,f[j,k-1,2]+(a[k]-a[k-1])*t)
    end;  //状态计算
  write(min(f[1,n,1],f[1,n,2]))  //注意，最后有两种可能！
end.
```