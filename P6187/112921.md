个人认为这是三题里最简单的一题（但想不出如何贪就尴尬了）。
### 分析

首先，当k=0，ans=每个数的平方的和。
否则，我们发现：要使总乘积大，则大的跟大的乘，小的自然就跟小的乘。所以样例中k=1时，ans=1\*2+1\*3+2\*4+3\*5+4\*6+5\*6。

似乎完了？想多了。

注意当k=2或3时，样例解释却不这样。这是为什么呢？因为有环。当k=2，有（1-2-3）（4-5-6）二环；k=3，有（1-2）（3-4）（5-6）三环。

那么我们如何判环呢？显然，当gcd（n，k）=x时，有x个环（因为此时n | k·x）。

判完了环，就要算了。如果暴力算，时间复杂度为O（nm），不行。我们注意到分成环了以后，按数的大小排序后是有序的。如k=2时，ans=（1\*2+1\*3+2\*3）+（4\*5+4\*6+5\*6），与k=1时，ans=1\*2+1\*3+2\*4+3\*5+4\*6+5\*6很像。

于是我们生出一个想法：利用前缀和思想，c\[i\]=a\[1\]\*a\[2\]+a\[1\]\*a\[3\]+…+a\[i-2\]\*a\[i\]+a\[i-1\]\*a\[i+1\]（c\[1\]=a\[1\]\*a\[2\]；c\[n\]=c\[n-1\]+a\[n-1\]\*a\[n\]）

此时，从a\[l\]到a\[r\]乘积和为c\[r\]-c\[l-1\]。但这样a\[l\]\*a\[l+1\]变成了a\[l-1\]\*a\l+1\]；a\[r-1\]\*a\[r\]变成了a\[l-1\]\*a\[r+1\]，所以需要校正（当l=1或r=n是不用校正）。

## 注意
ans最大为100000\*100000\*200000爆longint，要用int64。

### 代码

激动人心的代码时刻到了。
```pascal
var a,b:array[0..200005] of longint;
  c:array[0..200005] of int64;
  n,i,j,m,x:longint;
  ans:int64;
function gcd(p,q:longint):longint;
begin
  if q=0 then exit(p);
  exit(gcd(q,p mod q));
end;
function find(l,r,x:longint):int64;
var k,bo:longint;
begin
  find:=0;
  bo:=gcd(r-l+1,x);
  if bo<>1 then\\分成环
    begin
      for k:= 1 to bo do
        find:=find+find(l+(r-l+1) div bo*(k-1),l+(r-l+1) div bo*k-1,x div bo);
    end
  else
    begin
      find:=c[r]-c[l-1];
      if r<>n then find:=find-a[r-1]*(a[r+1]-a[r]);
      if l<>1 then find:=find+a[l+1]*(a[l]-a[l-1]);
    end;
end;
procedure kb(l,r:longint);
var i,j,mid:longint;
begin
  i:=l;j:=r;mid:=a[(l+r) div 2];
  repeat
    while a[i]<mid do inc(i);
    while a[j]>mid do dec(j);
    if i<=j then
      begin
        a[0]:=a[i];a[i]:=a[j];a[j]:=a[0];inc(i);dec(j);
      end;
  until i>j;
  if i<r then kb(i,r);
  if l<j then kb(l,j);
end;
begin
  readln(n,m);
  for i:= 1 to n do
    begin
      read(a[i]);
      ans:=ans+a[i]*a[i];//k为0时
    end;
  kb(1,n);//别忘了排序
  c[1]:=a[1]*a[2];
  for i:= 3 to n do
    c[i-1]:=c[i-2]+a[i-2]*a[i];
  c[n]:=c[n-1]+a[n-1]*a[n];
  for i:= 1 to m do
    begin
      read(x);
      if x=0 then writeln(ans)
             else writeln(find(1,n,x));
    end;
end.

```