本题第一篇题解。

首先看到总共只有$10^3$条线段，且坐标绝对值不超过$10^4$，所以可以直接利用贪心。

先对每条线段按**右端点从小到大**排序。

接着，判断每一条线段是否被标记。

对于每一条未标记的线段，**标记最右端**。这是因为如果你标记了这一条线段，肯定是标记最右端最优，这样才可以标记更多的其他线段。

**注意事项**：

1.这些线段中有些线段需要反转，例如样例中的$4$ $2$，就应该特判。

2.坐标可能为负数(翻译中并未说明)。

代码：
```pascal
var n,i,j,ans:longint; flag:boolean;
f:array[-10001..10001] of boolean;  //负数坐标
a,b,c:array[0..100001] of longint;
procedure sort(l,r: longint);
      var i,j,x,y: longint;
      begin
         i:=l;  j:=r;
         x:=b[(l+r) div 2];
         repeat
           while b[i]<x do inc(i);
           while x<b[j] do dec(j);
           if not(i>j) then
             begin
                y:=a[i];a[i]:=a[j];a[j]:=y;
                y:=b[i];b[i]:=b[j];b[j]:=y;
                inc(i); j:=j-1;
             end;
         until i>j;
         if l<j then sort(l,j);
         if i<r then sort(i,r);
      end;          //按右端点从小到大排序

begin
readln(n);
for i:=1 to n do
  begin
  readln(a[i],b[i]);
  if a[i]>b[i] then
    begin a[0]:=a[i];a[i]:=b[i];b[i]:=a[0]; end;
                          //翻转线段
  end;
sort(1,n);
for i:=1 to n do
  begin
  flag:=false;
  for j:=a[i] to b[i] do
    if f[j] then
      begin flag:=true; break; end;  
                       //判断是否被标记
  if flag then continue;
  inc(ans); c[ans]:=b[i]; f[b[i]]:=true;
                       //标记最右的端点并保存
  end;
writeln(ans);
for i:=1 to ans do write(c[i],' ');  //输出答案
end.
```
