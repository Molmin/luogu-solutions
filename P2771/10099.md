这道题可以直接**暴力枚举**

栅栏把农场分割成一堆块

有m块就要m-1的门（两块之间要一个门）

代码如下：

```pascal
var
  total,n,x,y,a,b,c,d,i,j:longint;
  f,q:array[0..4001,0..4001]of boolean;
  ch:char;
procedure fsb(xx,yy:longint);
begin
  if (xx<a)or(xx>c)or(yy<b)or(yy>d)or(f[xx][yy]) then//如果超出划定的范围或来过就退出
    exit;
  f[xx][yy]:=true;//这个标记为已来过
  if not q[xx][yy+1] then//没有栅栏就往这个方向延伸
    fsb(xx,yy+2);
  if not q[xx][yy-1] then
    fsb(xx,yy-2);
  if not q[xx+1][yy] then
    fsb(xx+2,yy);
  if not q[xx-1][yy] then
    fsb(xx-2,yy);
end;
begin
  readln(n);
  a:=maxlongint;//从无限远端开始
  b:=maxlongint;
  c:=-maxlongint;
  d:=-maxlongint;
  x:=2000;//从中心开始
  y:=2000;
  for i:=1 to n do
    begin
      read(ch);
      if ch='N' then
        begin
          y:=y+2;
          q[x][y-1]:=true;//构建栅栏
        end;
      if ch='E' then
        begin
          x:=x+2;
          q[x-1][y]:=true;
        end;
      if ch='S' then
        begin
          y:=y-2;
          q[x][y+1]:=true;
        end;
      if ch='W' then
        begin
          x:=x-2;
          q[x+1][y]:=true;
        end;
      if x<a then//缩小枚举的范围，防止超时
        a:=x;
      if y<b then
        b:=y;
      if x>c then
        c:=x;
      if y>d then
        d:=y;
    end;
  dec(a);//往里一格
  dec(b);
  inc(c);
  inc(d);
  for i:=a to c do
    begin
      if (i-a)mod 2=1 then//跨2格
        continue;
      for j:=b to d do
        begin
          if (j-b)mod 2=1 then
            continue;
          if f[i][j] then//来过
            continue;
          fsb(i,j);
          inc(total);//加一块
        end;
    end;
  writeln(total-1);
end.
```