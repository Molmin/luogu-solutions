看到本题，果断BFS

因为考虑到数据范围，可以先求出所有点到1,1的距离，然后直接输出

广搜求距离应该都会吧，只要做小小的改动就可以了

具体思路见标程：

```cpp
const
  fang:array[1..12,1..2] of longint=
    ((-2,-2),(-2,2),(2,-2),(2,2),(-2,-1),(-2,1),
    (-1,-2),(-1,2),(1,-2),(1,2),(2,-1),(2,1));//坐标数组，表示下一步可以跳到的位置
var
  h:array[0..1000,1..2] of longint;//队列数组，因为数据，其实400就够了
  f:array[-1..100,-1..100] of boolean;//表示有没有走过的boolean数组
  a:array[0..100,0..100] of longint;//表示该点走到1，1的最小距离
  i,j,head,tail,x,xx,y,yy:longint;
begin
  head:=0;
  tail:=1;
  h[1,1]:=1;
  h[1,2]:=1;
  for i:=1 to 20 do
    for j:=1 to 20 do
      f[i,j]:=true;
  f[1,1]:=false;
  while head<tail do
    begin
      inc(head);
      x:=h[head,1];
      y:=h[head,2];//将原点赋到x，y中，方便后面，也简化了代码
      for i:=1 to 12 do//枚举12个方向
        begin
          xx:=x+fang[i,1];
          yy:=y+fang[i,2];//走到的位置
          if f[xx,yy] then//不需要判断边界，因为边界都是false
            begin
              f[xx,yy]:=false;
              inc(tail);
              h[tail,1]:=xx;
              h[tail,2]:=yy;
              a[xx,yy]:=a[x,y]+1;//关键，将最短的路径保存下来
            end;
        end;
    end;
  readln(x,y);
  writeln(a[x,y]);
  readln(x,y);
  writeln(a[x,y]);//由于a数组已经保存了最短步数，所以直接输出即可
end.
```