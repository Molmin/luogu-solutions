    










```cpp
const
    MAX_LEN = 400;
type 
    dataType = record //记录类型
        x : Integer;             //坐标x
        y : Integer;            //坐标y
        count : longint;    //共跳了几步
    end;
    queue = record  //队列
        head : longint;
        tail : longint;
        a : array[0..MAX_LEN - 1] of dataType;
    end;
    direction = array[1..12, 1..2] of integer;//可能跳到的12种方向的坐标
const 
    dir : direction = (
            (-1, -2), (-2, -1), (-2, 1), (-1, 2),
            (1, -2), (2, -1), (2, 1), (1, 2),
            (-2, -2), (-2, 2), (2, -2), (2, 2)
        );    
var
    q : queue;
    x1,y1,x2,y2 : integer;
    jumped : array[1..100, 1..100] of boolean;//判断是否跳过的布尔型数组
procedure init(var q:queue);//队列初始化
begin
    q.head := 0;
    q. tail := 0;
end;
function isEmpty(var q : queue): boolean;//队列判空
begin
    if q.head = q.tail then exit(true);
    exit(false);
end;
procedure push_back(var q : queue;data : dataType);//入队列
begin
    if ((q.tail + 1) mod MAX_LEN) <> q.head then 
    begin
        q.a[q.tail] := data;
        q.tail := (q.tail + 1) mod MAX_LEN;//巧妙的方法可以自己举例子试一试
    end;
end;
function pop_back(var q : queue): dataType;//出队列
var
    tmp: dataType;
begin
    if not isEmpty(q) then 
    begin
        tmp := q.a[q.head];
        q.head := (q.head + 1) mod MAX_LEN;
        exit(tmp);
    end;
    tmp.count := -1;
    exit(tmp);
end;
function canJump(x, y : integer): boolean;//判断是否跳过此点
begin
    if (x >= 1) and (x <= 20) and (y >=1) and (y <= 20) then exit(true);
    exit(false);
end;
function search(x, y:integer; d:direction; n :integer): longint;//广度优先搜索
var
    cur, next: dataType;
    i : integer;
begin
    cur.x := x;
    cur.y := y;
    cur.count := 0;
    jumped[x, y] := true;
    push_back(q, cur);
    while not isEmpty(q) do
    begin
        cur := pop_back(q);
        //writeln(cur.x, ' ', cur.y);
        if (cur.x = 1) and (cur.y = 1) then exit(cur.count);
        for i := 1 to n do
        begin
            if canJump( cur.x + d[i, 1], cur.y + d[i, 2] ) and not jumped[cur.x + d[i, 1], cur.y + d[i, 2]] then
            begin
                next.x := cur.x + d[i, 1];
                next.y := cur.y + d[i, 2];
                jumped[next.x, next.y] := true;
                next.count := cur.count + 1;
                push_back(q, next);
            end;
        end;
    end;
    exit(-1);
end;
begin
    readln(x1, y1);
    readln(x2, y2);
    init(q);
    fillchar(jumped, sizeof(jumped), false);
    writeln(search(x1, y1, dir, 12));
    init(q);
    fillchar(jumped, sizeof(jumped), false);
    writeln(search(x2, y2, dir, 12));
end.
```