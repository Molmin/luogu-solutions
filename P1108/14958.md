没人写二分栈的做法，我来写一个。

题意很好理解，求最长下降子序列，并求方案数。

我们定义stack[i]表示长度为i的下降子序列的最后一个数的大小。Len表示做到当前的时候找到的最长下降子序列长度。f[i]表示以第i个数结尾的最长下降子序列长度。

对于序列中的一个数a[i]来说，我们需要知道它可以接到哪个数的后面得到最长。于是在栈中进行二分查找，查找区间是(0,len)，找到栈中最后一个比a[i]大的数的位置。假设这个位置是p，那么f[i]=p+1。同时要对栈进行维护，如果stack[p+1]<a[i]，那么用a[i]替换stack[p+1]，因为留在栈中的数尽量越大越好。这样就能保证栈的单调性。

然后对于a[i]我们需要知道它的方案数，换言之就是它可以接到哪几个数的后面，于是定义w[i]表示以i结尾的最长下降子序列的方案数，可以得出w[i]=sum(w[j])，前提是(j<i,a[j]>a[i],f[j]+1=f[i])。

于是可以想到在栈中采用挂链的方式，将某些以它结尾的长度相等的数挂在stack[i]上，每次二分找出p后只要在p的那条链上找符合要求的w[j]，累加起来就行了。要注意的是二种方案“看起来一样”时（就是说它们构成的价格队列一样的时候）,这2种方案被认为是相同的。也就是说两个相同的序列就算位置不同也只能算一次。所以对于一条链来说，相同的数只能保留一个。

这样做的时间复杂度是O(nlogn)，对于5000的数据只要1ms，就算是100000的数据也能秒过。

附上代码

```pascal
var i,p,n,next,len:longint;
    a,w,q,h,stack,f:array[0..5010] of int64;
    s:int64;

function ef(l,r,x:longint):longint;
var mid:longint;
begin
if l=r then exit(l);
mid:=(l+r+1) shr 1;
if stack[mid]<=x then exit(ef(l,mid-1,x))
else exit(ef(mid,r,x));
end;

begin
read(n);
for i:=1 to n do read(a[i]);
stack[0]:=maxlongint;
inc(n);
a[n]:=-maxlongint;//在数列的最后放一个负无穷，那么所以的方案就直接汇集到n+1
for i:=1 to n do
        begin
        p:=ef(0,len,a[i]);
        s:=0;
        next:=h[p];
        while next<>0 do//在链中寻找合法的w[j]
                begin
                if a[next]>a[i] then inc(s,w[next]);
                next:=q[next];
                end;
        w[i]:=s;
        inc(p);
        f[i]:=p;
        if p>len then len:=p;
        if f[i]=1 then w[i]:=1;
        if stack[p]<a[i] then stack[p]:=a[i];//维护栈
        next:=h[p];
        while next<>0 do//将链中相同的数去掉
                begin
                if a[i]=a[next] then w[next]:=0;
                next:=q[next];
                end;
        q[i]:=h[p];h[p]:=i;//用静态链表挂链，h是一个表头，表示长度为i的最后一个数的位置
        end;
writeln(len-1,' ',w[n]);//最后那个加上去的负无穷算了一个长度，要减掉
end.
```