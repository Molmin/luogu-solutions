# 来一发Pascal的题解吧
### 首先，考虑暴力DFS。
每次DFS传入3个参数：当前节点编号，当前节点权值，父节点编号。

然后再DFS中枚举子节点的权值，如果与当前节点不同就继续往下DFS，并求出每棵子树的最小权值和，最后加上当前节点权值就是以这种情况的最小子树权值和，并返回。

## Code：（DFS，19 pts）：
```pascal
uses math;
var
  n,i,x,y,z:longint;
  r,t:array[0..20005]of longint;
  h:array[0..10005]of longint;
function dfs(root,y,fa:longint):longint;
var
  x,z,i,sum:longint;
begin
  sum:=y;
  x:=h[root];
  while x<>0 do
  begin
    if r[x]=fa then
    begin
      x:=t[x];
      continue;
    end;
    z:=maxlongint;
    for i:=1 to 20 do
      if i<>y then z:=min(z,dfs(r[x],i,root));
    sum:=sum+z;
    x:=t[x];
  end;
  exit(sum);
end;
begin
  readln(n);
  for i:=1 to n do
  begin
    readln(x,y);
    inc(z);
    t[z]:=h[x];
    h[x]:=z;
    r[z]:=y;
    inc(z);
    t[z]:=h[y];
    h[y]:=z;
    r[z]:=x;
  end;
  x:=maxlongint;
  for i:=1 to 20 do x:=min(x,dfs(1,i,1));
  write(x);
end.
```
### 考虑优化
我们可以先把以一号节点为根节点的树建起来，并且存下每一个节点的深度和每一棵子树内最深的点的深度。

可以得出，如果要使当前节点的一棵子树内权值和最小，树内每一个节点的权值一定不会超过当前节点的子树内最深深度减去当前节点深度加一。

为什么要加一呢？

因为可能出现子树内每一层权值都不同的情况，而如果当前节点在[1,最深深度-当前深度+1]的范围内时，就会导致子节点缺少一种权值取值情况，所以要加一。

## Code：（DFS+优化，28 pts）
```pascal
uses math;
var
  n,i,x,y,z:longint;
  r,t:array[0..20005]of longint;
  h,d,sd:array[0..10005]of longint;//d表示当前深度，sd表示子树中的最大深度。
procedure build(root,fa,dep:longint);//建树预处理深度
var
  x:longint;
begin
  d[root]:=dep;
  sd[root]:=dep;
  x:=h[root];
  while x<>0 do
  begin
    if r[x]=fa then
    begin
      x:=t[x];
      continue;
    end;
    build(r[x],root,dep+1);
    sd[root]:=max(sd[root],sd[r[x]]);
    x:=t[x];
  end;
end;
function dfs(root,y,fa:longint):longint;
var
  x,z,i,sum:longint;
begin
  sum:=y;
  x:=h[root];
  while x<>0 do
  begin
    if r[x]=fa then
    begin
      x:=t[x];
      continue;
    end;
    z:=maxlongint;
    for i:=1 to sd[r[x]]-d[root]+1 do//优化
      if i<>y then z:=min(z,dfs(r[x],i,root));
    sum:=sum+z;
    x:=t[x];
  end;
  exit(sum);
end;
begin
  readln(n);
  for i:=1 to n do
  begin
    readln(x,y);
    inc(z);
    t[z]:=h[x];
    h[x]:=z;
    r[z]:=y;
    inc(z);
    t[z]:=h[y];
    h[y]:=z;
    r[z]:=x;
  end;
  build(1,1,1);
  x:=maxlongint;
  for i:=1 to 20 do x:=min(x,dfs(1,i,1));
  write(x);
end.
```
## 正解：（树形DP）

我们设 $f_{i,j}$ 为第 $i$ 个点，取权值为 $j$ 的时候，这个点以及它的子树所能取到的最小的权值和。

很明显，由于相邻点的权值不能相同，所以我们只需要在当前点的所有的儿子的所有权值取法中，每一个儿子取一种与当前点不同的权值中最小的权值和，再加上当前点取的权值，就可以了。

而唯一的问题就是权值取值范围，这个我也不知道具体是多少，但我取 $20$ 过了，所以不会高于 $20$ ，大约在 $log$ $n$ 左右吧。

时间复杂度 $O(20*20*n)$ 。

表述可能不太清晰，具体怎么做请看代码。

## Code：（树形DP，100 pts）
```pascal
uses math;
var
  r,t:array[0..20005]of longint;
  h:array[0..10005]of longint;
  f:array[0..10005,0..25]of longint;
  x,y,z,i,n:longint;
procedure dp(root,fa:longint);//树形DP
var
  x,y,i,j:longint;
begin
  x:=h[root];
  while x<>0 do
  begin
    if r[x]=fa then//如果这条边通向它的父亲，就跳过
    begin
      x:=t[x];
      continue;
    end;
    dp(r[x],root);//先DP它的儿子
    for i:=1 to 20 do//枚举当前点取的权值
    begin
      y:=maxlongint;
      for j:=1 to 20 do//枚举当前点的儿子取的权值
      begin
        if i=j then continue;//如果权值相同就跳过
        y:=min(y,f[r[x],j]);//求最小值
      end;
      f[root,i]:=f[root,i]+y;//将最小值累加
    end;
    x:=t[x];//DP下一个儿子
  end;
  for i:=1 to 20 do f[root,i]:=f[root,i]+i;//将每一种情况加上当前取的权值
end;
begin
  readln(n);
  for i:=1 to n-1 do//读入，用链式前向星存储
  begin
    readln(x,y);
    inc(z);
    t[z]:=h[x];
    h[x]:=z;
    r[z]:=y;
    inc(z);
    t[z]:=h[y];
    h[y]:=z;
    r[z]:=x;
  end;
  dp(1,1);//将一号节点设置为根节点，DP
  x:=maxlongint;
  for i:=1 to 20 do x:=min(x,f[1,i]);//找出最小权值和
  write(x);//输出
end.
```
本人最近在转C++，也顺便贴一发C++的代码吧QwQ。
## C++ Code（码风丑勿喷）：
```cpp
#include<bits/stdc++.h>
using namespace std;
int r[20005] = {0}, t[20005] = {0}, h[10005] = {0};
int f[10005][25] = {0};
inline int read(){
	int x = 0, m = 1;
	char ch = getchar();
	while(!isdigit(ch)){
		if(ch == '-') m = -1;
		ch = getchar();
	}
	while(isdigit(ch)){
		x = x * 10 + ch - 48;
		ch = getchar();
	}
	return x * m;
}
inline void write(int x){
	if(x < 0){
		putchar('-');
		write(-x);
		return;
	}
	if(x >= 10) write(x / 10);
	putchar(x % 10 + '0');
}
void dp(int root, int fa){
	int x = h[root], y;
	while(x){
		if(r[x] == fa){
			x = t[x];
			continue;
		}
		dp(r[x], root);
		for(int i = 1; i <= 20; i++){
			y = 10000007;
			for(int j = 1; j <= 20; j++){
				if(i == j) continue;
				y = min(y, f[r[x]][j]);
			}
			f[root][i] += y;
		}
		x = t[x];
	}
	for(int i = 1; i <= 20; i++) f[root][i] += i;
}
int main(){
	int n = read(), x = 0, y = 0, z = 0;
	for(int i = 1; i < n; i++){
		x = read(), y = read();
		t[++z] = h[x];
		h[x] = z;
		r[z] = y;
		t[++z] = h[y];
		h[y] = z;
		r[z] = x;
	}
	dp(1, 1);
	x = 10000007;
	for(int i = 1; i <= 20; i++) x = min(x, f[1][i]);
	write(x);
}
```