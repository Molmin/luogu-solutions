首先看只有矩形才能算作一艘船，也就是说，如果这个图是合法的，那么里面的每一个连通块都是矩形。
然后开始搜，将每一个搜到的连通块的最小、最大横纵坐标记录，那么两点之间一定可以形成一个囊括该连通块的矩形，且如该连通块为矩形，该矩形与连通块重合。然后再check一遍该矩形内是否全为‘#’，则可判定是否有船相接触。(好吧写的有点乱)
代码

```pascal
program hz;
type wz=record x,y:longint; end;
const yd:array[1..4,1..2] of longint=((1,0),(0,1),(-1,0),(0,-1));
var
  a,b:array[0..1001,0..1001] of boolean;
  k:array[1..1000000] of wz;
  dl:array[1..1000000] of wz;
  now,min,max:wz;
  n,m,t,i,j,head,tail,ans:longint; g:char; gg:boolean;
function check(x1,y1,x2,y2:longint):boolean;//检查船只接触
var i,j:longint;
begin
  check:=true;
  for i:=x1 to x2 do
    for j:=y1 to y2 do
      if not(b[i,j]) then exit(false);
end;
begin
  readln(n,m);
  fillchar(a,sizeof(a),false);
  fillchar(b,sizeof(b),false);
  for i:=1 to n do begin
    for j:=1 to m do begin
      read(g);
      if g='.' then a[i,j]:=false
      else begin
        a[i,j]:=true;
        b[i,j]:=true;
        inc(t); k[t].x:=i; k[t].y:=j;
      end;
    end;
    readln;
  end;
  gg:=false;
  for i:=1 to t do begin//对每个连通块进行bfs
    if a[k[i].x,k[i].y] then begin
      head:=0; tail:=1; dl[1].x:=k[i].x; dl[1].y:=k[i].y;
      min.x:=maxlongint; min.y:=maxlongint;
      max.x:=0; max.y:=0;
      repeat
        inc(head); a[dl[head].x,dl[head].y]:=false;
        if dl[head].x>max.x then max.x:=dl[head].x;
        if dl[head].y>max.y then max.y:=dl[head].y;
        if dl[head].x<min.x then min.x:=dl[head].x;
        if dl[head].y<min.y then min.y:=dl[head].y;
        for j:=1 to 4 do begin
          if a[dl[head].x+yd[j,1],dl[head].y+yd[j,2]] then begin
            inc(tail);
            dl[tail].x:=dl[head].x+yd[j,1];
            dl[tail].y:=dl[head].y+yd[j,2];
            a[dl[tail].x,dl[tail].y]:=false;
          end;
        end;
      until head=tail;
      if check(min.x,min.y,max.x,max.y) then inc(ans)
      else begin gg:=true; break; end;
    end;
  end;
  if gg then writeln('Bad placement.')
  else writeln('There are ',ans,' ships.');
end.


```