看到题目之后，首先分别对两组数据进行排序，得到有序的两组数据。

接下来，首先对指向两个数组的指针都赋为1。

现在是重点中的重点，设i为指向第一组数据的指针，j为第二组，那么，若a[i]=b[j]，则这就是一个获得两个奖项的人，将他的编号和输入时的序号（注意，两者不同！！），保存在另外的数组当中。更重要的是，若a[i]<b[j]，那么我们需要将i+1，为什么呢？因为若有a[k]=b[j],则k一定大于i，因为b[j]>a[i]，且a数组是有序单调递增的！若有a[x]=b[y]，那么a[x],b[y]一定大于a[i]！因为，i，j是只增不减的，那么b[y]一定大于b[j]，那么也一定大于a[i]！a[i]<b[j]是如此，反之亦然。

接下来给出标程，希望在标称中自己体会。

```delphi
type
  arr=array[0..100000] of longint;
var
  x,y,z,x1,y1,z1:arr;
  i,j,k,m,n:longint;
procedure qsort(r,l:longint; var a,b:arr);//a为关键字，在交换a的时候，也同时交换b
var
  i,j,t,mid:longint;
begin
  i:=r; j:=l;
  mid:=a[random(j-i+1)+i];
  repeat
    while a[i]<mid do inc(i);
    while a[j]>mid do dec(j);
    if i<=j then
      begin
        t:=a[i]; a[i]:=a[j]; a[j]:=t;
        t:=b[i]; b[i]:=b[j]; b[j]:=t;
        inc(i); dec(j);
      end;
  until i>j;
  if i<l then qsort(i,l,a,b);
  if r<j then qsort(r,j,a,b);
end;
begin
  randomize;//随机化快排
  read(m,n);
  for i:=1 to m do begin read(x[i]); x1[i]:=i; end;
  for i:=1 to n do begin read(y[i]); y1[i]:=i; end;//读入两组数据，将读入时的序号保存下来
  qsort(1,m,x,x1);
  qsort(1,n,y,y1);//排序
  i:=1;
  j:=1;
  while (i<=m) and (j<=n) do//还有元素
    begin
      if x[i]=y[j] then begin inc(k); z[k]:=x[i]; z1[k]:=x1[i];
                        inc(i); inc(j); end else//相等，记入另外的数组
        if x[i]<y[j] then inc(i)//x[i]较小，i+1
                     else inc(j);
    end;
  qsort(1,k,z1,z);//根据编号将新的数组排序，注意是z1在z之前，因为z1是关键字
  for i:=1 to k do write(z[i],' ');
end.[codep/]
