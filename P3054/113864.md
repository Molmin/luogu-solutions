看到没有pascal的题解，就来一波吧~

思路：

1. 把所有速度从大到小排序 
2. 预处理出最快的速度来求出比赛的时间t 
3. 把每一只奶牛在t时间内能跑多少圈求出来，然后分别存整数部分跟小数部分。 
4. 用归并排序求出小数部分的逆序对数量
5. 要进行下取整，因为有小数是因为没够一圈，所以舍去
6. 注意精度问题

### p3054.pas
------------


```pascal
var
        a,zheng:array[0..100000] of int64;
        xiao,b:array[0..100000] of real;
        ans,bns,sum:int64;
        n,l,c,i:longint;
        t:real;
procedure kp(x,y:longint);//从大到小做快速排序
var
        l,r,m:longint;
begin
        l:=x;
        r:=y;
        m:=a[(l+r)>>1];//位运算更快哦~
        repeat
                while a[l]>m do inc(l);
                while a[r]<m do dec(r);
                if l<=r then begin
                        a[0]:=a[l];
                        a[l]:=a[r];
                        a[r]:=a[0];
                        inc(l);
                        dec(r);
                end;
        until l>r;
        if x<r then kp(x,r);
        if l<y then kp(l,y);
end;
procedure gb(x,y:longint);//归并找逆序对
var
        l,r,h,m,i:longint;
begin
        if x=y then exit;
        m:=(x+y)>>1;//位运算更快哦~
        gb(x,m);
        gb(m+1,y);
        l:=x;
        r:=m+1;
        h:=x;
        while (l<=m) and (r<=y) do begin
                if xiao[l]-xiao[r]>-0.000001 then begin//注意精度
                        b[h]:=xiao[l];
                        inc(l);
                        inc(h);
                end else begin
                        b[h]:=xiao[r];
                        inc(h);
                        inc(r);
                        bns:=bns+m-l+1;
                end;
        end;
        while l<=m do begin
                b[h]:=xiao[l];
                inc(l);
                inc(h);
        end;
        while r<=y do begin
                b[h]:=xiao[r];
                inc(r);
                inc(h);
        end;
        for i:=x to y do xiao[i]:=b[i];
end;
begin
        read(n,l,c);
        for i:=1 to n do readln(a[i]);
        kp(1,n);
        t:=l*c/a[1];
        for i:=1 to n do begin
                zheng[i]:=trunc(a[i]*t/c);//整数部分
                l:=zheng[i]*c;
                xiao[i]:=a[i]*t-l;//小数部分
        end;
        for i:=n downto 1 do begin
                sum:=sum+(n-i)*(zheng[i]-zheng[i+1]);
                ans:=ans+sum;
        end;
        gb(1,n);
        writeln(ans-bns);
end.
```