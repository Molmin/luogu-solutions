
### 先说点别的
- emmm......,这是本蒟蒻的第一篇题解，大佬勿喷QwQ（~~其实是因为题解里没有Pascal的~~......）


------------


# 正文


------------





#### 前置技能
- SPFA:右转[P1608路径统计](https://www.luogu.org/problem/P1608)(可以顺带AC掉[P1144最短路计数](https://www.luogu.org/problem/P1144))

- DP:(~~嗯...应该不用说了吧~~)

#### 题意：求dis(1,n)<=MinDis(1,n)+k的路径数(mod p)


------------

### 1. 30pts：K=0
       裸的SPFA
### 2. 70pts：没有0边
       先求出MinDis(1,u)然后令f[u][l]为dis(1,u)=MinDis(1,u)+l的路径数
       f[u,l]:=(f[u,l]+f[v,l+w(u,v)+MinDis(1,v)-MinDis(1,u)]) mod p (v<u)
       反向建图然后从f[n,i])(0≦i≦k)开始记忆化枚举，最后累加并输出
       PS:w(u,v)指连接u～v边的长度,w(u,v)-MinDis(1,v)-MinDis(1,u)]指从u转移到v所增加的冤枉路
### 3. 100pts：70pts+0边
       在70pts的基础上引入一个新数组flag[u][l],表示[u,l]这个状态是否被访问过(初始值均为0），
       若访问时遇到flag[u,l]=1,则说明出现零环(因为l这一维不可能在u不变的情况下仍保持不变）
       PS:[u,l]在转移过程中又回到u点，l必会改变(即使有边值不为0的环，MinDis(u,v)和w(u,v)也必有差异，因此不可能出现这样的情况)

## 细节见以下代码(代码较长，敬请原谅)
```pascal
var i,j,n,m,k,p,a1,b1,c1,z,tot,tot1,t,sum:longint; next,val,vet,head,next1,val1,vet1,head1,que,que1:array[0..200005] of longint;
   d,alive:array[0..100005] of longint; ans,flag:array[0..100005,0..55] of longint;
procedure add(u,v,cost:longint);//链式前向星(边表）构建图(不懂的同学可动手模拟每个数组的变化)
begin
  inc(tot);
  next[tot]:=head[u];
  val[tot]:=cost;
  vet[tot]:=v;
  head[u]:=tot;
end;
procedure add1(u,v,cost:longint);//构建反向图(数组要分开用,即构建u>v的图)
begin
  inc(tot1);
  next1[tot1]:=head1[u];
  val1[tot1]:=cost;
  vet1[tot1]:=v;
  head1[u]:=tot1;
end;
procedure spfa;//裸的SPFA,(就不多讲了)PS:这里是正着遍历
var i,l,r,x,y,z1,e:longint;
begin
  for i:=2 to n do d[i]:=1000000000;
  d[1]:=0;
  l:=1; r:=1; que[1]:=1;
  while l<=r do
  begin
    x:=que[l]; inc(l);
    e:=head[x];
    while e<>0 do
    begin
      y:=vet[e]; z1:=val[e];
      if d[y]>d[x]+z1 then
      begin
        d[y]:=d[x]+z1;
        inc(r); que[r]:=y;
      end;
      e:=next[e];
    end;
  end;
end;
function dfs(a,b:longint):longint;//反向遍历,因为是记忆化，状态从n来
var e,y,z1,u,w,key:longint;
begin
  if (b<0) or (b>k) then exit(0);//状态不合法
  if flag[a,b]=1 then
  begin
    flag[a,b]:=0;
    exit(-1000000000);
  end;//判0环
  if ans[a,b]<>-1 then exit(ans[a,b]);//记忆化
  flag[a,b]:=1;//标记为已访问 key:=0;
  e:=head1[a];
  while e<>0 do
  begin
    y:=vet1[e]; z1:=val1[e]; u:=d[y]-d[a];
    w:=dfs(y,b-(z1+u));//遍历邻点
    if w=-1000000000 then
    begin
      flag[a,b]:=0;
      exit(-1000000000);
    end;
    key:=(key+w) mod p;
    e:=next1[e];
  end;
  if (a=1) and (b=0) then inc(key);
  ans[a,b]:=key;//记忆化
  flag[a,b]:=0;//归零
  exit(key);
end;
begin
  readln(t);
  while t>0 do
  begin
    readln(n,m,k,p);
    for i:=1 to n do begin head[i]:=0; head1[i]:=0; end;
    for i:=1 to n do
      for j:=0 to k do
      begin
        ans[i,j]:=-1;
        flag[i,j]:=0;
      end;
    tot:=0; tot1:=0;//每一组数据读入前变量清空
    for i:=1 to m do
    begin
      readln(a1,b1,c1);
      add(a1,b1,c1);
      add1(b1,a1,c1);//反向建图
    end;
    spfa;
    sum:=0;
    for i:=0 to k do
    begin
      z:=dfs(n,i);
      if z=-1000000000 then
      begin
        sum:=-1; break;
      end;
      sum:=(sum+z) mod p;
    end;//累加
    writeln(sum);
    dec(t);
  end;
end.


```

       
       