#下面Pascal的注释比较少，有点麻烦，我也来凑个热闹#

##ZJOI的题会是水题？好吧，确实有点难（shui）##

当然最小次数肯定用BFS。9个数最多也只会出现9!种方法，400000的队列足够了。但是判重怎么办呢？数组元素必须连续啊，肯定不能开9^9的数组去判重吧。这里介绍一种特殊的哈希：

康托展开。用康托展开的公式能求出一个排列是所有排列中的第几个（从0开始）。那么012345678就对应0，876543210就对应362879，一共362880个，也就是9!。那么存储空间就大大减少了。其他就是裸的BFS了。

```cpp
type
  arr=array[0..8]of longint;
var
  q:array[0..400000]of arr;
  b:array[0..400000]of boolean;
  pre,s:array[0..400000]of longint;
  h,t:longint;
function cantor(x:longint):longint; //康托展开，对q[x]进行哈希
var
  i,j,k,tmp,ans:longint;
begin
  tmp:=1; ans:=0;
  for i:=0 to 8 do 
    begin
      k:=0;
      for j:=0 to i-1 do 
        if q[x,j]>q[x,i] then inc(k);
      ans:=ans+k*tmp;
      tmp:=tmp*(i+1);
    end;
  exit(ans);
end;
function move1():longint; //操作1，顺便返回新的排列的康托展开
var
  tmp:longint;
begin
  inc(t);
  q[t,0]:=q[h,3];
  q[t,1]:=q[h,0];
  q[t,2]:=q[h,1];
  q[t,3]:=q[h,6];
  q[t,4]:=q[h,4];
  q[t,5]:=q[h,2];
  q[t,6]:=q[h,7];
  q[t,7]:=q[h,8];
  q[t,8]:=q[h,5];
  tmp:=cantor(t);
  if b[tmp] then begin dec(t); exit; end; //已经出现过了，就删去
  b[tmp]:=true; pre[t]:=h; s[t]:=s[h]+1; //入队，pre表示前一个状态，s表示转换了几次
  exit(tmp);
end;
function move2():longint; //与操作1同理
var
  tmp:longint;
begin
  inc(t); q[t]:=q[h];
  q[t,3]:=q[h,5];
  q[t,4]:=q[h,3];
  q[t,5]:=q[h,4];
  tmp:=cantor(t);
  if b[tmp] then begin dec(t); exit; end;
  b[tmp]:=true; pre[t]:=h; s[t]:=s[h]+1;
  exit(tmp);
end;
procedure print(x:longint); //递归输出，利用pre数组
begin
  if pre[x]>0 then print(pre[x]);
  writeln(q[x,0],' ',q[x,1],' ',q[x,2]);
  writeln(q[x,3],' ',q[x,4],' ',q[x,5]);
  writeln(q[x,6],' ',q[x,7],' ',q[x,8]);
  writeln;
end;
procedure bfs();
var
  i:longint;
begin
  fillchar(b,sizeof(b),0);
  h:=0; t:=1; pre[1]:=0; s[1]:=0;
  for i:=0 to 8 do read(q[1,i]); //初始状态进队列
  b[cantor(1)]:=true; //初始状态置true
  while h<t do 
    begin
      inc(h);
      if move1()=0 then begin writeln(s[t]); print(t); exit; end;
      if move2()=0 then begin writeln(s[t]); print(t); exit; end;
    end; //返回值也就是哈希值等于0，说明是0123456789（目标状态），就输出
  writeln('UNSOLVABLE'); //队列空了说明不行
end;
begin
  bfs();
end.
```