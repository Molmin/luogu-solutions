其实这题就是floodfill+tarjan。

开始先bfs一遍，如果不能被收买直接halt终止程序。

否则就说明都能被收买。那么tarjan求强连通分量，

同时进行缩点，并统计出每个强连通分量中最小的被收买的价格，

最后把入度为零的强连通分量的最小被收买价格累加起来。

我的代码已经写的很清楚了，照我的写就可以啦~~~

```delphi


program jdwl;
type point=^node;
     node=record
            data:longint;
            next:point;
          end;
var newn,n,i,r,mcost,ed,time,hao,a,b,p,head,tail:longint;
    v,sfzz:array[0..6100]of boolean;
    con:array[0..6100]of point;
    rp:point;
    d,dfn,low,zhan,cost,rd,fan,dgcost,belong:array[0..6100]of longint;

function min(a,b:longint):longint;
begin
  if a<b then exit(a);
  exit(b);
end;

procedure add(a,b:longint);
var p:point;
begin
  new(p);
  p^.data:=b;
  p^.next:=con[a];
  con[a]:=p;
end;

procedure bfs;//不能收买的情况直接floodfill
var p:point;
    zi:longint;
begin
  while head<=tail do
    begin
      p:=con[d[head]];
      while p<>nil do
        begin
          zi:=p^.data;
          if v[zi]=false then
            begin
              inc(tail);
              d[tail]:=zi;
              v[zi]:=true;//dfs开头,bfs中间,进行标记，进栈等
            end;
          p:=p^.next;
        end;
      inc(head);
    end;
end;

procedure dfs(x:longint);
var p:point;
    zi,st,i:longint;//zi:儿子节点
begin
  v[x]:=true;//dfs开头,bfs中间,进行标记，进栈等

  inc(ed);//入栈
  zhan[ed]:=x;//栈里每个编号对应一个点
  fan[x]:=ed;//每个点对应栈里一个编号
  sfzz[x]:=true;

  inc(time);
  dfn[x]:=time;
  low[x]:=time;
  //dfn:时间戳;low:能追溯到的栈中最小的时间戳

  p:=con[x];
  while p<>nil do
    begin
      zi:=p^.data;
      if v[zi]=false then
        begin
          dfs(zi);
          low[x]:=min(low[x],low[zi]);
          //如果未被访问过，找本身和儿子结点中最小的low
        end

                      else
        if sfzz[zi]=true then
          low[x]:=min(low[x],dfn[zi]);
        //如果在栈里，找本身的low和儿子结点的dfn中更小的
      p:=p^.next;
    end;

  if dfn[x]=low[x]then //出栈
    begin
      inc(newn);//强连通分量个数标号
      st:=fan[x];
      for i:=st to ed do
        begin
          sfzz[zhan[i]]:=false;//还原
          belong[zhan[i]]:=newn;
          //标记属于哪一个强连通分量
          if(cost[zhan[i]]>-maxlongint+1)and
            (cost[zhan[i]]<dgcost[newn])then
              dgcost[newn]:=cost[zhan[i]];
          //求出当前强连通分量的mincost
        end;
      ed:=st-1;
    end;
end;

begin
  readln(n);

  mcost:=0;
  newn:=0;
  for i:=1 to n do
    begin
      cost[i]:=-maxlongint+1;
      dgcost[i]:=maxlongint-1;
      v[i]:=false;
      sfzz[i]:=false;
      rd[i]:=0;
      con[i]:=nil;
    end;

  readln(p);
  for i:=1 to p do
    readln(hao,cost[hao]);

  readln(r);
  for i:=1 to r do
    begin
      readln(a,b);
      add(a,b);
    end;

  for i:=1 to n do
    if(v[i]=false)and(cost[i]>-maxlongint+1)then
      begin
        head:=1;
        tail:=1;
        d[1]:=i;
        v[i]:=true;
        bfs;
      end;//从每个可以被收买的点出发进行填充

  for i:=1 to n do
    if v[i]=false then
      begin
        writeln('NO');
        writeln(i);
        halt;
      end;


  for i:=1 to n do v[i]:=false;
  for i:=1 to n do
    begin
      if v[i]=true then continue;
      ed:=0;
      time:=0;
      //可清零可不清
      dfs(i);
    end;

  for i:=1 to n do
    begin
      rp:=con[i];
      while rp<>nil do
        begin
          if belong[i]<>belong[rp^.data]then
            inc(rd[belong[rp^.data]]);
          //若两个点不属于同一个强连通分量,
          //则子节点所属的强连通分量入度加一
          rp:=rp^.next;
        end;
    end;

  for i:=1 to newn do
    if rd[i]=0 then inc(mcost,dgcost[i]);
  //找入度为零的强连通分量进行统计累加
  writeln('YES');
  writeln(mcost);
end.      


```