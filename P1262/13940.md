本人能写的Pascal代码不多了，这个题解当做纪念吧。

题目大意：给你一些点，再指定其中的一部分点并加上权值，问是否有一种方案，使得从指定的点中的某几个点开始对图遍历，能把整个图遍历完，并且权值最小。

对于打出NO的，其实很简单：以每个指定的点为起始点做遍历，每到一个新的点就把计数器+1，最后比一下计数器数值与n的大小即可，如果打出NO则再进行一次爆扫即可。

那么如何处理计数器大小等于n，即打出YES的呢？

退一步思考，如果这个有向图是个DAG，怎么办？

这里只需要把所有入度为0的点的权值相加即可。（这一切都建立在能控制所有间谍的基础上）

因为如果不选用入度为0的点，那这些点就是无人控制的。

选了这些点之后，图也一定能被他们及他们的子孙控制，就是说其他备选的点不用选了。

那么好吧，怎么把一个带环有向图变成DAG？

强连通分量的模型就出来了。

本人采用的是tarjan缩点。

代码如下：

var first,last,money,name,r,temp,fa,dfn,low:array [1..3000] of longint;

```cpp
money1:array [1..3000] of longint;
b,int:array [1..3000] of boolean;
next,en:array [1..8000] of longint;
n,m,n1,top,ans,i,j,x,y,index,all:longint;
procedure inn;
begin
  assign(input,'spy.in');reset(input);
  assign(output,'spy.out');rewrite(output);
end;
procedure ot;
begin
  close(input);
  close(output);
end;//不要管他们。
procedure add(x,y:longint);
begin
  inc(top);
  if first[x]=0 then first[x]:=top
  else next[last[x]]:=top;
  en[top]:=y;
  last[x]:=top;
end;//邻接表
procedure search(x:longint);
var i:longint;
begin
  if b[x] then exit;
  inc(top);
  i:=first[x];
  b[x]:=true;
  while i>0 do
  begin
    search(en[i]);
    i:=next[i];
  end;
end;//图的遍历模块
procedure push(x:longint);
begin
  inc(top);
  temp[top]:=x;
end;
function pop:longint;
begin
  dec(top);
  exit(temp[top+1]);
end;
function min(a,b:longint):longint;
begin
  if a<b then exit(a);
  exit(b);
end;
procedure scc(u:longint);
var i,x:longint;
begin
  push(u);
  int[u]:=true;
  b[u]:=true;
  inc(index);
  dfn[u]:=index;
  low[u]:=index;
  i:=first[u];
  while i>0 do
  begin
    if not b[en[i]] then
    begin
      scc(en[i]);
      low[u]:=min(low[u],low[en[i]]);
    end
    else
      if int[en[i]] then
      low[u]:=min(low[u],dfn[en[i]]);
    i:=next[i];
  end;
  if dfn[u]=low[u] then
  begin
    inc(ans);
    repeat
      x:=pop;
      fa[x]:=ans;
      int[x]:=false;
    until x=u;
  end;
end;//tarjan模块，不知道的话自己百度。
procedure endl;
var i:longint;
begin
  for i:=1 to n do
    if not b[i] then break;
  writeln('NO');
  write(i);
  ot;
  halt;
end;//打NO的
begin
  inn;
  read(n,n1);
  fillchar(money,sizeof(money),60);
  for i:=1 to n1 do
  begin
    read(name[i],x);
    money[name[i]]:=x;
  end;
  read(m);
  for i:=1 to m do
  begin
    read(x,y);
    add(x,y);
  end;
  top:=0;
  for i:=1 to n1 do
    search(name[i]);
  if top<n then endl;
  fillchar(b,sizeof(b),false);
  top:=0;
  for i:=1 to n do
    if not b[i] then scc(i);//tarjan
  fillchar(money1,sizeof(money1),127);
  for i:=1 to n do
  begin
    money1[fa[i]]:=min(money1[fa[i]],money[i]);
    j:=first[i];
    while j>0 do
    begin
      if fa[en[j]]<>fa[i] then inc(r[fa[en[j]]]);
      j:=next[j];
    end;
  end;//统计入度与fa点的权值。一个强连通分量的权值是这个分量内各个点权值的最小值（因为其中任何一个点都能控制分量中的其余所有点）
  for i:=1 to ans do
    if r[i]=0 then all:=all+money1[i];
  writeln('YES');
  write(all);
  ot;
end.
```