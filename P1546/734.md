这道题目一般有两种解法，介绍一种（prim算法）；

算法时间复杂度是O(N^2);空间O(N^2);

```cpp
//首先B数组负true
fillchar(b,sizeof(b),true);
//首先将第一个节点加入，B数组负false
 b[1]:=false;
//用min[i]表示当前连接到i节点最少的路径
for i:=1 to n do min[i]:=a[1,i];
//然后每次贪心取最少的
for i:=2 to n do
    begin
      w:=0;
      for j:=2 to n do
        if b[j] then
          if min[j]<min[w] then w:=j;
//取好后，B数组负false
b[w]:=false;
```
累加总和  inc(ans,min[w]);
并且更新min数组的值

```cpp
for j:=2 to n do
        if b[j] then
          if a[w,j]<min[j] then
            min[j]:=a[w,j];
```
具体程序如下：
```cpp
var
  n,i,j,w,ans:longint;
  a:array[0..101,0..101] of longint;
  b:array[0..1000] of boolean;
  min:array[0..1000] of longint;
begin
  read(n);
  for i:=1 to n do
    for j:=1 to n do
      read(a[i,j]);//读入
  for i:=1 to n do min[i]:=a[1,i];//设置min数组的初值
  fillchar(b,sizeof(b),true);//B数组负true
  b[1]:=false;
  min[0]:=maxlongint;//为了简化代码，其实用一个数记录最小也可以
  for i:=2 to n do  // 把所有的节点全都连接，共需要n-1次（不包括第一次）
    begin
      w:=0;
      for j:=2 to n do
        if b[j] then //判重与回路
          if min[j]<min[w] then w:=j;
      inc(ans,min[w]);
      b[w]:=false;//非常关键，否则出错
      for j:=2 to n do
        if b[j] then //同上
          if a[w,j]<min[j] then
            min[j]:=a[w,j];//每次更新min的值，以便下次使用
    end;
  writeln(ans);
end.{prim算法}
```