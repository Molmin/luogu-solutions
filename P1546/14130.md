此题为典型的求最小生成树问题，Prim算法最佳，但是Kruskal算法同样适用，在这里提供Kruskal算法求最小生成树的pascal代码，运用并查集判断是否形成回路

```delphi

type node=record//边的记录
     v1,v2,s:longint;//v1，v2为边所连接的两点，s为农场间的距离，即光纤长度
     end;
var i,j,n,ans,p,k:longint;a:array[1..10000] of node;//把边以数组形式存储在a数组中
    f:array[1..10000] of longint;//父结点数组
function gf(x:longint):longint;//求根节点的过程
begin
    if f[x]=x then exit(x);
    f[x]:=gf(f[x]);
    exit(f[x]);
end;
procedure union(a,b:longint);//合并两集合的过程
begin
    if gf(a)<>gf(b) then f[gf(a)]:=gf(b);
end;
procedure sort(l,r: longint);//快排过程
      var
         i,j,x: longint;y:node;
      begin
         i:=l;
         j:=r;
         x:=a[(l+r) div 2].s;
         repeat
           while a[i].s<x do
            inc(i);
           while x<a[j].s do
            dec(j);
           if not(i>j) then
             begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
begin
    readln(n);//读入农场数
    for i:=1 to n do//因为读入数据以邻接矩阵的形式读入，所以在使用Kruskal算法时要注意转换成边集数组形式
      for j:=1 to n do
      begin
          inc(k);//k为边的数组下标
          a[k].v1:=i;//i，j分别为边连通的两点，记入到记录里
          a[k].v2:=j;
          read(p);
          a[k].s:=p;//存储此边的长度
      end;
      for i:=1 to n do
        f[i]:=i;//初始化各结点的集合，形成不同的集合
      sort(1,k);//把边按照长度排序
      i:=1;//i为要求的第i条边
      j:=1;//j为边的数组下标
      while i<=n-1 do
      begin
          if (gf(a[j].v1)<>gf(a[j].v2))and(a[j].s<>0) then//因为读入数据中，当两结点相同时会出现长度为0的情况，所以判断是否保留边时要排除，如果此边连接的两结点不在同一集合内则保留此边
          begin
              inc(i);//i后移
              ans:=ans+a[j].s;//更新光纤总长度
              union(a[j].v1,a[j].v2);//合并连接的两结点
          end;
          inc(j);//探究下一条边
      end;
      writeln(ans);//输出结果
end.

```