~~pascal来一发~~

以我的大蒟蒻来看，这题目是一题很经典的图论中的最小生成树！！

我的做法是prim的算法；

题意是：包含连接到每个农场的光纤的最小长度。

所以很符合我们的**prim**算法；

**详见代码注释**：

```pascal
var 
    a:array[1..100,1..100]of longint;//两个村庄之间路程多远
    dis:array[1..100]of longint;//每次更新的到目前所有点的最短距离
    used:array[1..100]of boolean;//有没有连上
    i,j,n,ans,k,sum:longint;
begin
  read(n);
  for i:=1 to n do dis[i]:=maxlongint;//初始成无穷大
  for i:=1 to n do
   for j:=1 to n do
    read(a[i,j]);//输入任意两个村庄之间的距离
  for i:=2 to n do dis[i]:=a[1,i];//从第一个村庄开始连，初始每个村庄和第一村庄之间的距离
  used[1]:=true;//第一个已联通
  for i:=2 to n do
   begin
    ans:=maxlongint;//初始成最大
    for j:=1 to n do
     if not(used[j])and(dis[j]<ans) then//判断没有在已联通村庄中
      begin  ans:=dis[j];k:=j;end;//求和已联通的村庄中任何一个联通最短的村庄
    sum:=sum+dis[k];//计算目前的总电缆长度
    used[k]:=true;//标记已联通
    for j:=1 to n do
     if not(used[j])and(dis[j]>a[k,j]) then//判断没有联通
      dis[j]:=a[k,j];//更新和目前联通村庄中任何一个的最短距离
   end;
   writeln(sum);
end.
```
~~偶自己认为我的代码还算短~~