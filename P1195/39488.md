简单的最小生成树（手动滑稽）

这道题目就是问怎么用最小的费用连成K个联通块

而最小生成树是用n-1条边最小费用的连成一个联通块

有相通之处哦

下面的大佬有的说建K个最小生成树

我的方法是先建一个最小生成树，然后删去k-1条边 一个最小生成树形成K个联通块了

贪心的我们肯定是删去最小生成树中最大的k-1条边

最小生成树的话用kruskal就可以了。就是把边权排序再用并查集

### **P党福利！！**
```
var
  point,a1,a2,que,tot,i,j,m,n,k,p:longint;
  father,ans,x,y,w:array[0..10005]of longint;
procedure qs(l,r:longint);//快排
var
  i,j,mid,a1:longint;
begin
  i:=l; j:=r;
  mid:=w[(l+r) div 2];
  repeat
    while w[i]<mid do inc(i);
    while w[j]>mid do dec(j);
    if not(i>j) then
      begin
        a1:=w[i]; w[i]:=w[j]; w[j]:=a1;
        a1:=x[i]; x[i]:=x[j]; x[j]:=a1;
        a1:=y[i]; y[i]:=y[j]; y[j]:=a1;
        inc(i); dec(j);
      end;
  until i>j;
  if i<r then qs(i,r);
  if l<j then qs(l,j);
end;
function find(t:longint):longint;//这里用了路径压缩
begin
  if father[t]=t then exit(t)
  else
    begin
      father[t]:=find(father[t]);
      find:=father[t];
    end;
end;
begin
  readln(n,m,k);
  for i:=1 to m do
    readln(x[i],y[i],w[i]);
  if k>n then//如果云不够连成K个棉花糖
    begin
      writeln('No Answer');
      exit;
    end;
  qs(1,m);//好羡慕你们C++啊
  for i:=1 to n do father[i]:=i;
  for i:=1 to m do
    begin
      a1:=find(x[i]); a2:=find(y[i]);
      if a1<>a2 then
        begin
          father[a1]:=a2;
          inc(tot); que:=que+w[i];//记录最小生成树的总权值
          ans[tot]:=w[i];//记录最小生成树种用了哪些边权
        end;
      if tot=n-1 then break;
    end;
  if k<>1 then
  for i:=n-1 downto 1 do
    begin
      que:=que-ans[i]; inc(point);
      if point=k-1 then break;
    end;
  writeln(que);
end.
```