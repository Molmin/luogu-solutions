# pascal的题解好少……

刚开始看这道题的时候，一脸懵逼……

首先简化问题，把$k$本书一次性全取出来，

后来发现，书的高度在$25到32$之间，居然这么小，最后想到了状压$dp$。

接下来考虑状态，首先$i$表示前$i$本书，$j$表示取了$j$本书，这是肯定要的。

还有就是留下的书中最后一本的高度，定义为$l$，以及每本书的高度的状态，不过这个状态表示什么呢？最后想到，我们还得把书摆回去，于是状态就记录留下的书中有没有一个高度的书，没有就为$0$，否则为$1$，这个状态定义为$k$，最后把书摆回去的时候，如果这个高度的书全被取出来了，就需要再加一段，此时状态中那个位为$0$，只要加上状态中$0$的个数即可。

#### 现在我们列方程：

$f[i,j+1,k,l]=min(f[i,j+1,k,l],f[i-1,j,k,l])$ {把这本书取出来} 

$f[i,j,k,l]=min(f[i,j,k,l],f[i-1,j,k,l])$ {$l=a[i]$时，把这本书留下}

$f[i,j,k,l]=min(f[i,j,k|(1<<a[i]),a[i]],f[i-1,j,k,l]+1)$ {其它情况下，把这本书留下，就会多了一段}（原谅我用了$C++$的打法，$pascal$打不出来或和左移）

### 下面是注意事项：

1、如果有些高度本来就没有的，就不需要再记录状态，最后算0时要减去。

2、注意边界

3、要用滚动数组，否则$MLE$！

时间复杂度$O(NK2^88)$

##### 以下是代码:

```pascal
var f:array[0..1,0..100,0..255,0..8] of longint;
    a,co:array[0..255] of longint;
    hash:array[0..8] of boolean;
    n,m,cse,minn,maxx,p,q,i,j,k,l:longint;
function min(a,b:longint):longint; begin if a<b then exit(a);exit(b);end;
function max(a,b:longint):longint; begin if a>b then exit(a);exit(b);end;
function count0(k:longint):longint;
var tot:longint;
begin
  tot:=8;
  while k>0 do
  begin
    dec(tot);dec(k,k and (-k));
  end;
  exit(tot);
end;
begin
  readln(n,m);
  for i:=0 to 255 do co[i]:=count0(i); //预处理二进制数0的个数
  while (n>0) or (m>0) do
  begin
    minn:=maxlongint;maxx:=-maxlongint;p:=0;fillchar(hash,sizeof(hash),false);
    for i:=1 to n do begin read(a[i]);dec(a[i],25);hash[a[i]]:=true;if a[i]>maxx then maxx:=a[i];end;inc(maxx); //哈希表判断高度是否存在
    for i:=0 to 7 do if hash[i] then inc(p);
    fillchar(f,sizeof(f),$7f);
    f[1,0,1 shl a[1],a[1]]:=1;f[1,1,0,maxx]:=0; //边界，取或不取第一个
    for i:=2 to n do //dp
    begin
      fillchar(f[i and 1],sizeof(f[i and 1]),$7f);
      for j:=0 to min(m,i-1) do
        for k:=0 to (1 shl maxx)-1 do
          for l:=0 to maxx do
          begin
            if f[(i-1) and 1,j,k,l]>2000000000 then continue; //这句话如果不加，会TLE
            if j<m then f[i and 1,j+1,k,l]:=min(f[i and 1,j+1,k,l],f[(i-1) and 1,j,k,l]);
            if l=a[i] then f[i and 1,j,k,l]:=min(f[i and 1,j,k,l],f[(i-1) and 1,j,k,l])
            else f[i and 1,j,k or (1 shl a[i]),a[i]]:=min(f[i and 1,j,k or (1 shl a[i]),a[i]],f[(i-1) and 1,j,k,l]+1);
          end;
    end;
    for i:=0 to m do
      for j:=0 to (1 shl maxx)-1 do
        for k:=0 to maxx-1 do minn:=min(minn,f[n and 1,i,j,k]+co[j]);
    inc(cse);
    writeln('Case ',cse,': ',minn-8+p);writeln; //输出去掉本身就不存在的，不要忘了再换一行
    readln(n,m);
  end;
end.
```