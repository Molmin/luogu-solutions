本题的算法是BFS或者DFS不用说注意hash判重！

三个操作：

1.将s的任意两位对换生成新的数字，例如143可以生成314，413，134；

2.将s的任意一位删除生成新的数字，例如143可以生成14，13，43

3.在s的相邻两位之间s[i]，s[i + 1]之间插入一个数字x，x需要满足s[i] < x < s[i + 1]且生成数的位数不能超过初始数s的位数

bfs拓展的时候依次枚举每一次操作的每一种可能，比较麻烦要注意检查。

注意bfs不能写在线必须写离线，因为问答太多，在线T的会比较厉害，不提倡

一次离线bfs把所有可能（桶来保存数i的最小操作次数）枚举出来再O（1）在线查询。

接下来是每个操作的拓展：

操作1;//拓展时枚举到底是那两位对换

```cpp
for i:=1 to length(hs) do
   for j:=i+1 to length(hs) do begin
    ts:=hs;
    ts[i]:=hs[j];
    ts[j]:=hs[i];
    val(ts,t);
    if hash[t] then continue;
    inc(tail);
    q[tail].step:=q[head].step+1;
    val(ts,q[tail].num);
    a[q[tail].num]:=min(a[q[tail].num],q[tail].step);
    hash[q[tail].num]:=true;
  end;
```
操作2；//拓展时枚举到底是一位数组删除

```cpp
for i:=1 to length(hs) do begin
   ts:='';
   for j:=1 to length(hs) do
    if j<>i then ts:=ts+hs[j];
   val(ts,t);
   if hash[t] then continue;
   inc(tail);
   q[tail].step:=q[head].step+1;
   val(ts,q[tail].num);
   a[q[tail].num]:=min(a[q[tail].num],q[tail].step);
   hash[q[tail].num]:=true;
  end;
```
操作3；//拓展时枚举到底是那两位相邻的数字插入，插入的是何种数字

```cpp
if length(hs)>=length(s) then continue;
  for i:=1 to length(hs)-1 do begin
   for x:=hs[i] to hs[i+1] do begin
    if x=hs[i] then continue;
    if x=hs[i+1] then continue; //s[i]<x<s[i+1];
    ts:=copy(hs,1,i);
    ts:=ts+x;
    ts:=ts+copy(hs,i+1,length(hs)-i+1);
    val(ts,t);
    if hash[t] then continue;
    inc(tail);
    q[tail].step:=q[head].step+1;
    val(ts,q[tail].num);
    a[q[tail].num]:=min(a[q[tail].num],q[tail].step);
    hash[q[tail].num]:=true;
   end;
  end;
```
接下来是程序（pas）

```cpp
type rec=record
 num,step:longint;
end;
var s:string;
    m,t,maxmax,i:longint;
    a:array[0..9999*100]of longint;
    q:array[0..9999*100]of rec;
    hash:array[0..9999*100]of boolean;
function min(a,b:longint):longint;
begin
 if a<b then exit(a)
 else exit(b);
end;
procedure bfs;
var head,tail,i,j,t:longint;
    hs,ts:string;
    x:char;
begin
 val(s,q[1].num);
 a[q[1].num]:=0;
 q[1].step:=0;
 head:=0; tail:=1;
 while head<tail do begin
  inc(head);
  str(q[head].num,hs);
  for i:=1 to length(hs) do
   for j:=i+1 to length(hs) do begin
    ts:=hs;
    ts[i]:=hs[j];
    ts[j]:=hs[i];
    val(ts,t);
    if hash[t] then continue;
    inc(tail);
    q[tail].step:=q[head].step+1;
    val(ts,q[tail].num);
    a[q[tail].num]:=min(a[q[tail].num],q[tail].step);
    hash[q[tail].num]:=true;
  end;
  for i:=1 to length(hs) do begin
   ts:='';
   for j:=1 to length(hs) do
    if j<>i then ts:=ts+hs[j];
   val(ts,t);
   if hash[t] then continue;
   inc(tail);
   q[tail].step:=q[head].step+1;
   val(ts,q[tail].num);
   a[q[tail].num]:=min(a[q[tail].num],q[tail].step);
   hash[q[tail].num]:=true;
  end;
  if length(hs)>=length(s) then continue;
  for i:=1 to length(hs)-1 do begin
   for x:=hs[i] to hs[i+1] do begin
    if x=hs[i] then continue;
    if x=hs[i+1] then continue;
    ts:=copy(hs,1,i);
    ts:=ts+x;
    ts:=ts+copy(hs,i+1,length(hs)-i+1);
    val(ts,t);
    if hash[t] then continue;
    inc(tail);
    q[tail].step:=q[head].step+1;
    val(ts,q[tail].num);
    a[q[tail].num]:=min(a[q[tail].num],q[tail].step);
    hash[q[tail].num]:=true;
   end;
  end;
 end;
end;
begin
 readln(s);
 readln(m);
 fillchar(a,sizeof(a),$7f);
 fillchar(hash,sizeof(hash),false);
 maxmax:=a[1];
 bfs;
 for i:=1 to m do begin
  readln(t);
  if a[t]=maxmax then writeln(-1)
  else writeln(a[t]);
 end;
end.

```