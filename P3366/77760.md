
# Prim+heap
库克斯科拉太简单了,还是用Prim吧...

heap+Prim听说很快,其实吧...

---
## Prim
先前我被其他人的题解搞蒙了,Prim难不成就是**找出最短边然后再加?**

那跟库克斯科拉有什么区别?????

然后仔细腐败...发现Prim的真正含义是:
```psacal
1.假如我已经有一个局部的最小生成树,名字叫做A。(一个集合)

2.有一些没有连的边(并且在A旁边(与A连通但是没有计入ans)),叫做B。(一个集合)
3.你会发现,我的A会和B是相连的,边权为mincost[B]。

4.有一些没有连的边(不再A旁边???),叫做C。(一个集合)
5.你会发现,我的A会和C不相连,边权应该为 mincost[C]:=maxlongint;
```
对于A,B,C,我们有什么操作?

```psacal
1.找出一个通向B最短的边,把它连起来(加入A),计入ans。
2.这时,有可能有通到B的更优解!!更新一些mincost[B]。(min)
3.这时,有可能会通到C!! 一些mincost[C]能连通了!! 变为B。
```

哇,神奇。

---

## Heap
大家都知道,普通维护Heap是log(n),而求出最小值,只需要O(1)!

所以我们可以用heap性质来求出A到B的最短边!只需要把mincost加入(当然你想mincost更新了怎么办?凉拌!再加入一次,反正又不会怎样...)

详见代码?

---

## Code
```pascal
// luogu-judger-enable-o2
Uses math;
var
        n,m,ans,tot,num:longint;
        ask:array[1..5010] of shortint;
        mincost,cnt:array[1..5010] of longint;
        heap:array[1..2,1..800010] of longint;
        right,sum,next:array[1..800010] of longint;

procedure down(x:longint);
var
        son:longint;
        t:longint;
begin
        if x*2>num then
                exit;
        son:=x*2;
        if (son+1<=num)and(heap[2,son+1]<=heap[2,son]) then
                inc(son);
        if (heap[2,x]>heap[2,son]) then
        begin
                t:=heap[2,x];
                heap[2,x]:=heap[2,son];
                heap[2,son]:=t;
                t:=heap[1,x];
                heap[1,x]:=heap[1,son];
                heap[1,son]:=t;
        end;
        down(son);
end;

procedure insert(x:longint);
var
        fa,t:longint;
begin
        fa:=x div 2;
        if (fa>0)and(heap[2,fa]>heap[2,x]) then
        begin
                t:=heap[2,fa];
                heap[2,fa]:=heap[2,x];
                heap[2,x]:=t;
                t:=heap[1,fa];
                heap[1,fa]:=heap[1,x];
                heap[1,x]:=t;
                insert(fa);
        end;
end;

procedure add(x,y,sum_:longint);
begin
        inc(tot);
        right[tot]:=y;
        sum[tot]:=sum_;
        next[tot]:=cnt[x];
        cnt[x]:=tot;
end;

procedure start;
var
        i,j:longint;
        srx:array[1..3] of longint;
begin
        read(n,m);
        for i:=1 to n do
        begin
                cnt[i]:=-1;
                ask[i]:=-1;
                mincost[i]:=maxlongint;
        end;

        tot:=0;
        for i:=1 to m do
        begin
                for j:=1 to 3 do
                        read(srx[j]);
                add(srx[1],srx[2],srx[3]);
                add(srx[2],srx[1],srx[3]);
        end;

        mincost[1]:=0;
        ask[1]:=1;
        i:=cnt[1];
        while i<>-1 do
        begin
                mincost[right[i]]:=min(mincost[right[i]],sum[i]);
                inc(num);
                heap[1,num]:=right[i];
                heap[2,num]:=mincost[right[i]];
                insert(num);
                i:=next[i];
        end;
end;

procedure Prim;
var
        i,minway,head,rope:longint;
begin
        rope:=0;
        repeat
                head:=heap[1,1];
                minway:=heap[2,1];
                if ask[head]=-1 then
                begin
                        ask[head]:=1;
                        inc(ans,minway);
                        inc(rope);

                        heap[1,1]:=heap[1,num];
                        heap[2,1]:=heap[2,num];
                        dec(num);
                        down(1);

                        i:=cnt[head];
                        while i<>-1 do
                        begin
                                if sum[i]<mincost[right[i]] then
                                begin
                                        mincost[right[i]]:=sum[i];
                                        inc(num);
                                        heap[1,num]:=right[i];
                                        heap[2,num]:=mincost[right[i]];
                                        insert(num);
                                end;
                                i:=next[i];
                        end;
                end
                else
                begin
                        heap[1,1]:=heap[1,num];
                        heap[2,1]:=heap[2,num];
                        dec(num);
                        down(1);
                end;
        until rope=n-1;
        writeln(ans);
end;

begin
        Start;
        Prim;
end.
```