看了一下其他人Prim堆优化的题解，都用STL，pas党感觉很无奈啊


所以我再发一个Prim+heap （手打的堆）


再说用惯STL可能会忘记模板的。。


Prim+Heap 速度还是很不错的


基本上200ms左右，，我还没用邻接表呢。。


这里采用邻接矩阵方便大家理解


其中

h数组是一个最小堆，，可以吧每次选堆的时间缩短为log m


cur[i]表示顶点i在数组中的位置


其他的就不用多说的吧，都是Prim的基本


不废话，上代码


``` pas
const INF=999999999;//最大值
const MAXN=5000;
const MAXM=200000;
var 
    vis:array[1..MAXN+1] of boolean;
    dis,h,cur:array[1..MAXN+1] of longint;
    g:array[1..MAXN+1,1..MAXN+1] of longint;
    n,m,i,j,sum,count,size,x,y,z:longint;
procedure swap(x,y:longint);//交换堆中两个顶点的位置
var t:longint;
begin
    t:=h[x];
    h[x]:=h[y];
    h[y]:=t;
    t:=cur[h[x]];//同步更新cur
    cur[h[x]]:=cur[h[y]];
    cur[h[y]]:=t;
end;
procedure down(i:longint);//向下调整函数
var t:longint;
    flag:boolean;
begin
    flag:=true;//flag用来标记是否继续要向下调整
    while (i*2<=size)and(flag) do
    begin
    //在自己和左儿子、右儿子中选一个最小的
        t:=i;
        if dis[h[i]]>dis[h[i*2]] then t:=i*2;
        if ((i*2+1)<=size)and(dis[h[t]]>dis[h[i*2+1]]) then t:=i*2+1;
        if t<>i then //如果不是自己，那就交换它们
        begin
            swap(t,i);
            i:=t;//更新i
        end 
        else flag:=false;//否则说明当前父节点是最小的，就不需要调整了
    end;
end;
procedure up(i:longint);//向上调整
var flag:boolean;
begin
    flag:=true;
    if i=1 then exit;//如果是在堆顶，那么就直接退出
    while (i<>1)and(flag) do
    begin
        //判断是否比父节点小
        if dis[h[i]]<dis[h[i div 2]] then 
            swap(i,i div 2)
        else 
            flag:=false;
        i:=i div 2;//更新i
    end;
end;
function top():longint;//取堆顶，也就是dis[i]最小的那个i
var t:longint;
begin
    t:=h[1];//保存堆顶
    cur[t]:=0;
    h[1]:=h[size];//将堆最后一个结点赋到堆顶
    cur[h[1]]:=1;
    size:=size-1;//堆的元素少1
    down(1);//因为堆顶已经改动，为了维护最小堆的特性，必须调整一次
    exit(t);
end;
begin
    readln(n,m);
    //初始化
    for i:=1 to n do
        for j:=1 to n do
            if i=j then g[i][j]:=0
                else g[i][j]:=INF;
    for i:=1 to m do
    begin
        readln(x,y,z);
        if z<g[x][y] then //去重边
        begin
            g[x][y]:=z;
            g[y][x]:=z;
        end;
    end;
    for i:=1 to n do g[i][i]:=0;//去自环
    for i:=1 to n do dis[i]:=g[1][i];//初始化dis数组
    //初始化堆
    size:=n;
    for i:=1 to size do 
    begin
        h[i]:=i;
        cur[i]:=i;
    end;
    for i:=size div 2 downto 1 do down(i);
    top();//先弹出一个堆顶元素，因为此时堆顶是一号顶点
    //Prim 算法核心部分
    fillchar(vis,sizeof(vis),1);
    vis[1]:=false;//将一号顶点加入生成树
    count:=1;sum:=0;
    while (count<n) do
    begin
        i:=top();//根据最小堆的特性，dis[i]最小的那个顶点就在堆顶，所以不必扫描，通过堆来节省时间，避免TLE
        vis[i]:=false;
        inc(count);
        sum:=sum+dis[i];
        //扫描当前顶点i的所有边，并进行松弛
        for j:=1 to n do 
            if (vis[j])and((g[i][j])<dis[j]) then
            begin
                dis[j]:=g[i][j];
                up(cur[j]);//由于dis[j]已经变小了，所以要将它向上调整
            end;
    end;
    if count=n then writeln(sum) else writeln('orz');
    //嗯输出好神奇
end.
```