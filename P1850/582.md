pascal的考场ac代码，之前发在讨论，这次放题解吧

思路很明了就是floyd+dp，时间复杂度是O(v^3+n^2)，空间复杂度O(v^2+n^2)可以滚动成O(v^2+n)，考场上就没去管了

dp思路是用f[i,j,0]表示到第i次课时申请j次并且该次不申请的期望最小值

f[i,j,1]表示到第i次课时申请j次并且该次申请的期望最小值

这个记录方式好处在于可以知道上次是否申请从而判断该次路径的起点

唯一的难点在于这个有点大的动态转移方程：

f[i,j,0]:=min(f[i-1,j,0]+dist[c[i-1],c[i]],f[i-1,j,1]+k[i-1]\*dist[d[i-1], c[i]]+(1-k[i-1])\*dist[c[i-1],c[i]]);

f[i,j,1]:=min(f[i-1,j-1,0]+k[i]\*dist[c[i-1],d[i]]+(1-k[i])\*dist[c[i-1], c[i]],f[i-1,j-1,1]+k[i-1]\*k[i]\*dist[d[i-1],d[i]]+(1-k[i-1])\*k[i]\*dist[c[i-1],d[i]]+k[i-1]\*(1-k[i])\*dist[d[i-1],c[i]]+(1-k[i-1])\*(1-k[i])\*dist[c[i-1],c[i]]);

没错这只是两行，dist[i,j]表示i,j之间的最短路


```cpp
uses math;
const maxv=1000;
      maxn=2100;
      maxlongint=10000000;
var n,m,v,e,i,j:longint;
    dist,a:array[1..maxv,1..maxv] of longint;
    c,d:array [1..maxn] of longint;
    k,value:array [1..maxn] of real;
    f:array [0..maxn,0..maxn,0..1] of real;
procedure init;
  var i,j,x,y,t:longint;
  begin
    readln(n,m,v,e);
    for i:=1 to n do read(c[i]);
    readln;
    for i:=1 to n do read(d[i]);
    readln;
    for i:=1 to n do read(k[i]);
    for i:=1 to v do
      for j:=1 to v do
        a[i,j]:=maxlongint;
    for i:=1 to v do a[i,i]:=0;
    for i:=1 to e do
    begin
      readln(x,y,t);
      a[x,y]:=min(a[x,y],t);
      a[y,x]:=min(a[y,x],t);
    end;
  end;
procedure floyd;
  var i,j,kk:longint;
  begin
    dist:=a;
    for kk:=1 to v do
      for i:=1 to v do
        for j:=1 to v do
          if dist[i,j]>dist[i,kk]+dist[kk,j] then
            dist[i,j]:=dist[i,kk]+dist[kk,j];
  end;
procedure dp;
  var i,j:longint;
  begin
    for i:=1 to n+1 do
      for j:=0 to m+1 do
      begin
        f[i,j,0]:=maxlongint;
        f[i,j,1]:=maxlongint;
      end;
    f[1,0,0]:=0;f[1,1,1]:=0;
    for i:=2 to n do
    begin
      f[i,0,0]:=f[i-1,0,0]+dist[c[i-1],c[i]];
      for j:=1 to min(i,m) do
      begin
        f[i,j,0]:=min(f[i-1,j,0]+dist[c[i-1],c[i]],f[i-1,j,1]+k[i-1]*dist[d[i-1], c[i]]+(1-k[i-1])*dist[c[i-1],c[i]]);
        f[i,j,1]:=min(f[i-1,j-1,0]+k[i]*dist[c[i-1],d[i]]+(1-k[i])*dist[c[i-1], c[i]],f[i-1,j-1,1]+k[i-1]*k[i]*dist[d[i-1],d[i]]+(1-k[i-1])*k[i]*dist[c[i-1],d[i]]+k[i-1]*(1-k[i])*dist[d[i-1],c[i]]+(1-k[i-1])*(1-k[i])*dist[c[i-1],c[i]]);
      end;
    end;
  end;
procedure oupt;
  var i:longint;
      ans:real;
  begin
    ans:=f[n,0,0];
    for i:=1 to m do
    begin
      ans:=min(ans,f[n,i,0]);
      ans:=min(ans,f[n,i,1]);
    end;
    writeln(ans:0:2);
  end;
Begin
  init;
  floyd;
  dp;
  oupt;
End.
```