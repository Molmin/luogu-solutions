#楼下似乎没有和我思路一样的。。。那我来补一发。。

玩过数独的人都知道数独同一行，同一列，同一个九宫格，不能填相同的数字。

那么我们就用三个数组来记录当前行，列，九宫格填k是否能行。

然后从(9,1)开始一个一个填（经蒟蒻的我测试：(9,9),(1,1),(1,9),(9,1)四个开始搜的点，只有(9,1)能对，当然仅限于蒟蒻的我的方法），填完一行则填上一行。

然而这样肯定是过不了的，还需要优化：

1：用类似邻接表的方法存储每个点可能填的数。

2：一开始把能填的先填好，即只能填一个数的点。

献上没有66666a代码丑陋的AC代码：


```cpp
const
  d:array[1..9,1..9]of longint=((1,1,1,2,2,2,3,3,3),
                                (1,1,1,2,2,2,3,3,3),
                                (1,1,1,2,2,2,3,3,3),
                                (4,4,4,5,5,5,6,6,6),
                                (4,4,4,5,5,5,6,6,6),
                                (4,4,4,5,5,5,6,6,6),
                                (7,7,7,8,8,8,9,9,9),
                                (7,7,7,8,8,8,9,9,9),
                                (7,7,7,8,8,8,9,9,9));//九宫格
  d1:array[1..9,1..9]of longint=((6,6,6,6,6,6,6,6,6),
                                (6,7,7,7,7,7,7,7,6),
                                (6,7,8,8,8,8,8,7,6),
                                (6,7,8,9,9,9,8,7,6),
                                (6,7,8,9,10,9,8,7,6),
                                (6,7,8,9,9,9,8,7,6),
                                (6,7,8,8,8,8,8,7,6),
                                (6,7,7,7,7,7,7,7,6),
                                (6,6,6,6,6,6,6,6,6));//分值
var
  a,b,c:array[0..10,0..10]of boolean;
  f:array[0..10,0..10]of longint;
  tt:array[0..10,0..10,0..10]of longint;
  n,i,j,k,x,time,s:longint;
  max,sum:int64;
procedure try(l,r:longint);
var
  i,j,s:longint;
  begin
    if l<1 then //边界
      begin
        if sum>max then max:=sum; //在所有可能的结果中找一个最大值
        exit;
      end;
    if f[l,r]<>0 then 
      begin
        if r=9 then try(l-1,1)
          else try(l,r+1);
        exit;
      end;//已经填好的数，就不用填了
    for i:=1 to tt[l,r,0] do
      begin 
        j:=tt[l,r,i];
        if (not a[l,j]) and (not b[r,j]) and (not c[d[l,r],j]) then 
          begin
            f[l,r]:=j;
            inc(sum,j*d1[l,r]);
            a[l,j]:=true;
            b[r,j]:=true;
            c[d[l,r],j]:=true;
            if r=9 then try(l-1,1)
              else try(l,r+1);
            dec(sum,j*d1[l,r]);
            f[l,r]:=0;
            a[l,j]:=false;
            b[r,j]:=false;
            c[d[l,r],j]:=false;
          end; 
      end;//不解释
  end;//dfs
begin
  for i:=1 to 9 do
    begin 
      for j:=1 to 9 do 
        begin
          read(f[i,j]);
          if f[i,j]<>0 then 
            begin
              a[i,f[i,j]]:=true;
              b[j,f[i,j]]:=true;
              c[d[i,j],f[i,j]]:=true;
              inc(sum,f[i,j]*d1[i,j]);
            end;
        end;
      readln;
    end;
  for i:=1 to 9 do 
    for j:=1 to 9 do
      begin
        if f[i,j]<>0 then continue;
        for k:=1 to 9 do 
          if not a[i,k] and not b[j,k] and not c[d[i,j],k] then 
            begin
              inc(tt[i,j,0]);
              tt[i,j,tt[i,j,0]]:=k;
            end;
        if tt[i,j,0]=1 then 
          begin
            f[i,j]:=tt[i,j,1];
            a[i,tt[i,j,1]]:=true;
            b[j,tt[i,j,1]]:=true;
            c[d[i,j],tt[i,j,1]]:=true;
            inc(sum,tt[i,j,1]*d1[i,j]);
          end;
        if tt[i,j,0]=0 then begin writeln(-1);halt; end;
      end;//一开始把能填的先填好，并用类似邻接表的方法存储每个点可能填的数
  try(9,1);//从（9,1）开始搜
  if max=0 then writeln(-1) else writeln(max);
end.

```