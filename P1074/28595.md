来自一名青名蒟蒻

看到题目就是纯搜索啊 常量数组那么好的东西你们为什么不用呢？像那种八皇后，或者单词方阵之类的前面定义一个常量数组放进去然后搜索的方向就非常好确定了啊！


——————————————————————————————————————


    



  
```cpp
const
    g:array [1..9,1..9] of longint=((1,1,1,2,2,2,3,3,3),       
                                    (1,1,1,2,2,2,3,3,3),
                                    (1,1,1,2,2,2,3,3,3),
                                    (4,4,4,5,5,5,6,6,6),
                                    (4,4,4,5,5,5,6,6,6),
                                    (4,4,4,5,5,5,6,6,6),
                                    (7,7,7,8,8,8,9,9,9),
                                    (7,7,7,8,8,8,9,9,9),
                                    (7,7,7,8,8,8,9,9,9));         //这个就是定义一下宫格的编号然后接下来就比较方便了啊
    score:array [1..9,1..9] of longint=((6,6,6,6,6,6,6,6,6),           
                                        (6,7,7,7,7,7,7,7,6),
                                        (6,7,8,8,8,8,8,7,6),
                                        (6,7,8,9,9,9,8,7,6),
                                        (6,7,8,9,10,9,8,7,6),
                                        (6,7,8,9,9,9,8,7,6),
                                        (6,7,8,8,8,8,8,7,6),
                                        (6,7,7,7,7,7,7,7,6),
                                        (6,6,6,6,6,6,6,6,6));         //在最后面处理一下啊啊啊就是计算分数，傻傻的我本来是用if然后套了一大堆
var
    i,j,ans,t:longint;
    map:array [0..9,0..9] of longint;             
    h,l,ge:array [1..9,1..9] of boolean;
    x,y:array [0..81] of longint;
    c:array [0..81] of boolean;    //这边开数组啊开数组
procedure answer;                            
var i,j,s:longint;
begin
    s:=0;
    for i:=1 to 9 do
     for j:=1 to 9 do
        s:=s+map[i,j]*score[i,j];
        if s>ans then ans:=s;                       
end;               //找到最大方案，不解释
procedure search(k:longint);      //the most important
var xx,yy,i,min,w,j,p:longint;    
begin
    if k>t then       //就是满足条件退了嘛 也就是所有都填上数了啊
    begin
        answer;           
        exit;           //退出这个副程序（玩梗ing）
    end;
    min:=maxlongint;        //其实也不用那么大
    for i:=1 to t do            
      if c[i] then                //如果这个点还活着那么就继续搜
       begin
         w:=0;
         for j:=1 to 9 do        
          if h[x[i],j] and l[y[i],j] and ge[g[x[i],y[i]],j] then    //判重 玩过数独的人都知道 
            begin
              inc(w);          
              if w>=min then break;        
            end;
          if w<min then       
            begin
              p:=i;           
              min:=w;
              end;
            end;
      xx:=x[p];          
      yy:=y[p];
      c[p]:=false;       //已填的数
      for i:=1 to 9 do
        if h[xx,i] and l[yy,i] and ge[g[xx,yy],i] then    
          begin                   
            map[xx,yy]:=i;         
            h[xx,i]:=false;
            l[yy,i]:=false;
            ge[g[xx,yy],i]:=false;
            search(k+1);       //继续搜索   
            h[xx,i]:=true;          
            l[yy,i]:=true;
            ge[g[xx,yy],i]:=true;
            map[xx,yy]:=0;       //回溯一步哟
          end;
        c[p]:=true;
end;
begin                 //名义上的主程序实际上只是个小程序（再次玩梗）   
    ans:=-1;
    t:=0;                   
    fillchar(h,sizeof(h),true);
    fillchar(l,sizeof(l),true);
    fillchar(ge,sizeof(ge),true);        //归零 智商归零！
    for i:=1 to 9 do
      for j:=1 to 9 do 
        begin
          read(map[i,j]);
          if map[i,j]=0 then
            begin
              inc(t);
              x[t]:=i;             
              y[t]:=j;             
              c[t]:=true;         
            end
          else
            begin
              h[i,map[i,j]]:=false;         
              l[j,map[i,j]]:=false;             
              ge[g[i,j],map[i,j]]:=false;       
            end;
        end;        //边读边操作真的是延年益寿就好像背了两句诗一样
    search(1);
    writeln(ans);        //输出即可
end.

```