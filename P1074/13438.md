用的是搜索。发现这种方法似乎没有pascal的题解所以贡献一份。

按照填数独的方法，先从能填的数字的可能最小的地方开始搜，可以减少很多的时间。




```cpp
const a:array[1..9,1..9]of longint=((6,6,6,6,6,6,6,6,6),
                                    (6,7,7,7,7,7,7,7,6),
                                    (6,7,8,8,8,8,8,7,6),
                                    (6,7,8,9,9,9,8,7,6),
                                    (6,7,8,9,10,9,8,7,6),
                                    (6,7,8,9,9,9,8,7,6),
                                    (6,7,8,8,8,8,8,7,6),
                                    (6,7,7,7,7,7,7,7,6),
                                    (6,6,6,6,6,6,6,6,6));
var i,num,c,n,ans,j:longint;
    x,y,f:array[1..9,1..9]of boolean;  
    s:array[0..2,0..2,1..9]of boolean;
    t1:boolean;
procedure p(num,n:longint);
var i,k,j,numa,numb,numc,minx,miny,min:longint;
    t:boolean;
begin
  if num>ans then ans:=num;
  if n=81 then begin t1:=true; exit; end;    //判断数独是否合法
  min:=10;  t:=true;
  for i:=1 to 9 do for j:=1 to 9 do if not f[i,j] then //因为填入数字之后每格的可能性又会发生变化，所以每次都算一遍每一格的填数字的可能
    begin
    numa:=0;
    for k:=1 to 9 do if numa<min then if (not x[i,k])and(not y[j,k]) then
    if not s[(i-1)div 3,(j-1) div 3,k] then inc(numa);
    if numa<min then begin min:=numa; t:=false; minx:=i; miny:=j;end;
    if numa=1 then break;
    end;
  if t then exit;
  for k:=1 to 9 do
  if (not x[minx,k])and(not y[miny,k]) then if not s[(minx-1)div 3,(miny-1) div 3,k] then
  begin
     x[minx,k]:=true;
     y[miny,k]:=true;
     f[minx,miny]:=true;
     s[(minx-1)div 3,(miny-1) div 3,k]:=true;
     p(num+k*a[minx,miny],n+1);
     x[minx,k]:=false;
     y[miny,k]:=false;
     f[minx,miny]:=false;
     s[(minx-1)div 3,(miny-1) div 3,k]:=false;
  end;
end;
begin
  for i:=1 to 9 do
    for j:=1 to 9 do
    begin
      read(c);
      num:=num+c*a[i,j];
      if c<>0 then
      begin
        inc(n);
        f[i,j]:=true;
        if not x[i,c] then x[i,c]:=true                  //初次判断数独是否合法， x(i,c)表示第i行 c这个数字是否填过
          else begin writeln('-1'); exit; end;
        if not y[j,c] then y[j,c]:=true                //y(i,c)表示第i列 c这个数字是否填过
          else begin writeln('-1'); exit; end;
        if not s[(i-1) div 3,(j-1)div 3,c] then s[(i-1) div 3,(j-1)div 3,c]:=true  //判断小九宫格里的数字的使用情况
          else begin writeln('-1'); exit; end;
      end;
    end;
    t1:=false;   
  p(num,n);
  if t1 then writeln(ans) else writeln(-1);
end.
```