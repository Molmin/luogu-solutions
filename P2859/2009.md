看到题解不多，就过来凑个热闹。

这道题我是这么做的。

- 首先，大的思路是贪心，大概就是按着时间先后顺序把牛放入stall，假如stall满了，那么就牛棚数cnt++。

- 但是又不想整个时间线for一遍毕竟太长了，那么就选择**for一遍牛的左端点**好了，所以就要**对左端点排序**。- 0.

- 然后在for的过程中，我们又会遇到牛出stall，所以右端点也需要排序，但是由于左端点for完之后就没有右端点的事儿了，所以给右端点全部排序是没有必要的，所以我们选择**用堆维护右端点**。（堆排）

- 最后还有一个问题，就是当我们需要把牛塞到空stall里面的时候，我们塞哪个？于是我选择用树状数组维护stall，**用二分cnt的方法，把（stall前缀和与mid相比较）**找到最小的那个空stall，然后通过树状数组的change填满那个stall。BTW，其中涉及到一个不常见的树状数组操作，就是新增加一个stall，关于新增结点的管辖范围。

```cpp
cnt++;
st[cnt]=sum(cnt-1)-sum(cnt-lowbit(cnt))+1;
```
- 解决了以上问题，OK主程序就是**for左端点**+**for的里面一定要记得先“出堆”**+**fill stall**+**记录每个cow的stall编号！**然后输出就好了。下面是本人写的代码，加了一点注释，不过是pascal的= =

其中le，ri分别左端点，右端点。

lm是左端点排序时牛编号，rm是右端点入堆时牛编号

st是stall的树状数组。

cow是每头牛的stall编号

len是堆的数目

cnt是stall的数目也是树状数组的上界

em是最小empty stall的编号，和check（二分求最小empty stall）函数一起用的。

就酱！

```pascal
const maxn=50000;
var n,i,len,cnt,em:longint;
    le,ri,lm,rm,st,cow:array[1..maxn]of longint;

procedure swap(var x,y:longint);//swap number
var t:longint;
begin   
    t:=x;
    x:=y;
    y:=t;
end;

procedure qs(l,r:longint);//ascend quicksort
var x,y,k:longint;
begin
    x:=l;y:=r;
    k:=le[(l+r)shr 1];
    repeat
        while le[x]<k do inc(x);
        while le[y]>k do dec(y);
        if x<=y then
        begin
            swap(le[x],le[y]);
            swap(lm[x],lm[y]);
            inc(x);
            dec(y);
        end;
    until x>y;
    if l<y then qs(l,y);
    if r>x then qs(x,r);
end;
//小根堆
procedure down(x:longint);
var t:longint;
begin
    t:=x shl 1;
    if t>len then exit
    else if (t<len)and(ri[t]>ri[t+1]) then inc(t);
    if ri[t]<ri[x] then
    begin
        swap(ri[x],ri[t]);
        swap(rm[x],rm[t]);
        down(t);
    end;
end;

procedure delete(x:longint);
begin
    ri[x]:=ri[len];
    rm[x]:=rm[len];
    dec(len);
    down(x);
end;
//小根堆
//BIT for stalls
function lowbit(x:longint):longint;
begin
    exit(x and (-x));
end;

procedure change(x,y:longint);
begin
    while x<=cnt do
    begin
        inc(st[x],y);
        inc(x,lowbit(x));
    end;
end;

function sum(x:longint):longint;
begin
    sum:=0;
    while x>0 do
    begin
        inc(sum,st[x]);
        dec(x,lowbit(x));
    end;
end;
//BIT for stalls
//Binary Search for the empty stall
function check(l,r:longint):longint;
var mid:longint;
begin
    while l<r do
    begin
        mid:=(l+r)shr 1;
        if sum(mid)=mid then l:=mid+1
        else r:=mid;
    end;
    exit(r);
end;

//main
begin
    readln(n);
    for i:=1 to n do
    begin
        readln(le[i],ri[i]);
        lm[i]:=i;
        rm[i]:=i;
    end;
    qs(1,n);//左端点排序
    len:=n;//len for heap
    for i:=n shr 1 downto 1 do//右端点入堆
    down(i);
    cnt:=0;//cnt for BIT
    for i:=1 to n do
    begin
        while ri[1]<le[i] do//need to empty stall first
        begin
            change(cow[rm[1]],-1);//empty stall
            delete(1);
        end;
        if sum(cnt)=cnt then
        begin
            inc(cnt);
            st[cnt]:=sum(cnt-1)-sum(cnt-lowbit(cnt))+1;
            cow[lm[i]]:=cnt;
        end//stall is full
        else
        begin
            em:=check(1,cnt);//find the smallest empty stall;
            cow[lm[i]]:=em;
            change(em,1);//fill the empty stall;
        end;
    end;
    writeln(cnt);
    for i:=1 to n do
    writeln(cow[i]);
end.
```