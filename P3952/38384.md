来一篇非C/C++的pascal题解

方法是栈~~（跟我读：只安zhan）~~

### 预热：

在做之前要先知道这个so eazy 的公式：(n^x)*(n^y)=n^(x+y)   

我们以此为基础来做这道题

//////////////////////////////////////

### 思路：

我们用一个stack数组记录每个循环的变量名和复杂度（1,0,-1），1表示该语句的复杂度为O(n)，0表示该语句的复杂度为O(1)，-1表示该语句语法错误，之后的循环都不会执行，那么在最后E句计算复杂度时，只要将栈中每个语句的复杂度累加起来就是当前语句结束时的时间复杂度

先分解出O(n^w)，将指数记为w

再读入循环体，分为F句和E句，用maxt记录最大的复杂度

最后比较w和maxt（别忘了判编译错误哦）输出结果

///////////////////////////////////////

### F句处理：

一.用一个数组used记录变量名是否被使用，如果多次被使用，则在最后直接输出ERR；

二.分离出循环的开始变量x和结束变量y

   这里分五种情况(这里将常数设为a、b)
   
   1.x=n,y=a：stack[i]=-1;
   
   2.x=n,y=n:stack[i]=0;
   
   3.x=a,y=n:stack[i]=1
   
   4.x=a,y=b, 且 x<=y : stack[i]=0
   
   5.x=a,y=b, 且 x>y ：stack[i]=-1

/////////////////////////////////////
   
### E句处理：
   
   1.计算累加复杂度，遇到-1直接结束，因为语法错误之后的语句都不会执行；
   
   2.比较更新max；
   
   3.变量使用还原；
   
   4.tail-1  此处若在任意时刻tail<0或最后时刻tail>0,则最后直接输出ERR
   
/////////////////////////////////////
   
   最后比较max与w，若相等，则输出Yes,反之输出No
   
### 具体解释详见代码

```pascal  

var stack:array[-100..100] of longint;//复杂度
    stack1:array[-100..100] of char;//变量名
    used:array['a'..'z'] of longint;      s,x1,y1:string;
    i,j,z,n,l,w,po,t,maxt,tail,x,y:longint; f:boolean;
begin
  assign(input,'1.in'); reset(input);
  assign(output,'1.out'); rewrite(output);
  readln(n);
  for z:=1 to n do
  begin
    fillchar(used,sizeof(used),false);//注意每次判断都要赋初值
    readln(s);
    po:=pos(' ',s);
    val(copy(s,1,po-1),l);//分离出l
    po:=pos('^',s);
    if po=0 then w:=0
            else val(copy(s,po+1,length(s)-po-1),w);//分离出复杂度
    maxt:=0; f:=true; tail:=0;
    for i:=1 to l do
    begin
      readln(s);//输入语句
      if s[1]='F' then//F句处理
      begin
        delete(s,1,2);
        if not(used[s[1]]) then used[s[1]]:=true
                           else f:=false;//判断变量名是否重复
        inc(tail);
        stack1[tail]:=s[1];//入栈
        delete(s,1,2);
        po:=pos(' ',s);
        x1:=copy(s,1,po-1);//分离x
        y1:=copy(s,po+1,length(s)-po);//分离y
        if x1='n' then begin
                       if y1='n' then stack[tail]:=0
                                 else stack[tail]:=-1;
                       end
                else if y1='n' then stack[tail]:=1
                               else begin
                                    val(x1,x); val(y1,y);
                                    if x<=y then stack[tail]:=0
                                            else stack[tail]:=-1;
                                    end;//判断本语句的复杂度，在此不再赘述
      end;
      if s[1]='E' then//E句处理
      begin
        t:=0;  j:=1;
        while (j<=tail)and(stack[j]<>-1) do
        begin
          t:=t+stack[j];
          j:=j+1;
        end;//计算复杂度
        used[stack1[tail]]:=false;//变量名重置
        if t>maxt then maxt:=t;//比较得出更“大”的复杂度
        if tail<=0 then f:=false;//判断是否错误
        dec(tail);
      end;
    end;
    if (tail=0)and(f) then
    begin
      if maxt=w then writeln('Yes')
                else writeln('No');
    end
    else writeln('ERR');
  end;
  close(input); close(output);
end.

```

### 这道题目坑点还是很多的，请谨慎考虑所有情况！