算法：tarjan\_LCA，树上差分，桶+DFS维护静态子树信息

经过两天多的努力，终于把这道noip2016最难的题攻下来了，至此，noip2016已改成满分


题目本身其实就是一个复杂的问题：给很多条路径，问对于树上每一个点，在给定的某时刻到达这个点的路径有多少

如果思路跟着人跑，那么永远都会是O(nm)的（暴力树剖除外）

尝试跳出跑的过程。

先考虑向上的一段。发现，如果满足条件，那么起点u与观察点x存在深度关系：

    de[x]+w[x]=de[u]

左边是定值，所以显然，在x子树里满足de[u]等于这个定值的点就是可以观察到的。

再考虑向下的。为了处理方便，肯定还是要想办法把代表一条路径的点放在x子树里。可以考虑终点v。

同理于上，可得

    w[x]-de[x]=dis[u,v]-de[v]

于是，两个方向上能观察到的条件都已经解决


接下来的问题是如何统计。

（以下将“某个下标的桶里的值”称为这个下标的“桶值”）

如果没有“在x子树里”的限制，方法就很显然了：建立两个以等式右边值为下标的桶，然后用每个观察点依次去桶里找数即可

但是“在x子树里”这是一件很麻烦的事

可以做dfs序，然后用数据结构解决“区间有多少个某数”的问题，但是太不优美了（我不会，以后还是要学的）


既然不可能给每个节点的子树都维护一个桶，那就考虑共用一个桶，直接取用桶值，再减去不在子树里的部分就行了


当进入一个子树x时，把目前为止x对应的两个值的桶值传给x，这样，当遍历了一遍x子树，回溯回来时，

现在的桶值减去那两个值，得到【桶值的变化量】，就是x子树里的值


起点终点的链表是沿用的tarjan的，lca需要另开一个链表

还有lca位置会把当前路径计算两次，因此处理lca时还需要减去一次


byAPT

```pascal
//NOIP2016 D1T2 “天天爱跑步” byAPT 2016.12.10
//仅供学习，禁止直接粘上去提交
var
    n,m,x,y,
    totl,totq,tlca         :longint;
    i,j,k               :longint;
    last,lca,lastq,fth,w,de,dis,
    plca,lalca,
    ans                 :array[0..300010]of longint;
    vis                 :array[0..300010]of boolean;
    pre,oth,preq,othq   :array[0..600010]of longint;
    a,b                 :array[-400005..400005]of longint; //Buckets

procedure conn(a,b:longint);
begin
    inc(totl);
    pre[totl]:=last[a];
    last[a]:=totl;
    oth[totl]:=b;
end;

procedure addlca(c:longint);
begin
    inc(tlca);
    plca[tlca]:=lalca[c];
    lalca[c]:=tlca;
end;

{--CLASS tarjan_lca}
procedure addq(a,b:longint);
begin
    inc(totq);
    preq[totq]:=lastq[a];
    lastq[a]:=totq;
    othq[totq]:=b;
end;

function getfth(x:longint):longint;inline;
begin
    if x=fth[x] then exit(x);
    fth[x]:=getfth(fth[x]);
    exit(fth[x]);
end;

procedure tarjan(x:longint);
var p,r:longint;
begin
    vis[x]:=true;
    p:=last[x];
    while p>0 do begin
        r:=oth[p];
        if not vis[r] then begin
            de[r]:=de[x]+1;
            tarjan(r);
            fth[r]:=x;
        end;
        p:=pre[p];
    end;

    p:=lastq[x];
    while p>0 do begin
        r:=othq[p];
        if vis[r] then lca[p>>1]:=getfth(r);
        p:=preq[p];
    end;
end;
{--END}

procedure dfs(x,sa,sb:longint);
var p,r:longint;
begin
    vis[x]:=true;

    p:=lastq[x];
    while p>0 do begin
        //if x=5 then writeln(';;;');
        if p and 1=0 then begin
            inc(a[de[x]]);
        end else begin
            inc(b[dis[p>>1]-de[x]]);
        end;
        p:=preq[p];
    end;

    p:=last[x];
    while p>0 do begin
        r:=oth[p];
        if not vis[r] then begin
            dfs(r,a[w[r]+de[r]],b[w[r]-de[r]]);
        end;
        p:=pre[p];
    end;

    ans[x]:=a[w[x]+de[x]]+b[w[x]-de[x]]-sa-sb;
    //writeln(ans[5]);
    {if ans[5]=1 then begin
        p:=1;
    end;}

    p:=lalca[x];
    while p>0 do begin
        //writeln('dele',p,' ',othq[p<<1 or 1],' ',othq[p<<1]);
        dec(a[de[othq[p<<1 or 1]]]);
        dec(b[dis[p]-de[othq[p<<1]]]);
        if de[othq[p<<1 or 1]]-de[x]=w[x] then dec(ans[x]);
        p:=plca[p];
    end;
end;

begin
    read(n,m);
    for i:=1 to n-1 do begin
        read(x,y);
        conn(x,y); conn(y,x);
    end;

    for i:=1 to n do read(w[i]);

    totq:=1;
    for i:=1 to m do begin
        read(x,y);
        addq(x,y); addq(y,x);
    end;

    for i:=1 to n do fth[i]:=i;
    tarjan(1);
    //for i:=1 to m do write(lca[i],' ');

    for i:=1 to m do
        dis[i]:=de[othq[i<<1]]+de[othq[i<<1 or 1]]-de[lca[i]]*2;

    for i:=1 to m do addlca(lca[i]);

    {for i:=1 to n do write(lalca[i],' ');
    writeln; }


    fillchar(vis,sizeof(vis),0);
    dfs(1,0,0);

    for i:=1 to n do write(ans[i],' ');

    //for i:=-5 to 5 do writeln(i,'::',a[i],' ',b[i]);

end.
```