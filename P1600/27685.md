这是茫茫C++中的pascal，难道pascal终要没落？

好了，讲题

##first

首先我们先将题目简化来看看。当树退化为一条链（节点记作a[0]..a[n]），且玩家只往右走时，我们容易推得u号点上的观察员要观察到v号点开始的玩家，那么显然有v + w[u] = u。

##second

我们很自然地将无根树变为有根树（程序中节点1为根，深度为0）。于是从s->t的路径可以表示为s->lca(s,t)->t。对于s->lca(s,t)，我们对应上一条中的结论，单独考虑s、lca(s,t)所在的那条链，不难发现若在这条链上的点u要观察到这个玩家，就有deep[u] - deep[s] = w[u]（路径长度，即深度差等于w[u]）。可推得deep[u] + w[u] = deep[s]。也就是说，u节点子树上点v能够被观察到的必要条件是deep[u] + w[u] = deep[s]。对于lca(s,t)->t也有类似结论（自己算）。所以我们可以开一个桶（vector，C++党不要喷我），记录deep[s]，然后做完了子树，回到u以后统计deep[u] + w[u]。

##third

依旧对于s->lca。上一条中，我们提到u节点子树上点v能够被观察到的必要条件是deep[u] + w[u] = deep[s]。显然若是再加上lca(s,t)是u或它的祖先，那么就变成了充要条件。为了实现这一点，我们可以在做到s的时候改变vector的同时在lca(s,t)上打一个deep[s]的标记，回到lca(s,t)时删除即可。具体的实现可以用链表。对于lca(s,t)->t同样做法。

##fourth

那么还有一个很严肃的问题。如上统计方法，可能会“跨子树运算”。即s和u满足deep[s] = deep[u] + w[u]且lca(s,t)是u或它的祖先（这里一定是它的祖先），但是不满足s在u为根节点的子树上。这时，我们神奇的差分（好像是这么叫的）就登场了。我们可以发现，dfs时，在进入到u之前一定没做过u子树内的点；在回到u时，一定已经做完了u子树内的点且没有做u子树外的点。然后发现其实u需要统计的数（还是拿s->lca(s,t)举例）只是deep[u] + w[u]，那么我们就可以在进入u的时候记录一下桶的值，出去的时候再剪一剪就好了。

##fifth

两条链的焦点，lca(s,t)，有可能被算到两次。那么如何解决？我看到大部分题解都是最后统计，但是本人懒到懒得推最后的式子，就这么做了：在做s->lca(s,t)时，回到u先删打的标记再统计；做lca(s,t)->t时，回到u先统计再删标记，那么lca(s,t)就只被算到了一次。

##具体实现看程序。

```pascal
var
        n,m,i,x,y,max_deep:longint;
        l1,l2,l3,l4:longint;
        f1,f2,f3,f4,next2,value,next3,goto3,next4,goto4,s,t :array[0..300010] of longint;
        next1,goto1:array[0..600010] of longint;
        vector:array[-300010..600010] of longint;
        b:array[0..300010,0..20] of longint;
        ans,lca,deep,w,len:array[0..300010] of longint;


procedure add1(x,y:longint);//这只是4个数组模拟链表
begin
        inc(l1);
        next1[l1] := f1[x];
        f1[x] := l1;
        goto1[l1] := y;
end;


procedure add2(x,y:longint);
begin
        inc(l2);
        next2[l2] := f2[x];
        f2[x] := l2;
        value[l2] := y;
end;


procedure add3(x,y:longint);
begin
        inc(l3);
        next3[l3] := f3[x];
        f3[x] := l3;
        goto3[l3] := y;
end;


procedure add4(x,y:longint);
begin
        inc(l4);
        next4[l4] := f4[x];
        f4[x] := l4;
        goto4[l4] := y;
end;


procedure load_lca(u,fa,depth:longint);//建树，顺便初始化一下树上倍增求lca（可以用Tarjan）
var
        x,i:longint;
begin
        if depth > max_deep then max_deep := depth;
        deep[u] := depth;
        b[u][0] := fa;
        for i := 1 to 19 do
         b[u][i] := b[b[u][i - 1]][i - 1];
        x := f1[u];
        while x <> 0 do
         begin
                if goto1[x] <> fa then load_lca(goto1[x],u,depth + 1);
                x := next1[x];
         end;
end;


procedure dfs1(u,fa:longint);//s->lca
var
        x,t:longint;
begin
        if deep[u] + w[u] <= max_deep then
         t := vector[deep[u] + w[u]];
        x := f1[u];
        while x <> 0 do
         begin
                if goto1[x] <> fa then dfs1(goto1[x],u);
                x := next1[x];
         end;
        x := f3[u];
        while x <> 0 do//改桶，在lca上打标记
         begin
                inc(vector[deep[u]]);
                add2(lca[goto3[x]],deep[u]);
                x := next3[x];
         end;
        x := f2[u];
        while x <> 0 do//删标记
         begin
                dec(vector[value[x]]);
                x := next2[x];
         end;
        if deep[u] + w[u] <= max_deep then//统计
         inc(ans[u],vector[deep[u] + w[u]] - t);
end;


procedure dfs2(u,fa:longint);
var
        x,t:longint;
begin
         t := vector[deep[u] - w[u]];
        x := f1[u];
        while x <> 0 do
         begin
                if goto1[x] <> fa then dfs2(goto1[x],u);
                x := next1[x];
         end;
        x := f4[u];
        while x <> 0 do
         begin
                inc(vector[deep[u] - len[goto4[x]]]);
                add2(lca[goto4[x]],deep[u] - len[goto4[x]]);
                x := next4[x];
         end;
         inc(ans[u],vector[deep[u] - w[u]] - t);
        x := f2[u];
        while x <> 0 do
         begin
                dec(vector[value[x]]);
                x := next2[x];
         end;
end;


function get_lca(x,y:longint):longint;//返回lca(x,y)
var
        t,i:longint;
begin
        if deep[x] < deep[y] then
         begin
                t := x;
                x := y;
                y := t;
         end;
        t := deep[x] - deep[y];
        for i := 0 to 19 do
         if ((t >> i) and 1) = 1 then
          x := b[x][i];
        if x = y then exit(x);
        for i := 19 downto 0 do
         if b[x][i] <> b[y][i] then
          begin
                x := b[x][i];
                y := b[y][i];
          end;
        exit(b[x][0]);
end;


begin
        read(n,m);
        for i := 1 to n - 1 do
         begin
                read(x,y);
                add1(x,y);
                add1(y,x);
         end;
        load_lca(1,1,0);
        for i := 1 to n do read(w[i]);
        for i := 1 to m do
         begin
                read(s[i],t[i]);
                lca[i] := get_lca(s[i],t[i]);
                add3(s[i],i);
                add4(t[i],i);//这样可以快速地找到那些玩家在某个点开始/结束
                len[i] := deep[s[i]] + deep[t[i]] - 2 * deep[lca[i]];
         end;
        dfs1(1,1);
        fillchar(f2,sizeof(f2),0);
        l2 := 0;
        fillchar(next2,sizeof(next2),0);
        fillchar(vector,sizeof(vector),0);
        fillchar(value,sizeof(value),0);
        dfs2(1,1);
        for i := 1 to n do write(ans[i],' ');
end.
```