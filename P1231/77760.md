$Pascal$ 跑到了第 $2$ 页!!!

声明 : 图片来自 [$programmy$](https://blog.csdn.net/mmy1996/article/details/52305135) 。

这是一道 **拆点** 的基础题目。首先,我们可以知道这是一道“三分图”,我们可以用网络流来搞。

但是发现我们要赋值的是点的流量而不是边的流量,所以我们可以**拆点**。

本来是这样子的 :

![](https://i.loli.net/2018/10/31/5bd93ce2c9599.jpg)

然后变成这样子 :

![](https://i.loli.net/2018/10/31/5bd93ce2d72e3.jpg)

所以这个样例会变成这样子 (草稿不要介意) :

![](https://cdn.luogu.com.cn/upload/pic/41048.png)

中间有两个 $n$ 对吧。建完模型跑最大流就可以了。

当然要跑 $ISAP$ 啦对吧。

```pascal
// luogu-judger-enable-o2
Uses math;

var
    value,reach,next:array[-1..210000] of longint;
    gap,dis,cnt:array[-1..81000] of longint;
    n,m,z,l,r,k,i,j,sum,tot,sink,source,number,maxflow:longint;

procedure add(l,r:longint);
begin
    inc(tot); reach[tot]:=r; value[tot]:=1; next[tot]:=cnt[l]; cnt[l]:=tot;
    inc(tot); reach[tot]:=l; value[tot]:=0; next[tot]:=cnt[r]; cnt[r]:=tot;
end;

function Dfs(now,flow:longint):longint;
var i,k,mindis,ret:longint;
begin
    mindis:=n-1; ret:=flow;
    if now=sink then exit(flow);
    i:=cnt[now];
    while i<>-1 do
    begin
        if value[i]>0 then
        begin
            if dis[now]=dis[reach[i]]+1 then
            begin
                k:=Dfs(reach[i],min(ret,value[i]));
                dec(value[i],k);inc(value[i xor 1],k);
                dec(ret,k);
                if dis[source]>=n then exit(flow-ret);
                if ret=0 then break;
            end;
             mindis:=min(mindis,dis[reach[i]]);
        end;
        i:=next[i];
    end;
    if ret=flow then
    begin
        dec(gap[dis[now]]);
        if gap[dis[now]]=0 then dis[source]:=n;
        dis[now]:=mindis+1;
        inc(gap[dis[now]]);
    end;
    exit(flow-ret);
end;

begin
    filldword(cnt,sizeof(cnt) div 4,maxlongint*2+1); tot:=1;
    read(n,m,z); source:=1; sink:=n+n+m+z+2;
    for i:=1 to m do add(source,i+1); // 连超源和练习册
    for i:=1 to z do add(i+n+n+m+1,sink); // 连答案和超汇
    for i:=1 to n do add(i+m+1,i+m+n+1); // 克隆一些书本然后相连
    read(k); for i:=1 to k do begin read(l,r); l:=l+m+1; r:=r+1; add(r,l); end; // 连练习册和书本,注意要连 r -> l 而不是 l -> r,当然你的建模不一样也是可以的
    read(k); for i:=1 to k do begin read(l,r); l:=l+n+m+1; r:=r+n+n+m+1; add(l,r); end; // 连书本和答案
    n:=sink; gap[source]:=n;
    while dis[source]<n do inc(maxflow,Dfs(source,maxlongint)); // 跑 ISAP
    writeln(maxflow); 
end.
```