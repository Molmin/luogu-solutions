~~(至今我没写过正规的数位DP...)..~~..

下面介绍1种奇怪的思路.适用于水过1些数位dp的题目..

暂时称之为.urdc...(不要问我这是什么.)..

主题思路.
```
求0~13678中符合要求的数.
可以转换为.求.
0~9999.
10000~12999.
13000~13599.
13600~13669.
13670~13677.
13678.
这几部分个数加起来.
再看下面几部分.
0000~9999.
0000~2999.
000~599.
00~69.
0~7.
0.
如果没有与题目要求冲突,这几部分完全等价上面几部分.
相当于只要,求整十整百整千这些东西.
而这种东西1般可以dp预处理,或者直接算出.
```
如果没有看懂.请看下面的题目具体分析.

------------

题意.求[a,b]中的相邻2个数之差 >= 2数的总数.没有前导0.

显然[a,b]可以转换为.[0,b]-[0,a-1].

先思考如何求[0,10^k-1].

设**f[i,j]**表示以j为第i位的windy数总数.个位是第1位.

如**f[2,3] = 7**,有30,31,35,36,37,38,39.

注意前导0不去管它.

这个很好求.

$f[i,j] = \sum_{k = 0}^{9}{f[i-1,k]\ (abs(j-k) >= 2)}$.

其中**f[1,i] = 1**.

所以答案似乎是.

$[0,10^i-1] = \sum_{j = 0}^{9}f[i,j]$.

然而对拍发现并不对...

发现**f[i,0]**出事.

**j = 0**相当于前导0,不受约束条件.

所以单独计算**f[i,0]**.

$last[i] = last[i-1]+\sum_{j = 1}^{9}f[i-1,j]$.

怎么变**last[i]**了..

**如果更高位还有不是0的,这个0依旧受限.**

也就是计算**非f[i,0]**的东西要用到**f[i,0]**.

所以发现.

$[0,10^i-1] = last[i+1] = last[i]+\sum_{j=1}^{9}f[i,j]$.

其中**last[1] = 1**.

啊,~~愉快~~的预处理完了..

------------

接下来看如何计算.

像上面1样,对n进行分割.

怎么弄的...按位啊...

第i位对答案的贡献.

$\sum_{j=0}^{p-1}f[i,j]$.

p是n第i位上的数.

就是忽略了高位的数来直接计算答案.

当然限制条件要有.

得从高位计算,上面计算到13670~13670的时候直接break.

若所有计算都满足限制,答案额外加1.就是上面孤立的1部分.

以及最高位的**f[i,0]**实际贡献是**last[i]**.理由治己想.

------------

所以这么复杂的东西有什么用...

····思维暴力........

····可以通过预处理规避前导0的计算.

**注意,last数组是由于前导0特性与限制条件共同影响产生的.预处理计算上仍然不用管前导0.**

所以只是个针对前导0优化...?

当然效率也很高了...

Diu代码.
```
program P2657;
 uses math;
 var
  t:array[0..11,0..9] of int64;
  lastt:array[0..11] of int64;
  i,j,k,n,l,r,p,ssum,lasted,haha,upass:longint;
  ts:int64;
 procedure hahacheck;   //我用来验证的..
  var
   i,j:longint;
  begin
   //writeln(n);
   for i:=1 to n do
    begin
     for j:=0 to 9 do
      write(t[i,j],' ');
     writeln(lastt[i]);
    end;
  end;
 function hahasum(n:longint):longint;
  begin
   if n=-1 then exit(0);
   if n=0 then exit(1);   //由于log不能为0所以只能特判..
   k:=trunc(log10(n))+1;  //计算n总共有几位.
   ts:=1;
   while ts<=n do
    ts:=ts*10;
   ts:=ts div 10;         //用来取出n的当前位.
   ssum:=0;               //计算答案.
   lasted:=1008208820;    //判断是否合法.
   haha:=0;               //特判标记.
   upass:=1;              //合法标记.
   while ts>=1 do
    begin
     inc(haha);
     p:=n div ts;
     n:=n mod ts;
     ts:=ts div 10;
     if abs(p-lasted)<=1 then upass:=0;   //与上1位冲突.
     for i:=0 to p-1 do
      if abs(lasted-i)>=2 then
       if (haha=1) and (i=0) then inc(ssum,lastt[k])
                             else inc(ssum,t[k-haha+1,i]);  //之前讲的对答案的贡献..
     lasted:=p;
     if upass=0 then break;               //不能继续做.
    end;
   hahasum:=ssum+upass;
  end;
 begin
  readln(l,r);
  n:=trunc(log10(r)+1);
  //hahacheck;
  for i:=0 to 9 do
   t[1,i]:=1;
  lastt[0]:=0;
  lastt[1]:=1;
  for i:=2 to n do           //预处理....
   for j:=0 to 9 do
    begin
     t[i,j]:=0;
     for k:=0 to j-2 do
      inc(t[i,j],t[i-1,k]);
     for k:=j+2 to 9 do
      inc(t[i,j],t[i-1,k]);
     if j=0 then
      begin
       lastt[i]:=0;
       for k:=1 to 9 do
        inc(lastt[i],t[i-1,k]);
       dec(lastt[i],-lastt[i-1]);
      end;
    end;
  //hahacheck;
  writeln(hahasum(r)-hahasum(l-1));
 end.
```
(ಡωಡ).
