首先思考如何存储数据，可以用一个前缀和存储，读到 $ H $ 就 $ +1 $ ， 读到 $ G $ 就 $ -1 $ 。

因为当前位置的答案可由之前的答案扩张得到，所以可以动态规划。

设 $ dp[i] $ 为区间 $ [1,i] $ 的答案，状态转移可以写成：

$ dp[i]=min(dp[i-j]+1,dp[i]) , f[i]-f[i-j]<=0 $

$ dp[i]=min(dp[i-j],dp[i]) , f[i]-f[i-j]>0 $

$ j \in [1,k] $

这样的转移方程时间复杂度为 $ O(n \cdot k) $ 

考虑优化此方程，因为每次转移时都要找最小值，所以可以用一个小根堆维护。但这个小根堆要同时维护两个关键字，分别为 $ dp $ 和 $ f $ 的值，要求：

$ heap[i].dp<heap[j].dp $ 或 $ heap[i].dp=heap[j].dp \ \ \ \ \ and \ \ \ \ \ heap[i].f<heap[j].f $

$ j $ 为 $ i $ 的儿子节点。

之后每次状态转移时，使用堆顶元素转移即可。时间复杂度 $ O(n \cdot \log k) $

```cpp
program redistricting;

type tp=record
  a,loc:longint;
end;

var
   f:array[0..300005]of longint;
   heap:array[0..300005]of tp;
   n,m,tot:longint;

function min(a,b:longint):longint;
begin
  if a<b then exit(a)
    else exit(b);
end;

procedure re;
var i:longint;
    s:ansistring;
begin
  readln(s);
  for i:=1 to n do begin
    if s[i]='H' then inc(f[i])
      else dec(f[i]);
    inc(f[i],f[i-1]);
  end;
end;

procedure push(q,p:longint);
var i,j:longint;
    t:tp;
begin
  inc(tot);
  heap[tot].a:=q;
  heap[tot].loc:=p;
  i:=tot;
  while i shr 1<>0 do begin
    j:=i shr 1;
    if (heap[j].a>heap[i].a) or ((heap[j].a=heap[i].a) and (f[heap[j].loc]>f[heap[i].loc])) then begin
      t:=heap[i];
      heap[i]:=heap[j];
      heap[j]:=t;
    end
      else exit;
    i:=j;
  end;
end;

procedure del;
var i,j:longint;
    t:tp;
begin
  heap[1]:=heap[tot];
  dec(tot);
  i:=1;
  while i shl 1<=tot do begin
    j:=i shl 1;
    if (j+1<=tot) and ((heap[j].a>heap[j+1].a) or ((heap[j].a=heap[j+1].a) and (f[heap[j].loc]>f[heap[j+1].loc]))) then inc(j);
    if (heap[i].a>heap[j].a) or ((heap[i].a=heap[j].a) and (f[heap[i].loc]>f[heap[j].loc])) then begin
      t:=heap[i];
      heap[i]:=heap[j];
      heap[j]:=t;
    end
      else exit;
    i:=j;
  end;
end;

procedure main;
var i,j:longint;
    dp:array[0..300005]of longint;
begin
  fillchar(dp,sizeof(dp),$5f);
  for i:=1 to 300005 do heap[i].a:=maxint;
  dp[0]:=0;
  push(0,0);
  for i:=1 to n do begin
    while heap[1].loc<i-m do del; 
    if f[i]-f[heap[1].loc]<=0 then dp[i]:=min(dp[i],heap[1].a+1)
      else dp[i]:=min(dp[i],heap[1].a);
    push(dp[i],i);
  end;
  writeln(dp[i]);
end;

begin
  readln(n,m);
  re;
  main;
end.

```