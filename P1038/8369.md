难得碰到这么坑的题目，来凑一发题解：

##坑点：

坑一：Σ后面的Ui算是算在Σ外面的，即阈值只用减一遍，希望不会有小伙伴跟我一样数学不好

坑二：输入层的阈值是不用减的

坑三：**输入层可能就是输出层**

坑四：只有在**Ci>0**即兴奋状态下才会向下一个神经元传递信号

坑五：只用输出**大于0**的输出层神经元状态，**从小到大输出**，如果都为0输出**NULL**


##思路：

其实坑点出来了之后这道题差不多就做完了……不过还是讲一讲吧：

将输入层的点进队列，然后操作所有与队首i相连的点j：

①i已经处理过了--统计vis i\*w i,j

②i还没处理且不在队列中，将i入队

最后处理完所有与队首相连的点后再将c[i]←c[i]-u[i]**【此处需要特判输入层】**

如果队首相连的所有点都处理过，说明该点在输出层，标记

所有点做完之后枚举一遍，将标记了的且c[i]>0的点输出


##代码如下(pascal):







```cpp
const MAXN=205;MAXM=MAXN*MAXN;
var n,p,i,h,t,x,y,z:longint;flag:boolean;
    c,u,he,q:Array[0..MAXN]of longint;
    inq,vis,f:Array[0..MAXN]of boolean;//inq:在队列中;vis:已经操作过;f:输出层标记
    v,w,next:Array[0..MAXM]of longint; //静态链接边集数组的邻接表
begin
    //读入部分
    readln(n,p);
    for i:=1 to n do
    begin
        read(c[i],u[i]);
        if c[i]>0 then                //输入层的点
        begin
            inc(t);q[t]:=i;           //入队，为了将输入层与其他点分开,inq没有赋true
        end;
    end;
    for i:=1 to p do
    begin
        read(x,y,z);                  
        //由于读入没有说清楚是否是给出的是单向边,防错就建成双向边了,但看↓题解好像有用单向边
        v[2*i-1]:=y;w[2*i-1]:=z;next[2*i-1]:=he[x];he[x]:=2*i-1;
        v[2*i]:=x;w[2*i]:=z;next[2*i]:=he[y];he[y]:=2*i;
    end;
    //操作部分
    while h<>t do
    begin
        inc(h);
        vis[q[h]]:=true;               //该点已操作
        flag:=false;
        x:=he[q[h]];                   //枚举所有与该点有关的边
        while x<>0 do
        begin
            if vis[v[x]] then          //v[x]是邻接的点
            begin
                if c[v[x]]>0 then inc(c[q[h]],w[x]*c[v[x]])//注意要兴奋才能传递哟
            end
            else begin
                flag:=true;            //有 没有操作过的点,说明队首不是输出层
                if not inq[v[x]] then
                begin                  //入队
                    inc(t);q[t]:=v[x];inq[v[x]]:=true;
                end;
            end;
            x:=next[x];
        end;
        if inq[q[h]] then dec(c[q[h]],u[q[h]]);//除了输入层之外,所有已入队的点inq为true
        if not flag then f[q[h]]:=true;//输出层标记
    end;
    //输出部分
    flag:=false;
    for i:=1 to n do
        if f[i] and (c[i]>0) then
        begin
            writeln(i,' ',c[i]);
            flag:=true;
        end;
    if not flag then writeln('NULL');
end.

```