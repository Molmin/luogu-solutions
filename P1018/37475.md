此题原可以直接DP过，但改了数据，不加高精只能得60分，因此大部分题解都是不能AC的，现在一个能用的PASCAL题解都没有，原主代码为


------------

```
for i1:=1 to k do
  for i2:=i1+1 to n do
    for i3:=i1 to i2-1 do
      f[i2,i1]:=max(f[i2,i1],f[i3,i1-1]*sum[i3+1,i2]);

sum数组代表要取的数，f数组代表之前得出的最大值
------------

思路：
  把数组作为字符串处理

  进行运算时变成数字（注：是高精乘高精），返回字符串

  建一个函数max比较字符串

------------

```
```pascal
var
  n,k,i,j,i1,i2,i3:longint;
  f,sum:array[0..100,0..100] of ansistring;
  s:string;
  
function gjc(a1,b1:string):string;
var
  lena,lenb,lenc:longint;
  i,j,x:longint;
  a,b,c:array[0..200] of longint;
  k:string;
begin
  fillchar(a,sizeof(a),0);  
  fillchar(b,sizeof(b),0);  //重置数组
  fillchar(c,sizeof(c),0);
  lena:=length(a1); lenb:=length(b1);
  for i:=1 to lena do a[lena-i+1]:=ord(a1[i])-48;  //转化为数字
  for i:=1 to lenb do b[lenb-i+1]:=ord(b1[i])-48;
  for i:=1 to lena do  
    begin 
      x:=0; 	            //高精度运算                                                 
      for j:=1 to lenb do
        begin                                
          c[i+j-1]:=a[i]*b[j]+x+c[i+j-1];              
          x:=c[i+j-1] div 10;
		  c[i+j-1]:=c[i+j-1] mod 10;
        end;
      c[i+j]:=x;                                               
    end;
  lenc:=i+j;k:='';
  while (c[lenc]=0) and (lenc>1) do dec(lenc);
  for i:=lenc downto 1 do k:=k+chr(c[i]+48);
  exit(k);  //返回字符串
end;

function max(a,b:string):string;  //比较字符串的大小
var
  lena,lenb:longint;
begin
  lena:=length(a); lenb:=length(b);
  if (lena>lenb) then exit(a);
  if (lena<lenb) then exit(b);
  if (lena=lenb) then 
    if (a>b) then exit(a)
	  else exit(b);
end;
  
begin
  readln(n,k);
  readln(s);
  for i:=1 to n do
    for j:=1 to n do
      sum[i,j]:=copy(s,i,j-i+1);  //直接拷贝，当字符串处理
  for i:=1 to n do f[i,0]:=sum[1,i]; //初始化
  for i1:=1 to k do
    for i2:=i1+1 to n do
      for i3:=i1 to i2-1 do
        f[i2,i1]:=max(f[i2,i1],gjc(f[i3,i1-1],sum[i3+1,i2]));  //高精计算返回字符串比较
  writeln(f[n,k]);
end.

```
```