## 好像又没有pascal题解。。。
### 思路：本题树形dp，然后还有dfs。
##### 代码如下
```pascal
var
  f:array[0..301,0..301,0..1] of longint;
  a:Array[0..301] of longint;
  lc,rc,data:array[0..301] of longint;
  ans,n,m,p:longint;
function Find(x:longint):longint;
begin
  if x=0 then exit(0);
  a[x]:=1;
  inc(a[x] , Find(rc[x]) + Find(lc[x]) );
  exit(a[x]);
end;
function fmin(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
procedure init;
var
  x,y,d,i:longint;
begin
  readln(n,m,p);
  for i:=1 to n-1 do
    begin
      readln(x,y,d);
      rc[y]:=lc[x];//多叉转二叉
      lc[x]:=y;
      data[y]:=d;
    end;
  a[1]:=Find(1);//预处理第i个点向下有多少个节点,包含自己
end;
function dfs(x,k,col:longint):longint;
var
  temp,i,t1,t2:longint;
begin
  if k<0 then exit(123456789);//状态不成立
  if(x=0)and(k=0)then//特判一下
     begin
       f[x,k,col]:=0;
       exit(0);
     end;
  if f[x,k,col]>=0 then exit(f[x,k,col]);//记忆化
  if f[x,k,col]=-1 then f[x,k,col]:=123456789;
  for i:=0 to fmin(k , a[x]) do//枚举
    begin
      t1:=dfs(lc[x],i-1,0)+ord(col=0)*data[x];
      t2:=dfs(lc[x],i,1)+ord(m=2)*ord(col=1)*data[x];
      temp:=fmin(t1 , t2);
      inc( temp , dfs(rc[x],k-i,col) );//向兄弟分担大头吃k-i个果子
      f[x,k,col]:=fmin(f[x,k,col] , temp);
    end;
  exit(f[x,k,col]);
end;
procedure main;
var
  i,j,k:longint;
begin
  if n-p<m-1 then//特判一下，果子不够吃
     begin
       writeln(-1);
       exit;
     end;
  fillchar(f,sizeof(f),255);
  ans:=dfs(lc[1],p-1,0);//大头必须要吃第一个果子
  writeln(ans);
end;
begin
  init;
  main; 
end.
```