如果以传统方法做只能拿部分分，#7到#9都会TLE。

因此，我有新的构思：**[color=red]构造回文数[/color]**。

对于一个任意的正整数N，将其转为字符串后其长度有两种可能：

1.如果它有奇数位，那么可以被分成copy(s,1,length(s) div 2+1)和copy(s,length(s) div 2+2,length(s))这两部分。如1被分为'1'和''，123被分为'12'和'3'等等。

2.如果它有偶数位，那么可以被分成copy(s,1,length(s) div 2)和copy(s,length(s) div 2+1,length(s))这两部分。如12被分为'1'和'2'，1234被分为'12'和'34'等等。

回文数同样适用于上述规则。如101被分为'10'和'1'，1221被分为'12'和'21'等。通过观察可以发现，对于任意一个回文数，如果将分解后的第一个子串称为s1，将第二个子串称为s2，那么通过s1可以造出两个回文数，分别是s2:=反转(copy(s1,1,length(s1)-1))和s2:=反转(s1)。造出来的回文数就是s1+s2。这也就意味着，我们可以通过枚举s1来实现对回文数的枚举。

注意：枚举出的回文数并不是有序的。例如，s1从1枚举到9，分别造出1，11，2，22，3，33，4，44……因此，造出回文数后首先判断它是否在指定的范围内，然后判断其是否为质数，全部构造完毕后再进行排序。因为回文质数的数量很少，因此可以用最低级的选择排序。

```delphi

var nn,mm,len1,len2,i:longint;
    s1,s2:string;
    a:array [0..10000] of longint;

function prime(n:longint):boolean;//判断质数
var i:longint;
begin
 if n=1 then exit(false);
 prime:=true;
 for i:=2 to trunc(sqrt(n)) do if n mod i=0 then exit(false);
end;

procedure work_(s:string;min,max:longint);//对回文数是否合法的判断
var n:longint;
begin
 val(s,n);
 if (n>=min) and (n<=max) then//范围判断
  if prime(n) then//质数判断
  begin
   inc(a[0]);
   a[a[0]]:=n;
  end;
end;

procedure work(s1,s2:string);
var n,m,i,j,temp:longint;
    s,ss:string;
begin
 val(s1,n);val(s2,m);
 for i:=n to m do//枚举回文数
 begin
  str(i,s);
  ss:='';
  for j:=length(s)-1 downto 1 do ss:=ss+s[j];
  work_(s+ss,nn,mm);
  work_(s+s[length(s)]+ss,nn,mm);
 end;
 for i:=1 to a[0]-1 do//排序
  for j:=i+1 to a[0] do
   if a[i]>a[j] then
    begin
     temp:=a[i];a[i]:=a[j];a[j]:=temp
    end;
 for i:=1 to a[0] do writeln(a[i]);
end;

begin
 read(nn,mm);
 str(nn,s1);str(mm,s2);
 if odd(length(s1)) then len1:=length(s1) div 2+1 else len2:=length(s2) div 2;
 if odd(length(s2)) then len2:=length(s2) div 2+1 else len2:=length(s2) div 2;
 s1:='1';
 for i:=1 to len1-1 do s1:=s1+'0';
 s2:='';
 for i:=1 to len2 do s2:=s2+'9';
 fillchar(a,sizeof(a),0);
 work(s1,s2);
end.

```