pascal的题解好少……

题目都给了一个图了，一看就知道和树有关。而题目是要求最优解，所以是树形dp。

先考虑输入（第一次见过输入这么坑的），使用递归读入，读完一个点后再读左结点和右结点，详见代码中的pasread。

## 接下来是dp方程：

先考虑死角，这是一个经典的01背包问题，不多讲。

考虑还有分岔路的，枚举现在剩下的时间，假设为$j$。再枚举分给左岔路的时间$k$，那么$j-k$就是分给右岔路的时间。

设$f[i,j]$为剩下$j$秒，从以$i$为根节点的子树所能得到的最大价值

$f[i,j]=max(f[i,j],f[left[i],k]+f[right[i],j-k])$

其中$left[i]$表示$i$的左结点，$right[i]$表示$i$的右结点。

结果就是$f[1,n]$。

## 另外，题目中有多个坑点：

1、时间要乘2，因为小偷不仅要进去，而且要出来，这样要2倍时间。这样就只用考虑进去了。

2、总时间要减1，因为如果恰好在第$n$秒到达出口，会被警察逮个正着，所以需要考虑在$n-1$秒内到达出口。

以下是代码：

```pascal
uses math;
var f:array[0..600,0..600] of longint;
    l,r,t,ans,h:array[1..600] of longint; //ans[i]表示在i位置的画的幅数
    w,c:array[0..600,0..30] of longint;
    time,n,i,j,k,len:longint;
procedure pasread; //递归读入
var lenn,i:longint;
begin
  inc(len);lenn:=len;read(t[len],ans[len]);t[len]:=t[len]*2;
  for i:=1 to ans[len] do read(w[len,i],c[len,i]);
  if ans[len]=0 then //有分岔路的情况
  begin
    pasread; //读左边
    l[lenn]:=lenn+1;r[lenn]:=len+1;
    pasread; //读右边
  end;
end;
procedure dfs(kk:longint); //先序遍历，存下访问结点的顺序，这样就可以不必递归访问。
begin
  if l[kk]>0 then dfs(l[kk]);
  if r[kk]>0 then dfs(r[kk]);
  inc(n);h[n]:=kk;
end;
begin
  readln(time);dec(time);
  pasread;
  dfs(1);
  for i:=1 to n do
    if l[h[i]]>0 then
      for j:=t[h[i]] to time do //dp，得从t[h[i]]开始是因为需要用这么多时间走过走廊
        for k:=0 to j-t[h[i]] do
          f[h[i],j]:=max(f[h[i],j],f[l[h[i]],k]+f[r[h[i]],j-k-t[h[i]]])
    else
      for j:=1 to ans[h[i]] do //01背包
        for k:=time downto c[h[i],j] do
          if k-c[h[i],j]>=t[h[i]] then //判断剩下的时间是否能够通过走廊
            f[h[i],k]:=max(f[h[i],k],f[h[i],k-c[h[i],j]]+w[h[i],j])
          else break;
  writeln(f[1,time]);
end.
```