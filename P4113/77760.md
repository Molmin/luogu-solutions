这是很明显的一个莫队的板子。

求的东西是什么呢？

- 区间所有数的个数的总和,要求每一个数字的个数要大于 $1$

所以就可以用莫队啦,如果一个数字的个数 $=2$ 产生贡献,变为 $1$ 了,产生负贡献。

时间复杂度: 由于 $N$ 和 $M$ 同阶,所以是 $O(N\sqrt{N})$。预计是 $100$ 分。

```pascal
// luogu-judger-enable-o2
var
    node_num,i,j,n,m,l,r,sum:longint;
    num:array[-1..2000010] of longint;
    id,left,right,recf,ans:array[-1..2000010] of longint;
    bucket:array[-1..2000010] of longint;

procedure Sort(l,r:longint);
var
    i,j,s1,s2,t:longint;
begin
    i:=l; j:=r; s1:=recf[(l+r) div 2]; s2:=right[(l+r) div 2];
    repeat
        while ((recf[i]<s1)or((recf[i]=s1)and(right[i]<s2))) do inc(i);
        while ((recf[j]>s1)or((recf[j]=s1)and(right[j]>s2))) do dec(j);
        if i<=j then
        begin
            t:=recf[i];  recf[i]:=recf[j];   recf[j]:=t;
            t:=id[i];    id[i]:=id[j];       id[j]:=t;
            t:=left[i];  left[i]:=left[j];   left[j]:=t;
            t:=right[i]; right[i]:=right[j]; right[j]:=t;
            inc(i); dec(j);
         end;
    until i>=j;
    if i<r then Sort(i,r);
    if j>l then Sort(l,j);
end;

function Locate(node:longint):longint;
begin
    if node mod node_num=0 then exit(node div node_num);
    exit(node div node_num+1);
end;

procedure Ready;
begin
    read(n,m,m);
    node_num:=trunc(sqrt(n));
    for i:=1 to n do read(num[i]);
    for i:=1 to m do begin id[i]:=i; read(left[i],right[i]); recf[i]:=Locate(left[i]); end;

    Sort(1,m);
end;

procedure add(x:longint); begin inc(bucket[x]); if (bucket[x]=2) then inc(sum); end;

procedure dim(x:longint); begin dec(bucket[x]); if (bucket[x]=1) then dec(sum); end;

begin
    Ready; l:=1; r:=0;
    for i:=1 to m do
    begin
        while r<right[i] do begin  inc(r); add(num[r]); end;
        while r>right[i] do begin dim(num[r]); dec(r); end;
        while l<left[i] do begin dim(num[l]); inc(l); end;
        while l>left[i] do begin dec(l); add(num[l]); end;
        ans[id[i]]:=sum;
    end;

    for i:=1 to m do writeln(ans[i]);
end.
```

如果不满足于 $100$ 分,我们可以把问题直接转化为:

- 区间里有多少个数是唯一的 (类似于 $HH$ 的项链)
 
然后直接主席树搞一搞。到时候再写。

时间复杂度: $O(N\log N)$,预计分数 $200$ 分。