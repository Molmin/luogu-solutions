先看题目意思：

本题的意思是想让你用两套系统拦截所有的导弹，求半径平方的最小值。

最容易想到的自然是$O$($n^3$)的想法，枚举两个工作半径，然而只有$40$分。

再想到枚举一个工作半径，处理出另一个半径，时间复杂度$O$($n^2$),70分到手。

现在我们思考正解：

先将所有导弹与两套系统的距离求出来。

再按第一套系统从小到大排序。

我们来看接下去的代码

```
b[n+1]:=0;
for i:=n downto 1 do
  b[i]:=max(b[i],b[i+1]);

```

很显然$b[i]$的意思为在第$i$到$n$中与第二套系统的距离最大值。

即需要的设定半径。（此时$a$数组已经从小到大排序）

再来一段：

```
a[0]:=0;
sum:=maxlongint;
for i:=0 to n do
  if sum>sqr(a[i])+sqr(b[i+1]) then
    sum:=sqr(a[i])+sqr(b[i+1]);
```
这段代码意为将与第一套系统距离的第$i$小值当作设置半径。

再看$b[i]$为在第$i$到$n$中与第二套系统的距离最大值，即为第二套系统的半径。

则$a$[$i$]* $a$[$i$] + $b$[$i$+$1$] * $b$[$i$+$1$]就为


将与第一套系统距离的第$i$小值当作设置半径所需代价。

再用循环跑一遍，最小值就出来了。时间复杂度$O$($n$ $log$ $n$)（算上快排）

### Code

```
var a,b:array[0..1000001]of real;
sum:real;
x1,y1,x2,y2,i,x,y,n:longint;
function max(x,y:real):real;
begin
if x>y then exit(x) else exit(y);
end;
procedure sort(l,r: longint);
var i,j: longint;x,y:real;
begin
i:=l;j:=r;x:=a[(l+r) div 2];
repeat
  while a[i]<x do inc(i);
  while x<a[j] do dec(j);
  if not(i>j) then
    begin
    y:=a[i];a[i]:=a[j];a[j]:=y;
    y:=b[i];b[i]:=b[j];b[j]:=y;
    inc(i);j:=j-1;
    end;
until i>j;
if l<j then sort(l,j);
if i<r then sort(i,r);
end;
begin
readln(x1,y1,x2,y2);
readln(n);
for i:=1 to n do
  begin
  readln(x,y);
  a[i]:=sqrt(sqr(x-x1)+sqr(y-y1));
  b[i]:=sqrt(sqr(x-x2)+sqr(y-y2));
  end;
sort(1,n);
b[n+1]:=0;
for i:=n downto 1 do
  b[i]:=max(b[i],b[i+1]);
a[0]:=0;
sum:=maxlongint;
for i:=0 to n do
  if sum>sqr(a[i])+sqr(b[i+1]) then
    sum:=sqr(a[i])+sqr(b[i+1]);
writeln(sum:0:0);
end.

```
