没有Pascal题解，来一发~~~

个人认为这是线段树经典题（当然其实也可以用分块）。

首先，直接进行区间修改是不存在的，所以只能用单点修改。

不过，这样的效率很低。考虑像搜索一样剪枝。

显然，$\lfloor \sqrt{1} \rfloor=1$，所以当一个点等于$1$时就没有更新的必要了。

同理，当一个区间里面全是$1$的话，也就没有更新的必要了。这里说数列中的每个数都大于$0$，我们只要判断区间和是否等于$r-l+1$就行了。

于是可以轻松的写出代码（线段树版）：

```pas
var
  a:array[1..100000]of int64;
  s:array[1..400000]of int64;  //线段树一般数组开4倍
  n,q,i,l,r,x,t:longint;
procedure pushup(x:longint);   //向上更新
begin
  s[x]:=s[x*2]+s[x*2+1];
end;
procedure build(l,r,x:longint);//建树
begin
  if l=r then
  begin
    s[x]:=a[l];
    exit;
  end;
  build(l,(l+r) div 2,x*2);
  build((l+r) div 2+1,r,x*2+1);
  pushup(x);
end;
procedure update(l,r,tl,tr,x:longint);//更新
begin
  if l=r then                  //单点修改
  begin
    s[x]:=trunc(sqrt(s[x]));
    exit;
  end;
  if (l>=tl) and (r<=tr) and (s[x]=r-l+1) then//把全是1的区间剪枝剪掉了
    exit;
  if tl<=(l+r) div 2 then
    update(l,(l+r) div 2,tl,tr,x*2);
  if tr>(l+r) div 2 then
    update((l+r) div 2+1,r,tl,tr,x*2+1);
  pushup(x);
end;
function query(l,r,tl,tr,x:longint):int64;//查询
begin
  if (l>=tl) and (r<=tr) then
    exit(s[x]);
  query:=0;
  if tl<=(l+r) div 2 then
    inc(query,query(l,(l+r) div 2,tl,tr,x*2));
  if tr>(l+r) div 2 then
    inc(query,query((l+r) div 2+1,r,tl,tr,x*2+1));
  pushup(x);
end;
begin
  read(n);
  for i:=1 to n do
    read(a[i]);
  build(1,n,1);
  read(q);
  for i:=1 to q do
  begin
    read(x,l,r);
    if l>r then                //一个需要注意的地方！r可能大于l
    begin
      t:=l;
      l:=r;
      r:=t;
    end;
    if x=0 then
      update(1,n,l,r,1)
    else
      writeln(query(1,n,l,r,1));
  end;
end.
```