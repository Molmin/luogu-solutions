下面也没有什么pascal的程序，作为p党的我，就为大家作主点贡献。

至于这道题嘛，乍一看，很明显是一个线段树，

线段树，类似区间树，它在各个节点保存一条线段（数组中的一段子数组）


线段树裸题，拿来当模板……

两个标记，一个记录乘，另一个记录加，然后规定好先乘后加，就可以像普通的带标记的线段树一样维护了。

线段树是个神奇的算法，希望大家好好学学。。。

=======================================================================================


```cpp
type
  tree=record
     l,r,sum,mul,plus:longint;   //sum区间和，mul乘标记，plus加标记
  end;
var
  n,m,p,i,t,x,y,d:longint;
  a:array[0..400040]of tree;
  num:array[0..100010]of longint;
procedure build(k,l,r:longint);        //建树
var
  i,mid:longint;
begin
   a[k].l:=l;a[k].r:=r;a[k].mul:=1;a[k].plus:=0;
   if l=r then begin a[k].sum:=num[l];exit; end;
   i:=k<<1;mid:=(l+r)>>1;
   build(i,l,mid);
   build(i+1,mid+1,r);
   a[k].sum:=a[i].sum+a[i+1].sum;
end;
procedure pushdown(k:longint);   //标记下传
var
  i:longint;
begin
   if (a[k].mul=1) and (a[k].plus=0) then exit;
   if a[k].l=a[k].r then begin a[k].mul:=1;a[k].plus:=0;exit; end;
   i:=k<<1;
   a[i].sum:=(a[i].sum*a[k].mul+a[k].plus*(a[i].r-a[i].l+1)) mod p;
   a[i+1].sum:=(a[i+1].sum*a[k].mul+a[k].plus*(a[i+1].r-a[i+1].l+1)) mod p;
   a[i].mul:=a[i].mul*a[k].mul mod p;
   a[i].plus:=(a[i].plus*a[k].mul+a[k].plus) mod p;
   a[i+1].mul:=a[i+1].mul*a[k].mul mod p;
   a[i+1].plus:=(a[i+1].plus*a[k].mul+a[k].plus) mod p;
   a[k].mul:=1;a[k].plus:=0;
end;
procedure changemul(k:longint);   //乘法修改
var
  i,mid:longint;
begin
   pushdown(k);
   if (x<=a[k].l) and (a[k].r<=y) then
       begin
          a[k].sum:=a[k].sum*d mod p;
          a[k].mul:=d mod p;
          exit;
       end;
   mid:=(a[k].l+a[k].r)>>1;i:=k<<1;
   if x<=mid then changemul(i);
   if mid<y then changemul(i+1);
   a[k].sum:=(a[i].sum+a[i+1].sum) mod p;
end;
procedure changeplus(k:longint);   //加法修改
var
  i,mid:longint;
begin
   pushdown(k);
   if (x<=a[k].l) and (a[k].r<=y) then
       begin
          a[k].sum:=(a[k].sum+d*(a[k].r-a[k].l+1)) mod p;
          a[k].plus:=d mod p;
          exit;
       end;
   mid:=(a[k].l+a[k].r)>>1;i:=k<<1;
   if x<=mid then changeplus(i);
   if mid<y then changeplus(i+1);
   a[k].sum:=(a[i].sum+a[i+1].sum) mod p;
end;
function ask(k:longint):longint;   //询问
var
  i,mid:longint;
begin
   pushdown(k);
   if (x<=a[k].l) and (a[k].r<=y) then exit(a[k].sum);
   ask:=0;
   mid:=(a[k].l+a[k].r)>>1;i:=k<<1;
   if x<=mid then ask:=ask(i);
   if mid<y then ask:=ask+ask(i+1);
   exit(ask mod p);
end;
begin
   //assign(input,'r.in');assign(output,'r.out');reset(input);rewrite(output);
   readln(n,p);
   for i:=1 to n do read(num[i]);
   readln(m);
   build(1,1,n);
   //for i:=1 to 10 do writeln(a[i].l,' ',a[i].r,' ',a[i].sum,' ',a[i].mul,' ',a[i].plus);writeln;
   for i:=1 to m do
       begin
          read(t,x,y);
          if t=1 then begin read(d);changemul(1); end;
          if t=2 then begin read(d);changeplus(1); end;
          if t=3 then writeln(ask(1));
       end;
   //close(input);close(output);
end.

```