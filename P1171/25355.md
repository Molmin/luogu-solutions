##第一次做状压dp。。。位运算一定要熟练。。。

##这里给出pas的状压题解

设状态f[i,j]表示当前最后一个到i，把j转成二进制，第k位上为1的表示k去过了。

则方程：

如果第j位上为0，f[j,k+1 << (j-1)]:=min{f[i,k]+a[i,j]}

判断第j位上是否为0需要用到一个位运算k and (1 << (j-1))，即把二进制下的k的第j位截了出来。

然而这样跑的很慢，#10会被卡掉，需要卡点常。。。

丑陋的代码：

```cpp
uses math;
const
  maxn=1 << 20;
var
  f:array[0..21,0..maxn]of longint;
  a:array[0..21,0..21]of longint;
  n,i,j,k,x,ans,t,tt,sum,m:longint;
begin
  readln(n);
  for i:=1 to n do 
    for j:=1 to n do read(a[i,j]);
  m:=1 << n-1;
  fillword(f,sizeof(f) >> 1,$7f);
  f[1,1]:=0;
  k:=1;
  while k<=m do 
    begin
      for i:=1 to n do 
        if f[i,k]<>8323199 then //如果当前状态存在
          for j:=1 to n do 
            begin
              x:=1 << (j-1);//如果不先用一个变量存起来，您可能会T。。。
              if k and x=0 then 
                f[j,k+x]:=min(f[j,k+x],f[i,k]+a[i,j]);
            end;
      inc(k,2);//因为第一个村庄已经去过了，所以可能的状态一定是个奇数，所以可以+2
    end;
  ans:=233333333;
  for i:=2 to n do
    ans:=min(ans,f[i,m]+a[i,1]);
  write(ans);
end.
```