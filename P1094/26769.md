****一道比较简单的贪心

本题重点在于怎样确保最优。总之我这一种是没有问题的，应该还有其他方法，在此不一一罗列了，用数学中的不等式可以推得


    

    

        
        
    
```cpp
var
    buck:array[5..200] of longint;
    a:array[1..30000] of integer;
    w,n,num,x,i,j,t,left,right,group:longint;
begin
    read(w);
    read(n);
    fillchar(buck,sizeof(buck),0);
    for i:=1 to n do
    begin
        read(num);
        inc(buck[num]);
    end;
    x:=0;
    for i:=w downto 5 do //从w开始就已经足够了
    begin
        t:=buck[i];
        if t = 0 then
            continue;
        for j:=1 to t do
        begin
            a[x + 1]:=i;
            inc(x);
        end;
    end;
    //以上这大段就是传说中的“桶排序”，O(n)的复杂度，可以把时间省到极限，尽管快排的n log n并没有超出10^6
    group:=0;
    left:=1;
    right:=n;
    while left < right do //注意不是<=
    begin
        if (a[left] + a[right] > w) then //该条件已经隐含了(a[left] >= w)
        begin
            inc(left);
            inc(group); // a[left]独立成为一组
            continue;
        end;
        inc(group);//a[left] 与 a[right] 共为一组
        inc(left);
        dec(right);
    end;
    if left = right then
        inc(group); //这一组即是唯一那个孤零零处在当中的数
    //若left>right,则中间并未夹有多余的数，无需无中生有地添一组
    write(group);
end.
```