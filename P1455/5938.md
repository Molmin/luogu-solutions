缩点+01背包，很容易看出来

我是来练习tarjan的，所以就用的tarjan来缩点，记录下每个强连通分量的cost和wealth，缩完点以后直接01背包搞定


PS：数据真是小，完全不用担心时间问题 {总用时#810ms 最长时间的点#239ms}


附上不怎么好看但还算工整的代码(P党福利)：






```cpp
var
  point,next,head,dfn,low,c,d,cost,wealth,stack,dp:array[0..10000] of longint;
  instack:array[0..10000] of boolean;
  n,m,w,x,y:longint;
  num,sccnum,i,j,top,ans:longint;
function min(a,b:longint):longint;
  begin
    if a<b then exit(a) else exit(b);
  end;
procedure addedge(u,v:longint);
  begin
    inc(num);
    point[num]:=v;
    next[num]:=head[u];
    head[u]:=num;
  end;
procedure tarjan(u:longint);
  var
    p,root:longint;
  begin
    inc(num);dfn[u]:=num;low[u]:=num;
    inc(top);stack[top]:=u;instack[u]:=true;
    p:=head[u];
    while p<>0 do
      begin
        if dfn[point[p]]=0 then
          begin
            tarjan(point[p]);
            low[u]:=min(low[u],low[point[p]]);
          end
        else
          if instack[point[p]] then
            low[u]:=min(low[u],dfn[point[p]]);
        p:=next[p];
      end;
    if dfn[u]=low[u] then
      begin
        inc(sccnum);
        repeat
          root:=stack[top];instack[root]:=false;dec(top);
          cost[sccnum]:=cost[sccnum]+c[root];
          wealth[sccnum]:=wealth[sccnum]+d[root];
        until u=root;
      end;
  end;
begin
  read(n,m,w);
  for i:=1 to n do read(c[i],d[i]);
  num:=0;
  for i:=1 to m do
    begin
      read(x,y);
      addedge(x,y);
      addedge(y,x);
    end;
  num:=0;sccnum:=0;
  for i:=1 to n do
    if dfn[i]=0 then
      begin
        top:=0;
        tarjan(i);
      end;
  for i:=1 to sccnum do
    for j:=w downto cost[i] do
      if dp[j]<dp[j-cost[i]]+wealth[i] then
        dp[j]:=dp[j-cost[i]]+wealth[i];
  ans:=0;
  for i:=1 to w do
    if dp[i]>ans then
      ans:=dp[i];
  writeln(ans);
end.

```