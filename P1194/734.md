本题描述得非常好，以至于很多人都没有看出来本题的真实面目。其实这是非常基础一道**[color=red]最小生成树[/color]**问题，但是需要进行一点小改动。下面就对本题进行一点小改动。

首先分析那个优惠，由题意，只有访问了i才能有j的优惠，也就是说，如果访问了i，我们就能以a[i,j]的代价访问j。我们发现这一点[color=red]和最小生成树的选取一个点[/color]非常相似。我们不妨将i往j连一条权威a[i,j]的边，同时加入一个辅助点0，使其与所有点的距离为a，由此，我们只要从0出发，求一边最小生成树问题就能得出解。由于B较小，建议使用prim算法。算法如下：

```delphi
var
  a:array[0..500,0..500] of longint;//图
  d:array[0..500] of longint;//到原点的路径
  f:array[0..500] of boolean;//访问标记
  i,j,k,min,m,n,s,ans:longint;
begin
  readln(m,n);
  for i:=1 to n do
    begin
      for j:=1 to n do
        begin
          read(a[i,j]);
          if (a[i,j]=0) and (i<>j) then a[i,j]:=m;
        end;
      readln;
    end;//连边
  for i:=0 to n do
    d[i]:=m;//赋初值
  fillchar(f,sizeof(f),true);
  f[0]:=false;
  for j:=1 to n do
    begin
      min:=0;
      for k:=1 to n do
        if f[k] and (d[k]<=d[min]) then min:=k;//查找最近的点
      f[min]:=false;//标记为已访问
      ans:=ans+d[min];//最小生成树值的更新
      for k:=1 to n do
        if f[k] and (a[min,k]<d[k]) then
          d[k]:=a[min,k];//更新到其他节点的距离
    end;
  writeln(ans);
end.
```