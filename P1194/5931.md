我们来分析一下这个问题QAQ 这题的出题人十分良心，两个物体可以捆绑在一起销售（hua_ji），而且~~价格~~边权还相同，我们就可以

1、把捆绑优惠的两个物体间连一条边。

2、我看到连边头一个想到的居然是最短路（果然还是我太菜了）。事实上，每个物体都要买，只要使这个图联通就行了。良心的出题人告诉我们边权相同，也就是无相图，然后用最小生成树，我们再瞎~~搞搞~~改改就可以AC了，想一想都很激动。

十分类似于[【模板】最小生成树](https://www.luogu.org/problemnew/show/P3366)

啊啊，最小生成树有两种做法，我用的是克鲁斯卡尔，个人认为比较好理解。

克鲁斯卡尔的主要思路：借助并查集，每次选取边权最小的，看看边连接的两点是否在一个并查集（也就是两个点是否已经可以联通）。不在，联通，假如一个并查集，在，就不管这条边了。


------------

哦，还有一点，这题与模板的差别是：首先是读入需要改一下，并且要加上第一点的权值。为什么要加呢，因为你买第一个东西是要钱的

~~代码自己手写吧~~良心点，加个代码吧

```
Type 
	hua_ji=record
		x,y,z:longint;
	end;

Var
	f:array[1..250000]of hua_ji; 
	a,b,i,j,top,x,ans,q:longint; //这里要多加个top，统计边数，其实可以用公式计算，但是我认为累加会更好理解
	father:array[1..500]of longint; 
	
Procedure qsort(l,r:longint);  //快排，关键字为边权
var
	i,j,e:longint;
	t:hua_ji;
begin
	i:=l;
	j:=r;
	e:=f[(l+r) div 2].z;
	repeat
		while f[i].z<e do inc(i);
		while f[j].z>e do dec(j);
		if i<=j then
			begin
				t:=f[i];
				f[i]:=f[j];
				f[j]:=t;
				inc(i);
				dec(j);
			end;
	until i>j;
	if i<r then qsort(i,r);
	if l<j then qsort(l,j);
end;
	
Function searchfather(k:longint):longint;
begin
	if k<>father[k] then searchfather:=searchfather(father[k])
		else searchfather:=k;
	father[k]:=searchfather;
end;
	
Begin
	readln(a,b);
	top:=0;
	for i:=1 to b do
		begin
			for j:=1 to b do
				begin
					read(x);
					inc(top); //累加，到目前为止共有top条边，其实top=(i-1)*b+j
					f[top].x:=i;
					f[top].y:=j;
					if x=0 then f[top].z:=a //不优惠就原价
						else f[top].z:=x;
				end;
			readln;
		end;
	qsort(1,top);
	for i:=1 to b do
		father[i]:=i; //就是一个并查集
	ans:=b;
	q:=a;
	for i:=1 to top do
		begin
			x:=searchfather(father[f[i].x]); //找祖先
			b:=searchfather(father[f[i].y]);
			if x<>b then //如果祖先不同合并
				begin
					father[x]:=b;
					q:=q+f[i].z;
					dec(ans); //ans记录的是当前边数
				end;
			if ans=1 then break; 这里是一个优化，一共要连b-1条边，达到b-1条边就可以退出
		end;
	writeln(q);
End.
```