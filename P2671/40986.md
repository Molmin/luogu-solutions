可以理解为n个元素每个元素有三个属性分别是数值Number、颜色和编号，对于任意两个满足2个条件的元素进行相应的统计，第1个条件其实等价于这两个元素的编号都是奇数或都是偶数，第2个条件是相同颜色值。

观察数据范围我们可以发现前4组数据的n只有3000，O(N^2)的算法就能拿分，直接穷举任意两个元素，这个代码非常简短的：


```cpp
var  //预期得 40分的程序
        n,m,i,j:longint;
        a,b:array[0..100000] of qword;
        ans:qword;
begin
    assign(input,'sum.in');reset(input);
    assign(output,'sum.out');rewrite(output);
    readln(n,m);
    for i:=1 to n do read(a[i]);readln;
    for i:=1 to n do read(b[i]);readln;
    for i:=1 to n do
        for j:=i+1 to n do
                if not odd(i+j) and (b[i]=b[j]) then begin
                    ans:=(ans+(a[i]+a[j])*(i+j)) mod 10007;
                end;
    writeln(ans);
    close(input);close(output);
end.
```
如何避免超时呢？
介绍两种满分的方法

【第一种方法】从给出公式入手：（假设编号为x1，x2，x3，x4，x5的元素彼此间都符合题目要求的2个条件，其实就是它们的奇偶性相同、颜色相同）

```cpp
(x1+x2)∗(???????1+??????x2)
(x1+x3)∗(???????1+??????x3)
(x1+x4)∗(???????1+??????x4)
(x1+x5)∗(???????1+??????x5)
(x2+x3)∗(???????2+??????x3)
(x2+x4)*(nu?????2+??????x4)
(x2+x5)*(nu?????2+??????x5)
(x3+x4)*(nu?????3+??????x4)
(x3+x5)*(nu?????3+??????x5)
(x4+x5)*(nu?????4+??????x5)
```
（1）把它们展开来就能发现xi\*???????i的数量都就是5-1=4个，如果这样的元素有N个，则有N-1个xi\*???????i
（2）然后是

x1(??????x2+??????x3+ ??????x4 +??????x5)+ x2(??????x3+??????x4+ ??????x5)+

x3 (??????x4+??????x5)+ x4 \*??????x5

（3）类似的

???????1(x2+x3+x4+x5)+ ???????2(x3+x4+x5)+ ???????3(x4+x5)+ ???????4\*x5

（2）和（3）我们可以归为一类（考虑有n个符合条件的元素）

（2）的推广式是Xi(??????xi+1+……+ ??????xn）

（3）的推广式是??????xi(Xi+1  +……+ Xn)

有了这个公式的建立，针对N个彼此颜色相同奇偶性相同的元素，可以用O(n)的时间求得题目要求的值：

首先O(n)的遍历i

然后是O(1)的求取(2)(3)的值：sumid[i]表示编号的总和，sumni表示number的总和，havid[i]表示1到i的编号总和，havni[i]表示1到i的number总和，于是(2)(3)式子中的括号部分可以用sumid-havid[i]、sumni-havni[i]来表示。

那么如何挑选出这些元素，换句话说就是如何归类出颜色相同奇偶性相同的元素，我们可以用双关键字排序（颜色主关键字，奇偶性次关键字），当然还有其他方式用O(nlogn)、O(n)的排序都可以，二维数组统计颜色、奇偶性都可以，这里我采用了快速排序。最终程序的时间复杂度就是O(NlogN)

```cpp
const cs=10007;//预期满分程序
type arr=record
    x,y,id:int64;
    z:boolean;
end;
var
    a:array[0..100001] of arr;
    sumid,sumni,sum,ans,tot:int64;
    tmpc,i,count,n,m:longint;
    havid,havni:int64;
    tmpodd:boolean;
procedure deal;
var j:longint;
begin
    tot:=0;havid:=0;havni:=0;//sumid，sumni表示编号、数字总和，havid,havni表示编号、数字前缀和
       for j:=count downto 1 do begin
                inc(havid,a[i-j].id);inc(havni,a[i-j].x);
                tot:=(tot+a[i-j].x*(sumid-havid))mod cs;//总和-前缀和就能得到我们想要的后缀和，题解中提取的公式运用
                tot:=(tot+a[i-j].id*(sumni-havni)) mod cs;
    end;
    ans:=ans+tot;
    ans:=((sum mod cs)*(count-1)+ans) mod cs;
end;
procedure qsort(h,t:longint);
var x,i,j:longint;
    tmp:arr;
begin
    x:=random(t-h+1)+h;
    tmp:=a[h];a[h]:=a[x];a[x]:=tmp;
    i:=h;j:=t;
    tmp:=a[i];
    while i<j do begin
        while (i<j) and ( (a[j].y>tmp.y)  or (a[j].y=tmp.y) and (a[j].z>tmp.z)) do dec(j);
        if i<j then begin
            a[i]:=a[j];
            inc(i);
        end;
        while (i<j) and ((a[i].y<tmp.y) or (a[i].y=tmp.y) and (a[i].z<tmp.z)) do inc(i);
        if i<j then begin
            a[j]:=a[i];
            dec(j);
        end;
    end;
    a[i]:=tmp;
    if h<i-1 then qsort(h,i-1);
    if i+1<t then qsort(i+1,t);
end;
begin
    assign(input,'sum.in');reset(input);
    assign(output,'sum.out');rewrite(output);
    readln(n,m);
    randomize;
    for i:=1 to n do read(a[i].x);readln;//表示number
    for i:=1 to n do read(a[i].y);readln;//表示颜色
    for i:=1 to n do begin
        a[i].id:=i;
        a[i].z:=odd(i);//奇偶
    end;
    qsort(1,n);//颜色主关键字，奇偶次关键字排序
    i:=1;
    while i<=n+1 do begin
            if (tmpc<>a[i].y) or (tmpodd<>a[i].z) then begin    //tmpc表示上一段颜色值，tmpodd表示上一段奇偶值
            if count>1 then deal;
            sumid:=a[i].id;sumni:=a[i].x;sum:=sumid*sumni;
            tmpc:=a[i].y;
            tmpodd:=a[i].z;
            count:=1;
        end
        else begin
            sumid:=(sumid+a[i].id);
            sumni:=(sumni+a[i].x);
            sum:=(sum+(a[i].id*a[i].x));
            inc(count);
        end;
        inc(i);
    end;
    writeln(ans);
  close(input);close(output);
end.
```
【第二种方法】
其实题目算式还有一种更彻底的转化：

```cpp
(X1+X2)∗(???????1+??????x2)
(X1+X3)∗(???????1+??????X3)
(X1+X4)∗(???????1+??????X4)
(X1+X5)∗(???????1+??????X5)
```
……
```cpp
(X1+Xn)∗(???????1+??????Xn)
(X2+X3)∗(???????2+??????X3)
(X2+X4)*(nu?????2+??????X4)
(X2+X5)*(nu?????2+??????X5)
```
……
(X2+Xn)\*(nu?????2+??????Xn)

(Xi+Xi+1)\*(nu?????i+??????Xi+1)

……
(Xi+Xn)\*(nu?????i+??????Xn)

……
(Xn-1+Xn)\*(nu????Xn-1+??????Xn)

为了方便描述以上式子统一表示成(A+B)\*(C+D)

所有的A\*C和B\*D最终形成①式(n-1)( X1 ???????1+ …Xi nu?????i +Xn ??????Xn)

所有的B\*C和A\*D最终形成

X1 (??????x2+……??????Xn)

X2(??????x1+ ??????x3……??????Xn)

……
Xi(??????x1+……??????xi-1+ ??????xi+1……??????Xn)

……
Xn(??????x1+……??????Xn-1)

通项式为Xi(number总和- ??????xi)=②式Xi\*number总和-Xi\* ??????xi

①+②式即为xi\*number总和-（N-2）\*(xi\* ??????xi)

n项求和后就是(X总和\*number总和)-(N-2)\*(x1\* ??????x1+……+ xn\* ??????n)

程序如下：

const cs=10007;//这里使用一个二维数组统计同种颜色同奇偶性的个数，以及相应和

```cpp
var
    n,m,i,j,colour:longint;
    sumid,sumn,sum,count:array[0..100001,0..1] of qword;
    ans:qword;
    number:array[0..100001] of longint;
begin
    assign(input,'sum.in');reset(input);
    assign(output,'sum.out');rewrite(output);
    readln(n,m);
    for i:=1 to n do read(number[i]);readln;
    for i:=1 to n do begin
        read(colour);
        sumid[colour,i mod 2]:=(sumid[colour,i mod 2]+i) mod cs;
        inc(count[colour,i mod 2]);
        sumn[colour,i mod 2]:=(sumn[colour,i mod 2]+number[i]) mod cs;
        sum[colour,i mod 2]:=(sum[colour,i mod 2]+(number[i] mod cs)*(i mod cs) mod cs) mod cs;
    end;
    readln;
    for i:=1 to m do
        for j:=0 to 1 do
            if count[i,j]>1 then ans:=(ans + sumid[i,j]*sumn[i,j] mod cs+(count[i,j]-2)*sum[i,j] mod cs) mod cs;
    writeln(ans);
    close(input);close(output);
end.
```
第一种方法对公式转化的要求比较低，代码稍长，使用排序统计划分出同种颜色同奇偶性，时间复杂度就是排序的时间复杂度O(NlogN)；
第二种方法对公式转化很彻底，代码很短，使用二维数组分出同种颜色同奇偶性,时间复杂度是O(N)；

这两种方法基本涵盖了解决此题的各种方法。
