> ## 一，题意

这题题面很长，我打比赛的时候误解了题意，然后听取WA声一片，所以先把题意贴在这里：

> 一家餐厅最开始有$\text{n}$个菜，第$\text{i}(1 \leq \text{i} \leq \text{n})$个菜的数量为$a_i$，成本为$c_i$。

> 然后会来$\text{m}$个顾客，第$\text{j}(1\leq \text{j} \leq \text{m})$个顾客需要$d_j$个编号为$t_j$的菜（菜是一次一次要的，也就是说他会点菜$d_j$次）。如果某次要菜时，这种菜没有了，那么顾客会点一份当前有的**最便宜的菜**（在有多种最便宜的菜时，**选编号最小的**）。如果什么菜都没有了，那么顾客会愤怒的离开，而且不会付钱（以前点的菜他照样会吃掉，但也不付钱了）。每个顾客的总成本就等于他点的所有菜的总成本，而他不付钱，也就意味着成本为0。

> 告诉你上文中提到的所有数据，请你算出每个顾客的总成本。

（禁言了提交不了翻译，如果管理员觉得可以的话，帮忙把翻译整到题面里去吧QAQ 源码在[这里](https://www.luogu.org/paste/57fqcnci)）

------------------

> ## 二，思路

就是模拟啊。。。但注意数据比较大，纯粹的暴力是不行的，可以用队列优化，具体看代码里的注释。

---------------

> ## 三，代码&具体讲解（P党福利）

```
var
	n,m,i:longint;
	sum,ans,x,y,t:int64;
	a,b,c:array[0..100010] of int64;
	f:array[0..100010] of boolean;
procedure qsort(l,r:longint); //快排
var
	i,j,t,mid:longint;
begin
	i:=l; j:=r; mid:=c[b[(l+r) div 2]];
	repeat
		while c[b[i]]<mid do inc(i); //关键字为价格
		while c[b[j]]>mid do dec(j); //同上
		if i<=j then
		begin
			t:=b[i]; b[i]:=b[j]; b[j]:=t;
			inc(i); dec(j);
		end;
	until i>j;
	if l<j then qsort(l,j);
	if i<r then qsort(i,r);
end;
begin
	readln(n,m);
	sum:=0;
	for i:=1 to n do
	begin 
		read(a[i]); //数量
		sum:=sum+a[i]; //还剩多少菜
	end;
	for i:=1 to n do read(c[i]); //成本
	for i:=1 to n do
	begin
		b[i]:=i; 
		f[i]:=true; //表示这种菜还能供应
	end;
	qsort(1,n); //按价格升序排列的菜单
	t:=1; //b[t]表示当前最便宜的菜的编号，显然t是从1开始的
	for i:=1 to m do
	begin
		read(x,y);
		if y>sum then //剩的菜不够吃了
		begin
			sum:=0; //因为顾客把剩下的菜吃光了=_=
			writeln(0); //还没付钱>_<
			continue;
		end;
		sum:=sum-y; //不管咋样，肯定要吃掉这么多了
		if y<=a[x] then //这种菜够吃
		begin
			writeln(c[x]*y); //输出总成本
			a[x]:=a[x]-y; //吃掉了y个
			if a[x]=0 then f[x]:=false; //这种菜已经吃光了
			continue;
		end;
		y:=y-a[x]; //先把这种菜吃完
		f[x]:=false; //吃光啦
		ans:=c[x]*a[x]; //这种菜的成本
		a[x]:=0; //吃光啦
		while y>a[b[t]] do //这种菜不够吃
		begin
			if f[b[t]]=false then //已经吃光了
			begin
				inc(t); //继续看下一个
				continue;
			end;
			y:=y-a[b[t]]; //吃掉了这么多
			f[b[t]]:=false; //吃光了
			ans:=ans+c[b[t]]*a[b[t]]; //加上成本
			a[b[t]]:=0; //吃光了
			inc(t); //继续看下一个
		end;
		ans:=ans+y*c[b[t]]; //最后一种菜的成本
		a[b[t]]:=a[b[t]]-y; //吃掉了这么多
		if a[b[t]]=0 then f[b[t]]:=false; //判断有没有吃完
		writeln(ans); //输出总成本
	end;
end.
```

------------

> ## 4.后记

感觉思维难度极低，但是写起来比较繁琐，重点是要有耐心，仔细分析，就能愉快地AC本题QAQ

如果有什么地方写错了，请私信告诉我，因为我不怎么看评论qwq