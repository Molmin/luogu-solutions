pascal的题解好少（事实上这题比较多）。

事实上，楼上各位的递推公式可以简化一下，变得更简单。

原来是f[n]=f[n-1]+f[n-2]+2g[n-2]和g[n]=g[n-1]+f[n-1]。

考虑f[n-2]=f[n-1]-f[n-3]-2g[n-3]，g[n-2]-g[n-3]=f[n-3]。

可得f[n]=2f[n-1]-f[n-3]+2(g[n-2]-g[n-3])=2f[n-1]+f[n-3]。

于是就很简单了，最近我正在学矩阵，就发一个矩阵的代码吧：

```pascal
const mo=10000;
type arr1=array[0..2] of int64;
     arr2=array[0..2,0..2] of int64;
var n,t:int64;
    f:array[0..2] of int64=(1,1,2);
    a:array[0..2,0..2] of int64=((0,0,1),(1,0,0),(0,1,2)); //构造矩阵
//矩阵乘法
procedure mul(var f:arr1;var a:array of arr1);
var i,j:longint;
    c:array[0..2] of int64;
begin
  fillchar(c,sizeof(c),0);
  for i:=0 to 2 do
    for j:=0 to 2 do
      c[i]:=(c[i]+f[j]*a[j,i]) mod mo;
  f:=c;
end;
procedure mulself(var a:arr2);
var i,j,k:longint;
    c:array[0..2,0..2] of int64;
begin
  fillchar(c,sizeof(c),0);
  for i:=0 to 2 do
    for j:=0 to 2 do
      for k:=0 to 2 do
        c[i,j]:=(c[i,j]+a[i,k]*a[k,j]) mod mo;
  a:=c;
end;
begin
  readln(n);
  while n>0 do
  begin
    if (n and 1)=1 then mul(f,a); //相当于n mod 2=1
    mulself(a);
    n:=n shr 1;
  end;
  writeln(f[0]);
end.
```