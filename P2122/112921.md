### 背景

纪念NOIP复活所作

### 分析

首先，显然，本题一看就知道要用**线段树**去做。

对于第一个操作，区间修改没问题。 

而第二个操作：求平均数，就是区间求和/区间长度。

第三个操作是重点。方差，凡学过初二下的数学统计的同学都知道，即：方差
**b=((a[i]-c)²+(a[i+1]-c)²+(a[i+2]-c)²+…+(a[j]-c)²)/(j-i+1)（c为a[i]到a[j]的平均数）**

看上去它不能被拆分进而通过区间查询解决（因为c），但我们可以用完全平方式将它变形，如下：(n=j-i+1)

    b=(n*c²+(a[i]²+a[i+1]²+a[i+2]²+…+a[j]²)-2c(a[i]+a[i+1]+a[i+2]+…a[j]))/n
     =((a[i]+a[i+1]+a[i+2]+…a[j])²/n+(a[i]²+a[i+1]²+a[i+2]²+…+a[j]²)-2(a[i]+a[i+1]+a[i+2]+…a[j])²/n)/n
     =((a[i]²+a[i+1]²+a[i+2]²+…+a[j]²)-(a[i]+a[i+1]+a[i+2]+…a[j])²/n)/n
     =((a[i]²+a[i+1]²+a[i+2]²+…+a[j]²)*n-(a[i]+a[i+1]+a[i+2]+…a[j])²)/n²
其中，(a[i]+a[i+1]+a[i+2]+…a[j])²可以由区间求和再平方得到，a[i]²+a[i+1]²+a[i+2]²+…+a[j]²可以再建一棵线段树。

但对它区间修改(每个数+z）又是一个问题，再算一波：(n=j-i+1)

    x=a[i]²+a[i+1]²+a[i+2]²+…+a[j]²;
    y=(a[i]+z)²+(a[i+1]+z)²+(a[i+2]+z)²+…+(a[j]+z)²
     =x+n*z²+2*z*(a[i]+a[i+1]+a[i+2]+…+a[j])
于是，又化为了第一棵线段树的问题。成功！

既约分数~~听上去好怪~~就用一下gcd。

最后，千万记得要用****int64****。

### 代码

附上代码：（超长qwq）
```pascal
var a,lazy,a1:array[0..400005] of int64;
  b:array[0..100005] of int64;
  n,m,i,j,c,x,y,z,t:longint;
  p,q,r:int64;
function gcd(p,q:int64):int64;
begin
  repeat
    gcd:=p mod q;
    p:=q;
    q:=gcd;
  until q=0;
  gcd:=p;
end;
procedure pushdown(l,r,mid,k:longint);
begin
  a1[k*2]:=a1[k*2]+(mid-l+1)*lazy[k]*lazy[k]+2*lazy[k]*a[k*2];
  a1[k*2+1]:=a1[k*2+1]+(r-mid)*lazy[k]*lazy[k]+2*lazy[k]*a[k*2+1];
  a[k*2]:=a[k*2]+(mid-l+1)*lazy[k];
  a[k*2+1]:=a[k*2+1]+(r-mid)*lazy[k];
  inc(lazy[k*2],lazy[k]);inc(lazy[k*2+1],lazy[k]);lazy[k]:=0;
end;
procedure build(l,r,k:longint);
var mid:longint;
begin
  if l=r then begin a[k]:=b[l];a1[k]:=b[l]*b[l];exit;end;
  mid:=(l+r) div 2;
  build(l,mid,k*2);
  build(mid+1,r,k*2+1);
  a[k]:=a[k*2]+a[k*2+1];
  a1[k]:=a1[k*2]+a1[k*2+1];
end;
function find(l,r,k:longint):int64;
var mid:longint;
  s1,s2:int64;
begin
  if(l>=x)and(r<=y)then exit(a[k]);
  mid:=(l+r) div 2;
  pushdown(l,r,mid,k);
  if x<=mid then s1:=find(l,mid,k*2) else s1:=0;
  if y>mid then s2:=find(mid+1,r,k*2+1) else s2:=0;
  find:=s1+s2;
end;
function find1(l,r,k:longint):int64;
var mid:longint;
  s1,s2:int64;
begin
  if(l>=x)and(r<=y)then exit(a1[k]);
  mid:=(l+r) div 2;
  pushdown(l,r,mid,k);
  if x<=mid then s1:=find1(l,mid,k*2) else s1:=0;
  if y>mid then s2:=find1(mid+1,r,k*2+1) else s2:=0;
  find1:=s1+s2;
end;
procedure update(l,r,k,t:longint);
var mid:longint;
begin
  if(l>=x)and(r<=y)then
    begin
      a1[k]:=a1[k]+(r-l+1)*t*t+2*t*a[k];
      a[k]:=a[k]+t*(r-l+1);
      lazy[k]:=t+lazy[k];
      exit;
    end;
  mid:=(l+r) div 2;
  pushdown(l,r,mid,k);
  if y<=mid then update(l,mid,k*2,t)else
  if x>mid then update(mid+1,r,k*2+1,t)else
    begin
      update(mid+1,r,k*2+1,t);update(l,mid,k*2,t);
    end;
  a[k]:=a[k*2]+a[k*2+1];
  a1[k]:=a1[k*2]+a1[k*2+1];
end;
begin
  readln(n,m);
  for i:= 1 to n do read(b[i]);
  build(1,n,1);
  for i:= 1 to m do
    begin
      read(Z);
      case z of
        1:begin
            read(x,y,t);
            update(1,n,1,t);
          end;
        2:begin
            read(x,y);
            p:=find(1,n,1);
            q:=y-x+1;
            r:=gcd(p,q);
            p:=p div r;
            q:=q div r;
            writeln(p,'/',q);
          end;
        3:begin
            read(x,y);
            p:=find(1,n,1);
            p:=(y-x+1)*find1(1,n,1)-p*p;
            q:=(y-x+1)*(y-x+1);
            r:=gcd(p,q);
            p:=p div r;
            q:=q div r;
            writeln(p,'/',q);
          end;
      end;
    end;
  close(input);close(output);
end.
```

本蒟蒻Markdown与LaTeX不太会用请谅解。