#### P党的福利
看到题目

## 我们的第一感觉是暴力

所以身为蒟蒻的我打了个DFS

思路很好想，这里就不赘述了

代码如下（特判就不写了）：

```pascal
var n,i,j,l,r,x:longint;
    s:int64;
function max(x,y:longint):longint;
begin
  if x>y then
   exit(x);
  exit(y);
end;
function min(x,y:longint):longint;
begin
  if x<y then
   exit(x);
  exit(y);
end;
procedure ss(x,k:int64);
var i:longint;
begin
  if k=n then
   begin
    inc(s);
    exit;
   end;
  for i:=max(0,x mod 10-2) to min(9,x mod 10+2) do
   begin
    ss(x*10+i,k+1);
   end;
end;
begin
  read(n);
  s:=0;
  for i:=1 to 9 do
   ss(i,1);
  writeln(s);
end.
```
计算时间复杂度

太大了拜拜

实测过n=11左右（电脑跑的比较快）
## 接下来我们可以顺利想出DP
方程是f[i][j]={f[i-1][k]|k=max(0,j-2)~min(9,j+2)}
### 注意：我们计算答案的时候统计1~9的f[]值
正确性证明：

因为我们知道每个sam数的相邻两位的数字之差不超过 2

又因为我们前面求出的也都是sam数

所以可以直接统计下一位合法的sam数

由于i这维可以滚掉，所以我们数组只要开0~9就可以了

### 注意：更新i时要清空数组
代码如下（特判也不写了）：
```pascal
const mo=1000000007;
var n,i,j,l,r,x:longint;
    s:int64;
    f,ff:array[0..9] of int64;
function max(x,y:longint):longint;
begin
  if x>y then
   exit(x);
  exit(y);
end;
function min(x,y:longint):longint;
begin
  if x<y then
   exit(x);
  exit(y);
end;
begin
  read(n);
  for i:=0 to 9 do
   f[i]:=1;
  for i:=2 to n do
   begin
    fillchar(ff,sizeof(ff),0);
    for j:=0 to 9 do
     begin
      l:=max(0,j-2);r:=min(j+2,9);
      for x:=l to r do
       ff[j]:=(ff[j]+f[x]) mod mo;
     end;
    f:=ff;
   end;
  s:=0;
  for i:=1 to 9 do
   s:=(s+f[i]) mod mo;
  writeln(s);
end.
```
## 接下来我们要步入正解了
由于这题数据范围为10^18

O(n)的DP也过不了

只能过30左右
### 所以我们要使用矩阵优化
矩阵快速幂不会的同学先看下[模板](https://www.luogu.org/problemnew/show/P3390)

我们可以知道初始矩阵长这个样子（其实就是转移方程里的i，j啦）（i是竖的，j是横的）
![](https://cdn.luogu.com.cn/upload/pic/27508.png)
接下来我们需要使用**矩阵快速幂来算出这个矩阵的k次方**

然后问题就解决啦

我们只要用算出的这个矩阵再乘这个小矩阵

![](https://cdn.luogu.com.cn/upload/pic/27511.png)

然后累计答案就好了（其实不用也可以，因为这个乘了之后相当于把整个矩阵的值全加起来，但是大多时候这个小矩阵不是全1，所以还是用比较好）
### 注意：别忘了我们计算答案的时候统计1~9的f[]值
最后的代码如下（特判加上）：
```pascal
type arr=array[0..101,0..101] of int64;
const mo=1000000007;
var n,i,j:longint;
    k,s:int64;
    x,a,ans:arr;
function cheng(a,b:arr):arr;
var i,j,k:longint;
    c:arr;
begin
  fillchar(c,sizeof(c),0);
  for i:=0 to 9 do
   for j:=0 to 9 do
    for k:=0 to 9 do
     c[i,j]:=(c[i,j]+a[i,k]*b[k,j]) mod mo;
  exit(c);
end;
begin
  read(k);
  if k=1 then
   begin
    writeln(10);
    halt;
   end;
  for i:=0 to 9 do
   for j:=0 to 9 do
    if abs(i-j)<=2 then
     a[i,j]:=1
    else
     a[i,j]:=0;
  for i:=0 to 9 do
   ans[i,i]:=1;
  dec(k);
  while k>0 do
   begin
    if k mod 2=1 then
     ans:=cheng(ans,a);
    a:=cheng(a,a);
    k:=k div 2;
   end;
  for i:=0 to 9 do
   x[i,0]:=1;
  x:=cheng(ans,x);
  for i:=1 to 9 do
   s:=(s+x[i,0]) mod mo;
  writeln(s);
end.
```
PS:代码巨丑，大佬见谅。