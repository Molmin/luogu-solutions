pascal里貌似有个叫数组模拟链表的东西~~~

只要定义两个数组：l[i]表示i同学左边的人，r[i]表示i同学右边的同学

每次更新分为两种可能：
- 把i插到k左边：那么i的右边变成k，i的左边变成l[k]，l[k]的右边变成i，k的左边变成i
- 把i插到k右边：那么i的左边变成k，i的右边变成r[k]，r[k]的左边变成i，k的右边变成i

如何删除？
若要删掉x，只要：把l[x]的右边变成r[x]，r[x]的左边变成l[x]即可。
但若x已经被删掉了，就不能再删了，否则会影响答案，所以用一个flag记录是否被删

如何输出？
显然，整个输出序列中只有最左边那一个数（即最先输出的）的l数组为0，那么一层循环找一下就好了。输出后把当前这个数变成右边那个数就好了

Code：
```
//该解释的上面已经解释好了，Code也很好理解的
var
    l,r:array[0..1000000] of longint;
    flag:array[0..1000000] of boolean;
    n,k,p,i,x,m:longint;

begin
    readln(n);
    for i := 2 to n do
    begin
        readln(k,p);
        if p = 0 then
        begin
            r[i] := k;
            l[i] := l[k];
            r[l[k]] := i;
            l[k] := i;
        end else
        begin
            l[i] := k;
            r[i] := r[k];
            l[r[k]] := i;
            r[k] := i;
        end;
    end;
    readln(m);
    for i := 1 to m do
    begin
        readln(x);
        if flag[x] then continue;
        flag[x] := true;
        l[r[x]] := l[x];
        r[l[x]] := r[x];
    end;
    for i := 1 to n do if (l[i] = 0) and not flag[i] then break;
    while i <> 0 do
    begin
        write(i,' ');
        i := r[i];
    end;
end.
```