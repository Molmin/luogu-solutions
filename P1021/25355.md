**P党的福利**


###看懂了楼下的题解，用Pascal语言实现。


解决本题所用的算法是dfs+dp


dfs：

搜索数的种数，每次取数取从上次取的数+1到之前取的数能拼到的最大+1（之前取的数能拼到的最大值需用dp解决）


dp：

状态：f[i]为拼i所需的最少数的个数

阶段：1 to 之前取的数的个数

状态转移方程：f[i]:=min(f[i],f[j-a[i]]+1)

边界：f[0]:=0;


AC代码：

```cpp
uses math;
var
  a,b:array[0..16]of longint;
  f:array[0..2006]of longint;
  n,m,i,j,maxn:longint;
function dp(k,sum:longint):longint;
var
  i,j:longint;
  begin
    for i:=0 to 2006 do f[i]:=66666666; //初始化，因为是求min，所以要设大点
    f[0]:=0;//边界
    for i:=1 to k do 
      for j:=a[i] to n*sum do 
        f[j]:=min(f[j],f[j-a[i]]+1);//dp
    for i:=1 to n*sum do 
      if f[i]>n then //最多贴n张，如果f[i]>n说明用n张取不到i，能取到的最大值则为i-1
        exit(i-1);
    exit(n*sum);//如果都能取到，则返回能拼到的最大值
  end;
procedure try(k,h,t,sum:longint);
var
  i,s:longint;
  begin
    if k>m then 
      begin
        if t>maxn then begin maxn:=t;b:=a; end;
        exit;
      end;//如果取的数已经>m，那么把当前能拼的数最大和之前的最大值作比较
    for i:=h+1 to t+1 do //取从上次取的数+1到之前取的数能拼到的最大+1
      begin
        a[k]:=i;
        s:=dp(k,sum+i);//用dp计算之前取的数能拼到的最大
        try(k+1,i,s,sum+i);//搜索
      end;
  end;
begin
  readln(n,m);
  try(1,0,0,0);
  for i:=1 to m do write(b[i],' ');
  writeln;
  write('MAX=',maxn);
end.
```