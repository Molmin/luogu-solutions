既然没有题解，那我就来发一波吧。

其实这道题的思路是比较明显的，跑一波最短路就好了。。。

具体怎么做呢？

考虑一个方格，设左上角为（i,j）,则右上角为（i,j+1）,左下角为（i+1,j）,右下角为（i+1,j+1）。

①该方格为'\'：

可以这样看，从（i,j）到（i+1,j+1）不需要花费，建一条权值为0的边，从（i,j+1）到（i+1,j）需要花费1，建一条权值为1的边。

②该方格为'/'：

同理，从（i,j）到（i+1,j+1）需要花费1，建一条权值为1的边，从（i,j+1）到（i+1,j）不需要花费，建一条权值为0的边。

然后把坐标转换为一个数（瞎搞就行）。

完事啦。。。

时限很小，我用了线段树优化dijstra。

Pascal Code：

```cpp
var
  d,head,next,l1,l2,tree,id,wz:array[0..1000005]of longint;
  h,l,i,j,n,ll,w,v,u,oo,x,y,num:longint;
  ch:char;
function wzl(x,y:longint):longint;
begin
  exit((x-1)*(l+1)+y);
end;
procedure add(x,y,z:longint);//链式前向星
begin
  inc(num);
  l1[num]:=y;
  l2[num]:=z;
  next[num]:=head[x];
  head[x]:=num;
end;
procedure build(p,l,r:longint);//建树
var
  mid:longint;
begin
  if l=r then
  begin
    tree[p]:=d[l];
    id[p]:=l;//记录id
    wz[l]:=p;
    exit;
  end;
  mid:=(l+r) div 2;
  build(p+p,l,mid);
  build(p+p+1,mid+1,r);
  if tree[p+p]<=tree[p+p+1] then
  begin
    tree[p]:=tree[p+p];
    id[p]:=id[p+p];
  end else
  begin
    tree[p]:=tree[p+p+1];
    id[p]:=id[p+p+1];
  end;
end;
procedure change(p:longint);
begin
  if p=0 then
    exit;
  if tree[p+p]<=tree[p+p+1] then
  begin
    tree[p]:=tree[p+p];
    id[p]:=id[p+p];
  end else
  begin
    tree[p]:=tree[p+p+1];
    id[p]:=id[p+p+1];
  end;
  change(p div 2);
end;
begin
  readln(h,l);
  for i:=1 to h do
  begin
    for j:=1 to l do
    begin
      read(ch);
      case ch of//建边
        '\':begin
              x:=wzl(i,j);
              y:=wzl(i+1,j+1);
              add(x,y,0);
              add(y,x,0);
              x:=wzl(i+1,j);//wzl，坐标转换为一个数
              y:=wzl(i,j+1);
              add(x,y,1);
              add(y,x,1);
            end;
        '/':begin
              x:=wzl(i,j);
              y:=wzl(i+1,j+1);
              add(x,y,1);
              add(y,x,1);
              x:=wzl(i+1,j);
              y:=wzl(i,j+1);
              add(x,y,0);
              add(y,x,0);
            end;
      end;
    end;
    readln;
  end;
  n:=wzl(h+1,l+1);//计算终点，起点显然是1
  oo:=100000005;
  for i:=1 to n do//开始dijstra
    d[i]:=oo;
  d[1]:=0;
  build(1,1,n);
  ll:=head[1];
  while ll<>0 do//初始化，其实可以不用
  begin
    w:=l1[ll];
    v:=l2[ll];
    d[w]:=v;
    tree[wz[w]]:=v;
    change(wz[w] div 2);
    ll:=next[ll];
  end;
  for i:=2 to n do//改成while较方便
  begin
    if tree[1]=oo then//特判，防卡常
      break;
    u:=id[1];
    tree[wz[u]]:=oo;
    change(wz[u] div 2);
    ll:=head[u];
    while ll<>0 do
    begin
      w:=l1[ll];
      v:=l2[ll];
      if d[w]>d[u]+v then
      begin
        d[w]:=d[u]+v;
        tree[wz[w]]:=d[w];
        change(wz[w] div 2);
      end;
      ll:=next[ll];
    end;
  end;
  if d[n]>=oo then//输出
    writeln('NO SOLUTION') else
    writeln(d[n]);
end.
```