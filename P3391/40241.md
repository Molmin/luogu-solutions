这道题连Pascal题解一篇都没有……

所以让我来发一篇吧。/滑稽

由于我太弱了，只能写正解splay。。。

具体的原理，百度一下，那些C++大佬已经讲的很清楚了。

这里奉上调了好几天的代码：

```pas
//Splay Tree
var
  a,s,l,r,f:array[0..100002]of longint;//a是原值，s是子树的节点数，l和r表示左儿子和右儿子，f表示父亲
  o:array[0..100002]of boolean;//标记
  n,m,i,x,y,root:longint;
procedure pushup(x:longint);
begin
  s[x]:=s[l[x]]+s[r[x]]+1;
end;
procedure pushdown(x:longint); //标记下传
var
  t:longint;
begin
  if o[x] then
  begin
    o[l[x]]:=not o[l[x]];
    o[r[x]]:=not o[r[x]];
    t:=l[x];
    l[x]:=r[x];
    r[x]:=t;
    o[x]:=false;
  end;
end;
procedure rotate(x:longint);   //旋转
var
  y,z:longint;
begin
  y:=f[x];
  z:=f[y];
  if l[z]=y then
    l[z]:=x
  else
    r[z]:=x;
  f[x]:=z;
  if l[y]=x then
  begin
    l[y]:=r[x];
    f[r[x]]:=y;
    r[x]:=y;
  end
  else
  begin
    r[y]:=l[x];
    f[l[x]]:=y;
    l[x]:=y;
  end;
  f[y]:=x;
  pushup(x);
  pushup(y);
end;
procedure splay(x,t:longint);  //伸展
begin
  while f[x]<>t do
  begin
    if f[f[x]]<>t then
      if (l[f[x]]=x) xor (l[f[f[x]]]=f[x]) then
        rotate(x)
      else
        rotate(f[x]);
    rotate(x);
  end;
  if t=0 then
    root:=x;
end;
procedure insert(x,k,n:longint);//插入节点，n表示第几个节点
var
  t:longint;
begin
  t:=0;
  while x>0 do
  begin
    t:=x;
    if k<a[x] then
      x:=l[x]
    else
      x:=r[x];
  end;
  if t>0 then
    if k<a[t] then
      l[t]:=k
    else
      r[t]:=k;
  a[n]:=k;
  l[n]:=0;
  r[n]:=0;
  s[n]:=1;
  f[n]:=t;
  o[n]:=false;
  splay(n,0);
end;
function kth(k:longint):longint;//查询第k小
var
  x:longint;
begin
  x:=root;
  while true do
  begin
    pushdown(x);
    if s[l[x]]+1=k then
      exit(x);
    if s[l[x]]>=k then
      x:=l[x]
    else
    begin
      dec(k,s[l[x]]+1);
      x:=r[x];
    end;
  end;
end;
procedure reverse(x,y:longint);//翻转
begin
  x:=kth(x);
  y:=kth(y+2);
  splay(x,0);
  splay(y,x);
  o[l[r[x]]]:=not o[l[r[x]]];
end;
begin
  read(n,m);
  root:=0;
  for i:=1 to n+2 do           //build
    insert(root,i,i);
  for i:=1 to m do
  begin
    read(x,y);
    reverse(x,y);
  end;
  for i:=2 to n+1 do
    write(kth(i)-1,' ');
  writeln;
end.
```