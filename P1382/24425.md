这一题还没有pascal的题解，所以补一篇pascal的堆+扫描线题解。~~（因为我不会线段树）~~

```pas
var
   p,lr,h,a,b:array[0..200005]of longint;
   heap,will:array[0..400005]of longint;
   n,x,y,z,i,t,tot,totwill,height,ans:longint;
```

两个大根堆，heap储存当前楼房的高度，will储存将要删除的楼房的高度。push(q)表示将q加入heap，del表示删除heap堆顶。pushwill(q)表示将q加入will，delwill表示删除will堆顶。

p[i]表示第i条边的x坐标

lr[i]表示第i条边是楼房的左侧边还是右侧边，lr[i]=1左侧边,lr[i]=2右侧边。

h[i]表示第i条边的高度。

a[i]表示第i个节点的x坐标,b[i]表示第i个节点的y坐标。

#### 读入
对于每个楼房，要读入左侧边和右侧边。
```pas
  for i:=1 to n do begin
    readln(z,x,y);
    t:=2*i;
    p[t-1]:=x;
    h[t-1]:=z;
    lr[t-1]:=1;
    p[t]:=y;
    h[t]:=z;
    lr[t]:=2;
  end;
```

#### 预处理
将边按x坐标从小到大（x坐标相同时，先右侧边后左侧边）进行排序。
```pas
  procedure qsort(l,r:longint);
  var i,j,mid,t,cen:longint;
  begin
    i:=l;j:=r;
    mid:=p[(i+j) div 2];
    cen:=lr[(i+j) div 2];
    repeat
      while (p[i]<mid) or ((p[i]=mid) and (lr[i]>cen)) do  inc(i);
      while (p[j]>mid) or ((p[j]=mid) and (lr[j]<cen)) do dec(j);
      if i<=j then begin
        t:=p[i];
        p[i]:=p[j];
        p[j]:=t;
        t:=lr[i];
        lr[i]:=lr[j];
        lr[j]:=t;
        t:=h[i];
        h[i]:=h[j];
        h[j]:=t;
        inc(i);
        dec(j);
      end;
    until i>j;
    if l<j then qsort(l,j);
    if i<r then qsort(i,r);
  end;
```

#### 扫描处理

height记录处理前楼房的最高高度。

如果这条边是左侧边，则将其加入heap，如果是右侧边则加入will。

x坐标相同的所有边加入完成后，比较heap[1]和will[1],如果相同，同时删去两个堆顶，并继续判断，直到它们不相同，得到新的heap。

记录答案。
```pas
  while i<=t do begin
    case lr[i] of
    1:begin                                //该x坐标上只有左侧边
        height:=heap[1];                   //height记录处理前的最大楼高
        repeat
          push(h[i]);
          inc(i);
        until (p[i]<>p[i-1]) or (i>t);
        dec(i);
        if heap[1]>height then begin       //新的最大楼高比之前高时，轮廓线才会变化 
          inc(ans);
          a[ans]:=p[i];b[ans]:=height;
          inc(ans);
          a[ans]:=p[i];b[ans]:=heap[1];
        end;
      end;
    2:begin                                //该x坐标上既有左侧边，又有右侧边。
        height:=heap[1];
        repeat                             //将该x坐标所有右侧边加入will
          pushwill(h[i]);
          inc(i);
        until (p[i]<>p[i-1]) or (lr[i]=1) or (i>t);
        if p[i]=p[i-1] then begin
          repeat                           //将该x坐标所有左侧边加入heap
            push(h[i]);
            inc(i);
          until (p[i]<>p[i-1]) or (i>t);
        end;
        dec(i);
        while totwill<>0 do                //将楼房删除，更新heap 
          if will[1]=heap[1] then begin delwill;del; end
            else break;
        if heap[1]<>height then begin      //新的最大楼高和之前不同时，轮廓线才会变化
          inc(ans);
          a[ans]:=p[i];b[ans]:=height;
          inc(ans);
          a[ans]:=p[i];b[ans]:=heap[1];
        end;
      end;
    end;
    inc(i);
  end;
```

#### 输出
先输出节点个数ans，再输出节点坐标。
```pas
  writeln(ans);
  for i:=1 to ans do writeln(a[i],' ',b[i]);
```

#### 完整代码
本人蒟蒻，若代码有错误，请大佬指出。
```pas
program project1;
var
   p,lr,h,heap,will,a,b:array[0..400005]of longint;
   n,x,y,z,i,t,tot,totwill,height,ans:longint;
procedure qsort(l,r:longint);
var i,j,mid,t,cen:longint;
begin
  i:=l;j:=r;
  mid:=p[(i+j) div 2];
  cen:=lr[(i+j) div 2];
  repeat
    while (p[i]<mid) or ((p[i]=mid) and (lr[i]>cen)) do inc(i);
    while (p[j]>mid) or ((p[j]=mid) and (lr[j]<cen)) do dec(j);
    if i<=j then begin
      t:=p[i];
      p[i]:=p[j];
      p[j]:=t;
      t:=lr[i];
      lr[i]:=lr[j];
      lr[j]:=t;
      t:=h[i];
      h[i]:=h[j];
      h[j]:=t;
      inc(i);
      dec(j);
    end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;

procedure push(q:longint);
var i,j,t:longint;
begin
  inc(tot);
  heap[tot]:=q;
  i:=tot;
  while (i shr 1)<>0 do begin
    j:=i shr 1;
    if heap[j]<heap[i] then begin
      t:=heap[i];
      heap[i]:=heap[j];
      heap[j]:=t;
    end else break;
    i:=j;
  end;
end;

procedure del;
var i,j,t:longint;
begin
  heap[1]:=heap[tot];
  heap[tot]:=0;
  dec(tot);
  i:=1;
  while (i shl 1)<=tot do begin
    j:=i shl 1;
    if (j+1<=tot) and (heap[j]<heap[j+1]) then inc(j);
    if heap[i]<heap[j] then begin
      t:=heap[i];
      heap[i]:=heap[j];
      heap[j]:=t;
    end else break;
    i:=j;
  end;
end;

procedure pushwill(q:longint);
var i,j,t:longint;
begin
  inc(totwill);
  will[totwill]:=q;
  i:=totwill;
  while (i shr 1)<>0 do begin
    j:=i shr 1;
    if will[j]<will[i] then begin
      t:=will[i];
      will[i]:=will[j];
      will[j]:=t;
    end else break;
    i:=j;
  end;
end;

procedure delwill;
var i,j,t:longint;
begin
  will[1]:=will[totwill];
  will[totwill]:=0;
  dec(totwill);
  i:=1;
  while (i shl 1)<=totwill do begin
    j:=i shl 1;
    if (j+1<=totwill) and (will[j]<will[j+1]) then inc(j);
    if will[i]<will[j] then begin
      t:=will[i];
      will[i]:=will[j];
      will[j]:=t;
    end else break;
    i:=j;
  end;
end;

begin
  readln(n);
  tot:=0;
  totwill:=0;
  ans:=0;
  fillchar(p,sizeof(p),0);
  fillchar(lr,sizeof(lr),0);
  fillchar(h,sizeof(h),0);
  fillchar(heap,sizeof(heap),0);
  fillchar(will,sizeof(will),0);
  for i:=1 to n do begin
    readln(z,x,y);
    t:=2*i;
    p[t-1]:=x;
    h[t-1]:=z;
    lr[t-1]:=1;
    p[t]:=y;
    h[t]:=z;
    lr[t]:=2;
  end;
  qsort(1,t);
  i:=1;
  while i<=t do begin
    case lr[i] of
    1:begin
        height:=heap[1];
        repeat
          push(h[i]);
          inc(i);
        until (p[i]<>p[i-1]) or (i>t);
        dec(i);
        if heap[1]>height then begin
          inc(ans);
          a[ans]:=p[i];b[ans]:=height;
          inc(ans);
          a[ans]:=p[i];b[ans]:=heap[1];
        end;
      end;
    2:begin
        height:=heap[1];
        repeat
          pushwill(h[i]);
          inc(i);
        until (p[i]<>p[i-1]) or (lr[i]=1) or (i>t);
        if p[i]=p[i-1] then begin
          repeat
            push(h[i]);
            inc(i);
          until (p[i]<>p[i-1]) or (lr[i]=2) or (i>t);
        end;
        dec(i);
        while totwill<>0 do
          if will[1]=heap[1] then begin delwill;del; end
            else break;
        if heap[1]<>height then begin
          inc(ans);
          a[ans]:=p[i];b[ans]:=height;
          inc(ans);
          a[ans]:=p[i];b[ans]:=heap[1];
        end;
      end;
    end;
    inc(i);
  end;
  writeln(ans);
  for i:=1 to ans do writeln(a[i],' ',b[i]);
end.

```