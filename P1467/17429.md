竟然没有pascal题解？来一发

这题是一道细节很多的纯模拟题，本蒟蒻的方法是从n+1开始搜索直到找到循环数，判断时直接边模拟边mod（数的位数）遍若回到原位置——1，则是循环数

可以将数字转成字符串以便计算长度、每一位数字

话不多说，上代码：

```cpp
var n:qword;//题目说明了，要开qword
function jj:boolean;//判断是否为循环数的子程序
var i,j:longint;//循环变量（要开longint）
    k,t,l,x:qword;//k是当前位置，t是当前共走几步，L是字符串长度，x用来字符串转一位数字（本蒟蒻习惯只要不是循环变量就开qword，反正空间充足，免的少开）
    w:array[0..99]of qword;//计数器
    s:string;//以字符串的形式存当前找到的数字
begin
  k:=1;//把当前位置设为1
  t:=0;//当前还没走，所以是0步
  str(n,s);//将数字转成字符串以便计算长度和每一位数字
  l:=length(s);//用L存下数字长度
  fillchar(w,sizeof(w),0);//初始化
  for i:=1 to l do//这里是统计有无重复数字或数字0，如果有，就不可能是循环数（自己思考一下），直接返回false
  begin
    val(s[i],x);//将这一位字符转成数字x
    if(w[x]>0)or(x=0)then//如果这个数字出现过或者是0
    exit(false);//返回false并直接跳出子程序
    inc(w[x]);//计数
  end;
  fillchar(w,sizeof(w),0);//继续初始化计数器
  while t<l do//模拟l遍循环判断（程序核心！）
  begin
    val(s[k],x);//将这一位字符转成数字x
    if w[x]>0 then//继续判断是否有重复数字（想一想为什么）
    exit(false);
    inc(w[x]);//记录次数
    inc(t);//更新t
    k:=(k+x)mod l;//更新k
    if k=0 then//可能出现k为0的情况
    k:=l;//要把k赋成L
  end;
  if k<>1 then//如果k没回到1（不是循环数）
  exit(false);//返回false并跳出
  exit(true);//返回true
end;
begin
  read(n);//读入
  while true do//此处不用设定跳出条件，可以在找到答案后直接输出并结束程序
  begin
    inc(n);//寻找循环数（注意，要先加后检验，题目要求找的是比n大的循环数）
    if jj then//如果这个数是循环数
    begin
      write(n);//直接输出
      halt;//结束程序
    end;
  end;
end.
```