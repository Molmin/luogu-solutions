**P党的题解来一发**

将下面大佬方法稍微汇总一下

首先
咳咳
看到题目就想到了线段树啊

不需要修改数据啊，就是先建树，然后找[i,i+k-1]区间里的最值

这里需要两棵线段树，一个关键字是最大，另一个是最小

下面看着代码解释

```cpp
var
  a1,a2:longint;
  len,i,j,m,n,k,p:longint;
  ansmax,ansmin,a,l,r,lc,rc,keymax,keymin:array[0..2000005]of longint;//key是关键字，lc,rc是左右孩子，l,r是这个点管的范围
function max(x,y:longint):longint;
begin
  if x>y then exit(x)
  else exit(y);
end;
function min(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
procedure built(x,y:longint);
var
  now,mid:longint;
begin
  inc(len); now:=len;
  l[now]:=x; r[now]:=y;
  lc[now]:=-1; rc[now]:=-1;//一开始认为没有左右孩子
  if x=y then
    begin
      keymax[now]:=a[x]; keymin[now]:=a[x];//如果只管一个点的话
    end
  else
    begin
      mid:=(x+y) div 2;//二分，将范围分给左右孩子
      lc[now]:=len+1; built(x,mid);
      rc[now]:=len+1; built(mid+1,y);
      keymax[now]:=max(keymax[lc[now]],keymax[rc[now]]);
      keymin[now]:=min(keymin[lc[now]],keymin[rc[now]]);
    end;
end;
function findmax(now,x,y:longint):longint;
var
  mid,a1,a2:longint;
begin
  if (l[now]=x)and(r[now]=y) then
    findmax:=keymax[now]
  else
    begin
      a1:=lc[now]; a2:=rc[now];
      mid:=(l[now]+r[now]) div 2;
      if mid>=y then findmax:=findmax(a1,x,y) else//如果全在左孩子的范围内
      if mid<x then findmax:=findmax(a2,x,y) else//如果全在右孩子范围内
      findmax:=max(findmax(a1,x,mid),findmax(a2,mid+1,y));//都有的话就在左右孩子中取个最值
    end;
end;
function findmin(now,x,y:longint):longint;//同理啊
var
  mid,a1,a2:longint;
begin
  if (l[now]=x)and(r[now]=y) then
    findmin:=keymin[now]
  else
    begin
      a1:=lc[now]; a2:=rc[now];
      mid:=(l[now]+r[now]) div 2;
      if mid>=y then findmin:=findmin(a1,x,y) else
      if mid<x then  findmin:=findmin(a2,x,y) else
      findmin:=min(findmin(a1,x,mid),findmin(a2,mid+1,y));
    end;
end;
begin
  readln(n,k);
  for i:=1 to n do read(a[i]);
  len:=0; built(1,n);
  if n<=k then//如果只有一个窗口
    begin
      a1:=-maxlongint; a2:=maxlongint;
      for i:=1 to n do
        begin
          if a1<a[i] then a1:=a[i];
          if a2>a[i] then a2:=a[i];
        end;
      writeln(a2); writeln(a1);
    end
  else
    begin
      for i:=1 to n-k+1 do
        begin
          ansmax[i]:=findmax(1,i,i+k-1);
          ansmin[i]:=findmin(1,i,i+k-1);
        end;
    end;
  for i:=1 to n-k+1 do write(ansmin[i],' '); writeln;
  for i:=1 to n-k+1 do write(ansmax[i],' ');
end.
```
之后我发现
**没有修改功能的线段树，太浪费了，只有70分TLE**

下面大佬就有说了 单调队列

就是维护一个单调的队列，一个新的元素进队前，先与队尾元素比较，直到找到一个能加进去形成单调队列的位置

队头就是答案，但是要看一下有没有在窗口范围内

```cpp
var
  i,j,m,n,k,p:longint;
  a,f,num:array[0..1000005]of longint;
procedure max;
var
  i,head,tail:longint;
begin
  head:=1; tail:=1; f[1]:=a[1]; num[1]:=1;
  for i:=2 to k do
    begin
      if f[tail]>a[i] then
        begin
          inc(tail);
          f[tail]:=a[i]; num[tail]:=i;
        end
      else
        begin
          while (f[tail]<a[i])and(tail>=head) do dec(tail);
          inc(tail); f[tail]:=a[i]; num[tail]:=i;
        end;
    end;
  write(f[head],' ');
  for i:=k+1 to n do
    begin
      if num[head]<i-k+1 then  inc(head);
      if f[tail]>a[i] then
        begin
          inc(tail);
          f[tail]:=a[i]; num[tail]:=i;
        end
      else
        begin
          while (f[tail]<a[i])and(tail>=head) do dec(tail);
          inc(tail); f[tail]:=a[i]; num[tail]:=i;
        end;
      write(f[head],' ');
    end;
end;
procedure min;
var
  i,head,tail:longint;
begin
  head:=1; tail:=1; f[1]:=a[1]; num[1]:=1;
  for i:=2 to k do
    begin
      if f[tail]<a[i] then
        begin
          inc(tail);
          f[tail]:=a[i]; num[tail]:=i;
        end
      else
        begin
          while (f[tail]>a[i])and(tail>=head) do dec(tail);
          inc(tail); f[tail]:=a[i]; num[tail]:=i;
        end;
    end;
  write(f[head],' ');
  for i:=k+1 to n do
    begin
      if num[head]<i-k+1 then inc(head);
      if f[tail]<a[i] then
        begin
          inc(tail);
          f[tail]:=a[i]; num[tail]:=i;
        end
      else
        begin
          while (f[tail]>a[i])and(tail>=head) do dec(tail);
          inc(tail); f[tail]:=a[i]; num[tail]:=i;
        end;
      write(f[head],' ');
    end;
end;
begin
  readln(n,k);
  for i:=1 to n do read(a[i]);
  min; writeln; max;
end.
```