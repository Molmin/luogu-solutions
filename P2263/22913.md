## 没有题解？P党来一发。
首先我们需要理解题意。

“从墙上搬走一块砖，或是从旁边的魔力砖堆（假设可用的魔力砖无限）中拿一块砖放置在墙上”
### 这句话的意思就是：每堵墙高度可以+1或者-1。
我们通过观察可以发现:k个连续的魔力砖高度相等时，
### 一定是这k个魔力砖的高度的中位数所付出的能量值最小。
正确性？显而易见是对的~~，（我不会告诉你这是七年级数学题）~~

观察题目所给数据的大小，1,000,000,000,000，所以我们需要将这些魔力砖的高度离散化。

代码大致如下（来自P党的忧伤）：
```
  for i:=1 to n do
   begin
    read(a[i]);
    b[i]:=i;
   end;
  sort1(1,n);           //把a按从大到小排序
  for i:=1 to n do      //防止重复的数
   if a[i]<>a[i-1] then
    begin
     c[i]:=c[i-1]+1;
     ls[c[i]]:=a[i];    //ls[x]表示的是离散后是x的数的原来的数
    end
   else
    c[i]:=c[i-1];
  a:=c;
  sort2(1,n);           //再排回去。。。
```
~~蒟蒻的我打离散时挂了无数回~~

### 然后我们需要考虑怎样把中位数算出来
我们可以考虑建立一棵权值线段树，里面存储的是权值~~（废话）~~和**这个区间里的数的和**（下面会用到）

建立完之后，我们就可以把中位数搞出来了。

建树的代码：
```
procedure update(i,l,r,x,p:int64);
var mid:int64;
begin
  t[i].s:=t[i].s+p;            //这个区间的权值
  t[i].ans:=t[i].ans+ls[x]*p;  //这个区间里的数的和
  if l=r then
   exit;
  mid:=(l+r) div 2;
  if mid>=x then
   update(i*2,l,mid,x,p)
  else
   update(i*2+1,mid+1,r,x,p);
end;
```
这里的p表示是插入还是删除，如果p=1，就是插入，如果p=-1，就是删除。~~（我太懒了）~~

查找中位数的代码：
```
function findnum(i,l,r,x:int64):int64;
var ans,mid:int64;
begin
  if i=0 then
   exit(0);
  if l=r then
   exit(l);
  mid:=(l+r) div 2;
  if t[i*2].s>=x then        //如果中位数的位置小于左子树的权值，就递归左子树。否则递归右子树
   ans:=findnum(i*2,l,mid,x)
  else
   ans:=findnum(i*2+1,mid+1,r,x-t[i*2].s);
  exit(ans);
end;
```
~~跟普通平衡树的查数有异曲同工之妙~~
### 那我们怎么统计答案呢？
~~（七年级数学题又来了）~~我们设midd=(k+1)>>1~~（我太懒了）~~显而易见，大于中位数的数有k-midd个。**那么大于中位数的数所需要的能量值为大于中位数的所有数的和-中位数*（k-midd）**

同理可得，**小于等于中位数的数所需要的能量值为中位数*（midd）-小于等于中位数的所有数的和**

所以，最终的能量值就是**大于中位数的数所需要的能量值+小于等于中位数的数所需要的能量值**

算能量值的代码：
```
  ans:=20000000000000000;
  midd:=(k+1) div 2;
  for i:=k to n do
   begin
    update(1,1,maxh,a[i],1);      //插入
    mid:=findnum(1,1,maxh,midd);  //中位数
    big:=getbig(1,1,maxh,mid);    //大于中位数的数的和
    small:=getsmall(1,1,maxh,mid); //小于中位数的数的和
    s:=big+ls[mid]*midd-ls[mid]*(k-midd)-small; //最终能量值
    if s<ans then //更新答案
     ans:=s;
    update(1,1,maxh,a[i-k+1],-1); //删除
   end;
  writeln(ans);
```
其中，getbig和getsmall的含义就是注释里写的。

代码如下：
```
function getsmall(i,l,r,x:int64):int64; //小于中位数的数的和
var mid:int64;
begin
  if r<=x then
   exit(t[i].ans);
  if l>x then
   exit(0);
  mid:=(l+r) div 2;
  getsmall:=getsmall(i*2,l,mid,x)+getsmall(i*2+1,mid+1,r,x);
end;
function getbig(i,l,r,x:int64):int64; //大于中位数的数的和
var mid:int64;
begin
  if l>x then
   exit(t[i].ans);
  if r<=x then
   exit(0);
  mid:=(l+r) div 2;
  getbig:=getbig(i*2,l,mid,x)+getbig(i*2+1,mid+1,r,x);
end;
```
至此，我们就已经得出正确答案了。
完整代码（码风巨丑）：
```
type node=record
      s,ans:int64;
     end;
var s,n,k,ans,maxh,mid,midd,big,small:int64;
    i:longint;
    a,b,c,ls:array[0..4000001] of int64;
    t:array[0..4000001] of node;
procedure sort1(l,r:int64);
var i,j,x,y:int64;
begin
  i:=l;j:=r;x:=a[(l+r) div 2];
  repeat
   while a[i]<x do
    inc(i);
   while x<a[j] do
    dec(j);
   if not(i>j) then
    begin
     y:=a[i];a[i]:=a[j];a[j]:=y;
     y:=b[i];b[i]:=b[j];b[j]:=y;
     inc(i);j:=j-1;
    end;
  until i>j;
  if l<j then
   sort1(l,j);
  if i<r then
   sort1(i,r);
end;
procedure sort2(l,r:longint);
var i,j,x,y:longint;
begin
  i:=l;j:=r;x:=b[(l+r) div 2];
  repeat
   while b[i]<x do
    inc(i);
   while x<b[j] do
    dec(j);
   if not(i>j) then
    begin
     y:=a[i];a[i]:=a[j];a[j]:=y;
     y:=b[i];b[i]:=b[j];b[j]:=y;
     inc(i);j:=j-1;
    end;
  until i>j;
  if l<j then
   sort2(l,j);
  if i<r then
   sort2(i,r);
end;
procedure update(i,l,r,x,p:int64);
var mid:int64;
begin
  t[i].s:=t[i].s+p;
  t[i].ans:=t[i].ans+ls[x]*p;
  if l=r then
   exit;
  mid:=(l+r) div 2;
  if mid>=x then
   update(i*2,l,mid,x,p)
  else
   update(i*2+1,mid+1,r,x,p);
end;
function findnum(i,l,r,x:int64):int64;
var ans,mid:int64;
begin
  if i=0 then
   exit(0);
  if l=r then
   exit(l);
  mid:=(l+r) div 2;
  if t[i*2].s>=x then
   ans:=findnum(i*2,l,mid,x)
  else
   ans:=findnum(i*2+1,mid+1,r,x-t[i*2].s);
  exit(ans);
end;
function getsmall(i,l,r,x:int64):int64;
var mid:int64;
begin
  if r<=x then
   exit(t[i].ans);
  if l>x then
   exit(0);
  mid:=(l+r) div 2;
  getsmall:=getsmall(i*2,l,mid,x)+getsmall(i*2+1,mid+1,r,x);
end;
function getbig(i,l,r,x:int64):int64;
var mid:int64;
begin
  if l>x then
   exit(t[i].ans);
  if r<=x then
   exit(0);
  mid:=(l+r) div 2;
  getbig:=getbig(i*2,l,mid,x)+getbig(i*2+1,mid+1,r,x);
end;
begin
  read(n,k);
  for i:=1 to n do
   begin
    read(a[i]);
    b[i]:=i;
   end;
  sort1(1,n);
  for i:=1 to n do
   if a[i]<>a[i-1] then
    begin
     c[i]:=c[i-1]+1;
     ls[c[i]]:=a[i];
    end
   else
    c[i]:=c[i-1];
  a:=c;
  sort2(1,n);
  maxh:=500000;
  for i:=1 to k-1 do
   update(1,1,maxh,a[i],1);
  ans:=20000000000000000;
  midd:=(k+1) div 2;
  for i:=k to n do
   begin
    update(1,1,maxh,a[i],1);
    mid:=findnum(1,1,maxh,midd);
    big:=getbig(1,1,maxh,mid);small:=getsmall(1,1,maxh,mid);
    s:=big+ls[mid]*midd-ls[mid]*(k-midd)-small;
    if s<ans then
     ans:=s;
    update(1,1,maxh,a[i-k+1],-1);
   end;
  writeln(ans);
end.
```
注释就不打了，上面很详细了~~（其实是我太懒了）~~
最后鸣谢[大佬](https://www.luogu.org/space/show?uid=29575)的启发