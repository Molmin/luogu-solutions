说实话

这道题作为普及组最后一道有点水了

好吧其他也挺水的

主要思路是建一个图

一条路中，没停的点向停的点连边

为什么？

因为要保证满足条件

没停的点的级别必须小于停的点

然后用dfs大法求出最长路径就行了





```cpp
uses math;
var
    flag:array[0..1000] of boolean;  //flag数组表示是否停靠
    b:array[0..1000,0..1000] of boolean;  //b数组避免重复连边
    q:array[0..1000,0..1000] of longint;  //邻接表
    a,f:array[0..1000] of longint;
    i,j,k,n,m,p,ans:longint;
procedure dfs(x:longint);
var
    i:longint;
begin
    if f[x]<>0 then
        exit;
    f[x]:=1; //最低等级为1
    for i:=1 to q[x][0] do
        begin
            dfs(q[x][i]);
            f[x]:=max(f[x],f[q[x][i]]+1);
        end;
end;
begin
    readln(n,m);
    for i:=1 to m do
        begin
            read(p);
            fillchar(flag,sizeof(flag),0);
            for j:=1 to p do
                begin
                    read(a[j]);
                    flag[a[j]]:=true;
                end;
            for j:=a[1] to a[p] do
                if not flag[j] then
                    begin
                        for k:=1 to p do
                            if not b[j][a[k]] then
                                begin
                                    inc(q[j][0]);
                                    q[j][q[j][0]]:=a[k];
                                    b[j][a[k]]:=true;
                                end;
                    end;
        end;
    for i:=1 to n do
        dfs(i);
    for i:=1 to n do
        ans:=max(ans,f[i]);
    writeln(ans);
end.

```