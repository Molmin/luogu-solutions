模拟+排序+字符串。

本题靠模拟，主要难点是字符串处理，因此给了普及-，按照整体思路下来是不难的。

本题可以不用排序，靠直接下来一个个处理也可以，这样时间复杂度就只有O(K\*N)了吧(k为处理数据时的常数，for循环中的语句总数)。

下面在代码里分析：

```cpp
program p1051;
  var a,b,e,f,i,n,ans,total:longint;
      s,name1,name2:string;
      c,d:char;
begin
  readln(n);
  for i:=1 to n do
  begin
    f:=0;
    readln(s);
    s:=s+' ';
    name2:=copy(s,1,pos(' ',s)-1);//将当前名字记录。
    delete(s,1,pos(' ',s));
    val(copy(s,1,pos(' ',s)-1),a);//记录当前期末成绩。
    delete(s,1,pos(' ',s));
    val(copy(s,1,pos(' ',s)-1),b);//记录当前评议成绩。
    delete(s,1,pos(' ',s));
    c:=s[1];//记录是否为干部。
    delete(s,1,2);
    d:=s[1];//记录是否来自西部。
    delete(s,1,2);
    val(copy(s,1,pos(' ',s)-1),e);//记录论文总数。
    delete(s,1,pos(' ',s));
    if (a>80) and (e>=1) then inc(f,8000);//满足第一个条件，此人奖学金+8000。
    if (a>85) and (b>80) then inc(f,4000);//满足第二个条件，此人奖学金+4000。
    if (a>90) then inc(f,2000);//满足第三个条件，此人奖学金+2000。
    if (a>85) and (d='Y') then inc(f,1000);//满足第四个条件，此人奖学金+1000。
    if (b>80) and (c='Y') then inc(f,850);//满足第五个条件，此人奖学金+850。
    if f>ans then//如果此人奖学金总数超过前面任意一个人，将TA的名字和奖学金记录。
    begin
      ans:=f;
      name1:=name2;
    end;
    inc(total,f);//累加总奖学金。
  end;
  writeln(name1);//输出最多奖学金人的名字。
  writeln(ans);//输出单人最多奖学金。
  writeln(total);//输出总奖学金。
end.
```