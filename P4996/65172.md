月赛的时候，想到了组合，然后进一步想到递推，然后因为来晚了就十五分钟就没打了......

题目的本质便是要看有歉意值的状态出现了多少次，用草稿纸推一下发现暴搜可能不太现实，那就是组合递推啦。

那么我们来考虑由全是0的状态到一个中间状态再到全是1的状态有多少种。

设$cnt[i]$表示由i个0变为有i个1的方法数,易知$cnt[0]=cnt[1]=1$;

比如将00变为11，那么有两种情况：

1.先变出一个1，此时就是$C(2,1)$(组合数,二选一），再由分步乘法原理，剩下1个0变为1，就是再乘上一个$cnt[1]$；

2.一次性变为11，就是$C(2,2)$咯，再乘一个$cnt[0]$；

所以$cnt[2]=C(2,1)*cnt[1]+C(2,2)*cnt[0]=3$.

那么对于由i个0变为有i个1的方法数:

1.挑一个变成1，C(i,1),再乘上由i-1个0变为1的方法数，即C(i,1)*cnt[i-1];

2.挑两个，C(i,2),然后分步原理，C(i,2)*cnt[i-2];

......

于是$cnt[i]=\sum_{j=1}^i{C(i,j)*cnt[i-j]}$;

那么考虑一下一个有i个1的状态会出现多少次，用一个分布乘法原理，
先由n个0变为i个1，再由i个1变为n个1，其实就是先把那i个0变为1，再把剩下n-i个0变为1，那么就是$cnt[i]*cnt[n-i]$，最后再往sum上面加一个$a*cnt[i]*cnt[n-i]$(a为歉意值）,于是递推和计算的工作就大功告成了。

顺便说一句，记得一步一mod，我就是因为没mod好交了四次80哦。

上p党代码

```pascal
const mo=998244353;
var
c:array[0..22,0..22] of int64;//组合数
cnt:array[0..22] of int64;    //方法数
n,m,i,j:longint;         
sum,a,k:int64;                //答案，歉意值，1的个数
ch:char;
begin
readln(n,m);
c[1,0]:=1;c[1,1]:=1;cnt[1]:=1;cnt[0]:=1;
for i:=2 to n do
     c[i,0]:=1;
for i:=2 to n do
     for j:=1 to i do
     begin
         c[i,j]:=(c[i-1,j-1] mod mo+c[i-1,j] mod mo) mod mo;  //杨辉三角递推组合数
         cnt[i]:=(cnt[i]+c[i,j]*cnt[i-j] mod mo) mod mo; //上文所说递推公式
     end;

for i:=1 to m do
 begin
     a:=0;
     for j:=1 to n do
     begin
         read(ch);
         if ch='1' then inc(k);  //字符处理，记录1的个数
     end;
     readln(a);                  //歉意值
     sum:=(sum + (cnt[k]*(cnt[n-k] * a mod mo) mod mo)) mod mo; //一步一mod！！！
 end;
writeln(sum mod mo);
end.
```
最后祝各位大佬noip2018 rp++（蒟蒻即将退赛搞数学啦）