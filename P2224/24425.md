这一题设f[i]为使用A机器加工了i分钟时，使用B机器加工耗时的最小值。

f[i]初始赋值为maxint，f[0]=0

对于每件物品，有三个选择，使用A机器耗时a分钟，使用B机器耗时b分钟，使用A，B一起加工耗时c分钟。则状态转移方程为：
```pas
   f[i]=f[i]+b,b<>0
   
   f[i]=maxint,b=0(不要真的赋值为maxint,赋值为一个很大的数就行，不然计算时可能会爆）
   
   f[i]=min(f[i],f[i-a]),i>=a且a<>0
   
   f[i]=min(f[i],f[i-c]+c),i>=c且c<>0
```
此时，需要考虑i的范围，如果对每一件物品都
```pas
                   for i:=5*n downto 0 do
```
这样进行状态扩展，总共要循环5n^2次，对于本题数据肯定会超时。

其实，当我们处理第k件物品时，能够影响到的状态只有

                       f[j],0<=j<=5*k
                       
因为处理k件物品，最多就花费5*k分钟。

因此，在处理第k件物品是状态扩展为：
```pas
                   for i:=5*k downto 0 do
```
这样便可有效减少循环次数，确保不会超时。

```pas

var
   f:array[0..30050]of longint;
   n,i,a,b,c,j,sum,ans:longint;

function max(a,b:longint):longint;
begin
  if a>b then exit(a) else exit(b);
end;

function min(a,b:longint):longint;
begin
  if a<b then exit(a) else exit(b);
end;

begin
assign(input,'pro.in');reset(input);
assign(output,'pro.out');rewrite(output);
  read(n);
  for i:=1 to 30050 do f[i]:=100000000;
  f[0]:=0;
  sum:=0;
  for i:=1 to n do begin
    read(a,b,c);
    if a=0 then a:=100000000;
    if b=0 then b:=100000000;
    if c=0 then c:=100000000;
    for j:=5*i downto 0 do begin
      if b<>100000000 then f[j]:=f[j]+b else f[j]:=100000000;
      if j>=a then f[j]:=min(f[j],f[j-a]);
      if j>=c then f[j]:=min(f[j],f[j-c]+c);
    end;
  end;
  ans:=maxint;
  for i:=0 to 5*n do ans:=min(ans,max(i,f[i]));
  writeln(ans);
end.

```