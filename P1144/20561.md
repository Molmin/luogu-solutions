#Pascal的题解还是如此的少，我来补一发#

既然无权，BFS应该比较合适吧。

BFS求无权图中单源最短路应该大家都会吧，从起点出发，一找到就是最短的（无权）。那么计数其实就加一个数组加几句话就行了。

分两种情况：

1. 第一次到达当前点y，记录最短路，假设是从x点到达y的，则ans[y]=ans[x]，然后入队。

2. 不是第一次。判断当前是否为最短路，如果是，则ans[y]+=ans[x]，不需要入队，注意取模。

但是点比较多（好吧其实是很多），怎么办呢？用邻接表存储，数组模拟链表（一维数组），记录一个点x与他相邻的所有点，需要head[x]（头），next[y]（下一条边）就能存下整个图。注意无向图，数组范围应该是2e。

```cpp
var
  n,m,e,i,j,x,y:longint; //e表示总边数，最后应该是2*m
  head,ans,dis:array[0..2000001]of longint; //分别表示链表的头，答案和最短路
  a,next,q:array[0..5000001]of longint; //分别表示邻接表，下一条边，队列
  b:array[0..2000001]of boolean; //表示有没有访问过
procedure add(x,y:longint);
begin
  inc(e); a[e]:=y; next[e]:=head[x]; head[x]:=e; //加入(x,y)这条有向边
end;
procedure bfs();
var
  h,t,i,x,y:longint;
begin
  fillchar(b,sizeof(b),1);
  fillchar(ans,sizeof(ans),0);
  h:=0; t:=1; q[1]:=1; ans[1]:=1; dis[0]:=0; b[1]:=false; //起点入队
  while h<t do 
    begin
      inc(h); x:=q[h]; i:=head[x]; //从头开始
      while i>0 do 
        begin
          y:=a[i];
          if b[y] then //第一种情况
            begin
              ans[y]:=ans[x];
              dis[y]:=dis[x]+1;
              inc(t); q[t]:=y;
              b[y]:=false;
            end
          else //第二种情况
            if dis[y]=dis[x]+1 then ans[y]:=(ans[y]+ans[x]) mod 100003;
          i:=next[i]; //下一条边
        end;
    end;
end;
begin
  readln(n,m); e:=0;
  fillchar(head,sizeof(head),0);
  for i:=1 to m do 
    begin
      readln(x,y);
      add(x,y);
      add(y,x);//无向图
    end;
  bfs();
  for i:=1 to n do writeln(ans[i]);
end.

```