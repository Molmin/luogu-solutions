# Problems
班级要组织一场综合能力竞赛，班里共有 $n$ 位学生，将分成两队互相竞争。

老师找到了越越并给了越越一张全班同学综合能力测试的成绩，要求他从全班同学中选出**一半**，并要求这些同学综合能力测试的成绩之和在**不超过班级总分一半**的前提下尽量达到最高。这样分成的两队实力是最平均的。越越堆着满脸的笑容找到了你，你就帮他写一个程序吧。
# Answer
设$f_{i,j,k}$表示从前$i$个人中选出$j$个人能否凑成$k$分。

那么转移方程就有：$f_{i-1,j,k}||f_{i-1,j-1,k}$。

将$i$压维得$f_{j,k}=f_{j,k}||f_{j-1,k-a_i}$，其中$a_{i}$就是题目中的$a_{i}$。

* 初始值：$f_{0,0}=true$。

于是可得代码：
```pascal
  for i:=1 to n do s:=s+a[i];//求和
  m:=n;s:=s div 2;n:=n div 2;//注意取一半
  f[0,0]:=true;//f数组初始值
  for i:=1 to m do//m就是最初n的替身
    for j:=i downto 1 do//从i个人中选出j个人
      for k:=s downto a[i] do//分数最多不多于一半
        f[j,k]:=f[j,k] or f[j-1,k-a[i]];//转移方程
```
最后输出最大值即可。
# Code
```pascal
var
  n,s,i,j,k,m:longint;
  a:array[0..101] of longint;
  f:array[0..101,0..10001] of boolean;//注意f数组的范围
  //因为是总分的一半，所以要定义100×100÷2，保险一点定义100×100
begin
  readln(n);//读入n
  for i:=1 to n do readln(a[i]);//读入a数组
  for i:=1 to n do s:=s+a[i];//求和
  m:=n;s:=s div 2;n:=n div 2;//注意取一半
  f[0,0]:=true;//f数组初始值
  for i:=1 to m do//m就是最初n的替身
    for j:=i downto 1 do//从i个人中选出j个人
      for k:=s downto a[i] do//分数最多不多于一半
        f[j,k]:=f[j,k] or f[j-1,k-a[i]];//转移方程
  for i:=s downto 0 do if f[n,i] then begin writeln(i);exit;end;//找最大值
end.
```