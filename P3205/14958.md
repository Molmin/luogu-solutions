这道题很坑啊，没有给出数据范围，应该是0<n<=1000,然后还漏了一个条件，就是将答案对19650827进行取模。

对于这道题，一开始可以想到对最终序列进行搜索，根据题目要求来还原原序列，找到一个序列就ans++。但是数据范围显然承受不住。

进一步分析，对于最终序列的一个区间[l,r]，最后一个添加的只可能是第l个人或者第r个人。如果最后一个是l，则对于这个区间，a[l]<a[l+1]或者a[l]<a[r]。如果最后一个是r，则对于这个区间a[r]>a[r-1]或者a[r]>a[l]。

于是想到一个区间动态规划，定义f[l,r]表示在最终序列区间序列[l,r]中，最后一个添加的人是l时所得到的方案数，g[l,r]表示在最终序列区间序列[l,r]中，最后一个添加的人是r时所得到的方案数。[l,r]这个区间可以从[l+1,r]和[l,r-1]转移过来，判断最后这个人和上一个人的大小关系，分成两类:除去最后这个人，上一个人添加在区间左边/右边。

得到动态转移方程：f[l,r]:=f[l+1,r]\*(a[l]<a[l+1])+g[l+1,r]\*(a[l]<a[r]);

g[l,r]:=f[l,r-1]\*(a[l]<a[r])+g[l,r-1]\*(a[r-1]<a[r]);

```pascal
var i,n,j,l,r,mo:longint;
    f,g:array[0..1001,0..1001] of longint;
    a:array[0..1001] of longint;

function pd(a,b:longint):longint;
begin
if a<b then exit(1) else exit(0);
end;

begin
mo:=19650827;
read(n);
for i:=1 to n do read(a[i]);
for i:=1 to n do
        begin
        f[i,i]:=1;
        end;
for i:=1 to n-1 do
for j:=1 to n do
        begin
        if j+i>n then continue;
        l:=j;r:=j+i;
        f[l,r]:=(f[l+1,r]*pd(a[l],a[l+1])+g[l+1,r]*pd(a[l],a[r])) mod mo;
        g[l,r]:=(f[l,r-1]*pd(a[l],a[r])+g[l,r-1]*pd(a[r-1],a[r])) mod mo;
        end;
writeln((f[1,n]+g[1,n]) mod mo);
end.
```