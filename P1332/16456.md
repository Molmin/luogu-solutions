一个pascal的题解

这道题数据点贼大，所以一个小小的mistake就会酿成大错

一开始我队列的head指针和tail指针都附值为1，结果wa\*9!!!

仔细查错才发现head应该为0；

嗯，题外话就说到这儿；

本题就是针对多个污染源的放射性问题，是污染区域是几何阶层倍的增加。

拿到这么一道题，首先的感觉就是拓展拓展再拓展，就想到的bfs或者dfs

再看数据n,m<=500！对于这么大的数据dfs甭想了，就上bfs吧！

但是有多个污染源啊，又不是一个，队头怎么写呢？

我们就按每个污染源依次入队，按照顺序依次扩展。

令这个矩阵为N\*M，污染源的数目为T1，询问个数为T2。

队头就可以这么写：

```cpp
 head:=0; tail:=1;
 for i:=1 to t1  do begin
  readln(x,y);
  map[x,y]:=0;
  u[x,y]:=true;
  a[tail].x:=x;
  a[tail].y:=y;
  a[tail].step:=0;
  inc(tail);
 end;
```
这只是第一步，接下来就是正儿八经的bfs了

我们写到函数里，其实就是一个拓展的模板（详见下标程）

我们把点(x,y)的感染时间设为map[x,y]的值。

需要注意的一点是，我们要判个最小值（虽然题目并没有卡你），详细看注释

但是如果数据保证递增的话可以不用取：传染的顺序有前后之分，而已被传染的不会在被传染

而队列正好是先入先出，可以确定先入的一定是感染时间比较早的人

```cpp
uses math;
type rec=record
x,y,step:longint;//x(y)该点的x(y)坐标,step该点的步数
end;
const dx:array[1..4]of integer=(-1,0,1,0);
      dy:array[1..4]of integer=(0,1,0,-1);
//坐标增量
var n,m,x,y,t1,t2,head,tail,i,j:longint;
    a:array[1..250000]of rec;//队列
    u:array[1..500,1..500]of boolean;//是否标记是否入列，防止冗余搜索 ，true代表是，false代表否
    map:array[1..500,1..500]of longint;//图（答案）
procedure bfs;
var  i,xx,yy:longint;
begin
 while head<tail do begin
  inc(head);
  for i:=1 to 4 do begin
   xx:=a[head].x+dx[i];
   yy:=a[head].y+dy[i];//下一个即将被遍历到的点的横纵坐标
   if (xx<=0)or(xx>n)or(yy<=0)or(yy>m)then continue;
   if u[xx,yy] then continue;
//一系列不符合条件的舍去，防止队列冗余
   a[tail].x:=xx; a[tail].y:=yy;
   a[tail].step:=a[head].step+1;//计算需要时间
   u[xx,yy]:=true;//标记入队
   map[xx,yy]:=min(a[tail].step,map[xx,yy]);//上面说过要取最小值，他没卡你
   inc(tail);
  end;
 end;
end;
begin
 readln(n,m,t1,t2);
 fillchar(u,sizeof(u),false);
 fillchar(map,sizeof(map),$7f);
 head:=0; tail:=1;//注意！！！！！
 for i:=1 to t1  do begin
  readln(x,y);
  map[x,y]:=0;//图的传染源，附值为0
  u[x,y]:=true;
  a[tail].x:=x;
  a[tail].y:=y;
  a[tail].step:=0;
  inc(tail);//入队完毕
 end;//队头
 bfs;
 for i:=1 to t2 do begin
  readln(x,y);
  writeln(map[x,y]);
 end;//依次输出
end.
```