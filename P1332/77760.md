# [p1332]血色先锋军

宽搜,注意数据。



```cpp
Uses math;
    var
            i,j,n,m,lord_num,inf_num,l,r,x,y,head,tail:longint;// l,r为本次感染源坐标,x,y为本次感染源要感染的人的坐标,head,tail为队列头指针和尾指针
            ask,long:array[0..1000,0..1000] of longint;//ask,long分别为是否感染和感染时间
            will:array[0..250000,1..2] of longint;//新的感染源
            dir:array[1..4,1..2] of longint;//方向
            lord:array[0..250000,1..2] of longint;//领主位置
    procedure make_direction;//建立四周感染方向
    begin
            dir[1,1]:=-1;
            dir[2,2]:=-1;
            dir[3,1]:=1;
            dir[4,2]:=1;
    end;
    procedure make_body;//初始值与输入
    var
            i:longint;
    begin
            head:=0;
            tail:=0;
            for i:=1 to inf_num do
            begin
                    read(l,r);
                    long[l,r]:=0;
                    inc(tail);//新建感染源
                    will[tail,1]:=l;
                    will[tail,2]:=r;
                    ask[l,r]:=1;
            end;
            for i:=1 to lord_num do
            begin
                    read(l,r);//记录领主位置
                    lord[i,1]:=l;
                    lord[i,2]:=r;
            end;
    end;
    function jud(a,b:longint):boolean;//判断是否超界
    begin
            if (a=0)or(b=0)or(a>n)or(b>m) then
                    exit(FALSE)
            else
                    exit(TRUE);
    end;
    begin
            read(n,m,inf_num,lord_num);
            make_body;
            make_direction;
            repeat
                    i:=head;//本次队列数(已经被感染的人)
                    for j:=1 to 4 do
                    begin
                            x:=will[i,1];//已经被感染的人的坐标
                            y:=will[i,2];
                            l:=x+dir[j,1];//下一个被感染的人的目标
                            r:=y+dir[j,2];
                            if jud(l,r) then//如果不超界
                            begin
                                    if ask[l,r]=0 then//没有被感染过
                                    begin
                                            ask[l,r]:=1;//已经被感染
                                            inc(tail);//维护队列
                                            will[tail,1]:=l;//加入新的感染源的坐标
                                            will[tail,2]:=r;
                                            long[l,r]:=long[x,y]+1;//本次感染是(i)上一个感染源感染时间+1
                                    end;
                            end;
                    end;
                    inc(head);//将对头弹出队列(本次感染源没有用了,因为4周都已经被他感染了)
            until head>tail;//没有更多的感染源(队列为空)
            for i:=1 to lord_num do
                    writeln(long[lord[i,1],lord[i,2]]);//输出
end.
```