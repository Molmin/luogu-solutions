# 前言
此题题目不难，思路很好想，但是我上界定太大爆炸了，$fst$直接下青，发现很多人也在此题$fst$了，于是来写篇题解
# 题意分析
有一些数据节点，主人公要去收集这些节点，问在指定时间内最多收集多少个
# 思路
可以轻易想到，数据节点的位置分布是有$\color{red}\text{特点}$的（详情请看题目），节点的横纵坐标是在不断增大，且增大的量越来越大($\text{ax,ay>1}$)，所以不论我们一开始到哪个节点去收集，接下来一定是向前一个节点收集能比往后一个节点收集的多，并且向前收集就会不断往前收集，直到到头再去考虑后面没有收集过的。

所以我们可以暴力枚举第一个采集的节点，然后先向前，在往后收集，判断有无超过时限即可
# 时间复杂度
$$O(log_2(t)^2)$$
# $\text{code}$
```pascal
const jie=50000000000000000;//不要定太大!!!，我就是作死写了1e17然后fst的，够大就可以了
var
  x,y:array[0..1000] of int64;
  ax,ay,bx,by,xs,ys,t,time:int64;
  n,i,ans,anss,j:longint;
begin
  readln(x[0],y[0],ax,ay,bx,by);
  readln(xs,ys,t);
  n:=100;
  for i:=1 to n do
  begin
    x[i]:=x[i-1]*ax+bx;
    y[i]:=y[i-1]*ay+by;
    if (x[i]>=jie) or (y[i]>=jie) then break;
  end;//预处理信息节点
  n:=i-1;
  for i:=0 to n do//枚举采集的第一个节点
  begin
    if anss>ans then ans:=anss;//取最大值
    anss:=0;
    time:=abs(xs-x[i])+abs(ys-y[i]);
    if time>t then continue;//超时就换第一个节点
    inc(anss);
    for j:=i-1 downto 0 do//先向前采
    begin
      time:=time+abs(x[j+1]-x[j])+abs(y[j+1]-y[j]);
      if time>t then break;
      inc(anss);
    end;
    time:=time+x[i+1]-x[0]+y[i+1]-y[0];
    if time>t then continue;
    inc(anss);
    for j:=i+1 to n-1 do//再向后采
    begin
      time:=time+abs(x[j+1]-x[j])+abs(y[j+1]-y[j]);
      if time>t then break;
      inc(anss);
    end;
  end;
  if anss>ans then ans:=anss;//取最大值
  writeln(ans);
end.

```
# 后记
题目不难，代码量也不大，但一定要仔细小心，避免$fst$之后掉到$2000+$然后下青的命运

$\mid\color{red}\text{诚实面对自己的悲伤，与事与愿违的世界释怀}$