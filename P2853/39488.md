这道题目可以深搜也可以用最短路做

我选了搜索后觉得直接暴搜的话会超时的（可能是我不会算时间复杂度吧）

可以用tarjan缩点来优化搜索速度（至少我评测的时候比暴搜快一点）

我还有一个地方没有搞懂，就是tarjan缩点后形成的应该是有向无环图
那么在dfs的时候应该不会访问到之前的节点，相应的也不需要标记这个节点有没有被访问过

可是就是这个点调了我好久，还是要标记的，有哪位大佬可以为我解释一下吗？

献上我丑陋的代码
```
var
  que,top,tot,t,len,a1,a2,q,i,j,m,n,k,p:longint;
  place,new,nextn,othern,w,color,f,dfn,low,where,next,
  ans,a,other,check:array[0..20005]of longint;
  flag:array[0..20005]of boolean;
function min(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
procedure tarjan(u:longint);
var
  k,v,i,j:longint;
begin
  inc(top); f[top]:=u;//tarjan处理用的栈
  check[u]:=1;
  inc(t); dfn[u]:=t; low[u]:=t;
  k:=a[u];
  while k<>0 do
    begin
      v:=other[k];
      if check[v]=0 then
        begin
          tarjan(v);
          low[u]:=min(low[u],low[v]);
        end
      else
        if check[v]=1 then
          low[u]:=min(low[u],dfn[v]);
      k:=next[k];
    end;
  if dfn[u]=low[u] then
    begin
      inc(tot);
      repeat
        j:=f[top]; dec(top);
        check[j]:=-1;
        color[j]:=tot;
      until j=u;
    end;
end;
procedure dfs(now,a1:longint);//就是这里，为什么要标记呢？
var
  k,p:longint;
begin
  ans[now]:=ans[now]+a1;
  k:=new[now]; flag[now]:=false;
  while k<>0 do
    begin
      p:=othern[k];
      if flag[p]=true then
        dfs(p,a1);
      k:=nextn[k];
    end;
end;
begin
  readln(q,n,m);
  for i:=1 to q do
    begin
      readln(a1);
      inc(where[a1]);//记录这个点上有几只牛
    end;
  for i:=1 to m do
    begin
      readln(a1,a2);
      inc(len);
      next[len]:=a[a1]; a[a1]:=len; other[len]:=a2;//前向星的三个数组
    end;
  for i:=1 to n do
    if check[i]=0 then
      tarjan(i);
  len:=0;
  for i:=1 to n do//tarjan后第二次建图
    begin
      w[color[i]]:=w[color[i]]+where[i];//记录这个新点有几头牛
      inc(place[color[i]]);//记录这个新点包括着几个点（后面累加用的）
      k:=a[i];
      while k<>0 do
        begin
          p:=other[k];
          if color[i]<>color[p] then
            begin
              a1:=color[i]; a2:=color[p];
              inc(len);
              nextn[len]:=new[a1]; new[a1]:=len; othern[len]:=a2;
            end;
          k:=next[k];
        end;
    end;
  {for i:=1 to tot do
    writeln(w[i],' ',place[i]);}
  for i:=1 to tot do
    if w[i]>0 then
      begin
        fillchar(flag,sizeof(flag),true);
        dfs(i,w[i]);
      end;
  for i:=1 to tot do
    if ans[i]=q then
      que:=que+place[i];
  writeln(que);
end.
```