代码：pascal

算法：BFS

```cpp
const
    ax:array[1..8] of longint=(-1,0,1,0,-1,1,1,-1);
    ay:array[1..8] of longint=(0,1,0,-1,1,-1,1,-1);
var
    f:array[-10..110,-10..110] of boolean;
    a:array[0..50000,1..2] of longint;
    n,m,i,j,s:longint;
    ch:char;
procedure search(x,y:longint);
var
    head,tail,i,x1,y1:longint;
begin
    inc(s);//湖的数量+1
    head:=0; tail:=1;//每次都从a[1,1],a[1,2]开始可以省空间
    f[x,y]:=false; a[1,1]:=x; a[1,2]:=y;//必须把f[x,y]置为false,以防重复
    while head<=tail do
    begin
        inc(head);//扩展第head个结点
        for i:=1 to 8 do//搜索8个方向
        begin
            x1:=a[head,1]+ax[i];
            y1:=a[head,2]+ay[i];
            if f[x1,y1] then//由于f数组是-10..110,-10..110,所以不用判断是否越界
            begin
                inc(tail);//存储扩展出的结点
                a[tail,1]:=x1;
                a[tail,2]:=y1;
                f[x1,y1]:=false;
            end;
        end;
    end;
end;
begin
    readln(n,m);
    for i:=1 to n do
    begin
        for j:=1 to m do
        begin
            read(ch);
            f[i,j]:=ch='W';//使用布尔型变量
        end;
        readln;//这个很重要，不然ch中会存入回车符
    end;
    s:=0;//总数
    for i:=1 to n do
        for j:=1 to m do
            if f[i,j] then search(i,j);//如果这个位置属于新的一个湖
    writeln(s);
end.
```