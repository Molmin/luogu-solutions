先建图，然后dfs


不过我加了**记忆化**进行优化：如果目标点已经被搜索过，并且我们并没有到达上表面，则我们就不再访问目标点


（其实用bfs更好，但是我bfs容易写错，dfs是求稳的）


思路见注释



```cpp
type int=longint;
     boo=boolean;
var i,j,k,n,t:int;
    h,r:int64;
    xj,yj,zj,xk,yk,zk:qword; //因为进过计算，x、y、z取极限值计算是**1.2*10^20>2^63**，会超过**int64**范围，所以用**qword**计算防止溢出
    first,x,y,z:array[0..1001] of int;
    map:array[0..1001,0..1001] of int;
    visit,aim:array[0..1001] of boo;
    bo:boo;
procedure dfs(now:int);
var i:int;
begin
  if aim[now] then begin bo:=true; exit; end; //如果当前点与上表面相通，则退出
  for i:=1 to map[now,0] do
  begin
    if not visit[map[now,i]] then
    begin
      visit[map[now,i]]:=true; //如果目标点并没有被访问过
      dfs(map[now,i]); 
      if bo then exit; //如果已经成功到达上表面则退出
    end;
  end;
end;
begin
  readln(t);
  for i:=1 to t do
  begin
    fillchar(aim,sizeof(aim),false); //aim[x]表示x号点能否到达上表面
    fillchar(first,sizeof(first),0); //first记录起点编号
    fillchar(map,sizeof(map),0); //map用邻接表储存
    fillchar(visit,sizeof(visit),false); //visit表示这个节点是否搜索过，用于记忆化优化
    readln(n,h,r);
    for j:=1 to n do
    begin
      readln(x[j],y[j],z[j]);
      xj:=x[j]; yj:=y[j]; zj:=z[j];
      if zj<=r then begin inc(first[0]); first[first[0]]:=j; end; //如果该点和下表面相通，则记录下来
      if zj+r>=h then aim[j]:=true; //如果该点和上表面相通，则记录下来
      for k:=1 to j-1 do //穷举之前所有的点与这个点是否相联通
      begin
        xk:=x[k]; yk:=y[k]; zk:=z[k];
        if (xj-xk)*(xj-xk)+(yj-yk)*(yj-yk)+(zj-zk)*(zj-zk)<=4*r*r then //如果两个点相通
        begin
          inc(map[j,0]); map[j,map[j,0]]:=k;
          inc(map[k,0]); map[k,map[k,0]]:=j; //邻接表记录
        end;
      end;
    end;
    bo:=false; //bo用来判断是否已经到达上表面
    for j:=1 to first[0] do dfs(first[j]); //从所有可以的起点出发，进行dfs
    if bo then writeln('Yes') else writeln('No'); 
  end;
end.
```