可以发现，两个数为k倍关系是不能被纳入集合，而且一个数只会与k倍和1/k倍发生关系，其余互不影响。

从大到小排序（从小到大可能\*k的时候会炸，只有80），f记录能否被取入，false表示暂时可以。

一个一个枚举，一个数找到后，寻找有无1/k倍的数，如果有，继续找直到没有了。

由于n比较大，须要二分查找。

这样会形成一条长链，其中最多可以取(l+1)/2个（整除），即不能取l/2个，这部分扔掉，找过的f赋为true。

```cpp
var
  n,k,i,ans,num:longint;
  m,s:int64;
  a,t:array[-1..100002] of int64;
  f:array[-1..100002] of boolean;
procedure q(l,r:longint);
var
  i,j,m,k:longint;
begin
  if l=r then exit;
  m:=(l+r) div 2;
  q(l,m);
  q(m+1,r);
  i:=l;
  j:=m+1;
  k:=l;
  while (i<=m) and (j<=r) do
    if a[i]>=a[j] then begin t[k]:=a[i]; inc(i); inc(k); end else begin t[k]:=a[j]; inc(j); inc(k); end;
  while i<=m do
    begin t[k]:=a[i]; inc(i); inc(k); end;
  while j<=r do
    begin t[k]:=a[j]; inc(j); inc(k); end;
  for i:=l to r do
    begin
      a[i]:=t[i];
    end;
end;
function find(x:int64):longint;
var
  l,r,m:longint;
begin
  l:=1;
  r:=n;
  while l<r do
    begin
      m:=(l+r) div 2;
      if a[m]>x then l:=m+1 else r:=m;
    end;
  exit(l);
end;
begin
  readln(n,k);
  for i:=1 to n do read(a[i]);
  q(1,n);//大到小
  ans:=n;//答案，鄙人用的是减法，加法也可。
  for i:=1 to n do if not f[i] then//找到没有被排除的
    begin
      f[i]:=true;
      s:=a[i];
      num:=1;//这个数列的总个数
      while (a[find(s div k)]=s div k) and (s mod k=0) do//首先要能整除，然后再保证有这个数
        begin
          s:=s div k;
          f[find(s)]:=true;//排除
          inc(num);//+1
        end;
      ans:=ans-num div 2;//如果长度为偶数可取一半，奇数可取头尾，故+1后一半，即不可取一半
    end;
  write(ans);
end.
```