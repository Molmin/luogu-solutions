可能有dalao发现我和下面的某位dalao比较相似（没错，偷了思路），不过没关系啊，我讲的更明白一点行吧0^0

先说下思想，这题就是搜索回溯，那么难点在哪里呢，一个单词能弄两次以及部分重叠（就是要多少重叠的字母才好）的问题

我们该怎么简化一点这个问题呢？

一、用一个数组来动态地存当前这几个单词使用的次数；

二、用一个二维数组f[i,j]来存第i个单词到第j个单词最多能延长多少；（这里到时候运算te方便）

搞清楚这两个之后就可以很轻松地dfs了

（一开始我为什么要想那么久（= =；）——》没看懂题目，以为可以选上个单词的一段接这个单词的一段【这怎么做。。。】，其实要连就要整个单词连上去，不能截取，不懂题目的同学看了我这句laosao或许就知道怎么做了）

下面是我的code：

```cpp
program words;
var
      a:array[1..20]of string;//存初始单词
      f:array[1..20,1..20]of longint;//f[i,j]记录第i个单词到第j个单词最多能延长多少
      e:array[1..20]of longint;//存使用次数
      n,tot,ans,i,j:longint;//tot用来存每次dfs完毕后的答案，ans是真 . 答案
      head:char;
function min(x,y:longint):longint;
begin
       if x>y then exit(y) else exit(x);
end;
procedure add(x,y:longint);//记录延长长度的具体实现
var
    i,j,leno,lent,sum:longint;
begin
      leno:=length(a[x]);
      lent:=length(a[y]);
      sum:=maxlongint;//这个就是用来记录最大延长长度的家伙
      for j:=leno downto 1 do//从后往前数方便操作
      if a[x][j]=a[y][1] then//第x个单词的第j个和第y个单词的第1个首先要符合
      if copy(a[x],j,leno-j+1)=copy(a[y],1,leno-j+1)then//copy(?,i,l)函数为截取?中第i位的接下来长度为l的一段，于是这里为比较一段
      if leno-j+1<>min(leno,lent) then//根据题目中不能存在包含关系而生
      begin
            sum:=min(sum,leno-j+1);//正式记录啦
            break;//优化（精髓），后面不可能还有比这个还要长的了，只会越来越短，自己体会一下
      end;
      if sum<>maxlongint then f[x,y]:=lent-sum;//不解释
end;
procedure dfs(x:longint);//深搜实现
var
      i:longint;
begin
      for i:=1 to n do
      begin
            if e[i]<>-2 then//还没有被用够两次
            if f[x,i]<>0 then//不是做无用功
            begin
                  inc(tot,f[x,i]);//加入当前的长度
                  dec(e[i]);//减少一次这个的使用次数
                  dfs(i);
                  inc(e[i]);//以下两行回溯的实现
                  dec(tot,f[x,i]);
            end;
      end;
      if tot>ans then ans:=tot;//当上面都不执行的时候，说明第?次深搜结束，可以记录结果了
end;
begin
      //assign(input,'word.in'); reset(input);//请自动忽略一下文件的无关文字
      readln(n);
      for i:=1 to n do
      readln(a[i]);
      for i:=1 to n do
      for j:=1 to n do
      begin
             add(i,j);//没有特殊情况，要包括自己连自己
      end;
      readln(head);
      for i:=1 to n do
      begin
            fillchar(e,sizeof(e),0);//不要忘了每次更新一次
            if a[i][1]=head then//找龙头
            begin
                  dec(e[i]);//减少一次使用次数
                  tot:=length(a[i]);//先把龙头单词长度记录了
                  dfs(i);//深搜
            end;
      end;
      write(ans);//输出0~0
      //close(input);
end.

```