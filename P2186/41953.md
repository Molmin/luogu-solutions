本蒟蒻第一道切掉的提高+/省选-的题目，值得纪念……

前前后后提交了13次，果然我还是太蒻了……

看到没有P题解就来发一篇……

一道~~zz工业码农~~模拟题，实现的其实只是栈的基本操作，但是判断合法真的很恶心……

主要判断有这几种：

1.退栈操作时栈为空。

2.二元操作时栈内不足二元。

3.一元操作时栈内不足一元。

4.进行入栈操作时入栈的数大于1e9。

5.除以0或模0

6.所有操作都运行完时，栈内剩余元素数量大于一。

下面就贴代码了，别怕，不超过200行的！

~~大部分函数代码是可以反复利用的~~

```pascal
type Stack=record
         x:array[1..5000]of int64;
         top:int64;
end;//P党需要手写一个栈
var p:Stack;
s,s1:string;l,x,n,i:longint;
m:array[1..5000]of string;q:array[1..5000]of int64;flag:boolean;

procedure print(k:Stack);
begin
    if (k.top=2) then writeln(k.x[k.top-1])
    else writeln('ERROR');//元素数量判定（6）
end;

function push(var k:Stack;num:int64):boolean;
begin
    if(abs(num)>1000000000) then exit(false);
    k.x[k.top]:=num;
    inc(k.top);
    exit(true);
end;

function pop(var k:Stack):boolean;
begin
    if (k.top=1) then exit(false);//退栈判定(1)
    dec(k.top);
    exit(true);
end;

function inv(var k:Stack):boolean;
begin
    if (k.top=1) then exit(false);
    k.x[k.top-1]:=-k.x[k.top-1];
    exit(true);
end;

function dup(var k:Stack):boolean;
begin
    if (k.top<2) then exit(false);
    k.x[k.top]:=k.x[k.top-1];
    inc(k.top);
    exit(true);
end;

function swp(var k:Stack):boolean;
var t1,t2:int64;
begin
    if(k.top<=2) then exit(false);
    t1:=k.x[k.top-1];
    t2:=k.x[k.top-2];
    pop(k);pop(k);
    push(k,t1);push(k,t2);
    exit(true);
end;

function add(var k:Stack):boolean;
var t:int64;
begin
    if (k.top<=2) then exit(false);
    t:=k.x[k.top-1]+k.x[k.top-2];
    if (abs(t)>1000000000) then exit(false);
    pop(k);pop(k);
    k.x[k.top]:=t;
    inc(k.top);
    exit(true);
end;

function sub(var k:Stack):boolean;
var t:int64;
begin
    if (k.top<=2) then exit(false);
    t:=k.x[k.top-2]-k.x[k.top-1];
    if (abs(t)>1000000000) then exit(false);
    pop(k);pop(k);
    k.x[k.top]:=t;
    inc(k.top);
    exit(true);
end;

function mul(var k:Stack):boolean;
var t:int64;
begin
    if (k.top<=2) then exit(false);
    t:=k.x[k.top-2]*k.x[k.top-1];
    if (abs(t)>1000000000) then exit(false);
    pop(k);pop(k);
    k.x[k.top]:=t;
    inc(k.top);
    exit(true);
end;


function chu(var k:Stack):boolean;
var t:int64;
begin
    if (k.top<=2) then exit(false);
    if (k.x[k.top-1]=0) then exit(false);
    t:=k.x[k.top-2] div k.x[k.top-1];
    if (abs(t)>1000000000) then exit(false);
    pop(k);pop(k);
    k.x[k.top]:=t;
    inc(k.top);
    exit(true);
end;

function mu(var k:Stack):boolean;
var t:int64;
begin
    if (k.top<=2) then exit(false);
    if (k.x[k.top-1]=0) then exit(false);
    t:=k.x[k.top-2] mod k.x[k.top-1];
    if (abs(t)>1000000000) then exit(false);
    pop(k);pop(k);
    k.x[k.top]:=t;
    inc(k.top);
    exit(true);
end;
begin
    i:=0;
    repeat
        inc(i);
        readln(m[i]);
    until m[i]='END';
    readln(n);
    for i:=1 to n do readln(q[i]);
    for l:=1 to n do
    begin
        flag:=true;
        p.top:=2;
        p.x[p.top-1]:=q[l];
        s:=m[1];
        i:=1;
        while(s<>'END') do
        begin
            s1:=copy(s,1,3);
            delete(s,1,4);
            if(s1='NUM') then
            begin
                val(s,x);
                if not push(p,x) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='POP') then
            begin
                if not pop(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='INV') then
            begin
                if not inv(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='DUP') then
            begin
                if not dup(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='ADD') then
            begin
                if not add(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='SUB') then
            begin
                if not sub(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='MUL') then
            begin
                if not mul(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='DIV') then
            begin
                if not chu(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='MOD') then
            begin
                if not mu(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            if(s1='SWP') then
            begin
                if not swp(p) then begin writeln('ERROR');flag:=false;break; end;
            end;
            inc(i);
            s:=m[i];
        end;
        if (flag) then print(p);
    end;
end.
```
写模拟才是真正的锻炼心性啊……
