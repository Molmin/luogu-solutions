当看到这道题时，很快速的就想到二分答案。

事实上，对于**‘最小中求最大’**和**‘最大中求最小’**这样的问题，**基本上都是二分答案**！

之后要思考的就是，如何对二分到的每个mid进行检查：

1.显然很难直接找出第m小赛道的长度是否为mid。所以，我们要查找的是，路径**长度大于或等于mid**的赛道个数是否有m条。

2.题目中给出的图是一颗**无根树**，所以可以用树形DP的方法进行处理，首先要确定叶节点和根节点，由于这题取任何节点为根都没有影响，所以我们就**取1号节点为根**就行。

3.题目中的这句话“一条赛道是一组**互不相同的道路** ，满足可以从某个路口出发，依次经过这些道路（**每条道路经过一次，不允许调头**）到达另一个路口。”给了我们第一个提示：**对于一个节点，与和它相连的子节点之间，最多只能选取两条边组成赛道。**因为如果边数超过2，很明显是无法在不掉头的情况下一次性走完这条赛道的。（比如下图）

![](https://cdn.luogu.com.cn/upload/pic/43800.png )

4.题目中的这句话“要求每条道路**至多**被一条赛道经过”给了我们第二个提示：**对于一个节点，与和它相连的子节点未被选取的边中，最多只有一条可以对之后的答案做出贡献**，我们只要把这条边的的长度向上传递就好。因为该节点与它父节点之间只有一条边，只可以建立一条赛道。（比如下图）

![](https://cdn.luogu.com.cn/upload/pic/43801.png)

5.基于 3 和 4 的特点，我们可以得出具体的处理方法：对于整棵树从根开始DFS，从**最底层**开始，先判断可以选取哪些边组成长度>=mid的赛道，每组成一条就将check加1（处理3），等无法组成赛道时，就将未选取的边中最长的一条向上传递（处理4），等整棵树处理完成后比较组成的赛道个数是否>=m（即check是否>=m),并二分更新答案（处理1）。

今年比赛几乎没有人用Pascal了，题解中也没有Pascal的文章，看样子Pascal是真的凉了......

~~不过我还是忠于组织的！！！~~

Pascal程序
```pas
program project1;
var
   rp,up,dis,p:array[0..50005]of longint;
   lp,v,w:array[0..100005]of longint;
   n,m,i,mid,x,y,z,t,ll,rr,check:longint;

procedure qsort(l,r:longint);   //普通的快排，当然C++可以直接用STL
var i,j,mid,t:longint;
begin
  i:=l;
  j:=r;
  mid:=dis[(i+j) div 2];
  repeat
    while dis[i]<mid do inc(i);
    while dis[j]>mid do dec(j);
    if i<=j then begin
      t:=dis[i];
      dis[i]:=dis[j];
      dis[j]:=t;
      inc(i);
      dec(j);
    end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;

procedure sc(f,fa:longint);
var i,k,l,r,s:longint;
begin
  k:=0;
  s:=0;
  i:=rp[f];
  while i<>0 do begin
    if v[i]<>fa then sc(v[i],f);
    i:=lp[i];
  end;
  i:=rp[f];
  while i<>0 do begin
    if v[i]<>fa then begin
      inc(k);
      dis[k]:=w[i]+up[v[i]];
    end;
    i:=lp[i];
  end;
  qsort(1,k);
  l:=1;
  r:=k;
  while dis[r]>=mid do dec(r);
  inc(check,k-r);
  while l<=r do begin                //这部分是最主要的处理，也是本题代码的核心
    while (dis[l]+dis[r]>=mid) and (l<r) do begin
      inc(s);
      p[s]:=dis[r];
      dec(r);
    end;
    if s<>0 then begin
      dec(s);
      inc(check);
    end else up[f]:=dis[l];
    inc(l);
  end;
  inc(check,s div 2);       
  if s mod 2<>0 then up[f]:=p[1];
end;

begin
  read(n,m);
  ll:=0;
  rr:=0;
  for i:=1 to n-1 do begin
    t:=2*i;
    read(x,y,z);
    lp[t]:=rp[x];
    rp[x]:=t;
    v[t]:=y;
    w[t]:=z;
    lp[t-1]:=rp[y];
    rp[y]:=t-1;
    v[t-1]:=x;
    w[t-1]:=z;
    inc(rr,z);
  end;
  while ll<=rr do begin     //标准二分模板，初始下界为0，初始上界为树的直径（我这里用的是所有边长度之和，用树的直径会更优）
    fillchar(up,sizeof(up),0);
    mid:=(ll+rr) shr 1;
    check:=0;
    sc(1,0);     
    if check>=m then ll:=mid+1
      else rr:=mid-1;
  end;
  writeln(ll-1);
end.

```