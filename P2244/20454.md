编号为 $x$ 的候选人怎样才能获得胜利呢.?

最简单的方法, 先让其他人比赛, 只留下1位编号 $y$ 的候选人. 而 $y$ 不能保证打败 $x$, 那么 $x$ 就可以获胜了.

如果留下的 $y$ 无论是谁都是能打败 $x$ 的,那么 $x$ 必败.

也就是说, 对于必定能打败 $x$ 的候选人集合 $S$, 和所有 $x$ 可能打败的候选人集合 $T$, 若对任意的 $i∈S,j∈T$ 都有 $i$ 能打过 $j$ , 在此情况下 $x$ 是1个必败的状态. 同时也是 $T$ 中所有人的必败状态.

否则只要找到集合 $S$ 中的候选人 $S_x$ , 集合 $T$ 中能打过 $S_x$ 的候选人 $T_y$ , 让 $S_x$ 先成为集合 $S$ 的赢家, 让 $T_y$ 打败 $S_x$ , 最后让 $x$ 打败集合 $T$ 即可让 $x$ 获胜.

问题来了, $S_x$ 不1定能成为集合 $S$ 的赢家, 也就是 $S_x$ 打不过集合 $S$ 中的其他人. 此时集合 $T$ 若同样打不过这些人, 则 $S_x$ 与 $x$ , 集合 $T$ 中的人均必败, 也是1个必败状态. 将 $S_x$ 从集合 $S$ 中删除, 新的集合 $S'$ 也能打败其他的所有人.

可以发现, 无论是怎样的必败状态, 都有1个胜者候选人集合 $S$, 和1个败者候选人集合 $T$, 满足对任意的 $i∈S,j∈T$ 都有 $i$ 能打过 $j$ . 所以每个可以胜利的候选人能打败的人的集合里必定包括必败的候选人.

由于能打败最多的人1定是可以胜利的(其他题解有详细的解释), 只要将候选人依据打败的人数降序排序, 然后不断将候选人加入可以胜利的集合 $S$ 中, 并统计集合 $S$ 中能打败不在集合 $S$ 的人数的总数 $sum$. 当满足  $|S| \times (n-|S|) = sum$ 时, 可以获胜的候选人与必败的候选人就全部确定下来了.

时间复杂度 $O(n \times log(n)+m)$, 比BFS慢但更简单.

代码.

```cpp
program P2244;
 uses Garrayutils;
 const
  sqrnd=1008208820;
  nmax=1000000;
 type
  wb=record
   n:^wb;
   t:longint;
  end;
  pwb=^wb;
  we=record
   i,d:longint;
  end;
  wa=array of we;
  cmp=object
   public
    function c(x,y:we):boolean;inline;static;
   end;
  sort=specialize Torderingarrayutils<wa,we,cmp>;
 var
  a:wa;
  b:array[0..2*nmax+1] of wb;
  h,d:array[0..nmax+1] of longint;
  i,j,k,p,n,x:longint;
  put,ti:longint;
  tj:pwb;
 procedure hahainc(x,y:longint);
  begin
   inc(put);
   b[put].n:=@b[h[x]];
   b[put].t:=y;
   h[x]:=put;
  end;
 function cmp.c(x,y:we):boolean;inline;static;
  begin
   exit(x.d>y.d);
  end;
 begin
  readln(n);
  setlength(a,n);
  filldword(h,sizeof(h)>>2,0);
  for i:=0 to n-1 do
   begin
    read(a[i].d);
    a[i].i:=i+1;
    for j:=1 to a[i].d do
     begin
      read(k);
      hahainc(i+1,k);
      hahainc(k,i+1);  //加了双向边,用于后面判断用.
     end;
    readln;
   end;
  sort.sort(a,n);      //按照打败的人数排序.
  filldword(d,sizeof(d)>>2,0);
  x:=0;
  for i:=0 to n-1 do
   begin
    d[a[i].i]:=1;
    inc(x,a[i].d);      //x即为统计sum, 先将人数累加.
    tj:=@b[h[a[i].i]];
    while tj<>@b[0] do  //遍历所有胜负关系.
     begin
      ti:=tj^.t;
      if d[ti]=1 then dec(x);  //如果能打败的是集合S中的人, 或者被集合S中的人打败(双向边), 则将人数减少.
      tj:=tj^.n;
     end;
    if x=(i+1)*(n-i-1) then break;  //满足条件即可退出.
   end;
  p:=0;
  for i:=1 to n do
   if d[i]=1 then
    begin
     inc(p);
     d[p]:=i;
    end;
  write(p,' ');
  for i:=1 to p-1 do
   write(d[i],' ');
  writeln(d[p]);
 end.
```
