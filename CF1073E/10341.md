居然没看题解干掉了一道这么难的**数位DP**，好开森啊~~（大佬莫鄙视，毕竟我是蒟蒻）~~。

# 状态：

dp[i][j][k][l][o]表示当前取到第i位，取到的数字集合为j（即状压），有k个不同的数字（其实没用，包含在j里了，但是为了方便，多定义一维），l表示是否有前导0（见注①），o表示当前数字是否可以任意取（数位DP常规操作，这里也定义在状态里了，true表示可以任意取）的数的和。

c[i][j][k][l][o]状态与dp数组相同，只不过表示的是方案数而不是数的和。

注：

①这道题需要判前导0，前导0不算在数字中，我用了一个zero来记录当前数字是否还在前导0部分，如果在前导0部分，zero为false。

# 转移：

两大种情况：

[1]当前还在前导0部分，即zero=false

[2]当前不在前导0部分，即zero=true

## [1]分为两种情况：

### ①当前数字仍为0：

```cpp
dfs(w-1,s,id,false,f or (i<>q[w]));
```

zero仍为false

### ②当前数字不为0

```cpp
dfs(w-1,1 << i,1,true,f or (i<>q[w]));
```

zero变成true，状态变为1 << i，同时出现了一个数

## [2]也是两种情况：

### ①当前加入的数字已经在原数中了：

```cpp
dfs(w-1,s,id,zero,f or (i<>q[w]));
```

s,id都不更新，因为已经存在了。

### ②当前加入的数字不在原数中：

```cpp
dfs(w-1,s or (1 << i),id+1,zero,f or (i<>q[w]));
```

同样，运用状压的思想。

统计答案不是简单的加起来，而且要加上这一位的贡献，即下一种转移有多少种情况当前数位所表达的值，以[1]②为例：

```cpp
res:=(res+ten[w]*i mod p*c[w-1][1 << i][1][true][f or (i<>q[w])] mod p+dp[w-1][1 << i][1][true][f or (i<>q[w])]) mod p;
```

ten[w]*i即当前数位所表达的值。

因为下一种转移有c[w-1][1 << i][1][true][f or (i<>q[w])]种情况，所以要乘以c[w-1][1 << i][1][true][f or (i<>q[w])]次，可以想象，这些值与dp[w-1][1 << i][1][true][f or (i<>q[w])]结合起来便组成了所有数。

c数组不断累计更新。

细节不再多说,~~我数组开小了调了1hQAQ~~。

Pascal Code：

```cpp
const
  p:int64=998244353;
var
  c:array[0..20,0..1024,0..10,false..true,false..true]of int64;
  dp:array[0..20,0..1024,0..10,false..true,false..true]of int64;
  ten:array[0..55]of int64;
  q:array[0..55]of longint;
  top,k,i:longint;
  ans,l,r:int64;
procedure dfs(w,s,id:longint; zero,f:boolean);//记忆化搜索
var
  i,sl:longint;
  res:int64;
begin
  if id>k then//不满足题目要求，退出
  begin
    c[w][s][id][zero][f]:=0;
    dp[w][s][id][zero][f]:=0;
    exit;
  end;
  if w=0 then//到底了
  begin
    c[w][s][id][zero][f]:=1;
    dp[w][s][id][zero][f]:=0;
    exit;
  end;
  if dp[w][s][id][zero][f]<>-1 then//已经处理过了
    exit;
  if f then//不能超过原数
    sl:=9 else
    sl:=q[w];
  c[w][s][id][zero][f]:=0;
  res:=0;
  for i:=0 to sl do//注意不断取模，否则WA飞。。。
  begin
    if not zero then
    begin
      if i=0 then
      begin
        dfs(w-1,s,id,false,f or (i<>q[w]));
        res:=(res+dp[w-1][s][id][false][f or (i<>q[w])]) mod p;
        c[w][s][id][zero][f]:=(c[w][s][id][zero][f]+c[w-1][s][id][false][f or (i<>q[w])]) mod p;
      end else
      begin
        dfs(w-1,1 << i,1,true,f or (i<>q[w]));
        res:=(res+ten[w]*i mod p*c[w-1][1 << i][1][true][f or (i<>q[w])]+dp[w-1][1 << i][1][true][f or (i<>q[w])]) mod p;
        c[w][s][id][zero][f]:=(c[w][s][id][zero][f]+c[w-1][1 << i][1][true][f or (i<>q[w])]) mod p;
      end;
    end else
    begin
      if s and (1 << i)<>0 then
      begin	    
        dfs(w-1,s,id,zero,f or (i<>q[w]));
        res:=(res+ten[w]*i mod p*c[w-1][s][id][zero][f or (i<>q[w])]+dp[w-1][s][id][zero][f or (i<>q[w])]) mod p;
        c[w][s][id][zero][f]:=(c[w][s][id][zero][f]+c[w-1][s][id][zero][f or (i<>q[w])]) mod p;
      end else
      begin
        dfs(w-1,s or (1 << i),id+1,zero,f or (i<>q[w]));
        res:=(res+ten[w]*i mod p*c[w-1][s or (1 << i)][id+1][zero][f or (i<>q[w])]+dp[w-1][s or (1 << i)][id+1][zero][f or (i<>q[w])]) mod p;
        c[w][s][id][zero][f]:=(c[w][s][id][zero][f]+c[w-1][s or (1 << i)][id+1][zero][f or (i<>q[w])]) mod p;
      end;
    end;
  end;
  dp[w][s][id][zero][f]:=res;
end;
function check(x:int64):int64;
var
  i,j,k:longint;
  l,o:boolean;
begin
  if x=0 then
    exit(0);
  top:=0;
  while x<>0 do
  begin
    inc(top);
    q[top]:=x mod 10;
    x:=x div 10;
  end;
  for i:=0 to 20 do//初始化
    for j:=0 to 1024 do
      for k:=0 to 10 do
        for l:=false to true do
          for o:=false to true do
          begin
            dp[i][j][k][l][o]:=-1;
            c[i][j][k][l][o]:=0;
          end;
  dfs(top,0,0,false,false);
  exit(dp[top][0][0][false][false]);
end;
begin
  readln(l,r,k);
  ten[1]:=1;
  for i:=2 to 20 do
    ten[i]:=ten[i-1]*10 mod p;//预处理10的幂次
  ans:=check(r)-check(l-1);
  ans:=(ans mod p+p) mod p;//可能减出来是负的
  writeln(ans);
end.
```

最后再推荐两道CF的数位DP：

[CF628D Magic Numbers](https://www.luogu.org/problemnew/show/CF628D)

[CF55D Beautiful numbers](https://www.luogu.org/problemnew/show/CF55D)