楼下的神犇讲的很好，补充一点，以每行/每列连通块为点，边实质上就是其交点。我们在找最大匹配时，选取的二分图里的边其实就是对应原图的点，那么显然交点是软石头的时候是不能连边的(不能放炮)，献上丑陋的pascal代码。






```cpp
const maxn=2500;
type link=^node;
     node=record
       data:longint;
       next:link;
     end;
var i,j,k,l,m,n,ans,totx,toty:longint;
    maps,pic:array[1..50,1..50]of longint;
    y:array[1..maxn]of longint;
    v:array[1..maxn]of boolean;
    a:array[1..maxn]of link;
procedure adds(x,y:longint);
var s:link;
begin
    new(s);s^.data:=y;s^.next:=a[x];
    a[x]:=s;
end;
procedure init;
var i,j,k:longint;
    ch:char;
begin
    fillchar(maps,sizeof(maps),0);
    fillchar(pic,sizeof(pic),0);
    fillchar(y,sizeof(y),0);
    totx:=0;toty:=0;
    readln(n,m);
    for i:=1 to n do
    begin
      for j:=1 to m do
      begin
        read(ch);
        if ch='#' then maps[i,j]:=-1;
        if ch='x' then pic[i,j]:=1;
      end;
      readln;
    end;
end;
procedure previous;
var i,j,k,head,tail:longint;
begin
    for i:=1 to m do
    begin
      head:=1;tail:=1;
      while tail<=n do
        if(maps[tail,i]>=0)then
        begin
          while(maps[tail,i]>=0)and(tail<=n)do inc(tail);
          inc(totx);for j:=head to tail-1 do maps[j,i]:=totx;
          head:=tail;
        end
        else
        begin
          inc(head);
          inc(tail);
        end;
    end;
    for i:=1 to n do
    begin
      head:=1;tail:=1;
      while tail<=m do
        if(maps[i,tail]>=0)then
        begin
          while(maps[i,tail]>=0)and(tail<=m)do inc(tail);
          inc(toty);
          for j:=head to tail-1 do
            if(pic[i,j]=0)then adds(maps[i,j],toty);
          head:=tail;
        end
        else
        begin
          inc(head);
          inc(tail);
        end;
    end;
end;
function find(i:longint):boolean;
var s:link;
begin
    s:=a[i];
    while s<>nil do
    begin
      if(v[s^.data])then
      begin
        v[s^.data]:=false;
        if(y[s^.data]=0)or(find(y[s^.data]))then
        begin
          y[s^.data]:=i;
          exit(true);
        end;
      end;
      s:=s^.next;
    end;
    exit(false);
end;
procedure work;
var i,j:longint;
begin
    previous;ans:=0;
    for i:=1 to totx do
    begin
      fillchar(v,sizeof(v),true);
      if(find(i))then inc(ans);
    end;
    writeln(ans);
end;
begin
    init;
    work;
end.

```