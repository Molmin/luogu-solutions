看到没有pascal，就来一波吧~

其实这题就是来练习最短路的

p党想看最短路板子朝这里看哦~

跑一遍最短路，统计一下答案就行了

所以刚开始为了方便，打了个裸的dijkstra

P2951.pas
------------
```pascal
var
        n,m,i,j,tot,v,minn,head,x,y,ans,bns,cns:longint;
        he,ne,t,w,dis:array[0..1000000] of longint;
        p:array[0..1000000] of boolean;
procedure link(x,y:longint);
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=1;
end;
begin
        readln(n,m);
        for i:=1 to m do begin
                read(x,y);
                link(x,y);
                link(y,x);
        end;
        for i:=2 to n do dis[i]:=maxlongint;
        head:=1;
        while not p[head] do begin
                p[head]:=true;
                v:=he[head];
                while v>0 do begin
                        if (not p[t[v]]) and (dis[t[v]]>dis[head]+w[v]) then
                         dis[t[v]]:=dis[head]+w[v];
                        v:=ne[v];
                end;
                minn:=maxlongint;
                for i:=1 to n do if (not p[i]) and (minn>dis[i]) then begin
                        minn:=dis[i];
                        head:=i;
                end;
        end;
        for i:=n downto 1 do begin
                if ans<=dis[i] then begin
                        ans:=dis[i];
                        bns:=i;
                end;
        end;
        for i:=1 to n do if dis[i]=ans then inc(cns);
        write(bns,' ',ans,' ',cns);
end.
```
结果t了两个点

所以我就老实的打了个堆优化的dijkstra上去

P2951（1）.pas
```pascal
var
        n,m,s,i,tot,bj,v,x,y,z,head:longint;
        ans,bns,cns:int64;
        he,ne,t,w,cj,num:array[0..4000010] of longint;
        dis,heap:array[0..4000010] of int64;
function max(x,y:int64):int64;
begin
        if x>y then exit(x) else exit(y);
end;
procedure swap(var x,y:int64);
var
        t:int64;
begin
        t:=x;
        x:=y;
        y:=t;
end;
procedure swap1(var x,y:longint);
var
        t:longint;
begin
        t:=x;
        x:=y;
        y:=t;
end;
procedure link(x,y,z:longint);
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=z;
end;
procedure insert(a,x:longint);
var
        i:longint;
begin
        if cj[x]<>0 then begin
                heap[cj[x]]:=a;
                i:=cj[x];
        end else begin
                inc(heap[0]);
                heap[heap[0]]:=a;
                num[heap[0]]:=x;
                i:=heap[0];
                cj[x]:=heap[0];
        end;
        while (i>>1>0) and (heap[i]<heap[i>>1]) do begin
                swap1(cj[num[i]],cj[num[i>>1]]);
                swap(heap[i],heap[i>>1]);
                swap1(num[i],num[i>>1]);
                i:=i>>1;
        end;
end;
procedure del;
var
        mid,i:longint;
begin
        cj[num[1]]:=0;
        heap[1]:=heap[heap[0]];
        num[1]:=num[heap[0]];
        dec(heap[0]);
        i:=1;
        if (heap[i<<1]<heap[i<<1+1]) or (i=heap[0]) then mid:=i<<1 else mid:=i<<1+1;
        while (mid<=heap[0]) and (heap[mid]<heap[i]) do begin
                swap1(cj[num[i]],cj[num[mid]]);
                swap(heap[i],heap[mid]);
                swap1(num[i],num[mid]);
                i:=mid;
                if (heap[i<<1]<heap[i<<1+1]) or (i<<1=heap[0]) then mid:=i<<1 else mid:=i<<1+1;
        end;
end;
begin
        readln(n,m);
        for i:=1 to m do begin
                readln(x,y);
                link(x,y,1);
                link(y,x,1);
        end;
        for i:=1 to n do dis[i]:=maxlongint;
        dis[1]:=0;
        head:=1;
        heap[0]:=1;
        heap[1]:=0;
        num[1]:=1;
        while heap[0]<>0 do begin
                v:=he[head];
                while v<>0 do begin
                        if dis[t[v]]>dis[head]+w[v] then begin
                                dis[t[v]]:=dis[head]+w[v];
                                ans:=max(dis[t[v]],ans);
                                insert(dis[t[v]],t[v]);
                        end;
                        v:=ne[v];
                end;
                del;
                head:=num[1];
        end;
         for i:=1 to n do if dis[i]=ans then begin
                inc(cns);
                if bj=0 then begin
                        bns:=i;
                        bj:=1;
                end;
        end;
        write(bns,' ',ans,' ',cns);
end.
```
72ms

但后来发现裸的spfa竟然可以过，所以又打了个裸的spfa

P2951（2）.pas
```pascal
var
        n,m,i,v,x,y,bj,tot,ans,bns,cns,head,tail:longint;
        he,ne,t,w,dis,q:array[0..4000000] of longint;
function max(x,y:longint):longint;
begin
        if x>y then exit(x) else exit(y);
end;
procedure link(x,y:longint);
begin
        inc(tot);
        ne[tot]:=he[x];
        he[x]:=tot;
        t[tot]:=y;
        w[tot]:=1;
end;
begin
        readln(n,m);
        for i:=1 to m do begin
                read(x,y);
                link(x,y);
                link(y,x);
        end;
        for i:=2 to n do dis[i]:=maxlongint;
        tail:=1;
        q[1]:=1;
        repeat
                inc(head);
                v:=he[q[head]];
                while v>0 do begin
                        if dis[t[v]]>dis[q[head]]+w[v] then begin
                                dis[t[v]]:=dis[q[head]]+w[v];
                                ans:=max(ans,dis[t[v]]);
                                inc(tail);
                                q[tail]:=t[v];
                        end;
                        v:=ne[v];

                end;
        until head>tail;
        for i:=1 to n do begin
                if dis[i]=ans then begin
                        inc(cns);
                        if bj=0 then begin
                                bns:=i;
                                bj:=1;
                        end;
                end;
        end;
        write(bns,' ',ans,' ',cns);
end.
```
79ms
