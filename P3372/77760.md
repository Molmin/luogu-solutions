# [p3372]线段树模版

如果真的母鸡抖线段树的可以找我的博客;

看见没有怕死狗的题解我就来撒一个;


    
    
    
    
    
    
    
    
    
    
    
    
    
```cpp
    var
            tree,lazy,left,right:array[1..800002] of int64;//分别为tree上的值,lazy标记,和左右指标
            n,m,k,l,r,x,y,find,srx,add:int64;
            i:longint;
    procedure make(l,r,k:int64);//建造一棵树
    var
            mid:int64;
    begin
            lazy[k]:=0;
            left[k]:=l;//标记左指标
            right[k]:=r;//标记右指标
            if l=r then
            begin
                    read(tree[k]);//直到l=r才输入
                    exit;
            end;
            mid:=(l+r) div 2;//中心
            make(l,mid,k*2);//往上递归
            make(mid+1,r,k*2+1);
            tree[k]:=tree[k*2]+tree[k*2+1];
    end;
    procedure SUC(k:int64);//down,往下lazy左儿子和右儿子,并清空自己的lazy
    var
            l,r:int64;
    begin
            l:=k*2;
            r:=k*2+1;
            inc(lazy[l],lazy[k]);//标记lazy
            inc(lazy[r],lazy[k]);//同上
            inc(tree[l],lazy[k]*(right[l]-left[l]+1));//分发给左儿子
            inc(tree[r],lazy[k]*(right[r]-left[r]+1));//分发给右儿子
            lazy[k]:=0;
    end;
    procedure change(k:longint);//区间修改
    var
            mid:int64;
    begin
            if (left[k]>=x)and(right[k]<=y) then//如果需要改变
            begin
                    inc(tree[k],add*(right[k]-left[k]+1));
                    inc(lazy[k],add);//标记lazy
                    exit;
            end;
            if lazy[k]>0 then
                    SUC(k);//down
            mid:=(left[k]+right[k]) div 2;
            if x<=mid then//2边查找
                    change(k*2);
            if y>mid then
                    change(k*2+1);
            tree[k]:=tree[k*2]+tree[k*2+1];
    end;
    procedure look(k:longint);//区间查询
    var
            mid:int64;
    begin
            if (left[k]>=x)and(right[k]<=y) then//如果到了叶子结点就add
            begin
                    inc(find,tree[k]);
                    exit;
            end;
            if lazy[k]>0 then//down
                    SUC(k);
            mid:=(left[k]+right[k]) div 2;//2边找
            if x<=mid then
                    look(k*2);
            if y>mid then
                    look(k*2+1);
    end;
    begin//主
            readln(n,m);
            make(1,n,1);//先建一棵树
            for i:=1 to m do
            begin
                    read(srx,x,y);
                    find:=0;
                    if srx=1 then
                    begin
                            read(add);
                            change(1);
                    end;
                    if srx=2 then
                    begin
                            look(1);
                            writeln(find);
                    end;
            end;
end.
```