虽然这道题是线段树的模板，然而我又懒得打线段树的代码，况且洛谷也没有树状数组区间修改，区间查询的模板题，于是我就来发个树状数组吧！（这就是你水题的理由？？？）
好了进入正文吧


树状数组是一个很神奇的数据结构，主要用于查询任意两位之间的所有元素之和，但是每次只能修改一个元素的值；经过简单修改可以在log(n)的复杂度下进行范围修改，但是这时只能查询其中一个元素的值(如果加入多个辅助数组则可以实现区间修改与区间查询)。

接下来发个图吧!![](http://s16.sinaimg.cn/middle/afe6bb33gc9f752512d9f&690)
!

定义sum(i)=a[1]+a[2]+...+a[i]

为了实现树状数组的区间修改，区间查询，在此引入差分数组d[i]=a[i]-a[i-1],在修改区间[l,r]时就只用修改d[l]和d[r+1]就可以了~所以sum(i)=d[1]+(d[1]+d[2])+(d[1]+d[2]+d[3])+...+(d[1]+d[2]+d[3]+...+d[i])

维护两个树状数组，c1记录d[i],c2记录(i-1)*d[i] (d[i]=a[i]-a[i-1])

sum(n)=n*sum(c1,n)-sum(c2,n);

代码如下：
```pascal
var
 c1,c2,d,a:array[0..100005]of int64;
 n,i,j,q,a1,b1,t,cz,x:longint;
function lowbit(x:longint):longint;
begin
 exit(x and (-x));
end;
procedure add(x,s:longint);//将a[x]加上s
var
 pre:longint;
begin
 pre:=x;
 while x<=n do
 begin
  c1[x]:=c1[x]+s*(pre-1);
  c2[x]:=c2[x]+s;
  x:=x+lowbit(x);
 end;
end;
function sum(x:longint):int64;//见上
var
 s,pre:int64;
begin
 s:=0;
 pre:=x;
 while x>0 do
 begin
  s:=s-c1[x]+pre*c2[x];
  x:=x-lowbit(x);
 end;
 exit(s);
end;
begin
 readln(n,q);
 for i:=1 to n do
 begin
  read(a[i]);
  d[i]:=a[i]-a[i-1];//求出差分数组
  add(i,d[i]);//初始化
 end;
 for i:=1 to q do
 begin
  read(cz);
  if cz=1 then
  begin
   read(a1,b1,x);
   add(a1,x);
   add(b1+1,-x);//执行区间修改
  end else
  begin
   read(a1,b1);
   writeln(sum(b1)-sum(a1-1));//求和
  end;
 end;
end.
```