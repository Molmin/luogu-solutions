本蒟蒻刚学会树状数组的区间求和，在线段树模板里练了个手~~~


先讲讲最普通的单点修改+区间求和


把a[x]+y，就是change(x,y)


求l~r的和，就是getsum(r)-getsum(l-1) //就是前缀和


具体不展开，楼下已提到，上面讲的是树状数组【模板1】


然后是区间修改+单点求和


难度提高了一点点，需要用到差分


所谓差分，比如数列a1,a2,a3,……,an


令deltai=a[i]-a[i-1]


那么a[i]=delta[1]+delta[2]+delta[3]+……+delta[i]


所以，要把l~r都加上x，只需change(l,x);change(r+1,-x);


算a[x]只需getsum(x)


难度微高一点点，是树状数组【模板2】


接下来步入正题，就是本题区间修改+区间求和


其实对线段树来说是水题==，我能说我线段树不熟练吗


依然用到差分，不过需要进行一个简单的推导：


1~i的和=sum[i]+i\*delta[1]+(i-1)\*delta[2]+……+2\*delta[i-1]+delta[i] //sum表示初始数据的前缀和



        =sum[i]+sigma(delta[x]\*(i-x+1))

        =sum[i]+(i+1)\*sigma(delta[x])-sigma(delta[x]\*x)

所以我们就维护两个树状数组delta,delta1，delta1[i]=delta[i]\*i


至于sum，一个预处理搞定


```cpp
type
    ar=array[0..100000] of int64;
var
    sum,delta,delta1:ar;
    l,r,x,y,i,n,m:longint;
function lowbit(x:int64):int64;
begin
    exit(x and -x);
end;
procedure change(var delta:ar;x,y:int64);
begin
    while x<=n do
        begin
            inc(delta[x],y);
            inc(x,lowbit(x));
        end;
end;
function getsum(var delta:ar;x:int64):int64;
begin
    getsum:=0;
    while x>0 do
        begin
            inc(getsum,delta[x]);
            dec(x,lowbit(x));
        end;
end;
begin
    readln(n,m);
    for i:=1 to n do
        begin
            read(x);
            sum[i]:=sum[i-1]+x;
        end;
    for i:=1 to m do
        begin
            read(x);
            if x=1 then
                begin
                    readln(l,r,x);
                    change(delta,l,x);
                    change(delta,r+1,-x); //delta的差分
                    change(delta1,l,x*l);
                    change(delta1,r+1,-x*(r+1));  //delta1的差分
                end else
                begin
                    readln(l,r);
                    writeln(sum[r]-sum[l-1]+(r+1)*getsum(delta,r)-l*getsum(delta,l-1)-getsum(delta1,r)+getsum(delta1,l-1));
                end;
        end;
end.
说难不难，理解就行
```