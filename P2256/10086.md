本题基本思想是并查集，在这里补充一个Pascal题解。

首先我们用name数组记录选手的名字，在读入选手名字的同时构建并查集初始状态（用ufs数组储存每个元素的祖先都指向其本身）。

然后读入分组状态，每组学生都是一个合并集合的过程。注意在这里两个选手的名字用空格分隔，因此不能简单地使用readln函数，而要进行字符串处理。在这里我使用了getstr函数，具体操作见代码。

同时我们要注意不能直接用选手的名字做ufs数组的下标，所以在这里我们的name数组就发挥了作用，name数组的下标可以看做每位选手的“编号”，我们在合并时只要处理编号就行了。

对于最后的k个询问，我们只要判断选手的“祖先”是否相同，若相同，则两位选手在同一个组里。

详细过程见代码注释。

'''pascal

```cpp
var
    n,m,k,i:longint;
    name:array[1..20000]of string;//存储选手姓名
    ufs:array[1..20000]of longint;//存储选手祖先
function getstr:string;//字符串读入处理
    var
        ch:char;
    begin
        getstr:='';
        read(ch);
        repeat
            getstr:=getstr+ch;
            if eoln then begin//如果为行末就跳出
                readln;
                exit()
            end;
            read(ch)
        until ch=' '//如果是空格就过滤并跳出
    end;
function find(s:longint):longint;//查询祖先
    var
        t:longint;
    begin
        while ufs[s]<>s do begin//一个优化：将所有相关联的元素直接连接到祖先
            t:=ufs[s];
            ufs[s]:=find(t);//递归调用，自上而下处理
            s:=t
        end;
        exit(s)
    end;
procedure union(const s1,s2:longint);//合并操作
    begin
        ufs[find(s1)]:=find(s2)//将两者祖先合并
    end;
function num(const s:string):longint;//利用name数组获取选手的“编号”
    var
        i:longint;
    begin
        for i:=1 to n do begin
            if name[i]=s then begin
                exit(i)
            end
        end
    end;
procedure union(const s1,s2:string);
    begin
        union(num(s1),num(s2))
    end;
begin
    readln(n,m);
    for i:=1 to n do begin
        readln(name[i]);
        ufs[i]:=i
    end;
    for i:=1 to m do begin
        union(getstr(),getstr())
    end;
    readln(k);
    for i:=1 to k do begin
        if find(num(getstr()))=find(num(getstr())) then begin
            writeln('Yes.')
        end
        else begin
            writeln('No.')
        end
    end
end.
'''
```