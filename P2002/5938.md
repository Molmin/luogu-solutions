缩点的典型应用题

同一个强连通分量的点可以等效为同一个点（互相直接或间接连通），求出所有强连通分量后遍历一遍，把每个强连通分量的入度求出来，入度为0的分量个数就是答案


注意：这道题会卡常数（不会告诉你们我栽了一次(´Д` )）



```cpp
var
  point,next,front:array[0..500000] of longint;
  head,dfn,low,stack,scc,indegree:array[0..100000] of longint;
  instack:array[0..100000] of boolean;
  n,m,x,y,i,num,edge_num,ans,top,sccnum,p:longint;
function min(a,b:longint):longint;
  begin
    if a<b then exit(a) else exit(b);
  end;
procedure addedge(u,v:longint);
  begin
    inc(edge_num);
    point[edge_num]:=v;
    front[edge_num]:=u;
    next[edge_num]:=head[u];
    head[u]:=edge_num;
  end;
procedure tarjan(u:longint);
  var
    p,root:longint;
  begin
    inc(num);dfn[u]:=num;low[u]:=num;
    inc(top);stack[top]:=u;instack[u]:=true;
    p:=head[u];
    while p<>0 do
      begin
        if dfn[point[p]]=0 then
          begin
            tarjan(point[p]);
            low[u]:=min(low[u],low[point[p]]);
          end
        else
          if instack[point[p]] then
            low[u]:=min(low[u],dfn[point[p]]);
        p:=next[p];
      end;
    if dfn[u]=low[u] then
      begin
        inc(sccnum);
        repeat
          root:=stack[top];instack[root]:=false;dec(top);
          scc[root]:=sccnum;//标记哪些点属于哪个强连通分量
        until root=u;
      end;
  end;
begin
  read(n,m);
  edge_num:=0;
  for i:=1 to m do
    begin
      read(x,y);
      addedge(x,y);
    end;
  sccnum:=0;
  for i:=1 to n do
    if dfn[i]=0 then
      begin
        top:=0;num:=0;
        tarjan(i);
      end;
  for i:=1 to edge_num do
    if scc[front[i]]<>scc[point[i]] then
      inc(indegree[scc[point[i]]]);//就是这里会卡常数= =
  ans:=0;
  for i:=1 to sccnum do
    if indegree[i]=0 then
      inc(ans);
  writeln(ans);
end.
```