应该说，这是一道很经典的dfs的题目

一开始，我是用贪心的，结果发现只能拿50分：从最高点开始一遍dfs

```cpp
const dx:array[1..4]of integer=(0,1,0,-1);
      dy:array[1..4]of integer=(-1,0,1,0);
var ans,n,m,i,j,max,min,maxi,maxj,mini,minj:longint;
    a:array[1..100,1..100]of longint;
    u:array[1..100,1..100]of boolean;
procedure dfs(k,x,y:longint);
var i,xx,yy:longint;
begin
 if k>ans then ans:=k;
 for i:=1 to 4 do begin
  xx:=x+dx[i]; yy:=y+dy[i];
  if (xx>0)and(xx<=n)and(yy>0)and(yy<=m)and(u[xx,yy])and(a[xx,yy]<a[x,y]) then begin
  // if k>ans then ans:=k;
   u[xx,yy]:=false;
   dfs(k+1,xx,yy);
   u[xx,yy]:=true;
  end;
 end;
end;
begin
 readln(n,m);
 max:=0; min:=maxlongint;
 for i:=1 to n do
  for j:=1 to m do begin
   read(a[i,j]);
   if a[i,j]>max then begin max:=a[i,j]; maxi:=i; maxj:=j; end;
 end;
 ans:=-maxlongint;
 fillchar(u,sizeof(u),true);
 u[maxi,maxj]:=false;
 dfs(1,maxi,maxj);
 writeln(ans);
end.
```
然后就发现了问题，来看一下第一个数据：
输入：

10 5
56 14 51 58 88

26 94 24 39 41

24 16 8 51 51

76 72 77 43 10

38 50 59 84 81

5 23 37 71 77

96 10 93 53 82

94 15 96 69 9

74 0 62 38 96

37 54 55 82 38

输出：7

而我们的程序输出 4，显然这个贪心不对。

所以不能从最高点开始一遍dfs，需要n\*m遍的dfs，这样就避免了上述情况

```cpp
const dx:array[1..4]of integer=(0,1,0,-1);
      dy:array[1..4]of integer=(-1,0,1,0);
var ans,n,m,i,j,max,min,maxi,maxj,mini,minj:longint;
    a:array[1..100,1..100]of longint;
    u:array[1..100,1..100]of boolean;
procedure dfs(k,x,y:longint);
var i,xx,yy:longint;
begin
 if k>ans then ans:=k;
 for i:=1 to 4 do begin
  xx:=x+dx[i]; yy:=y+dy[i];
  if (xx>0)and(xx<=n)and(yy>0)and(yy<=m)and(u[xx,yy])and(a[xx,yy]<a[x,y]) then begin
   u[xx,yy]:=false;
   dfs(k+1,xx,yy);
   u[xx,yy]:=true;
  end;
 end;
end;
begin
 readln(n,m);
 max:=0; min:=maxlongint;
 for i:=1 to n do
  for j:=1 to m do begin
   read(a[i,j]);
   if a[i,j]>max then begin max:=a[i,j]; maxi:=i; maxj:=j; end;
 end;
 ans:=-maxlongint;
 max:=-maxlongint;
 fillchar(u,sizeof(u),true);
// u[maxi,maxj]:=false;
 for i:=1 to n do
  for j:=1 to n do begin
 ans:=-maxlongint;
 dfs(1,i,j);
 if ans>max then max:=ans;
 end;
 writeln(max);
end.
```
当然，这种方法是AC不了的，为什么？时间复杂度太高！！是O(n\*m\*dfs)
本来dfs就是指数级了，这样的话会严重超时！！！

所以采用记忆化搜索，基本上AC的程序都是记忆化，我就不展开说了

```cpp
const dx:array[1..4]of integer=(0,1,0,-1);
      dy:array[1..4]of integer=(-1,0,1,0);
var ans,n,m,i,j,max,min,maxi,maxj,mini,minj:longint;
    f,a:array[1..100,1..100]of longint;
function dfs(x,y:longint):longint;
var i,xx,yy,t,tmp:longint;
begin
 if f[x,y]>0 then exit(f[x,y]);
 t:=1;
 for i:=1 to 4 do begin
  xx:=x+dx[i]; yy:=y+dy[i];
  if (xx>0)and(xx<=n)and(yy>0)and(yy<=m)and(a[xx,yy]>a[x,y]) then begin
  tmp:=dfs(xx,yy)+1;
  if tmp>t then t:=tmp;
  end;
 end;
 f[x,y]:=t;
 exit(t);
end;
begin
 readln(n,m);
 max:=0; min:=maxlongint;
 for i:=1 to n do
  for j:=1 to m do
   read(a[i,j]);
 for i:=1 to n do
  for j:=1 to m do begin
   f[i,j]:=dfs(i,j);
   if f[i,j]>max then max:=f[i,j];
end;
 writeln(max);
end.
```