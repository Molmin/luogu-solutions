题解：

**首先，不用说，肯定是使用记忆化搜索，然而前面几位大神们讲的都不算详细，那么本人在此呢，将此题最细化讲解，并提供源代码，仅供参考。**

        
先不说算法，首先我们需要的是了解题意，其实这个所给数据的方阵跟走迷宫没什么区别，只是必须从大数往小数走（也就是从高往低），本人一开始还异想天开，将所有数据排了个序，然后看那一条可以连得更长，然后输出最高点，不料还得了二十分儿，不过也发现自己对题目的理解有误。这道题形如走迷宫加上马的遍历的集合体，我这么说够明白了吧，所以请好好看下下面的算法解析。


![](https://cdn.luogu.com.cn/upload/pic/347.png)


**算法分析：由于一个人可以从某个点滑向上下左右向相邻的四个点之一，如上所示。而且仅当高度减小，对于任意一点[i,j]，当他的高度小于与之相邻的四个点的高度时，这四个点可以滑向[i,j]，用f[i,j]表示到[i,j]为止的最大的长度,f[i,j]=max{f(i+a,j+b)}+1,其坐标增量{(a,b)=[(1,0),(-1,0),(0,1),(0,-1)],0<i+a<=r,0<j+b<=c,high[i,j]<high[i+a,j+b]}。**

为了保证满足条件的f[i+a,j+b]在f[i,j]前算出，需要对高度排一次序，然后从大到小规划（高度）。最后再比较一下所有f[i,j]{0<i<=r,0<j<=c}，找出其中最长的一条路线。我们还可以用记忆化搜索的方法，他的有点事不需要进行排序，按照行的顺序，利用递归逐点求出区域中到达此点的最长路径，每个点的最长路径只求一次。


```delphi
const dx:array[1..4] of shortint=(-1,0,1,0);
      dy:array[1..4] of shortint=(0,1,0,-1);
var r,c,i,j,p,t,ans:longint;
    m,f:array[1..100,1..100] of longint;
function search(x,y:longint):longint; //函数的作用是求到[x,y]点的最长路径。
var i,t,tmp,nx,ny:longint;
begin  //此点长度已经求出，不必进行进一步递归，保证每一个点的最大长度只求一次，这是记忆化搜索的特点  
  if f[x,y]>0 then begin
    search:=f[x,y];exit;
  end;
  t:=1;
  for i:=1 to 4 do  //从四个方向上搜索能达到[x,y]的点
  begin
    nx:=x+dx[i];ny:=y+dy[i];  //加上横、纵坐标
    if (nx>=1)and(nx<=r)and(ny>=1)and(ny<=c)  //边界设置
    and(m[x,y]<m[nx,ny]) then begin  //高度比较
      tmp:=search(nx,ny)+1;  //递归进行记忆化搜索
      if tmp>t then t:=tmp;  
    end;
  end;
  f[x,y]:=t;
  search:=t;
end;
begin
  readln(r,c);
  ans:=0;
  for i:=1 to r do
    for j:=1 to c do read(m[i,j]);  //读入每个点的高度
  for i:=1 to r do    //按照行的顺序，利用递归逐点求出区域中到达此点的最长路径
   for j:=1 to c do
   begin
     t:=search(i,j);
     f[i,j]:=t;
     if t>ans then ans:=t;  //寻找最大值
   end;
   write(ans);
end.
```