这题用的方法是并查集，判断是否同一亲戚，因此，我们要将具有亲戚关系的人标记为同一父节点，在每次查询的时候，让次节点指向最终的父节点。

例如

![1](https://s2.ax1x.com/2019/07/28/elu2TA.png)

现在，6和5有关系，则变成

![2](https://s2.ax1x.com/2019/07/28/elu5Sf.png)

在路径查询时父节点会更新，最终变成

![3](https://s2.ax1x.com/2019/07/28/eluTOg.png)

```pascal
var
 n,m,p,i,x,y,fx,fy:longint;
 f:array[0..5001] of longint;//父节点数组
function sf(t:longint):longint;//查找父节点
begin
  if f[t]<>t then sf:=sf(f[t])//不是最终的，查找上一层的父节点
   else sf:=t;//因为最终的父节点肯定是指向自己的，所以当f[t]=t时t即最终的父节点
  f[t]:=sf;//随时更新父节点
end;
begin
  read(n,m,p);
  for i:=1 to n do f[i]:=i;//起先都指向自己
  for i:=1 to m do
  begin
    read(x,y);
    fx:=sf(x);
    fy:=sf(y);
    if fx<>fy then f[fx]:=fy;//如果这两个不是同一父节点，则将一个的父节点替换成另一个的父节点
  end;
  for i:=1 to p do
  begin
    read(x,y);
    fx:=sf(x);
    fy:=sf(y);
    if fx=fy then writeln('Yes')//同一父节点即为亲戚
     else writeln('No');
  end;
end.
```