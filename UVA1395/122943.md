这道题是一道经典的 _最小生成树_ ，可以用**kruskal**算法来解决。

Kruskal

•把所有边排序，记第i小的边为e[i]（1<=i<m）

•初始化MST为空

•初始化连通分量，让每个点自成一个独立的连通分量
```pascal
•for i:=1 to m do
   if(e[i].u和e[i].v不在同一个连通分量) begin
    把边e[i]加入MST
    合并e[i].u和e[i].v所在的连通分量
    end;
```
```pascal
Kruskal算法伪代码
•循环n-1次，n表示节点数{

▫找相关节点未在同一棵树中的最小边E find(i)<>find(j)//find函数返还的是树的顶点

▫连接E相关的两棵树 union(i,j)——father[i]:=j;
•}

•function find(i){Father[i]表示树中i节点的前继节点

▫if father[i]=i then exit(i);
▫ find:=find(father[i]);

▫ father[i]:=find;

•}
```
代码如下：
```pascal
var u,v,w:array[1..5000]of longint;//u数组记录一条边的起点，v数组记录一条边的终点，w数组记录一条边的长度
    p:array[1..100]of longint;//记录该点所在的集合的根
    f:boolean;//判断是否有解
    i,j,n,sum,m,x,y,cnt:longint;
    procedure sort(l,r: longint);//按边的长度由小到大排序
      var
         i,j,y: longint;x,t:longint;
      begin
         i:=l;
         j:=r;
         x:=w[(l+r) div 2];
         repeat
           while w[i]<x do
            inc(i);
           while x<w[j] do
            dec(j);
           if not(i>j) then
             begin
                t:=w[i];
                w[i]:=w[j];
                w[j]:=t;
                y:=u[i];
                u[i]:=u[j];
                u[j]:=y;
                y:=v[i];
                v[i]:=v[j];
                v[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
function find(x:longint):longint;//寻找该点所在的集合根
begin
  if p[x]=x then exit(x)
   else begin p[x]:=find(p[x]);exit(p[x]);end;//如果自身不是这个集合的根，就直接和这个集合的根连起来，节省之后的查询时间
end;
begin
  read(n,m);
  while (n<>0) do//多组数据
  begin
  for i:=1 to m do
   read(u[i],v[i],w[i]);
  if m=0 then writeln(-1)//如果一条路都没有，进行特判，输出-1
  else begin
  sort(1,m);//排序
  sum:=maxlongint;//sum的值是最长边与最短边之差，为了使它们之差尽量小，初始成无穷大
  f:=false;//初始成无解，若有解，就更改为true
  for i:=1 to m do//枚举最短的边
  begin
   for j:=1 to n do p[j]:=j;//每次初始成每个点都没相连，即这个点所在的集合根为自己
   cnt:=n;//判断有没有全部连上
   for j:=i to m do//求出最大边
   begin
    x:=find(u[j]);//寻找该边起点的所在集合的根
    y:=find(v[j]);//寻找该边终点的所在集合的根
    if x<>y then//判断有没有联通
    begin
     dec(cnt);//如果没联通，就把他们联通
     p[x]:=y;//两个集合合并成一个集合
    end;
    if cnt=1 then//当cnt=1时，即所有点都联通时
    begin
     f:=true;//更改为有解
     if w[j]-w[i]<sum then//判断这个解是否比之前解小
      sum:=w[j]-w[i];//更新解
     break;//已找到最大值，跳出循环
    end;
   end;
  end;
  if f then writeln(sum) else writeln(-1);
  end;
  read(n,m);
 end;


end.
```