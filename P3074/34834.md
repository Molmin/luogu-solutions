#看到题目，就看到了先后关系：X必须在Y之前挤奶。那会让我们想到什么？

#拓扑！

##没错，拓扑就是可以用来解决带有这种先后关系的题目。（写第一篇题解的大佬用了并查集，我不知道拓扑是不是正解）

###那该怎么处理呢？假设我们有一个父节点X，我们可从X众多的儿子节点中找出一个最大的值，然后用该时间加上挤X 的时间与已求好的挤到X的时间。

###可能类似于DP（或求最长路径，有这种意思）

上代码（有不懂可以私信，代码有注释）：



```cpp
var
  n,m,i,tot,x,y,ans:longint;
  link,f,q,dis:array[0..10005] of longint;
  next,w,son:array[0..50005] of longint;
  function max(a,b:longint):longint;  //求出a,b两者的较大值
  begin
    if a>b then exit(a)
           else exit(b);
  end;
  procedure BFS;
  var
    h,t,i,j:longint;
  begin
    h:=0;
    t:=0;
    for i:=1 to n do
    if f[i]=0 then
    begin
      inc(t);
      q[t]:=i;
      dis[i]:=w[i];  //寻找到入度为0的点（即叶子节点）将其入队
    end;
    while h<>t do
    begin
      inc(h);  //要省空间可以滚起数组来
      j:=link[q[h]];  //取q[h]的最后一条边的编号
      while j<>0 do
      begin
        dec(f[son[j]]);  //这个点已经被遍历过，因此将它的儿子节点的入度减一
        dis[son[j]]:=max(dis[son[j]],dis[q[h]]+w[son[j]]);  //刷最大值“最长路径”
        if f[son[j]]=0 then
        begin
          inc(t);
          q[t]:=son[j];  //如果它的儿子节点全部被遍历过，那么就入队
        end;
        j:=next[j];  //取第j条边的上一条边的编号
      end;
    end;
    ans:=0;
    for i:=1 to n do ans:=max(ans,dis[i]);  //刷一趟答案
  end;
begin
  readln(n,m);  
  for i:=1 to n do readln(w[i]);  //帅气的读入
  for i:=1 to m do
  begin
    readln(x,y);
    inc(f[y]);  //统计一个点的入度
    inc(tot);
    son[tot]:=y;
    next[tot]:=link[x];
    link[x]:=tot;  //高端的邻接表建图
  end;
  BFS;  //使用无敌的BFS版拓扑
  writeln(ans);  //绝对正确的输出
end.
```