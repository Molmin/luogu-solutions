这道题可真是烦人，编完程序调了一个多小时才调出来。

首先，这是一道极其麻烦的模拟题。

第二，我们先分析一下题目，发现总共只有六种语句，分别为：param语句，赋值语句，return语句，if语句，else语句和end if语句。我们对每一条语句进行遍历。

param语句：在输入开头，只需要用个数组记录已经定义的变量。

赋值语句：记录等号左边的变量，并判断等号右边的变量是否出现。

return语句：先判断return后的变量是否出现，再将后面的语句输出警告。

if语句：进行递归，将if嵌套的内容当作一个小程序，进行如上操作。

else和end if语句：跳出递归。

最后，这题有几个比较坑的地方：

1、会出现A=A+1的情况，我们先判断等式右边，再判断等式左边；

2、end if和else不能警告；

3、有时候程序做到一半就return了，而且是在if中return，如下例子：

IF A=1 THEN

RETURN A

ELSE
RETURN A

END IF
对于这种情形，需要用一个变量记录if和else中return的情况；

4、有的变量在if和else都出现，这种情况下，这个变量已经被记录，下次出现不能警告，如下例子：

```cpp
IF A=1 THEN
Q=1
ELSE
Q=2
END IF
```
这种情况下，Q已经被定义；
5、if或else内部定义的变量在外部可能被警告；

下面献上丑陋的代码：




















```cpp
program p2509;
type arr=array[1..26] of boolean;
var tot:longint;
    s:array[1..100] of ansistring;
    b:arr;
procedure init;
begin
  tot:=0;
  while not eof do
  begin
    inc(tot);
    readln(s[tot]);
  end;
end;
procedure warn1(n:longint;c:char);
begin
  writeln('Line ',n,': variable ',c,' might not have been initialized');
end;
procedure warn2(n:longint);
begin
  writeln('Line ',n,': unreachable code');
end;
function judge(str:ansistring):longint;
begin
  if str[2]='F' then
    exit(3)
  else if str[2]='E' then
    exit(2)
  else if str[2]='L' then
    exit(4)
  else if str[2]='N' then
    exit(5)
  else
    exit(1);
end;
function dfs(var k:longint;var c:arr):boolean;
var d,e,c1,c2:arr;
    str:ansistring;
    i,j,sum:longint;
    a:array[1..26] of longint;
    b1,b2:boolean;
begin
  d:=c;
  while k<=tot do
  begin
    str:=s[k];
    if judge(str)=1 then                                                                                                 //赋值语句
    begin
      fillchar(a,sizeof(a),0);
      for i:=5 to length(str) do
        if (str[i]>='A') and (str[i]<='Z') and not c[ord(str[i])-64] then
          inc(a[ord(str[i])-64]);
      for i:=1 to 26 do
        if a[i]>0 then
          warn1(k,chr(i+64));
      c[ord(str[1])-64]:=true;
    end
    else if judge(str)=2 then                                                                                        //return语句
    begin
      fillchar(a,sizeof(a),0);
      for i:=8 to length(str) do
        if (str[i]>='A') and (str[i]<='Z') and not c[ord(str[i])-64] then
          inc(a[ord(str[i])-64]);
      for i:=1 to 26 do
        if a[i]>0 then
          warn1(k,chr(i+64));
      sum:=0;
      j:=k;
      inc(k);
      while k<=tot do
      begin
        str:=s[k];
        if judge(str)=3 then inc(sum);
        if sum=0 then
        if (str[2]='L') or (str[2]='N') then
        begin
          for i:=j+1 to k-1 do
            if judge(s[i])<=3 then warn2(i);
          c:=d;
          exit(true);
        end;
      if str[2]='N' then dec(sum);
      inc(k);
    end;
    if k>tot then
      for i:=j+1 to tot do
        if judge(s[i])<=3 then warn2(i);
    end
    else if judge(str)=3 then                                                                                      //if语句
    begin
      j:=3;
      while copy(str,j,4)<>'THEN' do
        inc(j);
      fillchar(a,sizeof(a),0);
      for i:=3 to j-1 do
        if (str[i]>='A') and (str[i]<='Z') and not c[ord(str[i])-64] then
          inc(a[ord(str[i])-64]);
      for i:=1 to 26 do
        if a[i]>0 then
          warn1(k,chr(i+64));
      e:=c;
      inc(k);
      b1:=dfs(k,c);
      c1:=c;
      c:=e;
      if judge(s[k])=4 then
      begin
        inc(k);
        b2:=dfs(k,c);
        c2:=c;
        c:=e;
        if b1 and not b2 then
          c:=c2
        else if not b1 and b2 then
          c:=c1
        else if not b1 and not b2 then
        begin
          for i:=1 to 26 do
            if c1[i] and c2[i] then c[i]:=true;
        end;
        b1:=b1 and b2;
      end
      else
        b1:=false;
      if b1 then
      begin
        sum:=0;
        j:=k;
        inc(k);
        while k<=tot do
        begin
          str:=s[k];
          if judge(str)=3 then inc(sum);
          if sum=0 then
          if (str[2]='L') or (str[2]='N') then
          begin
            for i:=j+1 to k-1 do
              if judge(s[i])<=3 then warn2(i);
            c:=d;
            exit(true);
          end;
          if str[2]='N' then dec(sum);
          inc(k);
        end;
        if k>tot then
          for i:=j+1 to tot do
            if judge(s[i])<=3 then warn2(i);
      end;
    end
    else if judge(str)=4 then                                                                             //else语句
    begin
      exit(false);
    end
    else if judge(str)=5 then                                                                              //end if语句
    begin
      exit(false);
    end;
    inc(k);
  end;
end;
procedure main;
var i,l:longint;
    bool:boolean;
begin
  l:=length(s[1]);
  fillchar(b,sizeof(b),false);
  for i:=7 to l do
    if (s[1][i]>='A') and (s[1][i]<='Z') then
      b[ord(s[1][i])-64]:=true;
  i:=2;
  bool:=dfs(i,b);
end;
begin
  init;
  main;
end.
```