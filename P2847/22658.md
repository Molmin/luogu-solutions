**P党的福利**

最浅显的方法：这道题的关键就是建图O(n^2)+Kruskal

~~二分答案~~太复杂啦

我的建图方法：对于读入的点，两两之间用距离公式算出长度，然后对于建立的图，跑一遍Kruskal。**在跑的过程中，记下最长的边长即为答案。**

举个栗子!
![](https://cdn.luogu.com.cn/upload/pic/23276.png)
对于输入数据：

4

1 2

1 3

2 2

2 0

我们建立一个这样的图，共6条边

可以推得边数：$\frac{n(n-1)}{2}$

然后就可以愉快地Kruskal了

贴代码：
```pascal
var i,ans,tot,n,j:longint;
x,y,father:array[0..10000] of longint;
a,b,v:array[0..600000] of longint;

procedure swap(var x,y:longint);
var temp:longint;
begin
temp:=x;x:=y;y:=temp;
end;

procedure sort(l,r:longint);   //排序，kruskal基本操作
var i,j,t,mid:longint;
begin
i:=l;j:=r;
mid:=v[(l+r) div 2];
repeat
    while v[i]<mid do
        inc(i);
    while v[j]>mid do
        dec(j);
    if i<=j
        then begin
                 swap(a[i],a[j]);
                 swap(b[i],b[j]);
                 swap(v[i],v[j]);
                 inc(i);dec(j);
             end;
until i>j;
if l<j
    then sort(l,j);
if i<r
    then sort(i,r);
end;

function getfather(x:longint):longint;   //并查集
begin
if father[x]=x
    then exit(father[x]);
father[x]:=getfather(father[x]);
exit(father[x]);
end;

procedure merge(x,y:longint);
var xx,yy:longint;
begin
xx:=getfather(x);
yy:=getfather(y);
father[xx]:=yy;
end;

function query(x,y:longint):boolean;
begin
exit(getfather(x)=getfather(y));
end;

begin
readln(n);
for i:=1 to n do
    readln(x[i],y[i]);
for i:=1 to n-1 do    //O(n^2)的建图
    for j:=i+1 to n do
        begin
            inc(tot);
            a[tot]:=i;b[tot]:=j;
            v[tot]:=sqr(x[i]-x[j])+sqr(y[i]-y[j]); //距离公式建边
        end;
sort(1,tot);
for i:=1 to n do
    father[i]:=i;
for i:=1 to tot do
    if query(a[i],b[i])=false
        then begin
                 if v[i]>ans   
                     then ans:=v[i];   //保存最长的边
                 merge(a[i],b[i]);
             end;
writeln(ans);
end.
```