$USACO$ ~~金组大水题~~ 最小生成树入门练手题

既然是最小生成树，
## 何必要二分答案？
我们完全可以不用$Kruskal$，
## 何必要$O(n^2)$建边？
显然，答案是最小生成树中最长边的平方，直接勾股定理就行——
## 何必要处理小数精度问题？

对于边权需要自行计算的完全图（如本题只给出坐标的情况），在时间复杂度允许的情况下，我们一般采用$Prim$算法。

$Prim$的思想类似于最短路中的$dijkstra$。

初始时，将任意结点（通常选$1$号结点）加入最小生成树，然后我们维护一个$min[i]$数组用于记录结点$i$到已经确定的最小生成树的最短距离。

每次循环中，我们根据贪心思想，找到一个离最小生成树最近的且未被加入到树中的结点，添加到树中，并用当前的结点去松弛与其相连的结点。重复该操作直到所有元素被加入到最小生成树中。

时间复杂度$O(n^2)$，在稠密图中有不俗的表现。

### $Prim$正确性证明：

对于任意一个顶点$v$，连接到该顶点的所有边中的一条最短边$(v, vj)$必然属于最小生成树（即任意一个属于最小生成树的连通子图，从外部连接到该连通子图的所有边中的一条最短边必然属于最小生成树）

传送门：[P3366 【模版】最小生成树](https://www.luogu.org/problem/P3366)

然后我们就可以用$Prim$愉快地$AC$这道金组~~难题~~辣

## Code
```c
program cast;
const INF=99999999999999;
var ans,m,u:int64;
    min,x,y:array[0..1010] of int64;
    vis:array[0..1010] of boolean;
    i,j,n:longint;
begin
  readln(n);
  for i:=1 to n do readln(x[i],y[i]);
  for i:=1 to n do min[i]:=INF;
  min[1]:=0;
  for i:=1 to n do
  begin
    m:=INF;
    for j:=1 to n do
      if (not vis[j])and(min[j]<m) then
      begin
        m:=min[j]; u:=j;
      end;
    vis[u]:=true;
    for j:=1 to n do
      if (not vis[j])and(sqr(x[j]-x[u])+sqr(y[j]-y[u])<min[j]) then
        min[j]:=sqr(x[j]-x[u])+sqr(y[j]-y[u]);
  end;
  for i:=1 to n do
    if min[i]>ans then ans:=min[i];
  writeln(ans);
end.

```