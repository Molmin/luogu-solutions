鉴于打了太多注释，这里这简单说一下做法，如果单纯DFS的话样例或需要用5s，一部分优化可能过50%到70%，又容易推得贪心，DP一定不是正确的做法（反例好找的SO EASY）。AC的做法可以是二分人数，不断地DFS，然后把所有能想到的剪枝，什么可行性，最优性简直都放上，卡卡就能过了。因为代码复杂度太高，时间复杂度也不好算了，反正东卡西卡过了。


复制代码





```cpp
type sz=array[0..2000] of longint;
var a,b,sum:sz;
    n,i,x,y,st,lst,mid,m,summ,ans:longint;
    flag:boolean;
procedure qsort(r,l:longint;var a:sz);
var
     i,j,k:longint;
begin
     i:=r;
     j:=l;
     k:=a[(i+j) shr 1];
     repeat
          while a[i]<k do inc(i);
          while a[j]>k do dec(j);
          if i<=j then begin
              a[0]:=a[i];
              a[i]:=a[j];
              a[j]:=a[0];
              inc(i);
              dec(j);
          end;
     until i>j;
     if i<l then qsort(i,l,a);
     if r<j then qsort(r,j,a);
end;
//通用快排，由小到大的顺序
procedure dfs(beg,k:longint);
//b数组是排好序的，beg表示当前是从第几个蛋糕开始的
var
     i:longint;
begin
     if k=0 then 
        begin
            flag:=true;
            exit;
        end;
     if summ+sum[mid]>ans then exit;
     //优化，如果当前合法的蛋糕没有的值加上此刻处理人的蛋糕权值要比总权值要大，则退出。
     for i:=beg to n do 
        if a[i]>=b[k] then 
            begin
                a[i]:=a[i]-b[k];
                if a[i]<b[1] then inc(summ,a[i]);
                //summ表示没有用的蛋糕数量，如果第一个人都无法满足那么蛋糕a[i]等于没用
                if b[k]=b[k-1] then dfs(i,k-1) else dfs(st,k-1);
                //如果b[k]=b[k+1],那么只有i到n这些蛋糕可取(想想这是为什么)
                if a[i]<b[1] then dec(summ,a[i]);
                a[i]:=a[i]+b[k];
                //两行回溯
                if flag then exit;
            end;
end;
begin
     readln(n);
     for i:=1 to n do readln(a[i]);
     readln(m);
     for i:=1 to m do readln(b[i]);
     qsort(1,n,a);
     qsort(1,m,b);
     st:=1;
     while (a[st]<b[1]) and (st<n) do inc(st);    
     //把没用的蛋糕去掉，处理后sum是能用的蛋糕
     for i:=st to n do inc(ans,a[i]);
     lst:=m;
     while (b[lst]>a[n]) and (lst>1) do dec(lst); 
     //把不能满足的人去掉，现在只需要满足ss个人就行了
     for lst:=1 to lst+1 do 
        begin
            sum[lst]:=sum[lst-1]+b[lst];
            //sum[i]表示到i蛋糕时所有蛋糕的总权值，所有蛋糕加起来都无法满足可以的人那么退出
            if sum[lst]>ans then break;
        end;
     dec(lst);//从上层循环的影像中退出来
     x:=0;
     y:=lst;
     while x<y do 
     //需要满足的人数小于所有的人数
        begin
            mid:=(x+y+1) shr 1;
            //二分查找的是人，位运算:shr 1==>div 2;
            flag:=false;
            summ:=0;
            dfs(st,mid);
            if flag then x:=mid else y:=mid-1;
        end;
     writeln(x);
end.
```