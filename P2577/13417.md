本题可以用根据冷却时间的顺序先进行一步贪心，再DP，保证得到最优值

f[i,j]代表共加工了前i个产品，1号机器加工使用j单位时间，得到的最小完成时间

状态转移方程f[i,j]:=max{j+b[i],f[i-1,j-a[i]],c[i]-j+b[i],f[i-1,j]} c代表前i个产品的总加工时间 当然还要注意判断能否取到j-a[i]和c[i]-j

pascal代码如下

```pascal
var a,b,c:array[1..200] of longint; f:array[0..200,0..40000] of longint;
    i,j,n,ans:longint;
function min(x,y:longint):longint;
begin
  if x>y then exit(y) else exit(x);
end;
function max(x,y:longint):longint;
begin
  if x>y then exit(x) else exit(y);
end;
procedure sort;
var i,j,x:longint;
begin
  for i:=1 to n-1 do
    for j:=i+1 to n do
      if (b[i]<b[j]) or ((b[i]=b[j]) and (a[i]<a[j])) then
      begin
        x:=a[i]; a[i]:=a[j]; a[j]:=x;
        x:=b[i]; b[i]:=b[j]; b[j]:=x;
      end;
end;
begin
  filldword(f,sizeof(f)>>2,maxlongint>>1); f[0,0]:=0; ans:=maxlongint;//初始化
  readln(n);
  for i:=1 to n do read(a[i],b[i]);
  sort;//排序

  c[1]:=a[1];
  for i:=2 to n do c[i]:=c[i-1]+a[i];

  for i:=1 to n do     //前i个产品
    for j:=0 to c[i] do//1号机器加工用了j时间
    begin
      if j>=a[i] then                                         //判断是否可以用1号机器加工
        f[i,j]:=min(f[i,j],max(j+b[i],f[i-1,j-a[i]]));  //用的时间可能是上一次加工得到的，或是这一次加工更新得到的
      if c[i]-j>=a[i] then                                  //判断是否可以用2号机器加工
        f[i,j]:=min(f[i,j],max(c[i]-j+b[i],f[i-1,j])); //同上
      if i=n then ans:=min(ans,f[i,j]);//得到加工完后时间的最小值
    end;

  writeln(ans);
end.
```