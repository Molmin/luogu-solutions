此题采用枚举法，必然会超时。不对啊，Ye哥仔细一想，发现最大枚举也就30000种重量，不可能会超时啊，原来其中有很多是重复枚举的！

ye这就想到了记忆化搜索（不重复枚举），哈哈。

ye哥先用-1标记，再考虑此次的最大可能值，用它来代替-1，这样就可以不重复搜索！

每一个箱子考虑装与不装两种情况，最后找出最大值即可！

记忆化搜索：

```cpp
var
  f:array[0..30,0..20000] of longint;
  a:array[0..35] of longint;
  v,n,i,j:longint;
Function max(q1,q2:longint):longint;  //选取两个数最大值函数
begin
  if q1>q2 then max:=q1
  else max:=q2;
end;
function dfs(x,y:longint):longint;      //记忆化搜索
var
  t,q:longint;
begin
  if x=n+1 then exit(y);           //如果要选的箱子已经超过了总箱子数，则返回目前最大值
  if f[x,y]<>-1 then exit(f[x,y]);     //如果该情况已经讨论过，则返回以前的讨论值
  t:=0; 
  if y+a[x]<=v then t:=dfs(x+1,y+a[x]); //如果目前箱子重量加上此次选择的物品，则装上它，继续选择下一个物品。
  q:=dfs(x+1,y);                      //也可以选择不装，继续选择下一个物品
  f[x,y]:=max(t,q);           //选择装与不装两种情况最优方案
  dfs:=f[x,y];                //返回最大值
end;
begin
  readln(v);
  readln(n);
  for i:=1 to n do
  for j:=0 to v do
  f[i,j]:=-1;
  for i:=1 to n do
  readln(a[i]);
  writeln(v-dfs(1,0));
end.
```