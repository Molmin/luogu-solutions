为什么到紫题就很少有pascal题解了呢qwq

那我来写一份比较详细的吧（毕竟是算法模板题）

这道题是三维偏序的模板

1D 我们对第一维直接进行排序就好啦，这样可以保证第一维有序

2D: 这里我采用了cdq分治

cdq分治是一种神奇的分治算法

例如本题，将第二维的区间采取二分，首先递归计算左边内部对自己的贡献，然后计算左边对右边的贡献，最后递归计算右边自己内部贡献

所以重点在如何处理左边对右边的贡献

假设左边区间为[l,mid] 右边区间为[mid+1.r] (推荐使用闭区间，比较好判断边界~~

我们先将两个区间排序，注意要打上序号，这样等会才方便判断这个数是在左区间还是在右区间

然后将数组扫一遍，每当我们遇见左区间的数时统计下来，遇到右区间的数就将前面的左区间的数统计到该数的答案里

因为，左区间的数第一维一定小于右区间，而上述操作便统计了第二维也小的数

当然，到此为止是二维偏序的做法，而本题是三维偏序(见下面

3D 第三维当然也可以再套一层cdq分治，但推荐使用树状数组来维护,因为树状数组常数小，而二维树状数组额外空间过大，会MLE,至于treap，我不会打qwq

接着第二步说，由于第三维的限制，不一定第一维，第二维都满足条件就可以，
所以我们用树状数组来维护统计答案步骤~~~

当我们访问到左区间的点时，我们将他的第三维对应的点加1，来统计左区间中第三维为此数的点，当我们访问到右区间的点时，就查询树状数组，来找到第三维也小的左节点数量

嗯就这样啦~~~

附码

```pascal
var n,k,i:longint;
    x,y,z,f,b,s,p,m:array[0..100000]of longint;
    c:array[1..200000]of longint;
procedure sort(l,r:longint);
var a,b,i,j,c,d:longint;
begin
  a:=x[(l+r)div 2];
  c:=y[(l+r)div 2];
  d:=z[(l+r)div 2];
  i:=l;
  j:=r;
  repeat
    while (x[i]<a)or((x[i]=a)and(y[i]<c))or((x[i]=a)and(y[i]=c)and(z[i]<d)) do
      inc(i);
    while (x[j]>a)or((x[j]=a)and(y[j]>c))or((x[j]=a)and(y[j]=c)and(z[j]>d)) do
      dec(j);
    if i<=j then
    begin
      b:=x[i];
      x[i]:=x[j];
      x[j]:=b;
      b:=y[i];
      y[i]:=y[j];
      y[j]:=b;
      b:=z[i];
      z[i]:=z[j];
      z[j]:=b;
      inc(i);
      dec(j);
    end;
  until i>j;
  if i<r then sort(i,r);
  if l<j then sort(l,j);
end;
procedure sort2(l,r:longint);
var x,y,z,i,j:longint;
begin
  x:=b[(l+r)div 2];
  z:=s[(l+r)div 2];
  i:=l;
  j:=r;
  repeat
    while (b[i]<x)or((b[i]=x)and(s[i]<z)) do
      inc(i);
    while (x<b[j])or((b[j]=x)and(s[j]>z)) do
      dec(j);
    if i<=j then
    begin
      y:=b[i];
      b[i]:=b[j];
      b[j]:=y;
      y:=s[i];
      s[i]:=s[j];
      s[j]:=y;
      inc(i);
      dec(j);
    end;
  until i>j;
  if i<r then sort2(i,r);
  if l<j then sort2(l,j);
end;
procedure ins(x,y:longint);
begin
  while x<=k do
  begin
    inc(c[x],y);
    x:=x+(x and -x);
  end;
end;
function query(x:longint):longint;
begin
  query:=0;
  while x>0 do
  begin
    inc(query,c[x]);
    x:=x-(x and -x);
  end;
end;
procedure cdq(l,r:longint);
var mid,i,j:longint;
begin
  if l>=r then exit;
  mid:=(l+r)div 2;
  cdq(l,mid);//递归左区间的答案
  for i:=l to r do//标记+排序
  begin
    b[i]:=y[i];
    s[i]:=i;
  end;
  sort2(l,r);
  for i:=l to r do
  begin
    if s[i]=n then
    begin
      s[i]:=s[i];
    end;
    if s[i]<=mid then ins(z[s[i]],1);
    //如果是左区间的点，树状数组统计
    if s[i]>mid then f[s[i]]:=f[s[i]]+query(z[s[i]]);
    //右区间的点查询统计
  end;
  for i:=l to r do
    if s[i]<=mid then ins(z[s[i]],-1);
  cdq(mid+1,r);//递归右区间
end;
begin
  readln(n,k);
  for i:=1 to n do
    readln(x[i],y[i],z[i]);
  sort(1,n);//第一维排序啦啦啦
  cdq(1,n);//开始分治
  for i:=n-1 downto 1 do
    if (x[i]=x[i+1])and(y[i]=y[i+1])and(z[i]=z[i+1]) then f[i]:=f[i+1];
  for i:=1 to n do//依题意扫答案
    inc(p[f[i]]);
  for i:=0 to n-1 do
    writeln(p[i]);
end.
```

Ps:三维偏序中，cdq的分治统计右区间内部贡献和左区间对右区间的贡献并不冲突，所以交换顺序也是可以的，这样我们就可以在递归左右区间的时候可以通过归并排序来减少常数（因为我们已经维护了树状数组，所以这点小优化不影响时间复杂度）