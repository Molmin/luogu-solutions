##居然没有P的，强行水一发

##NOIP2017考前我们一次膜你赛的t4。。。我是pj。。。（这是最骚的

用边表存图，先用tarjan缩点，即把每一个强连通分量缩成一个点，用color[i]表示染色，即第i个点在缩点后属于第几个强连通分量

然后用spfa跑一个最长路

**昨天才学tarjan的蒟蒻**~~滑稽~~

丑陋的代码：


```cpp
uses math;
var
  a,next,last,b,c,q,color,x,y,dfn,low,dis,f:array[0..500001]of longint;
  vis,bar:array[0..500001]of boolean;
  n,m,i,j,tot,top,k,start,d,sum,ans,t:longint;
procedure add(x,y:longint);
begin
  inc(tot); 
  a[tot]:=y; 
  next[tot]:=last[x]; 
  last[x]:=tot;
end;
procedure tarjan(k:longint);
var
  i,t:longint;
begin
  inc(d); inc(top);
  q[top]:=k;vis[k]:=true;
  low[k]:=d;dfn[k]:=d;
  i:=last[k];
  while i>0 do 
    begin
      t:=a[i];
      if dfn[t]=0 then //如果节点t未被访问过
        begin
          tarjan(t); //继续向下找
          low[k]:=min(low[k],low[t]);
        end
        else
          if vis[t] then low[k]:=min(low[k],dfn[t]); //如果节点t还在栈内
      i:=next[i];
    end;
  if dfn[k]=low[k] then //说明k为一个强连通分量的根
    begin
      inc(sum);
      while q[top+1]<>k do
        begin
          color[q[top]]:=sum;
          vis[q[top]]:=false;
          inc(b[sum],c[q[top]]);
          dec(top);
        end;//更新新的一个强连通分量
    end;
end; //tarjan
procedure spfa(start:longint);
var
  i,y,h,t:longint;
begin
  fillchar(vis,sizeof(vis),0);
  h:=0; t:=1;
  f[1]:=start;
  dis[start]:=b[start];
  while h<t do 
    begin
      inc(h);
      y:=f[h];
      vis[y]:=false;
      i:=last[y];
      while i>0 do 
        begin
          if dis[y]+b[a[i]]>dis[a[i]] then 
            begin
              dis[a[i]]:=dis[y]+b[a[i]];
              if not vis[a[i]] then 
                begin
                  inc(t);
                  f[t]:=a[i];
                  vis[a[i]]:=true;
                end;
            end;
          i:=next[i];
        end;
    end;
  for i:=1 to sum do 
    if bar[i] then ans:=max(ans,dis[i]);
end;//普通的SPFA 不解释
begin
  readln(n,m);
  for i:=1 to m do 
    begin
      readln(x[i],y[i]);
      add(x[i],y[i]);
    end;
  for i:=1 to n do read(c[i]);//读入
  for i:=1 to n do 
    if color[i]=0 then tarjan(i);//对没有染色点进行一次tarjan
  //for i:=1 to n do write(color[i],' ');
  fillword(a,sizeof(a) >> 1,0);
  fillword(next,sizeof(next) >> 1,0);
  fillword(last,sizeof(last) >> 1,0);
  tot:=0;
  for i:=1 to m do 
    if color[x[i]]<>color[y[i]] then add(color[x[i]],color[y[i]]);//重新建图
  readln(start,k);
  start:=color[start];
  for i:=1 to k do 
    begin
      read(t);
      bar[color[t]]:=true;
    end;//记录酒吧所在的强连通分量
  spfa(start);
  writeln(ans);
end.

```