这道题我们可以这样做：
先找出在那个数中最大的斐波那契数，然后再把那个最大的斐波那契数给减掉，之后再以上面的方式继续做，最后对所得到的数进行快排，输出结果。

以下程序段为拆分那个数：
```pascal
procedure chai(n:longint);
begin
t:=t+1;//用t来计算数组总数据
a[t]:=fei(n);//等会儿给你们展示fei这个函数
n:=n-fei(n);//拆分
if n=0 then exit;//拆分完毕，退出
chai(n);//继续拆
end;
```
这就是fei这个function：
```pascal
function fei(k:longint):longint;
var x,y,p:longint;
begin
x:=1;y:=1;//赋初值
while y<=k do
  begin
  p:=x;x:=y;y:=y+p;//斐波那契
  end;
exit(x);//注意：这里不是返回y，是x（p也行）
end;
```
现在就可以把程序编出来了：
```pascal
var i,n,t,j,x:longint;
a:array[1..1000000] of longint;//数组可以开大一点
procedure sort(l,r: longint);
      var
         i,j,x,y: longint;
      begin
         i:=l;
         j:=r;
         x:=a[(l+r) div 2];
         repeat
           while a[i]<x do
            inc(i);
           while x<a[j] do
            dec(j);
           if not(i>j) then
             begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;//从C盘里“偷来”的快排
function fei(k:longint):longint;
var x,y,p:longint;
begin
x:=1;y:=1;
while y<=k do
  begin
  p:=x;x:=y;y:=y+p;
  end;
exit(x);
end;//刚才的fei
procedure chai(n:longint);
begin
t:=t+1;
a[t]:=fei(n);
n:=n-fei(n);
if n=0 then exit;
chai(n);
end;//刚才的chai
begin
readln(n);
for i:=1 to n do
  begin
  fillchar(a,sizeof(a),0);{等于for j:=1to t do a[j]:=0;}
  t:=0;//注意赋初值0
  read(x);
  chai(x);//把x给拆了
  sort(1,t);//要从小到大输出哦
  write(x,'=');
  for j:=1 to t-1 do
    write(a[j],'+');
  writeln(a[t]);//植树问题，最后一个不用加加号
  end;
end.
```
