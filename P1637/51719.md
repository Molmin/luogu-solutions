来篇pascal的


思路很容易想到，乘法原理


对于每一个数，在他前面并比他小的数的个数\*在他后面并比他大的数的个数累加起来就是答案


所以这里我们需要维护两个树状数组


一个用来求在他前面并比他小的数的个数


一个用来求在他后面并比他大的数的个数


类似的算法？就是逆序对


不过需要弄点手段：


求在他前面并比他小的数的个数时，来个快排，从小到大，并且相等的初始位置后的排在前


为什么？


若有相等的，i<j,ai=aj，排好序后i在前


那么算i时没影响，但算j时会把i的那份也加进去，就wa了


求在他后面并比他大的数的个数，快排，从大到小，并且相等的初始位置前的排在前


诶，不一样了！


对，是不一样


因为我们要记录位置后面，数值大于自己，与刚才完全反了一下


而且树状数组修改是往前加了，求和是往后加了，也不一样


```cpp
var
    a,b,c,d,tree,minsum,maxsum:array[0..100000] of int64;
    n,i:longint;
    ans:int64;
function lowbit(x:int64):int64;
begin
    exit(x and -x);
end;
procedure change(x:int64);
begin
    while x<=n do
        begin
            inc(tree[x]);
            inc(x,lowbit(x));
        end;
end;
function getsum(x:int64):int64;
begin
    getsum:=0;
    while x>0 do
        begin
            inc(getsum,tree[x]);
            dec(x,lowbit(x));
        end;
end;
procedure change1(x:int64);
begin
    while x>0 do
        begin
            inc(tree[x]);
            dec(x,lowbit(x));
        end;
end;
function getsum1(x:int64):int64;
begin
    getsum1:=0;
    while x<=n do
        begin
            inc(getsum1,tree[x]);
            inc(x,lowbit(x));
        end;
end;
procedure sort(l,r: longint);
      var
         i,j,x,y,m: longint;
      begin
         i:=l;
         j:=r;
         x:=a[(l+r) div 2];
         m:=b[(l+r) div 2];
         repeat
           while (a[i]<x) or (a[i]=x) and (b[i]>m) do
            inc(i);
           while (x<a[j]) or (x=a[j]) and (m>b[j]) do
            dec(j);
           if not(i>j) then
             begin
                y:=a[i];
                a[i]:=a[j];
                a[j]:=y;
                y:=b[i];
                b[i]:=b[j];
                b[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           sort(l,j);
         if i<r then
           sort(i,r);
      end;
procedure qsort(l,r: longint);
      var
         i,j,x,y,m: longint;
      begin
         i:=l;
         j:=r;
         x:=c[(l+r) div 2];
         m:=d[(l+r) div 2];
         repeat
           while (c[i]>x) or (c[i]=x) and (d[i]<m) do
            inc(i);
           while (x>c[j]) or (x=c[j]) and (m<d[j]) do
            dec(j);
           if not(i>j) then
             begin
                y:=c[i];
                c[i]:=c[j];
                c[j]:=y;
                y:=d[i];
                d[i]:=d[j];
                d[j]:=y;
                inc(i);
                j:=j-1;
             end;
         until i>j;
         if l<j then
           qsort(l,j);
         if i<r then
           qsort(i,r);
      end;
begin
    readln(n);
    for i:=1 to n do
        begin
            read(a[i]);
            c[i]:=a[i];
            b[i]:=i;
            d[i]:=i;
        end;
    sort(1,n);
    qsort(1,n);
    for i:=1 to n do
        begin
            minsum[b[i]]:=getsum(b[i]);
            change(b[i]);
        end;
    fillchar(tree,sizeof(tree),0); //第二个树状数组
    for i:=1 to n do
        begin
            maxsum[d[i]]:=getsum1(d[i]);
            change1(d[i]);
        end;
    for i:=1 to n do
        inc(ans,minsum[i]*maxsum[i]);  //乘法原理
    writeln(ans);
end.
注意：开int64，不然会wa
```