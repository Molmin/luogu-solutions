一开始看此题,发现没有什么思路,后面受到某些大佬的启发.

发现我们可以把原来的式子转化成xi-wi>=xj+wj,于是我们把一个点看作一段在[xi-wi,xi+wi]的线段,此题就顺利转化为了贪心了.

怎么贪心呢?发现只要两个线段没有交点(交在边界也行)便可以连边,并且选出的一连串不相交的边一定是可以两两连边的,所以符合题目给出的条件.

于是我们按照线段右端点排序后暴力取点就可以,时间复杂度O(nlogn).

~~吐槽一句,楼下大佬按左端点排序的,我一开始看题解非常迷......~~

再给出我的AC代码,跑的可慢了......
```pascal
type
        re=record
                l,r:longint;
        end;
var
        n,i,x,y,now,ans:longint;
        point:array[1..200000]of re;
procedure swap(var a,b:re);
var
        c:re;
begin
        c:=a;
        a:=b;
        b:=c;
end;
procedure sort(l,r:longint);
var
        i,j,mid:longint;
begin
        i:=l;
        j:=r;
        mid:=point[(l+r)>>1].r;
        repeat
                while point[i].r<mid do inc(i);
                while point[j].r>mid do dec(j);
                if i<=j then
                begin
                        swap(point[i],point[j]);
                        inc(i);
                        dec(j);
                end;
        until i>j;
        if i<r then sort(i,r);
        if l<j then sort(l,j);
end;
begin
        read(n);
        for i:=1 to n do
        begin
                read(x,y);
                point[i].l:=x-y;
                point[i].r:=x+y;
        end;
        sort(1,n);
        now:=-maxlongint;
        for i:=1 to n do
        if point[i].l>=now then 
        begin
                inc(ans);
                now:=point[i].r;
        end;
        writeln(ans);
end.
```