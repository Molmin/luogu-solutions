# 主要算法：差分+二分答案+KMP（还有一点点的暴力）

~~（写了一天的题怎么能不发篇题解纪念一下呢qwq）~~

本题要求我们在所有数列中找出最长的公共子串，但略有特殊的是：只要一个数列的所有数同时加上一个相同的数就可以变成另一个数列，例如$(1,3,6,7,10)$和$(2,4,7,8,11)$和$(11,13,16,17,20)$等数列是等价的。

我们可以发现，虽然数列中的值会有所变化，但相邻两个数的差是不会变的。这样我们就可以把原始数列变成**差分**数列，这样问题就转化成了普通的**在所有数列中找出最长的公共子串**的问题。

于是我们可以先固定一个模式串，再与其他数列一一匹配，若都匹配通过，则这个答案是可以达到的。

现在我们怎么确定这个模式串呢，其实随便选一个数列都可以为格式串，但最优我们可以选那个**最短的数列为格式串**，并保证了格式串长度一定小于等于其他的数列。

但我们可以发现：一个模式串与其他数列匹配不一定要从第一个开始，事实上它从任意位置开始匹配都是可行的。

因此我们KMP匹配时，可以用许多个$fail$数组，来分别表示该模式串后$i$个数组成的数列的$fail$数组。例如$(2,3,1,3)$这个模式串，我们就可以记录$(2,3,1,3)$、$(3,1,3)$、$(1,3)$、$(3)$这四个数列的$fail$数组。（有点小暴力qwq）

接下来就要开始搜索答案了。我们可以比较容易的发现，这题的答案是满足**二分答案**的性质的，因此我们可以二分这个公共子串的长度，再验证答案。

~~（这就是我花了一天时间才写完的题qwq）~~

------------

pascal代码如下：
```pascal
var n,m,i,j,l,x,y,r,mid,min:longint;
a:array[1..1000,1..100]of longint;//各个数列
len:array[0..1000]of longint;//各个数列的长度
f:array[1..100,1..100]of longint;//模式串的各个fail数组
function go(x:longint):boolean;
var i,j,k,l:longint;
ch:boolean;
begin
  for k:=1 to len[min]-x+1 do//每个点为起点的情况都搜一遍
  begin
    for l:=1 to n do//与每个数列作比较
    if l<>min then
    begin
      j:=0;
      ch:=false;
      for i:=1 to len[l] do
      begin
        //因起点不同，所以这里的以下关于j的操作要非常小心，思维不清晰很容易出错
        while (j>0)and(a[l,i]<>a[min,j+k]) do
        j:=f[k,j+k-1];
        if a[l,i]=a[min,j+k] then
        begin
          inc(j);
          if j=x then//找到该模式子串，退出
          begin
            ch:=true;
            break;
          end;
        end;
      end;
      if not ch then//如果该数列不包含该模式子串，直接退出并以模式串的下一个点为起点继续搜
      break;
    end;
    if ch then//每个数列都包含该模式子串
    exit(true);
  end;
  exit(false);
end;
begin
  readln(n);
  len[0]:=maxlongint;
  for m:=1 to n do//读入时直接把原始数列变为差分数列
  begin
    read(len[m],x);
    dec(len[m]);
    if len[m]<len[min] then
    min:=m;//最短的数列为模式串
    for l:=1 to len[m] do
    begin
      read(y);
      a[m,l]:=y-x;
      x:=y;
    end;
    readln;
  end;
  for l:=1 to len[min] do
  begin
    j:=0;
    for i:=l+1 to len[min] do//生成模式串中各个点为第一个点的fail数组
    begin
      //因起点不同，所以这里的以下关于j的操作要非常小心，思维不清晰很容易出错
      while (j>0)and(a[min,j+l]<>a[min,i]) do
      j:=f[l,j+l-1];
      if a[min,j+l]=a[min,i] then
      inc(j);
      f[l,i]:=j;
    end;
  end;
  l:=1;
  r:=len[min];
  while l<=r do//二分答案
  begin
    mid:=(l+r) div 2;
    if go(mid) then
    l:=mid+1
    else
    r:=mid-1;
  end;
  writeln(r+1);//因我们是用查分数列比较，而原始数列比查分数列多一个数，所以答案+1
end.
```