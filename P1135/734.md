这道题目并不是很难，是一道经典的广搜，详细思路在程序中给出：

```cpp
var
  h:array[0..100000,1..2] of longint;
  a:array[0..100000] of longint;
  f:array[0..100000] of boolean;
  n,x,y,i,head,tail:longint;
begin
  read(n,x,y);
  for i:=1 to n do read(a[i]);//读入
  if x=y then begin write(0); halt; end;//起点和终点在同一层，直接输出0
  head:=0;
  tail:=1;
  fillchar(f,sizeof(f),true);//所有的层都没有走过
  f[x]:=false;//起始层赋为走过，免得重复搜索
  h[1,1]:=x;//队列标记
  while head<>tail do//循环队列
    begin
      inc(head);
      if head>10000 then head:=1;
      x:=h[head,1]+a[h[head,1]];//走到的楼层
      if (x<=n) and f[x] then//可以走
        begin
          f[x]:=false;//标记为走过
          inc(tail);
          if tail>10000 then tail:=1;
          h[tail,1]:=x;
          h[tail,2]:=h[head,2]+1;//步数加一
          if x=y then begin writeln(h[tail,2]); halt; end;//找到了就直接退出，根据广搜的最优性，找到的必为最优解。
        end;
      x:=h[head,1]-a[h[head,1]];
      if (x>0) and f[x] then
        begin
          f[x]:=false;
          inc(tail);
          if tail>10000 then tail:=1;
          h[tail,1]:=x;
          h[tail,2]:=h[head,2]+1;
          if x=y then begin writeln(h[tail,2]); halt; end;
        end;//和上面一样，就不在讲解了
    end;
  writeln(-1);//因为输出后是直接退出的，所以当队列为空时，表示没有方案
end.

```