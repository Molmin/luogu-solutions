# 算法：深度优先搜索+剪枝


首先，普通深搜大家应该都写的出来：




```cpp
var
  n,a,b,i,ans:longint;
  t:array[0..500]of longint;
  procedure try(c,s:longint);
  begin
    if c=b then
    begin
      if s<ans then ans:=s;
      exit;
    end;
    if c-t[c]>0 then try(c-t[c],s+1);
    if c+t[c]<=n then try(c+t[c],s+1);
  end;
begin
  readln(n,a,b);
  for i:=1 to n do read(t[i]);
  ans:=maxlongint;
  try(a,0);
  if ans=maxlongint then ans:=-1;
  writeln(ans);
end.
```
c表示当前楼层，s表示当前按键次数，ans表示最终答案
因为最终答案应该最小所以ans初值为maxlongint。如果深搜自然结束（到不了目标层），ans就不会被更新，要把初始值maxlongint改成-1，然后输出。


但这个程序只有20分，到后来MLE。


所以，我剪了枝：





```cpp
var
  n,sta,fin,i,ans:longint;
  t,b:array[0..500]of longint;
  function min(a,b:longint):longint;
  begin if a<b then exit(a) else exit(b); end;
  procedure try(c,s:longint);
  begin
    if c=fin then
    begin
      if s<ans then ans:=s;
      exit;
    end;
    if (b[c]>0)and(s>b[c]) then exit;
    if c-t[c]>0 then
    begin
      if b[c-t[c]]=0 then b[c-t[c]]:=s+1
                     else b[c-t[c]]:=min(s+1,b[c-t[c]]);
      try(c-t[c],s+1);
    end;
    if c+t[c]<=n then
    begin
      if b[c+t[c]]=0 then b[c+t[c]]:=s+1
                     else b[c+t[c]]:=min(s+1,b[c+t[c]]);
      try(c+t[c],s+1);
    end;
  end;
begin
  readln(n,sta,fin);
  for i:=1 to n do read(t[i]);
  fillchar(b,sizeof(b),0);
  ans:=maxlongint;
  try(sta,0);
  if ans=maxlongint then ans:=-1; 
  writeln(ans);
end.
```
sta为start（开始）的缩写，fin为finish（结束）的缩写。
b[i]表示到第i层的最优解。

显然，如果当前到第i层的步数比最优解多，那接下来就没有前途了，应该剪掉。

b[i]为0表示这层还没有到达过（b[sta]除外）。


这个程序是可以过题的，时间也蛮省的，一共7ms。
