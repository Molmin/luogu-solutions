可以不用visited数组来存是否访问过的

只要在初始化的时候吧map数组设成-1，在拓展的时候检查是不是-1就行了。

还有，pascal输出还是很强大啊，下面代码两句代替四个判断，还是大爱 pascal ……

下附代码：

[codep]


```cpp
program p1443;
type
  point=record
    x,y:longint;
  end;
const
  dir:array[1..8,1..2] of longint=((1,2),(1,-2),(-1,2),      //马走日的方向
    (-1,-2),(2,1),(2,-1),(-2,1),(-2,-1));
var
  n,m,i,j,sx,sy:longint;
  h,t,x,y,newx,newy:longint;                //bfs专用……
  map:array[1..250,1..250] of longint;
  q:array[1..80000] of point;
  te:string;         //输出优化，下面有
begin
  fillchar(map,sizeof(map),255);       //fillchar 255 赋值后整个数组就变成-1啦！
  readln(n,m,sx,sy);
  map[sx,sy]:=0;  //起始点不走就到了
  t:=1; h:=0;
  q[t].x:=sx; q[t].y:=sy;
  while h<t do
    begin
      inc(h);
      x:=q[h].x;
      y:=q[h].y;
      for i:=1 to 8 do
        begin
          newx:=x+dir[i,1];
          newy:=y+dir[i,2];
          if (newx>=1) and (newx<=n) and (newy>=1) and
            (newy<=m) and (map[newx,newy]=-1) then     //这里就可以少一个visited啦
              begin
                map[newx,newy]:=map[x,y]+1;
                inc(t);
                q[t].x:=newx; q[t].y:=newy;
              end;
        end;
    end;
  for i:=1 to n do
    begin
      for j:=1 to m do
        begin
          str(map[i,j],te);                         //输出优化：把数字转成字符串，按长度补空位
          write(te,'':5-length(te));
        end;
      writeln;
    end;
end.
[/codep]
```