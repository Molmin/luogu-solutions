# 题意很简单：
每输入一个数，就从大到小排序，并用题目给出的公式计算出分数线，输出。

## 第一次提交（插排）：
```pascal
var
  n,w,i,x,j,p:longint;
  a:array[0..100005]of longint;
function max(x,y:longint):longint;//找最大值
begin
  if x>y then exit(x);
  exit(y);
end;
begin
  readln(n,w);//读入人数和百分比
  for i:=1 to n do
  begin
    read(x);//读入每个分数
    j:=i;
    while(j>1)and(a[j-1]<x)do begin a[j]:=a[j-1]; dec(j); end;//逐个读入用插排
    a[j]:=x;
    p:=max(1,trunc(i*w/100));//计算
    write(a[p],' ');//输出
  end;
end.
```
## 然而85分，T了3个点……
## 插排不行，就用快排！
## 第二次提交（快排）：
```pascal
var
  n,w,i,p:longint;
  a:array[0..100005]of longint;
function max(x,y:longint):longint;//求最大值
begin
  if x>y then exit(x);
  exit(y);
end;
procedure sort(l,r:longint);//快排
var
  i,j,x,y:longint;
begin
  i:=l;
  j:=r;
  x:=a[(l+r) div 2];
  repeat
    while a[i]>x do inc(i);
    while x>a[j] do dec(j);//从大到小
    if not(i>j) then
    begin
      y:=a[i];
      a[i]:=a[j];
      a[j]:=y;
      inc(i);
      j:=j-1;
    end;
  until i>j;
  if l<j then sort(l,j);
  if i<r then sort(i,r);
end;
begin
  readln(n,w);//输入选手个数和百分比
  for i:=1 to n do
  begin
    read(a[i]);//读入分数
    sort(1,i);//快排调用
    p:=max(1,trunc(i*w/100));//计算分数线
    write(a[p],' ');//输出
  end;
end.
```
## 然而分数更少，只有50分，T了10个点……
## 好吧，明显，快排更慢……
## 看了一下题目，发现分数最大只有600，那就用桶排吧。
# AC code:
```pascal
var
  n,w,i,x,j,p,s:longint;
  a:array[0..605]of longint;
function max(x,y:longint):longint;//求最大值
begin
  if x>y then exit(x);
  exit(y);
end;
begin
  readln(n,w);//输入选手分数和百分比
  for i:=1 to n do
  begin
    read(x);
    inc(a[x]);//放入桶，累加当前分数的选手人数
    p:=max(1,trunc(i*w/100));//计算分数线
    s:=0;
    for j:=600 downto 0 do//从大到小找
    begin
      s:=s+a[j];//累加人数
      if s>=p then begin write(j,' '); break; end;//如果当前人数到达，那么就输出
    end;
  end;
end.
```
## 这是为什么呢？
因为，插排的效率不够快，n到达10^5，时间复杂度为O(n^2)，插排只能拿一部分的分。

快排，后来想了想，纯快排时间复杂度为O(n log n)，可是外面加了层循环，时间复杂度就变成了O(n^2 log n)了。

而桶排寻找的关键字是分数，而不是人数，所以时间复杂度为O(n*600)，显然不会T。

# ~~求管理员大大给过QwQ~~