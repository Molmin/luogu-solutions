这题n,m最大只有300（水），n(m)^3能过，但再大些就会超时。

我用的悬线法， 参考：王知昆《浅谈用极大化思想解决最大子矩形问题》（算法2）

https://wenku.baidu.com/view/728cd5126edb6f1aff001fbb.html

时间复杂度 O（MN）。

pascal代码

```pascal
uses math;
var
  n,m,i,j,k,l1,r1,ans,s:longint;
  a,h,l,r,f:array[0..1000,0..1000] of longint;
//h为悬线高，l,r为左右最多能移到的位置
begin
  readln(n,m);
  for i:=1 to n do
    begin
      for j:=1 to m do
        begin
          read(a[i,j]);
          f[i,j]:=f[i,j-1]+a[i,j];//前缀和
        end;
      readln;
    end;
  for i:=1 to m do r[0,i]:=m;//初始化，后面用的min
  for i:=1 to n do
    begin
      l1:=0; r1:=m+1;
      for j:=1 to m do
          if a[i,j]=0 then
            begin
              h[i,j]:=0;//等于没有
              l[i,j]:=1; r[i,j]:=m;
              l1:=j;//记录 左侧为0的点中最右边的
            end
                      else
            begin
              h[i,j]:=h[i-1,j]+1;
              l[i,j]:=max(l[i-1,j],l1+1);
            end;
      for j:=m downto 1 do//倒推，找的是右侧为0的点中最左边的
        if a[i,j]=0 then
          r1:=j
                      else
            begin
              r[i,j]:=min(r[i-1,j],r1-1);
              s:=0;
              for k:=i downto i-h[i,j]+1 do//偷懒，就直接枚举了
                inc(s,f[k,r[i,j]]-f[k,l[i,j]-1]);
              ans:=max(ans,s);
            end;
    end;
  write(ans);
end.
```