这是1道曾经是 普及- 的题目..

光看样例没什么感觉..先暴力找找规律..

于是发现.

....当$f(i) = i$时,$g(j) = i(i < j <= n)$.

....当$g(j) = j$时,必定满足条件.

很好证明.因为$f(g(i)) = g(f(i))$.

当$f(i) = i$时,$f(g(i)) = g(i)$.相当于$f(x) = x(x = g(i))$.所以$g(i)$取满足条件$f(i) = i$最小的i.

当$g(j) = j$时,$f(j) = g(f(j))$.相当于$g(x) = x(x = f(j))$.(就是前面那个$g(j) = j$...)成立.

这时候,$g(j)$确定,$f(j)$已知,那么就能知道$g(f(j))$,进而知道$f(g(j)),g(f(g(j)))$..这样能推出全部的$g(j)$.

具体的.若$g(j)$已被确定.则跳过.否则若满足第1条.则$g(j) = i$,否则$g(j) = j$.

主要代码..
```
i = 1;
while (i <= n)
    if (g[i]){
        gsum = f[g[i]];
        fsum = f[i];
        while (!g[fsum]){
            g[fsum] = gsum;
            gsum = f[g[fsum]];
            fsum = f[fsum];
        }
        while (g[++i]);
```
其实就是1个找环的过程........
```
f(i) -> g(f(i)) -> f(g(f(i))) ->.....
```
可以看出.对于
```
f = (2, 3, 4, 5, 1)
f(1) -> f(f(1)) -> f(f(f(1))) ->.....
2    -> 3       -> 4          ->.....
```
是1个长度为5的环.

可是..只用这2个性质.50..

似乎还有更优的方法.?

强行把$g(j) = j$改成$g(j) = 1$.30..

取最小的没选过的.?50..没有区别..

沉寂2天..

自己造数据对拍.固定$n = 10,f(2) = 2$.

真的发现1组不对的数据..~~(想不起来.).~~

简单栗子.
```
f = (4, 3, 2, 1);
g = (1, 2, 3, 4);//错误.
g = (1, 1, 4, 4);//正确.
```
**原来2个同长度的环可以互换.**.

这样.只要取字典序最小的.就是最优的了..

然而....**长度能整除的换都能被更换..**.

栗子.
```
f = (6, 3, 4, 5, 2, 1, 8, 9, 10,7);
g = (1, 1, 6, 1, 6, 6, 1, 6, 1, 6);
```
需要所有能被整除的长度中取字典序最小..

这样.开头说的第1条.$f(i) = i$说明是个长度为1的环..所以可以替换所有的环..

还有1个重要的条件.题目已经说明给出的$f(i)$是全排列..

那就没有交叉环的现象了..也能解释开头的弟2条..有没有解倒是没研究过..

用$sum[i]$记录第i位的环的长度.$f[i]$记录长度为i的环字典序最小的开头.

~~(sum[i]写成[i]..)..~~

先设$k = i$.枚举$sum[i]$所有的约数.用最小的$f[x]$替换$k$.

80W有点大...只要枚举$\sqrt{sum[i]}$.就可以过了....

($n*\sqrt{\sqrt{80W}}???$)....

另外,由于不同长度的环最多不超过1500种.也可以对环长度离散后暴力判断..

Diu代码..
```
//第2种.
program P2582;
 var
  a,b,sum,f,s,fst:array[0..800001] of longint;
  i,ij,j,k,p,n,nf:longint;
 begin
  readln(n);
  for i:=1 to n do
   read(a[i]);             //f数组.
  readln;
  fillchar(b,sizeof(b),0); //g数组.
  for i:=1 to n do
   sum[i]:=1;              //统计环长度.
  fillchar(f,sizeof(f),0); //统计环长度为i的字典序最小的开头..
  for i:=1 to n do         //先找环长度..
   if b[i]=0 then
    begin
     p:=i;
     while a[p]<>i do
      begin
       inc(sum[i]);
       b[p]:=1;
       p:=a[p];
      end;
     if f[sum[i]]=0 then f[sum[i]]:=i;
    end;
  fillchar(b,sizeof(b),0);
  nf:=0;                   //环长度不同的个数..
  for i:=1 to n do
   if f[i]<>0 then
    begin
     inc(nf);
     f[nf]:=f[i];
     s[nf]:=i;             //记录长度.
    end;
  for i:=1 to nf do
   for j:=1 to nf do
    if (s[i] mod s[j]=0) and (f[j]<f[i]) then f[i]:=f[j];                //字典序修改.
  for i:=1 to nf do
   fst[s[i]]:=f[i];        //还原.
  for i:=1 to n do
   if b[i]=0 then          //重新再跑1遍环..求出g数组.
    begin
     k:=fst[sum[i]];
     p:=i;
     while b[p]=0 do
      begin
       b[p]:=k;
       k:=a[b[p]];
       p:=a[p];
      end;
    end;
  for i:=1 to n-1 do
   write(b[i],' ');
  writeln(b[n]);
 end.
```
```
//第1种..
program P2582;
 var
  a,b,sum,f:array[0..800001] of longint;
  i,ij,j,k,p,n:longint;
 begin
  readln(n);
  for i:=1 to n do
   read(a[i]);
  readln;
  fillchar(b,sizeof(b),0);
  for i:=1 to n do
   sum[i]:=1;
  filldword(f,sizeof(f)>>2,1008208820);
  for i:=1 to n do
   if b[i]=0 then
    begin
     p:=i;
     while a[p]<>i do //先找长度..
      begin
       inc(sum[i]);
       p:=a[p];
      end;
     k:=i;
     for j:=1 to n do
      if j*j<=sum[i] then //枚举长度..
       begin
        if (sum[i] mod j=0) then
         begin
          p:=sum[i] div j;
          if f[j]<k then k:=f[j];
          if f[p]<k then k:=f[p];//对2个约数判断字典序.
         end;
       end
                     else break;
     if f[sum[i]]>=k then f[sum[i]]:=k;//修改字典序最小..
     p:=i;    //重新复原g数组..
     while b[p]=0 do
      begin
       b[p]:=k;
       k:=a[b[p]];
       p:=a[p];
      end;
    end;
  for i:=1 to n-1 do
   write(b[i],' ');
  writeln(b[n]);
 end.
```
(ಡωಡ).
