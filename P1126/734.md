显然这是一道经典的广搜题目，使用BFS，可以用O(4MN)的时间通过，对于m，n只有50的情况，时间绰绰有余！！

然而这道题目不同于普通的广搜，有方向限制，因此我们把数组再增加一维，表示方向，并增加专项的功能，代码：

```delphi
var  
  fang:array[1..4,1..3,1..2] of longint;  
  h:array[0..300000,1..4] of longint;  
  a:array[0..51,0..51] of longint;  
  f:array[0..51,0..51,1..4] of boolean;  
  b:array[0..51,0..51] of boolean;  
  head,tail,i,j,k,s,n,m,x,y,z,xx,yy,zz,x1,x2,y1,y2:longint;  
  ch:char;  
function pre(sx:longint):longint;  
begin  
  if sx=1 then exit(4) else exit(sx-1);  
end;  
function next(sx:longint):longint;  
begin  
  if sx=4 then exit(1) else exit(sx+1);  
end;  
begin  
  for i:=1 to 3 do  
    begin  
      fang[1,i,1]:=-i;  
      fang[2,i,2]:=-i;  
      fang[3,i,1]:=i;  
      fang[4,i,2]:=i;  
    end;  //方位坐标
  readln(m,n);  
  for i:=1 to m do  
    for j:=1 to n do  
      read(a[i,j]);  
  dec(m); dec(n);  
  for i:=1 to m do  
    for j:=1 to n do  
      if (a[i,j]=0) and (a[i+1,j]=0) and (a[i,j+1]=0) and (a[i+1,j+1]=0) then  
        begin  
          for k:=1 to 4 do  
            f[i,j,k]:=true;  
          b[i,j]:=true;  
        end;  //预处理
  read(x1,y1,x2,y2);  
  if (x1=x2) and (y1=y2) then begin writeln(0); halt; end;  
  if not b[x1,y1] or not b[x2,y2] then begin writeln(-1);halt; end;//特殊情况判断  
  read(ch);  
  read(ch);  
  case ch of  
    'N':s:=1;  
    'W':s:=2;  
    'S':s:=3;  
    'E':s:=4;  
  end;  //初始方位
  f[x1,y1,s]:=false;  
  h[1,1]:=x1;  
  h[1,2]:=y1;  
  h[1,3]:=s;  
  head:=0;  
  tail:=1;  
  while head<tail do  
    begin  
      head:=head mod 300000+1;  //循环队列
      x:=h[head,1];  
      y:=h[head,2];  
      z:=h[head,3];  
      zz:=pre(z); //左转 
      if f[x,y,zz] then  
        begin  
          tail:=tail mod 300000+1;
          h[tail,1]:=x;  
          h[tail,2]:=y;  
          h[tail,3]:=zz;  
          h[tail,4]:=h[head,4]+1;  
          f[x,y,zz]:=false;  
        end;  
      zz:=next(z);  //右转
      if f[x,y,zz] then  
        begin  
          tail:=tail mod 300000+1;
          h[tail,1]:=x;  
          h[tail,2]:=y;  
          h[tail,3]:=zz;  
          h[tail,4]:=h[head,4]+1;  
          f[x,y,zz]:=false;  
        end; //之所以不再转向除放置到终点的判断，是因为转向的方位，即横纵坐标不变，前一次没有到，这次怎么可能到呢？ 
      for i:=1 to 3 do//往前走1,2,3步  
        begin  
          xx:=x+fang[z,i,1];  
          yy:=y+fang[z,i,2];  
          if f[xx,yy,z] then  
            begin  
              tail:=tail mod 300000+1;  
              h[tail,1]:=xx;  
              h[tail,2]:=yy;  
              h[tail,3]:=z;  
              h[tail,4]:=h[head,4]+1;  
              f[xx,yy,z]:=false;  
              if (xx=x2) and (yy=y2) then  
                begin  
                  writeln(h[tail,4]);  
                  halt;  //直接输出并退出程序
                end;  
            end else if not b[xx,yy] then break;  //中间已经有障碍，直接跳过不走了
        end;  
    end;  
  writeln(-1); //无解 
end. [codep/]