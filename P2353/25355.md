#作为第一个过此题的P党，我果断选择发波题解造福广大P党的朋友们

因为单词的数量很少，所以我们可以用一个数组sum保存到第i位时第j个单词出现了几次

那么，其实此题的思路其实就是kmp+前缀和。

注意两个地方：

1.每个单词的长度一定要用一个数组保存好，如果每次都是去length，那只有90。

2.最后对于每个区间[l,r]，答案为∑（下标为j=1,上面为n）sum[r,j]-sum[l+len[j]-2,j]，这个的原因主要是蒟蒻的我的方法有点鬼畜。。。

##具体看代码。。。

代码：

```cpp
var
  sum:array[0..1000000,1..10]of longint;
  word:array[0..11]of ansistring;
  len:array[0..11]of longint;
  n,m,i,j,l,r,x,ans:longint;
  s:ansistring;
procedure kmp(k:longint);
var
  next:array[0..1000000]of longint;
  i,j,tt,zz:longint;
  x:ansistring;
begin
  fillword(next,sizeof(next) div 2,0);
  j:=0;
  x:=word[k];
  for i:=2 to len[k] do 
    begin
      while (j>0)and(x[j+1]<>x[i]) do j:=next[j];
      if x[j+1]=x[i] then inc(j);
      next[i]:=j;
    end;//kmp的常规操作——计算next数组
  j:=0;
  tt:=0;
  for i:=1 to length(s) do
    begin
      sum[i,k]:=tt;
      while (j>0)and(x[j+1]<>s[i]) do j:=next[j];
      if x[j+1]=s[i] then inc(j);
      if j=len[k] then 
        begin
          inc(tt);
          j:=next[j];
          sum[i,k]:=tt;
        end;
    end;//在文章中匹配第k个单词
end;//kmp，计算前缀和
begin
  readln(n,m);
  readln(s);
  for i:=1 to n do 
    begin
      readln(word[i]);
      len[i]:=length(word[i]);//注意单词的长度一定要用一个数组存储
    end; 
  for i:=1 to n do kmp(i);//匹配每个单词
  for i:=1 to m do 
    begin
      readln(x,r);
      ans:=0;
      for j:=1 to n do
        begin
          if r-x+1<len[j] then continue;//如果区间的长度比这个单词的长度要小，那个这个单词肯定不会出现在这个区间里
          l:=x;
          inc(l,len[j]-1);//精髓，自己理解
          inc(ans,sum[r,j]-sum[l-1,j]);
        end;
      writeln(ans);
    end;
end.
```