这道题是典型的高精度+进制转换

通过数组模拟高精度数位，进行N进制加法来求得结果。

这道题还是比较难的，因为既要判断回文还要用高精度进行加法。

代码思路：

1：读入，采用字符串读入，用数组存储

2：如果读入的是0的话那么就输出0（data可能没这个点，因为我输出的是0不是STEP=0）

3：求得A的反序数B，并进行N进制高精度加法

4：判断位数是否超过30或者是否为回文数

5：输出，搞定。

贴个代码，建议不要抄，先可根据代码思路来尝试写代码，然后如果有困难在看一下代码参考。



```cpp
type arr=array[0..101] of longint;
var
  n,i,ans:longint;
  a,b:arr;
procedure init(var a:arr);                       //将数串s转化为整数数组a
var
  s:string;
  i:longint;
begin
  readln(n);
  readln(s);                                  //读入字符串s
  fillchar(a,sizeof(a),0);                        //数组a清0
  a[0]:=length(s);                             //用a[0]储存字符串s的位数
  for i:=1 to a[0] do
   if (s[i]>='0') and (s[i]<='9') then
     a[a[0]-i+1]:=ord(s[i])-ord('0')
   else a[a[0]-i+1]:=ord(s[i])-ord('A')+10;
end;
function check(a:arr):boolean;                   //判别整数数组a是否为回文数
var i:longint;
begin
  for i:=1 to a[0] do
    if a[i]<>a[a[0]-i+1] then exit(false);
  exit(true);
end;
procedure jia(var a:arr);                        //整数数组a与其反序数b进行n进制加法运算
var i,k:longint;
begin
  for i:=1 to a[0] do b[i]:=a[a[0]-i+1];            //反序数b
  for i:=1 to a[0] do a[i]:=a[i]+b[i];              //逐位相加
  for i:=1 to a[0] do
  begin
    a[i+1]:=a[i+1]+a[i] div n;                  //处理进位
    a[i]:=a[i] mod n;
  end;
  if a[a[0]+1]>0 then inc(a[0]);                 //修正新的a的位数（a+b最多只能多一个位数）
end;
begin
  //assign(input,'huiwen.in'); reset(input);
  //assign(output,'huiwen.out'); rewrite(output);
  init(a);//输入A
  if check(a) then                            //判断a本身就是回文数的情况
  begin
    writeln(‘STEP=0’);//直接输出STEP=0
    halt;
  end;
  ans:=0;                                   //步数初始化为0
  while (ans<30) do//步数必须在30以内
  begin
    inc(ans);//步数加一
    jia(a);//加法
    if check(a) then//如果A是回文那么就输出步数
    begin
      writeln('STEP=',ans);//STEP是大写的！！！
      halt;
    end;
  end;
  writeln('Impossible!');                        //输出无解信息，注意感叹号是英文的
  //close(output);
end.

```