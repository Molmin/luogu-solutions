### 这题不是暴力就能过的吗
我们看看题目说了什么
- 题目说：质因数，我知道了，用到**筛法**
- 题目说：合并，我知道了，用到**并查集**
- 题目说：A≤B≤100000；我知道了，**暴力**就能过

代码步骤：

- 线筛求出范围内的所有质数
- 枚举p~b的质数，合并它的倍数
- 统计有几个不同的祖先

Code：
```pascal
var
    prime, f : array[0..1000000] of longint;
    flag : array[0..1000000] of boolean;
    a, b, p, i, j, tot, ans, f1 : longint;

function find(k : longint) : longint;

begin
    if k <> f[k] then f[k] := find(f[k]);
    find := f[k];
end;

begin
    readln(a, b, p);
    for i := 2 to b do//线筛
    begin
        if not flag[i] then
        begin
            inc(tot); prime[tot] := i;
        end;
        for j := 1 to tot do
        begin
            if i * prime[j] > b then break;
            flag[i * prime[j]] := true;
            if i mod prime[j] = 0 then break;
        end;
    end;
    for i := 1 to b do f[i] := i;
    for i := 1 to tot do
        if prime[i] >= p then
        begin
            f1 := find(prime[i]);
            for j := 1 to b do//合并
            begin
                if j * prime[i] > b then break;
                f[find(j * prime[i])] := f1;
            end;
        end;
    fillchar(flag, sizeof(flag), 0);
    for i := a to b do//统计
        if not flag[find(i)] then
        begin
            flag[find(i)] := true;
            inc(ans);
        end;
    writeln(ans);
end.
```