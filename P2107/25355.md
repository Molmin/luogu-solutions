**没人发题解**

###通过率真的是低的可以（主要是样例过水导致的）

一开始看到题目没有一点头绪。。。

想了一会，然后就用时间复杂度n^3的dp乱搞，60分（喜滋滋）。

简要讲下：

f[i,j]表示去了i个机房,最后一个为j

**转移方程：f[i,j]:=x[j]+t[j]+min{f[i-1,k]}**

接下来就去看通过的dalao的代码，居然有Splay（蒟蒻的我一脸懵逼），不过最终还是找到了一个我看得懂的：堆。

思路是这样的：

先对坐标进行排序，然后去枚举1到n个机房。

如果当前机房的坐标（注意是坐标）大于m就跳出循环，然后假设我们当前机房为要到的最后一个，那么他要**走的**路程就应该为x[i]（之前已经对坐标排过序了），那么剩下的体力，就要去AK了。

用一个大根堆保存从第一个机房到当前机房**去的**机房的t[]，用一个变量保存当前大根堆中保存着的t[]的总和，然后把当前的t[i]加入大根堆（堆的常规操作put）,总和加上t[i]，接下来用个while循环，如果当前去的机房的总和小于剩下的体力就退出，否则总和需减去堆中最大那个值，也就是堆顶heap[1]（这里有种贪心的思想在里面，想要去得多，那么我们就要尽可能去t[]小的，少去大的），然后把堆顶的元素从大根堆中删除（堆的常规操作get），那么最后我去的机房数就应为大根堆里的元素个数，再在这些元素个数中取一个max为我们要的答案。

整个过程就是这样，本蒟蒻居然花了一下午去调试，真的是妖怪吧！

接下来献上我那没有jzl代码丑陋的代码

60分dp代码：

```cpp
uses math;
var
  x,y:array[0..100000]of int64;
  f:array[0..1001,0..1001]of qword;
  n,i,j,k,ans,x1:longint;
  m:int64;
procedure kp(l,r:longint);
var
  i,j:longint;
  mid:int64;
  begin
    i:=l;
    j:=r;
    mid:=x[(l+r) >> 1];
    repeat
      while x[i]<mid do inc(i);
      while x[j]>mid do dec(j);
      if i<=j then 
        begin
          x[0]:=x[i];x[i]:=x[j];x[j]:=x[0];
          y[0]:=y[i];y[i]:=y[j];y[j]:=y[0];
          inc(i);
          dec(j);
        end;
    until i>j;
    if i<r then kp(i,r);
    if l<j then kp(l,j);
  end;//快排
begin
  readln(n,m);
  for i:=1 to n do readln(x[i],y[i]);
  kp(1,n);
  for i:=1 to n do 
    begin
      f[1,i]:=x[i]+y[i];
      if x[i]+y[i]<=m then ans:=1;
    end;//边界
  for i:=2 to n do 
    for j:=i to n do 
      begin
        f[i,j]:=maxlongint shl 31;
        x1:=0;
        for k:=i-1 to j-1 do
          if f[i-1,k]<f[i,j] then begin f[i,j]:=f[i-1,k];x1:=k; end;
        inc(f[i,j],x[j]+y[j]-x[x1]);
        if f[i,j]<=m then 
          if i>ans then ans:=i; 
      end;//dp
  write(ans);
end.
```
AC代码：


```cpp
var
  heap,x,y:array[0..200001]of int64;
  n,i,j,len,ans:longint;
  m,sum,zz:int64;
procedure swap(var x,y:int64);
var
  t:int64;
  begin
    t:=x;
    x:=y;
    y:=t;
  end;
procedure kp(l,r:longint);
var
  i,j:longint;
  mid:int64;
  begin
    i:=l;
    j:=r;
    mid:=x[(l+r) >> 1];
    repeat
      while x[i]<mid do inc(i);
      while x[j]>mid do dec(j);
      if i<=j then 
        begin
          x[0]:=x[i];x[i]:=x[j];x[j]:=x[0];
          y[0]:=y[i];y[i]:=y[j];y[j]:=y[0];
          inc(i);
          dec(j);
        end;
    until i>j;
    if i<r then kp(i,r);
    if l<j then kp(l,j);
  end;//对坐标进行排序
procedure put(x:longint);
var
  son:longint;
  begin
    inc(len);
    heap[len]:=x;
    son:=len;
    while (son<>1)and(heap[son]>heap[son >> 1]) do
      begin
        swap(heap[son],heap[son >> 1]);
        son:=son >> 1;
      end; 
  end;//堆的常规操作put，不懂的同学可以去查百度
procedure get;
var
  fa,son:longint;
  begin
    dec(len);
    heap[1]:=heap[len];
    fa:=1;
    while (fa*2<=len)or(fa*2+1<=len) do 
      begin
        if (fa*2+1>len)or(heap[fa*2]>heap[fa*2+1]) 
          then son:=fa*2
          else son:=fa*2+1;
        if heap[fa]<heap[son] then 
          begin
            swap(heap[fa],heap[son]);
            fa:=son;
          end
          else break;
      end;
  end;//堆的常规操作get
begin
  readln(n,m);
  for i:=1 to n do readln(x[i],y[i]);
  kp(1,n);
  for i:=1 to n do 
    begin
      zz:=m-x[i];
      if zz<0 then break;
      put(y[i]); inc(sum,y[i]);
      while sum>zz do 
        begin
          dec(sum,heap[1]);
          get;
        end;
      if len>ans then ans:=len;
    end;//上面应该讲的很清楚了 ~~妖怪吧~~
  write(ans);
end.

```