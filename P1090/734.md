看到合并果子这道题目，果断用堆排。

其实单调队列也能过，而且代码复杂度很低很低（至少比堆低），但是我个人认为，堆是一个非常有用的东西。它可以用logn的时间完成插入和查询，因此这里讲述一下堆排。以最小堆为例。



概念：首先堆是一棵完全二叉树（不解释了），每一棵子树都必须保证根节点<=叶节点，每一刻子树都符合这样规律的堆被称为最小堆。


插入：首先把这个节点放在最后，然后逐一与父节点比较，不断地交换，直到到达根节点或不能交换位置为止（即比父节点大了），由于最多logn层，所以对于n个数，时间复杂度为nlongn


输出：首先将根节点输出，然后将最后一个节点放在根节点处，调整堆，即将这个节点不断向下找。直到满足最小堆的性质为止。


补充：对于没一个节点（根节点除外），其父节点编号为i div 2，孩子节点（叶节点除外）为2i以及2i+1

  
现在就是合并果子了，首先将最小的两个节点合并，即将1、2或1、3合并。然后将最后一个节点放在首位，将这个合并好的节点放在第二位，不断调整堆即可。总的时间复杂度为O(NlogN）


详细算法见标程：

```cpp
var
  n,i,ans,k,ii,t,kk:longint;
  a:array[0..100000] of longint;
begin
  read(n);
  for i:=1 to n do
    begin
      read(a[i]);
      k:=i;
      while (k>1) and (a[k]<a[k shr 1]) do//如果能调整
        begin
          t:=a[k];
          a[k]:=a[k shr 1];
          a[k shr 1]:=t;
          k:=k shr 1;//更新它的位置
        end;//调整堆
    end;//建立一个最小堆
  for i:=n downto 2 do
    begin
      if i=2 then k:=2 else
      if a[2]<a[3] then k:=2 else k:=3;//取两个中小的那个
      a[k]:=a[1]+a[k];
      ans:=ans+a[k];//累加所需的力气
      if i=2 then break;//合并完成，退出并输出
      a[1]:=a[i];
      a[i]:=maxlongint;//注意要将这个点赋为maxlongint，否则这个点可能被重复计算
      while (k<(i+1) shr 1) and ((a[k]>a[k shl 1+1]) or (a[k]>a[k shl 1])) do
        begin
          if a[k shl 1+1]>a[k shl 1] then k:=k shl 1 else k:=k shl 1+1;
          t:=a[k];
          a[k]:=a[k shr 1];
          a[k shr 1]:=t;
        end;//首先调整第二个节点
      k:=1;
      while (k<(i+1) shr 1) and ((a[k]>a[k shl 1+1]) or (a[k]>a[k shl 1])) do
        begin
          if a[k shl 1+1]>a[k shl 1] then k:=k shl 1 else k:=k shl 1+1;
          t:=a[k];
          a[k]:=a[k shr 1];
          a[k shr 1]:=t;
        end;//再调整根节点
    end;
  writeln(ans);
end.
```