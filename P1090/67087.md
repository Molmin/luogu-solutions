1、算法分析

将这个问题换一个角度描述：给定n个叶结点，每个结点有一个权值w[i],将它们中两个、两个合并为树，假设每个结点从根到它的距离是d[i]，使得最终∑（wi*di）最小

于是，这个问题就变为了经典的Huffman树问题。Huffman树的构造方法如下：

（1）从森林里取两个权和最小的结点；
（2）将它们的权和相加，得到新的结点，并且把原结点删除，将新结点插入到森林中；
（3）重复（1）~（2），直到森林里只有一棵树。

2、数据结构

很显然，问题当中需要执行的操作是：（1）从一个表中取出最小的数  （2）插入一个数字到这个表中。   支持动态查找最小数和动态插入操作的数据结构，我们可以选择用堆来实现。因为取的是最小元素，所以我们要用小根堆来实现。

用堆的关键部分是两个操作：put操作，即往堆中加入一个元素；get操作，即从堆中取出并删除一个元素。

3、操作实现

整个程序开始时通过n次get操作建立一个小根堆，然后不断重复如下操作：两次get操作取出两个最小数累加起来，并且形成一个新的结点，再插入到堆中。如1+1=2，再把2插入到堆的后面一个位置，然后从下往上调整，使得包括2在内的数组满足堆的性质。


代码
Pascal
```

const maxn=10000;
var heap:array[1..maxn]of longint;
    ans,n,len,a,b,i,t:longint;
procedure put(x:longint);
var son,t:longint;
begin
	len:=len+1;
	heap[len]:=x;
	son:=len;
	while(son<>1)and(heap[son div 2]>heap[son])do
		begin
			t:=heap[son div 2];
			heap[son div 2]:=heap[son];
			heap[son]:=t;
			son:=son div 2;
		end;
end;
function get:longint;
var fa,son,t:longint;
    s:boolean;
begin
    get:=heap[1];
	heap[1]:=heap[len];
	len:=len-1;
	fa:=1;
	s:=false;
	while((fa*2<=len)or(fa*2+1<=len))and(not s)do
		begin
			if(fa*2+1>len)or(heap[fa*2]<heap[fa*2+1])
				then son:=fa*2 else son:=fa*2+1;
			if heap[fa]>heap[son]then
				begin
					t:=heap[fa];
					heap[fa]:=heap[son];
					heap[son]:=t;
					fa:=son;
				end
			else s:=true;
		end;
end;
begin
	len:=0;
	readln(n);
	for i:=1 to n do
		begin
			read(t);
			put(t);
		end;
	ans:=0;
	for i:=1 to n-1 do
		begin
			a:=get;
			b:=get;
			ans:=ans+a+b;
			put(a+b);
		end;
	writeln(ans);
end.

~~打字打得手酸~希望通过~~