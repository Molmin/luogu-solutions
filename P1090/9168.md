用堆(虽然我用得不好，但我也只会这方法了)，还是很高效的

```delphi

var
  n,i,ans:longint;
  a:array[0..10000] of longint;

procedure swap(var x,y:longint);
var t:longint;
begin
  t:=x;
  x:=y;
  y:=t;
end;

procedure wh(i:longint);
begin
  while a[i]<a[i div 2] do
  begin
    swap(a[i],a[i div 2]);
    i:=i div 2;
  end;
end;

procedure wh2(i:longint);
begin
 while i*2<=n do
 begin
  i:=i*2;
  if (a[i]>a[i+1])and(i+1<=n) then inc(i);
  if a[i]<a[i div 2] then swap(a[i],a[i div 2])
  else exit;
 end;
end;

begin
  readln(n);
  for i:=1 to n do
  begin
    read(a[i]); //加入堆
    wh(i); //保持最小堆
  end; 

  while n>1 do //n=1说明合并完了
  begin
   swap(a[1],a[n]);
   dec(n);
   wh2(1); //找出第二小的，同时保持最小堆
   inc(a[1],a[n+1]); //合并两个最小数
   inc(ans,a[1]);  //计算答案
   wh2(1); //保持最小堆
  end;

  writeln(ans);
end.

```