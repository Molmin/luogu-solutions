## pascal的题解好少……

### 看了题面和数据范围，$O(n)$算法过不去，只能是数位$dp$了。

这题需要维护的条件有点多，我们一个个来看：

#### 1、不得出现$4$和$8$。

这很简单，用一个布尔变量判断即可，注意是“和”，而不是“或”。

#### 2、这是手机号码。

所以，号码中不可能出现前导$0$，对于第一位，我们可以从$1$开始枚举，这样就避免了前导$0$的判断。

#### 3、数中至少有连续三个数字相同。

既然没有了前导$0$，这点也就很好处理了。用两个数分别表示当前数位的前一位和再前一位的数，每一次判断三个数位是否相等即可。

那么，本题结束，注意$f$数组得记录$6$个数：当前数位所在位、前一位、再前一位、是否有$4$、是否有$8$、是否存在连三。

### 代码如下：

```pascal
var f:array[0..12,0..9,0..9,0..1,0..1,0..1] of int64; //恐怖的六维数组，更大维数的我只在八数码难题中打出过……
    l,r:int64;
    a:array[0..12] of longint;
function dfs(len,beg1,beg2:longint;ff,you4,you8,lian:boolean):int64; //记忆化搜索解决数位dp
var tot:int64;
    t1,t2,t3,endd,i:longint;
begin
  tot:=0;t1:=0;t2:=0;t3:=0;
  if you4 then t1:=1;if you8 then t2:=1;if lian then t3:=1;
  if you4 and you8 then exit(0); //注意是and
  if len=0 then exit(t3);
  if (not ff) and (f[len,beg1,beg2,t1,t2,t3]<>-1) then exit(f[len,beg1,beg2,t1,t2,t3]);
  if ff then endd:=a[len]
  else endd:=9;
  for i:=0 to endd do
    inc(tot,dfs(len-1,i,beg1,ff and (i=endd),you4 or (i=4),you8 or (i=8),lian or (beg1=beg2) and (beg1=i)));
  if not ff then f[len,beg1,beg2,t1,t2,t3]:=tot;
  exit(tot);
end;
function query(r:int64):int64;
var len,i:longint;
    ans:int64;
begin
  len:=0;ans:=0;
  fillchar(f,sizeof(f),255);
  while r>0 do begin inc(len);a[len]:=r mod 10;r:=r div 10;end;
  if len<11 then exit(0); //这里很坑！如果l是1e10，那么l-1就不是电话号码了，但结果还会把它记录下来，所以要特判！！
  for i:=1 to a[len] do inc(ans,dfs(len-1,i,0,i=a[len],i=4,i=8,false)); //这里同样要注意：枚举的数就已是4或8，或者满足limit条件
  exit(ans);
end;
begin
  readln(l,r);
  writeln(query(r)-query(l-1));
end.
```