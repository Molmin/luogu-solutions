**P党的福利**

来发dp的P党题解

状态:fmax[i,j]//表示前i个数分成j个部分的最大值

fmin[i,j]//表示前i个数分成j个部分的最小值

边界:fmax[i,1]:=(sum[i] mod 10+10) mod 10（sum[i]为前i个数的总和）;fmin[i,1]:=(sum[i] mod 10+10) mod 10;

状态转移方程:

fmax[i,j]:=max(fmax[i,j],fmax[k,j-1]\*ff(sum[i]-sum[k]));

fmin[i,j]:=min(fmin[i,j],fmin[k,j-1]\*ff(sum[i]-sum[k]){ff为取sum[i]-sum[k]对10取余的结果});//找一个中间点，把1到k分j-1个部分，而之前我们已经做出了决策，答案保存在f[k,j-1]里，另外k+1到i看成一部分，利用前缀和求出从k+1到i的值。


当然，我认为推出这状态转移方程并不是很难（连本蒟蒻都推出来了），本题还有一个难点——处理环。

我对此的解决方法是：把环看成一条链，旋转出这条环所有的可能性（旋转即把整个数组里的数都往前1格，第一个数则到最后一个位置）


AC代码：

```cpp
uses math;
var
  a,sum:array[0..51]of longint;
  fmax,fmin:array[0..51,0..10]of longint;
  n,m,i,j,k,x,maxn,minn,t:longint;
function ff(x:longint):longint;
begin
  exit(((x mod 10)+10) mod 10);
end;//对取10余
procedure dp;
var
  i,j,k:longint;
begin
  for i:=1 to n do 
    sum[i]:=sum[i-1]+a[i];//当前可能性的前缀和
  for i:=0 to n do 
    for j:=0 to m do 
      begin
        fmax[i,j]:=-66666666; 
        fmin[i,j]:=66666666;
      end;//赋初值
  for i:=1 to n do
    begin 
      fmax[i,1]:=ff(sum[i]);;
      fmin[i,1]:=ff(sum[i]);
    end;//边界
  for i:=1 to n do 
    for j:=2 to m do
      for k:=j-1 to i-1 do  
        begin
          fmax[i,j]:=max(fmax[i,j],fmax[k,j-1]*ff(sum[i]-sum[k]));
          fmin[i,j]:=min(fmin[i,j],fmin[k,j-1]*ff(sum[i]-sum[k]));
        end;//dp
  maxn:=max(maxn,fmax[n,m]);
  minn:=min(minn,fmin[n,m]);//最终两个最优解分别存在fmax[n,m]和fmin[n,m]，然后打个擂台
end;
begin
  readln(n,m);
  for i:=1 to n do readln(a[i]);
  minn:=66666666;
  for i:=1 to n do 
    begin
      t:=a[1];
      for j:=1 to n-1 do 
        a[j]:=a[j+1];
      a[n]:=t;//把整个数组里的数都往前1格，第一个数则到最后一个位置
      dp;//求当前这种可能的两个最优解
    end;//旋转出这条环所有的可能性
  writeln(minn);
  writeln(maxn);
end.
```