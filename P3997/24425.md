这题很多题解都是用扫描线+权值线段树/Splay，在这里提供一个只需普通线段树的解法。


------------
### 1.将扇形转化为线段

题目中将整个圆**分成 $ 2m $ 个小区间**，我们将其从 $ 1 $ 到 $ 2m $ 编号。以便用线段树进行维护。

![](https://i.loli.net/2019/02/12/5c62a2acc70ac.png)

### 2.确定第 $ k $ 大半径

首先将扇形按半径由大到小排序，之后依次覆盖到图上。每次覆盖其实就是一次区间修改，如果一个区间已经被修改了 $ k $ 次，便代表其已被 $ k $ 个扇形覆盖。当前扇形的半径便是第 $ k $ 大半径。

### 3.确定覆盖区间

1. $ a_1 < a_2 $ :

 此情况较为简单， $ a_1+m+1 $ 为区间左端点， $ a_2+m $ 为区间右端点

2. $ a_1 > a_2 $ :

 此时需要修改两个区间，分别是 $ a_1+m+1 $ 到 $ 2m $ ，以及 $ 1 $ 到 $ a_2+m $
 
```cpp
for i:=1 to n do begin
  if a[i].x<=a[i].y then change(1,2*m,1,a[i].x+m+1,a[i].y+m,a[i].r)
    else begin
      change(1,2*m,1,a[i].x+m+1,2*m,a[i].r);
      change(1,2*m,1,1,a[i].y+m,a[i].r);
    end;       
 end;
```

### 4.进行区间修改

修改过程不能像普通的区间加一样，因为其懒标记若没在准确的时间下放，会造成答案偏小。

如图，当 $ k=2 $ ，此时已有 $ DE $ 区间被覆盖 $ 2 $ 次，但由于线段树懒标记未下放，就无法统计答案。

![](https://i.loli.net/2019/02/12/5c62a5c633ea1.png)

所以，我们需要找到方法来判断懒标记下放时间。

```cpp
   tree,tree_max,p,q:array[0..8000005]of int64;
   tf:array[0..8000005]of boolean;
```

$ tree[i] $ 统计覆盖次数的线段树

$ treemax[i] $ 记录当前区间内，最多处有多少个扇形覆盖

$ p[i] $ 懒标记

$ q[i] $ 当前区间内，已有多少个小区间统计过答案

$ tf[i] $ 当前区间是否全部小区间都统计过答案

在处理过程中，每当区间 $ t $ 被完全覆盖，若区间 $ t $ 未处理，就将 $ tree[t] \ ,\  treemax[t] \ ,\  p[t] $ 加 $ 1 $ ，如果 $ tree[t]>=k $ ,就统计答案，并将 $ tf[t] $ 标记为已处理。若区间 $ t $ 已处理，则直接返回。当区间$ t $ 未被完全覆盖，而 $ treemax[t]+p[t]>=k $ ,就将懒标记下放。

```cpp
procedure change(l,r,t,x,y,o:longint);     //o为半径
var mid:longint;
begin
  if tf[t]=true then exit;
  if q[t]=r-l+1 then begin     //此区间所有小区间皆处理过，相当于此区间处理过
    tf[t]:=true;
    exit;
  end;
  if (x<=l) and (y>=r) then begin
    inc(tree[t]);
    inc(tree_max[t]);
    inc(p[t]);
    if tree[t]>=k then begin
      tf[t]:=true;
      inc(ans,o*o*(r-l+1-q[t]));   //计算答案
      q[t]:=r-l+1;
      tree_max[t]:=0;
      exit;
    end;
    if tree_max[t]+p[t]>=k then down(l,r,t,o);
    exit;
  end;
  mid:=(l+r) shr 1;
  down(l,r,t,o);
  if x<=mid then change(l,mid,t shl 1,x,y,o);
  if y>=mid+1 then change(mid+1,r,t shl 1 + 1,x,y,o);
  q[t]:=q[t shl 1]+q[t shl 1 + 1];
  tree_max[t]:=max(tree_max[t],max(tree_max[t shl 1],tree_max[t shl 1 + 1]));
end;
```

但不同于一般线段树，懒标记不是下放一层，而是直接下放到叶节点，并且在期间也要统计答案。（详见程序）

```cpp
procedure down(l,r,t,o:longint);
var mid:longint;
begin
  if tf[t]=true then exit;
  if q[t]=r-l+1 then begin
    tf[t]:=true;
    exit;
  end;
  if tree[t]>=k then begin
    tf[t]:=true;
    inc(ans,o*o*(r-l+1-q[t]));
    q[t]:=r-l+1;
    tree_max[t]:=0;
    exit;
  end;
  if l=r then exit;
  if p[t]<>0 then begin
    mid:=(l+r) shr 1;
    inc(tree[t shl 1],p[t]);
    inc(tree[t shl 1 + 1],p[t]);
    inc(tree_max[t shl 1],p[t]);
    inc(tree_max[t shl 1 + 1],p[t]);
    inc(p[t shl 1],p[t]);
    inc(p[t shl 1 + 1],p[t]);
    p[t]:=0;
    if tree_max[t shl 1]+p[t]>=k then down(l,mid,t shl 1,o);
    if tree_max[t shl 1 + 1]+p[t]>=k then down(mid+1,r,t shl 1 + 1,o);
    q[t]:=q[t shl 1]+q[t shl 1 + 1];
  end;
end;
```

### 5.完整程序

```cpp
program project1;
type tp=record
  x,y,r:int64;
end;

var
   tree,tree_max,p,q:array[0..8000005]of int64;
   tf:array[0..8000005]of boolean;
   a:array[0..100005]of tp;
   n,m,k,ans:int64;

function max(a,b:int64):int64;
begin
  if a>b then exit(a) else exit(b);
end;

procedure re;
var i:longint;
begin
  for i:=1 to n do read(a[i].r,a[i].x,a[i].y);
end;

procedure qsort(l,r:longint);
var i,j:longint;
    mid:int64;
    t:tp;
begin
  i:=l;
  j:=r;
  mid:=a[(i+j) shr 1].r;
  repeat
    while a[i].r>mid do inc(i);
    while a[j].r<mid do dec(j);
    if i<=j then begin
      t:=a[i];
      a[i]:=a[j];
      a[j]:=t;
      inc(i);
      dec(j);
    end;
  until i>j;
  if i<r then qsort(i,r);
  if l<j then qsort(l,j);
end;

procedure down(l,r,t,o:longint);
var mid:longint;
begin
  if tf[t]=true then exit;
  if q[t]=r-l+1 then begin
    tf[t]:=true;
    exit;
  end;
  if tree[t]>=k then begin
    tf[t]:=true;
    inc(ans,o*o*(r-l+1-q[t]));
    q[t]:=r-l+1;
    tree_max[t]:=0;
    exit;
  end;
  if l=r then exit;
  if p[t]<>0 then begin
    mid:=(l+r) shr 1;
    inc(tree[t shl 1],p[t]);
    inc(tree[t shl 1 + 1],p[t]);
    inc(tree_max[t shl 1],p[t]);
    inc(tree_max[t shl 1 + 1],p[t]);
    inc(p[t shl 1],p[t]);
    inc(p[t shl 1 + 1],p[t]);
    p[t]:=0;
    if tree_max[t shl 1]+p[t]>=k then down(l,mid,t shl 1,o);
    if tree_max[t shl 1 + 1]+p[t]>=k then down(mid+1,r,t shl 1 + 1,o);
    q[t]:=q[t shl 1]+q[t shl 1 + 1];
  end;
end;

procedure change(l,r,t,x,y,o:longint);
var mid:longint;
begin
  if tf[t]=true then exit;
  if q[t]=r-l+1 then begin
    tf[t]:=true;
    exit;
  end;
  if (x<=l) and (y>=r) then begin
    inc(tree[t]);
    inc(tree_max[t]);
    inc(p[t]);
    if tree[t]>=k then begin
      tf[t]:=true;
      inc(ans,o*o*(r-l+1-q[t]));
      q[t]:=r-l+1;
      tree_max[t]:=0;
      exit;
    end;
    if tree_max[t]+p[t]>=k then down(l,r,t,o);
    exit;
  end;
  mid:=(l+r) shr 1;
  down(l,r,t,o);
  if x<=mid then change(l,mid,t shl 1,x,y,o);
  if y>=mid+1 then change(mid+1,r,t shl 1 + 1,x,y,o);
  q[t]:=q[t shl 1]+q[t shl 1 + 1];
  tree_max[t]:=max(tree_max[t],max(tree_max[t shl 1],tree_max[t shl 1 + 1]));
end;

procedure main;
var i:longint;
begin
  for i:=1 to n do begin
    if a[i].x<=a[i].y then change(1,2*m,1,a[i].x+m+1,a[i].y+m,a[i].r)
      else begin
        change(1,2*m,1,a[i].x+m+1,2*m,a[i].r);
        change(1,2*m,1,1,a[i].y+m,a[i].r);
      end;      
  end;
  writeln(ans);
end;

begin
  read(n,m,k);
  re;
  qsort(1,n);
  main;
end.

```