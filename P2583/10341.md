我来发一个Pascal题解吧。。。

这道题其实和UVA1025 A Spy in the Metro是同一道题，可以用动态规划来解决。

由于DP方程是蒟蒻自己想出来的，所以比大佬们难看一些。

状态是二维的，f[i][j]表示在第i时刻到达第j个车站所需要等待的最短时间。

状态转移方程：

只对向回开的车进行考虑，先求出最早到达本站的车，这样才能使等待的时间尽可能得少，蒟蒻用了一重循环。。。

设等待时间为tot。

然后枚举下一次开往哪一站，开往第k站到达的时间为i+tot+time[k]-time[j].

f[i+tot+time[k]-time[j]][k]:=min(f[i+tot+time[k]-time[j]][k],f[i][j]+tot); （1<=k<j）

向终点开的车也是如此：

f[i+tot+time[k]-time[j]][k]:=min(f[i+tot+time[k]-time[j]][k],f[i][j]+tot); (j<k<=n)

大功告成啦。

Pascal Code：

```cpp
uses math;//调用数学库
var
  f:array[0..505,0..505]of int64;
  a,b:array[0..505]of longint;
  time:array[0..505]of longint;
  n,m,i,j,k,g_o,come,o,x:longint;
  inf,tot,ans:int64;
begin
  inf:=1000000007;
  repeat
    readln(n);
    if n=0 then
      exit;
    inc(o);
    readln(m);
    for i:=2 to n do
    begin
      read(x);
      time[i]:=time[i-1]+x;//前缀和
    end;
    readln;
    readln(g_o);
    for i:=1 to g_o do
      read(a[i]);
    readln;
    readln(come);
    for i:=1 to come do
      read(b[i]);
    readln;
    for i:=0 to m do
      for j:=0 to n do
        f[i][j]:=inf;
    f[0][1]:=0;//初始化
    for i:=0 to m do
      for j:=1 to n do
      begin
        if f[i][j]=inf then//若f[i][j]不存在，以下都是无意义的
          continue;
        tot:=inf;
        for k:=1 to come do
        begin
          if i>b[k]+time[n]-time[j] then
            continue;
          tot:=min(tot,b[k]+time[n]-time[j]-i);
        end;//计算tot
        if tot<>inf then
        begin
          for k:=j-1 downto 1 do
            if i+tot+time[j]-time[k]<=m then
              f[i+tot+time[j]-time[k]][k]:=min(f[i+tot+time[j]-time[k]][k],f[i][j]+tot);//向回开
        end;
        tot:=inf;
        for k:=1 to g_o do
        begin
          if i>a[k]+time[j] then
            continue;
          tot:=min(tot,a[k]+time[j]-i);
        end;
        if tot<>inf then
        begin
          for k:=j+1 to n do
            if i+tot+time[k]-time[j]<=m then
              f[i+tot+time[k]-time[j]][k]:=min(f[i+tot+time[k]-time[j]][k],f[i][j]+tot);//向终点开
        end;
      end;
    ans:=inf;
    for i:=0 to m do
      ans:=min(ans,f[i][n]+m-i);//选择最优方案
    if ans=inf then
      writeln('Case Number ',o,': impossible') else
      writeln('Case Number ',o,': ',ans);
  until false;
end.
```