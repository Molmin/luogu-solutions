地铁间谍：时空DP

特工玛利亚被送到S市执行一个特别危险的任务。她需要利用地铁完成他的任务，S市的地铁只有一条线路运行，所以并不复杂。

玛利亚有一个任务，现在的时间为0，她要从第一个站出发，并在最后一站的间谍碰头。玛利亚知道有一个强大的组织正在追踪她，她知道如果一直呆在一个车站，她会有很大的被抓的风险，躲在运行的列车中是比较安全的。所以，她决定尽可能地呆在运行的列车中，她只能往前或往后坐车。

玛利亚为了能准时且安全的到达最后一个车站与对方碰头，需要知道在在车站最小等待时间总和的计划。你必须写一个程序，得到玛丽亚最短的等待时间。当然，到了终点站之后如果时间还没有到规定的时刻，她可以在车站里等着对方，只不过这个等待的时刻也是要算进去的。

这个城市有n个车站，编号是1-n，火车是这么移动的：从第一个车站开到最后一个车站。或者从最后一站发车然后开会来。火车在每特定两站之间行驶的时间是固定的，我们也可以忽略停车的时间，玛利亚的速度极快，所以他可以迅速上下车即使两辆车同时到站。

输入输出格式 Input/output

输入格式：

输入文件包含多组数据，每组数据都由7行组成

第1行：一个正整数N（2<=N<=50）表示站的数量

第2行：一个正整数T（0<=T<=200）表示需要的碰头时间

第3行：1-（n-1）个正整数（0<ti<70）表示两站之间列车的通过时间

第4行：一个整数M1（1<=M1<=50）表示离开第一个车站的火车的数量

第5行：M1个正整数：d1，d2……dn，（0<=d<=250且di<di+1）表示每一列火车离开第一站的时间

第6行：一个正整数M2（1<=M2<=50）表示离开第N站的火车的数量

第7行：M2个正整数：e1,e2……eM2,(0<=e<=250且ei<ei+1)表示每一列火车离开第N站的时间

最后一行有一个整数0。

输出格式：

对于每个测试案例，打印一行“Case Number N: ”（N从1开始）和一个整数表示总等待的最短时间或者一个单词“impossible”如果玛丽亚不可能做到。按照样例的输出格式。

输入输出样例 Sample input/output

样例测试点#1

输入样例： 在线IDE

4
55
5 10 15

4
0 5 10 20

4
0 5 10 15

4
18
1 2 3
5
0 3 6 10 12

6
0 3 5 7 12 15

2
30
20
1
20
7
1 3 5 7 11 13 17

0

输出样例：

Case Number 1: 5

Case Number 2: 0

Case Number 3: impossible

说明 description

第一组样例说明，她0分钟时上车，在3号站下车，立刻坐上（0分始发）15分开的车回去，到2号车站，立刻坐上（20分始发）25开的车到终点，50分到，还需要等待5分钟。


思路：这个题可以看出是DP，但是难处理的就是列车有很多，还从两边同时开出，发车时间还不一样，所以就给我们进行时间和空间上的转移造成了不变，那么这个题难点1就在于预处理，否则你边DP边处理会疯的！


在额外说一个技巧：题目有多组数据，结尾有个0，但是 每组数据的第一个 N>=2 所以我们可以设置一个N的傀儡变量C，每次先读入C，如果C=0那么说明数据结束，程序跳出；如果C<>0那么C的值实际上是N，我们就N:=C;再继续读入数据就可以了


讲一下如何预处理：开一个数组a【i,j】开始初始化为0，a【i,j】=k表示第i辆列车，在时间点j的时候到达了k，相当于a【i,j】中记录了每辆列车的时刻表。这样在进行DP的时候就相当方便！对于本题我是把从两边开的列车分开用两个数组a1和a2分别存的，合起来也没什么影响。

当预处理反向列车的时刻表的时候，用前缀和写起来比较方便。


在讲一下如何DP以及状态转移的问题：

我们开一个数组f【i,j】以第一维（时间）为主轴，第二维（车站标号）为副轴进行转移

```cpp
for i:=0 to t do//枚举时间
for j:=1 to n do//枚举车站标号
begin
for k:=1 to m1+m2 do//方案1：找一辆车上去
```
if 在时间i的时候第k辆的列车经过了j车站 then
for h:=i+1 to t do//上车，枚举下车站点

if 第k辆列车在时间点h的时候到了另一个车站 then//更新此车时刻表上剩下的车站。

f[h,下一站]:=min(f[h,下一站],f[i,j]);

f[i+1,j]:=min(f[i+1,j],f[i,j]+1)//方案2：呆在这里等。

end；

无后效性说明：我们以时间为主轴在外层循环，所以我们不可能让时间倒流，也就是当确定了i时刻的f【i，1..n】之后我们就进入了i+1时刻，这也是最优子结构的体现。


AC代码：

```delphi

program exam1;
var tt:array[1..50]of longint;//记录每两站之间的时间       比如tt[3] 记录了2-3的时间
var f:array[0..200,0..50]of longint;//DP数组
var a1,a2:array[1..50,0..200]of longint;//列车时刻表
var d,e:array[1..50]of longint;//发车时间
ttt:array[0..50]of longint;//前缀和
var h,num,c,i,j,m1,m2,n,t,k,ans:longint;
function min(q1,q2:longint):longint;
begin
if q1<q2 then exit(q1)
else exit(q2);
end;

begin
read(c);//读入第一组的N的傀儡

while c<>0 do
begin
inc(num);//组数+1
n:=c;//将傀儡值还给N
read(t);

fillchar(tt,sizeof(tt),0);
fillchar(ttt,sizeof(ttt),0);
fillchar(d,sizeof(d),0);
fillchar(e,sizeof(e),0);
fillchar(a1,sizeof(a1),0);
fillchar(a2,sizeof(a2),0);//必须要有的初始化

for i:=2 to n do
read(tt[i]);
read(m1);
for i:=1 to m1 do
read(d[i]);
read(m2);
for i:=1 to m2 do
read(e[i]);//本组数据读入完毕

read(c);//读入下一组数据的N或者0

for i:=2 to n do
ttt[i]:=ttt[i-1]+tt[i];//前缀和       ttt[i]  表示从1到i的时间

for i:=1 to m1 do//计算正向列车的时刻表
if d[i]<=t then//如果发车时间合法
begin
a1[i,d[i]]:=1;//发车时刻 此车在发车站
for j:=2 to n do//枚举到j号车站的时间
if d[i]+ttt[j]<=t then//如果时间合法
a1[i,d[i]+ttt[j]]:=j;//记录第i列车  在时间d[i]+ttt[j]到达了j车站
end;

for i:=1 to m2 do//计算反向列车的时刻表
if e[i]<=t then//如果发车时间合法
begin
a2[i,e[i]]:=n;//发车时刻 此车在终点站
for j:=n-1 downto 1 do//枚举到j号车站的世间安
if e[i]+ttt[n]-ttt[j]<=t then//如果时间合法
a2[i,e[i]+ttt[n]-ttt[j]]:=j;//记录第i列车 在时间e[i]+ttt[n]-ttt[j]到了j车站       ttt[n]-ttt[j]是从n到j的时间
end;


for i:=0 to t do for j:=1 to n do f[i,j]:=100000000;//初始化
f[0,1]:=0;//初始化

for i:=0 to t do//时间主轴
for j:=1 to n do  //now is here       车站副轴
begin
for k:=1 to m1 do //1che      枚举正向列车编号k
if a1[k,i]=j then       //1che is here    如果此时（时刻i）此车（第k个）在这里（j车站）
for h:=i+1 to t do     //search next        上车 转移他所能到的剩余的车站
if a1[k,h]<>0 then    //next is in station    如果经过了一个车站
f[h,a1[k,h]]:=min(f[h,a1[k,h]],f[i,j]);      转移之

for k:=1 to m2 do //2che    枚举反向列车编号k
if a2[k,i]=j then     //如果此时（时刻i）此车（编号k）在这里（j车站）
for h:=i+1 to t do    //转移时刻表上剩下的车站
if a2[k,h]<>0 then   //在h时刻经过了一个车站
f[h,a2[k,h]]:=min(f[h,a2[k,h]],f[i,j]);//转移之

f[i+1,j]:=min(f[i+1,j],f[i,j]+1);//除了坐车之外，我们还可以在这里等1秒钟
end;


ans:=100000000;//寻找答案
for i:=0 to t do   //我们可能在0..t中的多个时刻到达n车站 此时f[i,n]中记录了路上所需要的等待时间，我们还需要额外等t-i的时间接上头
if f[i,n]<>100000000 then  //如果时刻i到达过n
ans:=min(ans,f[i,n]+t-i);//更新答案
write('Case Number ',num,': ');//输出首部  注意：冒号后面有个空格！！！！！！！！！
if ans=100000000 then writeln('impossible')//如果答案没被更新过 则表示我们无法在规定时间内到达n
else writeln(ans);
end;
end.

```

希望在题目中提醒一下那个冒号后面十分隐蔽的空格！
