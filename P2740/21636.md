发题解的大神全都用C++,我只能发个Pascal......






```cpp
var
  a,pre,now,son,v,opp:array[-1..10000]of longint;  //a：记录当前点到源点所经过的最少点数，pre：记录当前列的父亲节点上一次当父亲的列的序号，
                                                   //now：记录这个点当前当父亲的最列的序号，son：记录当前列的子节点，v：记录当前管道的流量，
                                                   //opp：记录反向边的列的序号
  h:array[-1..100000]of longint;                   //h：记录队列元素
  tot,m,n,ans,aa,b,c,i:longint;                    //m：边数，n:点数，ans：记录总流量，aa，b，c：一条管子的开始点，结束点，流量
procedure cc(a,b,c:longint);                       //做指父链
begin
  inc(tot);
  pre[tot]:=now[a];
  now[a]:=tot;
  son[tot]:=b;
  v[tot]:=c;
end;
function bfs:boolean;
var
  t,w,p:longint;
begin
  fillchar(a,sizeof(a),255);
  t:=0;w:=1;                 //队列,t:头指针,w:尾指针
  h[1]:=1;                   //h:队列元素
  a[1]:=1;                   //a数组表示从原点到当前点所要经过的最少点数
  repeat
    inc(t);                  //从左到右依次搜索每一个数
    p:=now[h[t]];            //取现在的数的最后一次当父亲的那一列，赋值为p（即以当前数为父亲节点）
    while p>0 do             //只要t这个数还有当父亲的列，就继续搜索
    begin
      if (a[son[p]]=-1)and(v[p]<>0) then   //如果从父亲点到子节点有路，并且子节点还没赋过值
      begin
        a[son[p]]:=a[h[t]]+1;              //到子节点经过的点数等于到父节点经过的点数+1
        if son[p]=n then                   //如果走到了汇点
          exit(true);                      //表示这条路可以走，退出，用find函数使ans加上这条路可以通过的水量
        inc(w);                            //如果还没有走到汇点，
        h[w]:=son[p];                      //将这个子节点加入队列
      end;
      p:=pre[p];                           //向上一个t当父亲的列搜索
    end;
  until t=w;                               //一直搜索到没有新点，即已经从头搜索到尾了，就退出
  exit(false);        //如果将所有的点都搜索完了，还没有找到可以从源点走到汇点的路，表示所有的路都已经走过了，即ans不会再增加了，就退出
end;
function min(a,b:longint):Longint;        //取最小值
begin
  if a<b then
    exit(a);
  exit(b);
end;
function find(po,f:longint):longint;  //添加反向边（取消流）
var
  ans,p,k:longint;
begin
   ans:=0;
   if po=n then      //如果当前点为汇点，即已经从源点走到了汇点，就退出，ans加上这条路可以通过的水量
     exit(f);
   p:=now[po];       //以当前点为父亲节点，搜索
   while p>0 do      //只要当前点还有当父亲的行，就运行
   begin
     if (v[p]>0)and(a[po]<a[son[p]]) then     //如果父亲节点和子节点间有路可以走，并且子节点到源点经过的点数大于父亲节点到源点的点数
     begin
       k:=find(son[p],min(f,v[p]));     //以子节点为父亲节点，向下搜索，取这一点在单位时间内通过的水量和两点间管子单位时间内流量的最小值，
                                        //即求出两点间在单位时间内最大流量是多少
       dec(v[p],k);                     //将两点间管子的流量减去这两点间总流量的最大值，即表示这两点间的管子还可以在单位时间内流多少的水
       inc(v[opp[p]],k);                //当前边的反向边加上新的流量
       inc(ans,k);                      //总流量ans加上这一个管子可以流的流量（加上新算出的流量）
       dec(f,k);                        //这一点单位时间内汇入的水量减少
       if f=0 then                      //如果这个点的水全部都在单位时间内流完了，就退出，即这个点已经没用了
         break;
     end;
     p:=pre[p];                         //继续向上找
   end;
   if p=0 then                          //如果当前点当父亲节点的列都找完了
     a[po]:=-1;                         //标记当前点到源点的距离为-1，以后就不会再找这个点了
   exit(ans);                           //将这一次新找出的流量返回
end;
begin
  tot:=0;
  readln(m,n);
  for i:=1 to m do
  begin
    readln(aa,b,c);
    cc(aa,b,c);                         //正反两边做指父链
    cc(b,aa,0);
    opp[tot-1]:=tot;                    //opp：记录当前边的反向边        
    opp[tot]:=tot-1;
  end;
  ans:=0;
  while bfs do                          //将每个点都找一遍
     inc(ans,find(1,maxlongint));       //ans加上返回的新找出的流量
  writeln(ans);                         //输出
end.
```