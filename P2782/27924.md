排序+最长不上升（下降）子序列

先排序，再二分


```pascal
var
 a,b,c:array[0..trunc(2e5)+10] of longint;
// a，b表示南北两岸的城市坐标
// c表示最长不下降子序列
 i,n,m{最长不下降子序列的长度},l,r,mid:longint;

procedure swap(var x,y:longint);//交换
 var
  z:longint;
 begin
  z:=x;
  x:=y;
  y:=z;
end;

procedure qsort(l,r:longint);//快排
 var
  i,j,temp,mid:longint;
 begin
  i:=l; j:=r;
  mid:=a[(l+r) div 2];
  repeat
   while a[i]<mid do inc(i);
   while a[j]>mid do dec(j);
   if i<=j then
    begin
     swap(a[i],a[j]);
     swap(b[i],b[j]);
     inc(i);
     dec(j);
    end;
  until i>j;
  if l<j then qsort(l,j);
  if i<r then qsort(i,r);
end;

begin
 readln(n);
 for i:=1 to n do
  readln(a[i],b[i]);
 qsort(1,n);
 c[1]:=b[1]; m:=1;
 for i:=2 to n do
  begin
   if b[i]>=c[m] then
    begin
     inc(m);//序列长度+1
     c[m]:=b[i];//存下数据
     continue;//直接跳过二分
    end;

   l:=1; r:=m; mid:=(l+r) div 2;
   while l<=r do//二分，找到插入位置
    begin
     if b[i]>c[mid] then l:=mid+1//在右半边查找
                    else r:=mid-1;//在左半边查找
     mid:=(l+r) div 2;
    end;
   c[l]:=b[i];//放入数组中
  end;
 writeln(m);
end.
```