```cpp
Const maxn=10000; 
      maxq=100000; 
Var a:array[0..1,0..maxn]of string;//变换规则 
    q:array[0..1,0..maxq]of string;//两个队列 
    step:array[0..1,0..maxn]of longint;//步数
    head,tail:array[0..1]of longint;//两个队列的头指针和尾指针 
    int,aim,s1,s2,s:string; 
    n:longint; 
Procedure split(s:string);//将目标状态和初始状态记录下来 
var k:longint; 
begin 
  k:=pos(' ',s); 
  s1:=copy(s,1,k-1); 
  s2:=copy(s,k+1,length(s)-k); 
end; 
Procedure init; //读入
begin 
  readln(s); 
  split(s); 
  int:=s1;//初始状态
  aim:=s2;//目标状态 
  n:=0; 
  while not eof do 
  begin 
    readln(s); 
    if s='' then exit; 
    inc(n); 
    split(s); 
    a[0,n]:=s1;//初始的可以转换的状态
    a[1,n]:=s2;//由初始状态转换一步得到的目标状态
  end; 
end; 
Function vis(s:string;t:byte):boolean; 
var i:longint; 
begin 
  vis:=false; 
  for i:=1 to tail[t] do//遍历队列
  if q[t,i]=s then exit(true);//如果找到目标状态就返回值true 
end; 
Procedure print(k:longint); 
begin 
  writeln(k);//（如果合法）输出最少变换步数 
  halt; 
end; 
Procedure check(t:byte); 
var i:longint; 
begin 
  for i:=1 to tail[1-t] do //遍历队列（当前的状态保存在队列里）
  if q[1-t,i]=q[t,tail[t]] then //如果两个广搜碰头了
  print(step[1-t,i]+step[t,tail[t]]);//总的步数就是两个广搜步数之和 
end; 
Procedure bfs(t:byte); //广搜（t=0是正着搜，t=1是反着搜）
var i,j,k:longint; 
    pre,tmp:string; 
begin 
  inc(head[t]);//头指针加一
  pre:=q[t,head[t]];//入队 
  for i:=1 to n do//遍历变换规则 
  begin 
    k:=length(a[t,i]); 
    for j:=1 to length(pre)-k+1 do//按照变换规则扩展状态 
    begin 
      if copy(pre,j,k)=a[t,i] then//如果规则符合 
      begin 
        tmp:=copy(pre,1,j-1)+a[1-t,i]+copy(pre,j+k,length(pre)-j-k+1);//扩展下一个状态 
        if not vis(tmp,t) then//如果没有找到目标状态 
        begin 
          inc(tail[t]); 
          q[t,tail[t]]:=tmp; 
          step[t,tail[t]]:=step[t,head[t]]+1;//步数++ 
        end; 
        check(t);//检查是否终止搜索（注意位置，不然就T了）
      end; 
    end; 
  end; 
end; 
Procedure doublebfs;//用数组下标来区分两个队列和两个广搜 
begin 
  head[0]:=0;//第一个队列的头指针
  head[1]:=0;//第二个队列的头指针
  tail[0]:=1;//第一个队列的尾指针
  tail[1]:=1;//第二个队列的尾指针 
  q[0,1]:=int;//初始状态
  q[1,1]:=aim;//目标状态
  step[0,1]:=0;//步数
  step[1,1]:=0;//步数 
  while (head[0]<tail[0])and(head[1]<tail[1])do 
  if tail[1]<tail[0] then 
  bfs(1) else bfs(0);//保持两个广搜的同步 
end; 
Begin 
  init; 
  doublebfs; 
  writeln('NO ANSWER!'); 
End.
```