这题用广搜来做，字符串aa为起点，目标为bb，每次判断aa中可以被替换的子串，存到队列尾。要判断替换后的字符串是否出现过（否则会TLE）。还有就是替换子串了（用delete和insert）。判断过程中如果变化次数已经到11而且还未转化成bb那么可以直接输出'NO ANSWER!'。还有h（队列）数组范围，一开始只开了0..1000就RE了……最多10步所以略开大一点（可以自己举一个变化10次的例子试一下要开多大数组才不会201）。

代码如下：

```pascal

var m,aa,bb,s:string;
    t,w,i,j,k,n:longint;
    a,b:array[1..10]of string;
    h:array[0..10000]of string;
    ans:array[0..10000]of longint;
function had:boolean;//判断这种情况是否出现过
var i:longint;
 begin
  for i:=1 to w do
   if h[i]=m then exit(false);
  exit(true);
 end;
procedure find(x,y:string;k:longint);//寻找该字符串中是否有可替换的子串
var i:longint;mm:string;
 begin
  i:=1;
  while i<=length(y) do
   begin
    if y[i]=x[1] then//如果开头一样再考虑是否包含该子串
    begin
     mm:=copy(y,i,length(x));
     if mm=x then
      begin
       m:=y;
       delete(m,i,length(x));//删除
       insert(b[k],m,i);//插入
       if had then//如果该情况是第一次出现
               begin
                inc(w);
                h[w]:=m;//增加一个队列尾
                ans[w]:=ans[t]+1;//变化次数+1
                if h[w]=bb then
                 if ans[w]<=10 then 
                  begin write(ans[w]);halt;end;//宽搜一旦搜到就是最优解
                if ans[w]>11 then begin write('NO ANSWER!');halt;end;//剪枝
               end;
      end;
     end;
    inc(i);//字符串中可能包含多个相同子串
   end;
 end;

begin
 readln(s);
 i:=1;
 while s[i]<>' ' do
  begin
   aa:=aa+s[i];
   inc(i);
  end;
 delete(s,1,i);
 bb:=s;
 if aa=bb then begin write(0);halt;end;
 while not eoln do
  begin
   readln(s);
   inc(n);
   i:=1;
   while s[i]<>' ' do
    begin
     a[n]:=a[n]+s[i];
     inc(i);
    end;
   delete(s,1,i);
   b[n]:=s;
  end;//读入
 t:=1;
 w:=1;
 h[1]:=aa;//h队列
 while t<=w do//宽搜开始
  begin
   for i:=1 to n do
    find(a[i],h[t],i);//判断h[t]中是否有a[i]子串
   inc(t);
  end;
 writeln('NO ANSWER!');
end.