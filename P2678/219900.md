这题的算法是![](https://s2.ax1x.com/2019/07/30/eG9cvj.png)

主要做法：二分跳跃的的最大值，然后判断，用now表示之前的石头，用i循环现在的石头，比较a_now和a_i的值，如果比现在答案小了，累加累加器，否则更新now。之后判断一下累加器和m就可以了

理解难点：pd部分

我们假设现在有石头3，4，5，3和4之间的距离是2（不符合要求），4和5之间的距离是4（符合要求），此时now是3，i是5

![](https://s2.ax1x.com/2019/07/30/eG9Rrn.png)

那么我们就要舍去石头4，更新i后，就要比较石头3和石头5之间的距离，所以此时就不应该更新now

![](https://s2.ax1x.com/2019/07/30/eG9Wbq.png)

当我们后来遇到不用舍去的石头时，就要把now更新为此时的有效石头

```pascal
var
  l,n,m,i,z,y,mid,ans:longint;
  a:array[0..50001] of longint;
function pd(x:longint):boolean;//判断答案是否成立
var
  i:longint;
  s:longint=0;
  now:longint=0;//过程中的变量要清零！
begin
  for i:=1 to n+1 do
    if (a[i]-a[now]<x) then
      inc(s)//不到最短距离，累加器+1，不用更新上次石头
    else
      now:=i;//更新上次石头
  if s>m then exit(false)//此时答案的s,超了题目的m
    else exit(true);
end;
begin
  read(l,n,m);
  for i:=1 to n do
    read(a[i]);
  a[0]:=0;//注意起点是0
  a[n+1]:=l;//注意终点是n+1
  z:=1;y:=l;
  while z<=y do//二分最长距离
  begin
    mid:=(z+y) shr 1;
    if pd(mid) then
    begin
      z:=mid+1;//寻找可能的最优答案
      ans:=mid;//及时保存可行答案
    end
    else
      y:=mid-1;
  end;
  writeln(ans);
end.
```

