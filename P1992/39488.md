简单的tarjan

缩完点后看一下每个强联通分量点的个数有没有>=2，如果有，那就是存在环
YES和NO的问题就处理好了

但是题目还是要皮一下，如果是YES要输出2^k，如果是NO要输出k^2；

前面的写快速幂就行了，后面的要提醒一下P党的同学(C++是不是这样我不知道诶)
**如果你k定义为longint,后面就不能直接writeln(k*k);**因为这句话其实是先用一个中间变量存一下答案，然后再输出，而那个中间变量的类型是根据前面这个k的类型而定的，这样的话就会爆longint。

Code:
```
var
  base,ans:int64;
  bi:boolean;
  top,tot,a1,a2,len,t:longint;
  i,j,m,n,k,p:longint;
  next,other,a,check,low,dfn,color,f,num:array[0..10005]of longint;
function min(x,y:longint):longint;
begin
  if x<y then exit(x)
  else exit(y);
end;
procedure tarjan(u:longint);
var
  k,v,j:longint;
begin
  inc(top); f[top]:=u; check[u]:=1;
  inc(t); dfn[u]:=t; low[u]:=t;
  k:=a[u];
  while k<>0 do
    begin
      v:=other[k];
      if check[v]=0 then
        begin
          tarjan(v);
          low[u]:=min(low[u],low[v]);
        end
      else
        if check[v]=1 then
          low[u]:=min(low[u],dfn[v]);
      k:=next[k];
    end;
  if dfn[u]=low[u] then
    begin
      inc(tot);
      repeat
        j:=f[top]; dec(top);
        check[j]:=-1;
        color[j]:=tot;
        inc(num[tot]);
      until j=u;
    end;
end;
begin
  readln(n,m,k);
  for i:=1 to m do
    begin
      readln(a1,a2);
      inc(len);
      next[len]:=a[a1]; a[a1]:=len; other[len]:=a2;
    end;
  for i:=1 to n do
    if check[i]=0 then
      tarjan(i);
  bi:=true;
  for i:=1 to tot do
    if num[i]>1 then
      begin
        bi:=false; break;
      end;
  if bi=false then
    begin
      writeln('No');
      ans:=k*k;
      writeln(ans);
    end
  else
    begin
      writeln('Yes');
      ans:=1; base:=2;
      while k<>0 do
        begin
          if k and 1=1 then ans:=(ans*base) mod 9997;
          base:=(base*base) mod 9997;
          k:=k shr 1;
        end;
      writeln(ans);
    end;
end.

```