用了个dfs+hash记忆化，没想到一遍过，数据太弱了。。。

```cpp
type    node    = record c , k : longint ; end ;
        arr     = array[1..5] of longint ;
var     f       : array[0..5,0..5,0..5,0..5,0..5] of longint ;
        hash    : array[0..1000] of longint ;
        rebate  : array[0..100,1..5] of node ;
        hold    : array[0..100] of longint ;
        prefer  : array[0..100] of longint ;
        price   : array[0..100] of longint ;
        buy     : array[0..100] of node ;
        cnt , sellnum , buynum : longint ;
procedure       dp (a : arr) ;
var     i , j , tmp , ret
                : longint ;
        b       : arr ;
        flag    : boolean ;
begin
        if (f[a[1]][a[2]][a[3]][a[4]][a[5]] <> -1) then
         exit ;
        ret := 1000000000 ;
        for i := 1 to 5 do
         if (a[i] > 0) then begin
          move (a , b , 20) ;
          dec (b[i]) ;
          dp (b) ;
          tmp := f[b[1]][b[2]][b[3]][b[4]][b[5]] + price[i] ;
          if (tmp < ret) then
           ret := tmp ;
         end ;
        for i := 1 to sellnum do begin
         move (a , b , 20) ;
         flag := true ;
         for j := 1 to hold[i] do begin
          dec (b[rebate[i][j].c] , rebate[i][j].k) ;
          if (b[rebate[i][j].c] < 0) then begin
           flag := false ;
           break ;
          end ;
         end ;
         if (not flag) then
          continue ;
         dp (b) ;
         tmp := f[b[1]][b[2]][b[3]][b[4]][b[5]] + prefer[i] ;
         if (tmp < ret) then
          ret := tmp ;
        end ;
        f[a[1]][a[2]][a[3]][a[4]][a[5]] := ret ;
end ;
procedure       solve ;
var     i , j , n , c , k , p : longint ;
        a       : array[1..5] of longint ;
begin
        cnt := 0 ;
        readln (sellnum) ;
        for i := 1 to sellnum do begin
         read (n) ;
         hold[i] := n ;
         for j := 1 to n do begin
          read (c , k) ;
          if (hash[c] = 0) then begin
           inc (cnt) ;
           hash[c] := cnt ;
          end ;
          c := hash[c] ;
          rebate[i][j].c := c ;
          rebate[i][j].k := k ;
         end ;
         readln (prefer[i]) ;
        end ;
        readln (buynum) ;
        for i := 1 to buynum do begin
         readln (c , k , p) ;
         if (hash[c] = 0) then begin
          inc (cnt) ;
          hash[c] := cnt ;
         end ;
         c := hash[c] ;
         buy[i].c := c ; buy[i].k := k ;
         price[c] := p ;
        end ;
        fillchar (a , sizeof (a) , 0) ;
        for i := 1 to buynum do
         inc (a[buy[i].c] , buy[i].k) ;
        fillchar (f , sizeof (f) , $ff) ;
        f[0][0][0][0][0] := 0 ;
        dp (a) ;
        writeln (f[a[1]][a[2]][a[3]][a[4]][a[5]]) ;
end ;
begin
        solve ;
end .
```
后来看网上题解，发现竟然是dp背包。。。
状态设置：F[a1][a2][a3][a4][a5]为买a1件物品1,a2件物品2,a3件物品3,a4件物品4,a5件物品5时，所需的最少价格

边界条件：F[0][0][0][0][0]=0;

状态转移方程：

F[a1][a2][a3][a4][a5]=min{F[ a1-P[i][1] ][ a2-P[i][2] ][ a3-P[i][3] ][ a4-P[i][4] ][ a5-P[i][5] ]+P[i][0]}

其中i=1..s+b; 且 ak-p[i][k]>=0

(来自nocow)

代码(来自nocow)

 
 
 
 
```cpp
var name:array[1..999] of integer;
    a:array[1..5] of integer;
    sale:array[1..104,0..5] of integer;
    s,b:integer;
procedure init;
  var n,c,k,p,i,j,t:integer;
  begin
    fillchar(name,sizeof(name),0);
    fillchar(sale,sizeof(sale),0);
    t:=0;
    readln(s);
    for i:=1 to s do
      begin
        read(n);
        for j:=1 to n do
          begin
            read(c,k);
            if name[c]=0 then begin t:=t+1; name[c]:=t; end;
            sale[i,name[c]]:=k;
          end;
        readln(sale[i,0]);
      end;
    readln(b);
    for i:=1 to b do
      begin
        readln(c,a[i],p);
        if name[c]=0 then begin t:=t+1; name[c]:=t; end;
        sale[s+i,name[c]]:=1;
        sale[s+i,0]:=p;
      end;
  end;
function min(a,b:integer):integer;
  begin
    if a<b then min:=a else min:=b;
  end;
procedure work;
  var f:array[0..5,0..5,0..5,0..5,0..5] of integer;
      i,j,k,l,m,n:integer;
  begin
    f[0,0,0,0,0]:=0;
    for i:=0 to a[1] do
      for j:=0 to a[2] do
        for k:=0 to a[3] do
          for l:=0 to a[4] do
            for m:=0 to a[5] do
              if i+j+k+l+m<>0 then
                begin
                  f[i,j,k,l,m]:=maxint;
                  for n:=1 to s+b do
                    if (i>=sale[n,1]) and (j>=sale[n,2]) and (k>=sale[n,3]) and (l>=sale[n,4]) and (m>=sale[n,5]) then
                      f[i,j,k,l,m]:=min(f[i,j,k,l,m],f[i-sale[n,1],j-sale[n,2],k-sale[n,3],l-sale[n,4],m-sale[n,5]]+sale[n,0]);
                end;
    writeln(f[a[1],a[2],a[3],a[4],a[5]]);
  end;
begin
  init;
  work;
end.
```