## P1735 【字母迷宫】
题目链接：[【P1735 字母迷宫】](https://www.luogu.com.cn/problem/P1735)


------------
前言：此题我两年前曾打过 AC 程序，用裸的 BFS 甚至走过的就不再走，对于此题来说明显是不理智的行为，能过我也是醉了。

题意：Mini 可以把 $[0,1]$ 或 $[0,N]$ 或 $[N,0]$ 当做起点，终点为 $[N,N]$ 求起点到终点的最短路。矩阵中有4种字符。A 表示 Mini 可以用一秒向上下左右传一格。B 表示 Mini 可以用一秒向上下左右传两格。C 表示 Mini 可以用两秒向左上左下右上右下四个方向中的任意一个方向传送。而 # 则表示不能传送到。

例子解析：样例实在太水了，所以我们直接采用讨论里老哥发的 hack 数据。


```
输入
10
ABBACCCBCA
ABB*C*BA**
CCACABBA*A
B*CBBCABAB
BAA*B*ABCB
CBC***CCBC
ABA*AABB*B
CAABCA*AAA
BABAB**CAA
*AABC*ACAC
输出
12
```
走法是 $[0,10]$ , $[1,10]$ , $[1,9]$ , $[2,8]$ , $[3,8]$ , $[3,7]$ , $[5,7]$ , $[5,8]$ , $[7,8]$ , $[7,10]$ , $[9,10]$ , $[10,10]$ 。对于这组数据 AC 程序跑出来是 17 而正解是 12 ，可见 AC 程序跑出来并不是最优。

解题思路：这题直接套用 SPFA 的模板，开始加队列里加入三个起点，用 BFS 跑。想对于普通 BFS 来说将原本的 bool 数组改为整数数组记录到当前位置的最优解。

代码（~~虽然已转 C++ ，但改两年前 Pascal 更快，希望 C++ 党别介意我偷懒~~）：
```pascal
const tx:array[1..4,1..2] of longint=((1,0),(-1,0),(0,-1),(0,1));//四种方向
ty:array[1..4,1..2] of longint=((1,1),(-1,-1),(1,-1),(-1,1));//天堂之门四种方向
var b,c:array[0..2250000] of integer;
f:array[0..1301,0..1301] of longint;//记录每个点的最小值
a:array[0..1301,0..1301] of word;
used:array[0..1301,0..1301] of boolean;
n,i,j:longint;ch:char;
procedure bfs;
var t,w,i,x1,y1,d:longint;
begin
t:=1;w:=3;
b[1]:=0;c[1]:=n;f[0,n]:=0;//三个起点初始化
b[2]:=n;c[2]:=0;f[n,0]:=0;
b[3]:=1;c[3]:=0;f[1,0]:=0;
while t<=w do
  begin
  for i:=1 to 4 do
    begin
    d:=f[b[t],c[t]];
    if (a[b[t],c[t]]=0) or (a[b[t],c[t]]=1) then//时空之门
      begin
      x1:=b[t]+tx[i,1];
      y1:=c[t]+tx[i,2];
      d:=d+1;
      end;
    if (a[b[t],c[t]]=2) then//海洋之门
      begin
      x1:=b[t]+tx[i,1]*2;//走两步
      y1:=c[t]+tx[i,2]*2;
      d:=d+1;
      end;
    if (a[b[t],c[t]]=3) then//天堂之门
      begin
      x1:=b[t]+ty[i,1];
      y1:=c[t]+ty[i,2];
      d:=d+2;
      end;
    if (x1>0) and (y1>0) and (x1<=n) and (y1<=n) and (a[x1,y1]<>4) and (f[x1,y1]>d) then//若在界内且更优
      begin
      f[x1,y1]:=d;//更新
      if (used[x1,y1]=false) then //如果还在队列里就不加入队列
        begin 
         inc(w);
        b[w]:=x1;
        c[w]:=y1;
         used[x1,y1]:=true;//记录当前点已经在队列里
         end;
      end;
    end;
  used[b[t],c[t]]:=false;
  inc(t);//出队
  end;
end;
begin
readln(n);
for i:=1 to n do
  begin
  for j:=1 to n do
    begin
    read(ch);
    if ch='A' then a[i,j]:=1
    else if ch='B' then a[i,j]:=2
    else if ch='C' then a[i,j]:=3
    else a[i,j]:=4;
    f[i,j]:=maxlongint div 2;//初始化
    end;
  readln;
  end;
if a[n,n]=4 then writeln('No answer')
else
  begin
  bfs;
  if f[n,n]<>maxlongint div 2 then writeln(f[n,n])
  else writeln('No answer');//判断
  end;
end.
```

后话：打完后才发现题解的一楼老哥直接把起点和终点换了下直接用 bool 数组过了，学到了学到了。

~~最后弱弱地问一句：啥时候改数据？~~