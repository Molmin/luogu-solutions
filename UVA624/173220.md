## $Problems$
你即将开车出远门，你的车上只有播放录音带的设备，但是你最喜欢的音乐却都存放在 $CD$ 上。所以你需要把 $CD$ 上的音乐转录到录音带上。

现在你必须解决的问题是：你的空白录音带长共 $N$ 分钟，你如何选择 $CD$ 上的歌使得尽可能的利用录音带的空间。

	注意!!!
	CD上的歌最多不会超过20首。
	没有任何一首歌的长度超过N分钟。
	要录在录音带上的歌不能重复。
	每首歌的长度以一整数表达。
	N也是一个整数。
    
你必须找出该放哪些 $CD$ 上的歌到录音带上（按 $CD$ 上的顺序），使得录音带空白的空间最小？
## $Answer$
这道题是一道背包问题，但是背包又有 $01$ 背包和完全背包，那么该怎么做呢？？？
由题目可知，没个歌曲只有放或不放 $2$ 中可能，自然就是 $01$ 背包。

一般的 $01$ 背包一般只能知道最优值，但无法知道最优路径，所以最优路径的输出是本题的难点。

因为二维数组会记录路径，所以使用二维数组，定义 $f_{i,j}$ 表示从第 $1$ 首到第 $i$ 首歌曲中选择若干首使得歌曲时间最长，则 $f_{i,j}=max(f_{i-1,j},f_{i-1,j-a_i}+a_i),$但是我们还是不知道最优路径的输出方案。
```pascal
for i:=1 to m do
	for j:=0 to n do
		if (j<a[i]) then f[i,j]:=f[i-1,j]
			else f[i,j]:=max(f[i-1,j],f[i-1,j-a[i]]+a[i]);
```
我们可以看出 $f$ 数组是从前往后更新的，那么最后变化的那么数据就是我们需要的。

因为最后一首歌曲和最长的时间 $f_{m,n}$ 是最后一次变化的，如果 $f_{m,n}:=f_{m-1,n-a_m}+a_m,$则说明最后一首歌曲一定是选中的一个，使其保存在 $b$ 数组中，然后在求 $f_{m-1,j-a_m}$是从哪一步演变而来的，所以我们就可以从后往前的推出路径。

注意这种推导 $b$ 保存的数是从后往前的，所以输出是需要逆向输出。
## $Code$
```pascal
uses math;
var
    n,m,i,j,k:longint;
    a,b:array[0..1010]of longint;
    f:array[0..110,0..100010]of longint;
begin
    while not eof do
    begin
        read(n,m);
        fillchar(f,sizeof(f),0);
        for i:=1 to m do read(a[i]);
        for i:=1 to m do
            for j:=0 to n do
                if (j<a[i]) then f[i,j]:=f[i-1,j]
                    else f[i,j]:=max(f[i-1,j],f[i-1,j-a[i]]+a[i]);
        k:=0;j:=n;
        for i:=m downto 1 do
        begin
            if (j-a[i]>=0)and(f[i,j]=f[i-1,j-a[i]]+a[i]) then
            begin
                inc(k);
                b[k]:=a[i];
                j:=j-a[i];
            end;
        end;
        for i:=k downto 1 do write(b[i],' ');
        write('sum:',f[m,n]);
        writeln;
    end;
end.
```