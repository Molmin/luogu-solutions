这题我不推荐用dfs，如果题目的数据规模大一些的话会爆栈。虽然就这题本身而言dfs+记忆化就可以ac（笑）

嘛，反正这题的bfs也不算难写....直接上代码。这个算法好像有个名字叫AO什么来着？

```pascal
program p3183;

var
  nn,mm:longint;
  edge,next:array[1..200005] of longint; //邻接表
  map,du,ans,queue:array[1..100005] of longint; //du = 入度
  //无环图保证每个点最多入队一次，队列不必开太大
  hen,tai:longint; //队列头尾指针。。
  answer:longint;

  i,j:longint;

begin
  readln(nn,mm);
  fillchar(map,sizeof(map),0);
  du:=map;
  ans:=map;
  //以上两句是把du和ans清零，貌似大部分人都是再用两次fillchar
  for i:=1 to mm do begin
    readln(j,edge[i]);
    next[i]:=map[j];
    map[j]:=i;
    inc(du[edge[i]]);
  end;
  hen:=0; tai:=0;
  for i:=1 to nn do
    if (du[i]=0) and (map[i]<>0) then begin //寻找入度0且出度非0（不孤立）的点
      inc(tai);
      queue[tai]:=i;
      ans[i]:=1;
    end;
  while hen<tai do begin
    inc(hen);
    i:=map[queue[hen]];
    while i<>0 do begin
      inc(ans[edge[i]],ans[queue[hen]]);
      dec(du[edge[i]]);
      if du[edge[i]]=0 then begin
        inc(tai);
        queue[tai]:=edge[i];
      end;
      i:=next[i];
    end;
  end;
  answer:=0;
  for i:=1 to nn do
    if map[i]=0 then inc(answer,ans[i]);
    //寻找出度0的点。孤立点未经遍历，ans为0，不用排除
  writeln(answer); readln;
end.
```
附dfs+记忆化的pascal代码，稍微参考了一下楼下的。dfs的思路似乎比bfs简单一些。

```pascal
program p3183;

var
  nn,mm:longint;
  edge,next:array[1..200005] of longint;
  map,du,ans:array[1..100005] of longint;
  answer:longint;

  i,j:longint;

function dfs(x:longint):longint;
var i:longint;
begin
  if ans[x]>0 then exit(ans[x]);
  if map[x]=0 then exit(1);
  i:=map[x];
  while i<>0 do begin
    inc(ans[x],dfs(edge[i]));
    i:=next[i];
  end;
  exit(ans[x]);
end;

begin
  readln(nn,mm);
  fillchar(map,sizeof(map),0);
  du:=map;
  ans:=map;
  for i:=1 to mm do begin
    readln(j,edge[i]);
    next[i]:=map[j];
    map[j]:=i;
    inc(du[edge[i]]);
  end;
  answer:=0;
  for i:=1 to nn do
    if (du[i]=0) and (map[i]<>0) then inc(answer,dfs(i));
  writeln(answer); readln;
end.
```
亲测n=1000000（比题目多一个0）且食物网为单链无支链时dfs爆栈（废话），不过这么大的数据规模bfs也要花零点几秒
