其实不太难，清楚思路就行了。别吓到


应该看得懂


```cpp
var
  p,d:array[0..10]of real;  //（p数组=price,d数组=distance)
  n:longint;
  d1,c,d2:real;//（d1终点距离，c油箱容量，d2一升油能开多远）
procedure init;
  var
    i:longint;
  begin
    readln(d1,c,d2,p[0],n);
    for i:=1 to n do
      readln(d[i],p[i]);
  end;
procedure qs(l,r:longint);//(按距离排序，方便处理，无聊打了下快排）
  var
    i,j:longint;
    k,t:real;
  begin
    i:=l;j:=r;k:=d[(i+j) div 2];
    repeat
      while d[i]<k do inc(i);
      while d[j]>k do dec(j);
      if i<=j then
      begin
        t:=d[i];d[i]:=d[j];d[j]:=t;
        t:=p[i];p[i]:=p[j];p[j]:=t;
        inc(i);dec(j);
      end;
    until i>j;
    if i<r then qs(i,r);
    if l<j then qs(l,j);
  end;
procedure main;
  var
    i,pre,t:longint;
    min,v,s:real;
  begin
    pre:=0;s:=0;v:=0;//（pre当前加油站，s花费，v当前油箱的油）
    repeat
      i:=pre+1;//（下一个加油站）
      while (p[i]>p[pre]) and (i<=n) and not(d[pre]+c*d2<d[i]) do
        inc(i);//（寻找有没有油费更低的加油站）
      if (i<=n) and (d[pre]+c*d2>=d[i])
      then begin//（1.情况，有油费更低的加油站）
           // （处理下v，不处理的话会弄出负数）
             if (d[i]-d[pre])/d2>v then  //（要开到下一站的油量比当前油箱油量大）
             begin
               s:=s+((d[i]-d[pre])/d2-v)*p[pre]; //（多加点油）
               v:=0;  //（在下一站耗光）
             end
             else v:=v-(d[i]-d[pre])/d2;  //（不用加油了，直接开过去）
             pre:=i;
           end
      else begin
             if d[pre]+c*d2>=d1 then//（能开到终点了，最优解出来了）
             begin
               s:=s+((d1-d[pre])/d2-v)*p[pre];  //（v不会大于d1-d[pre]的，不解释了）
               writeln(s:0:2);
               exit;
             end;
             //（还到不了终点）
             t:=0;min:=maxlongint;
             for i:=pre+1 to n do //（寻找能到站中油费最小的站）
               if d[pre]+c*d2<d[i] then break
               else if p[i]<min then begin
                                       t:=i;
                                       min:=p[i];
                                     end;
            if t=0 then begin //(t=0表示后面没站了，到不了终点了---无解）
                          writeln('No Solution');
                          exit;
                        end;
            s:=s+(c-v)*p[pre];//（加满油）
            v:=c;
            v:=v-(d[t]-d[pre])/d2; //（开到下一站，油箱的油减去对应的值）
            pre:=t;
          end;
    until false;
  end;
begin
  init;
  qs(1,n);
  main;
end.
```