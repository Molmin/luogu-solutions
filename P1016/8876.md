简单的贪心+递归

简单思路：

1、先判断下一个目的地，再计算油费和加的油

2、如果当前已经在最后一个加油站并且加满油仍然走不到终点则输出No Solution

3、每次在可以走到的范围内找油费最小的加油站

2.1如果该加油站的油费当前所在地点的油费要便宜，就加恰好能开到该加油站的油

2.2否则判断能否开到终点，如果能则加恰好能开到终点的油，否则加满油

4、进行第2步的同时，寻找可以走到的最远的加油站，如果最远的加油站就是当前所在地点且当前不在终点的话输出No Solution

5、如果最远可以走到的最远的加油站是终点，就输出答案。

[color=red]代码：[/color]

[codep ]

```cpp
Var
    d1,d2,c,p:real;
    i,n:longint;
    dist,price:array[0..100000] of real;
Procedure work(nowloc:longint;restfuel,ans:real); //nowloc储存了当前位置, restfuel储存了当前剩余的油, ans储存了当前花费的预算
    var
        i:longint;
        minloc,maxfar:longint;
    begin
        if dist[n+1]-dist[n]>c*d2 then begin // 上文的步骤2
            writeln('No Solution');
            halt;
        end;
        minloc:=nowloc; //将价格最便宜的加油站和能走到的最远距离都初始化为当前地点
        maxfar:=nowloc;    
        for i:=nowloc+1 to n+1 do begin //遍历所有加油站
            if ((dist[i]-dist[nowloc])/d2)>c then break; //如果即使油箱满也无法走到则停止遍历
            maxfar:=i; //每次将该点更新为能走到的最远的点
            if price[i]<=price[minloc] then minloc:=i; //如果当前点的油费低于之前最便宜的加油站的油费则更新
        end;
        if ((maxfar=nowloc) and (nowloc<>n+1)) then begin //如果能走到的最远点是当前点且当前不在终点，则代表无法前进，退出
            writeln('No Solution');
            halt;
        end;
        if minloc=nowloc then begin //如果油费最便宜的地方是当前点，则判断是否能走到终点
            if ((dist[n+1]-dist[nowloc])/d2)<=c then begin //如果能走到终点则加恰好走到终点的油
                ans:=ans+((dist[n+1]-dist[nowloc])/d2-restfuel)*price[nowloc];
                writeln(ans:0:2);
                halt;
            end
            else begin //否则加满油
                ans:=ans+(c-restfuel)*price[nowloc];
                restfuel:=c-(dist[maxfar]-dist[nowloc])/d2; //将剩余油量减去走到最远能走到的点花费的油
                if maxfar=n+1 then begin //如果最远能走到的点是终点就输出
                    writeln(ans:0:2);
                    halt;
                end;
                    work(maxfar,restfuel,ans); //递归
                    exit;
            end;
        end;
        if minloc>nowloc then begin //如果存在更便宜的点则走到那个加油站
            ans:=ans+((dist[minloc]-dist[nowloc])/d2-restfuel)*price[nowloc]; //加恰好能走到那个加油站的油
            restfuel:=0; //因为是恰好走到所以剩余的油一定是0
            if minloc=n+1 then begin //如果最便宜的点是终点则退出（回过头来看这段判断并没有意义）
                writeln(ans:0:2);
                halt;
            end;
            work(minloc,restfuel,ans); //递归
            exit;
        end;
    end;
Begin
    read(d1,c,d2,p,n); //读入
    dist[0]:=0; //先将起点距离初始化为0
    dist[n+1]:=d1; //将终点距离初始化为d1。
    price[0]:=p; //将起点的价格初始化为p
    price[n+1]:=maxlongint; //将终点价格设置为maxlongint(防止上文中认为终点是最便宜的点)
    for i:=1 to n do //正常读入
        read(dist[i],price[i]);
    work(0,0,0); //从起点为0，花费为0，剩余油量为0开始递归
End.
```
[/codep ]