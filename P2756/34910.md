何谓最大流？首先它必须是一个可行流；其次，它的流量必须达到最大。这样的流就称为最大流。譬如对图5-1而言，它的最大流如下：

下面探讨如何求得最大流。

在定义“可改进路”概念时，提到可以通过一定规则修改“可改进路”上弧的流量，可以使得总流量放大。下面我们就具体看一看是什么“规则”。

对可改进路P上的弧<vi, vj>，分为两种情况讨论：

第一种情况：<vi, vj>∈P+，可以令fij增加一个常数delta。必须满足fij + delta ≤ cij，即delta ≤ cij – fij。

第二种情况：<vi, vj>∈P-，可以令fij减少一个常数delta。必须满足fij - delta ≥ 0，即delta ≤ fij

根据以上分析可以得出delta的计算公式：

因为P+的每条弧都是非饱和弧，P-的每条弧都是非零流弧，所以delta > 0。

容易证明，按照如此规则修正流量，既可以使所有中间点都满足“流量守恒”（即输入量等于输出量），又可以使得总的流量有所增加（因为delta > 0）。

因此我们对于任意的可行流f，只要在f中能找到可改进路，那么必然可以将f改造成为流量更大的一个可行流。我们要求的是最大流，现在的问题是：倘若在f中找不到可改进路，是不是f就一定是最大流呢？

答案是肯定的。下面我们给出证明。

定理1 可行流f是最大流的充分必要条件是：f中不存在可改进路。

证明：

首先证明必要性：已知最大流f，求证f中不存在可改进路。

若最大流f中存在可改进路P，那么可以根据一定规则（详见上文）修改P中弧的流量。可以将f的流量放大，这与f是最大流矛盾。故必要性得证。

再证明充分性：已知流f，并且f中不存在可改进路，求证f是最大流。

我们定义顶点集合U, W如下：

（a） S∈U，

（b） 若x∈U，且fxy<cxy，则y∈U;

若x∈U，且fyx>0，则y∈U。

（这实际上就是可改进路的构造规则）

（c） W = V \ U。

由于f中不存在可改进路，所以T∈W；又S∈U，所以U、W是一个割切（U, W）。

按照U的定义，若x∈U，y∈W，则fxy = cxy。若x∈W，y∈U，则fxy = 0。

所以，

又因 v(f)≤C(U,W)

所以f是最大流。得证。

根据充分性证明中的有关结论，我们可以得到另外一条重要定理：

最大流最小割定理：最大流等于最小割，即max V(f) = min C(U, W)。

至此，我们可以轻松设计出求最大流的算法：

step 1. 令所有弧的流量为0，从而构造一个流量为0的可行流f（称作零流）。

step 2. 若f中找不到可改进路则转step 5；否则找到任意一条可改进路P。

step 3. 根据P求delta。

step 4. 以delta为改进量，更新可行流f。转step 2。

step 5. 算法结束。此时的f即为最大流。

对于此题，我们只需添加一个超级源点和一个超级汇点，并将这两个点分别连向飞行员和外籍飞行员的点，连边的容量置为1

飞行员和外籍飞行员中的连边的容量置为大于1的任何数就可以用最大流做了。


```cpp
type
  node=record toit,cap,rev,next:longint; end;
var
  road:array[0..1000000]of node;
  level,iter,list:array[-50..1000000] of longint;
  n,m,s,t,i,u,v,w,cnt,x,y,j:longint;
function min(a,b:longint):longint;
begin
  if a<b then exit(a) else exit(b);
end;
procedure add(u,v,w:longint);
begin
  inc(cnt);
  road[cnt].toit:=v;road[cnt].next:=list[u];road[cnt].cap:=w;
  list[u]:=cnt; road[cnt].rev:=cnt+1;
  inc(cnt);
  road[cnt].toit:=u;road[cnt].next:=list[v];road[cnt].cap:=0;
  list[v]:=cnt; road[cnt].rev:=cnt-1;
end;
procedure bfs(s:longint);
var w,h,t:longint;
  queue:array[0..1000000]of longint;
begin
  fillchar(level,sizeof(level),255);
  level[s]:=0;
  h:=1; t:=1; queue[1]:=s;
  while h<=t do
  begin
    w:=list[queue[h]];
    while w<>0 do
    begin
      if (road[w].cap>0)and(level[road[w].toit]<0) then
      begin
        level[road[w].toit]:=level[queue[h]]+1;
        inc(t);
        queue[t]:=road[w].toit;
      end;
      w:=road[w].next;
    end;
    inc(h);
  end;
end;
function dfs(v,t,f:longint):longint;
var w,d:longint;
begin
  if v=t then exit(f);
  iter[v]:=list[v];
  w:=iter[v];
  while w<>0 do
  begin
    if (road[w].cap>0)and(level[road[w].toit]>level[v]) then
    begin
      d:=dfs(road[w].toit,t,min(f,road[w].cap));
      if d>0 then
      begin
        dec(road[w].cap,d);
        inc(road[road[w].rev].cap,d);
        exit(d);
      end;
    end;
    iter[v]:=road[iter[v]].next;
    w:=road[w].next;
  end;
  exit(0);
end;
function max_flow(s,t:longint):longint;
var flow,f:longint;
begin
  flow:=0; f:=0;
  while true do
  begin
    bfs(s);
    if level[t]<0 then exit(flow);
    fillchar(iter,sizeof(iter),0);
    f:=dfs(s,t,maxlongint);
    while f<>0 do
    begin
      flow:=flow+f;
      f:=dfs(s,t,maxlongint);
    end;
  end;
  exit(0);
end;
begin
  readln(m,n);
  readln(x,y);
  while (x<>-1)and(y<>-1) do
  begin
    add(x,y,50000);
    readln(x,y);
  end;
  s:=m+n+50; t:=s+1;
  for i:=1 to m do add(s,i,1);
  for i:=1 to n do add(i+m,t,1);
  writeln(max_flow(s,t));
  for i:=1 to m  do
  begin
    j:=list[i];
    while j<>0 do
    begin
      if (road[j].cap<50000)and(road[j].toit<>t)and(road[j].toit<>s)
      then begin writeln(i,' ',road[j].toit); break; end;
      j:=road[j].next;
    end;
  end;
end.

```