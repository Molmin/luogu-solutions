树状数组

设原数列为c,树状数组为a.

(实际编程时c是用不到的)

a中存最大值

先算出数列总长len。

建立树状数组的时候反向建立。

num存数列为空的长度，

即a[1]到a[num]为-maxlongint，a[num+1]到a[len]存有数据。

对于Q操作，即求c[1]到c[num+L]的最大值

对于A操作，在c[num]处更新。

附上pascal代码

```cpp
const
  oo=-maxlongint;
var
  a:array[1..200000] of longint;
  s:char;
  d,i,j,m,t,len,num:longint;
  b:array[1..200000,1..2] of longint;
function max(x,y:longint):longint;
  begin
    if x>y then exit(x)
    else exit(y);
  end;
function lowbit(x:longint):longint;
  begin
    lowbit:=x and -x;
  end;
procedure update(n,x:longint);//更新操作（操作A）
  begin
    while n<=len do
    begin
      a[n]:=max(a[n],x);
      n:=n+lowbit(n);
    end;
  end;
function ask(x:longint):longint;//询问操作（操作Q）
  begin
    ask:=oo;
    while x>0 do
    begin
      ask:=max(ask,a[x]);
      x:=x-lowbit(x);
    end;
  end;
begin
  readln(m,d);
  len:=0;
  for i:=1 to m do
  begin
    read(s);
    readln(b[i,2]);
    if s='Q' then  b[i,1]:=1
    else if s='A' then
    begin
      b[i,1]:=2;
      b[i,2]:=b[i,2] mod d;
      inc(len);  //有一个A长度就+1
    end;
  end;
  t:=0;
  for i:=1 to len do
    a[i]:=oo;
  num:=len;//刚开始所有数都是-maxlongint
  for i:=1 to m do
  begin
    if b[i,1]=1 then//Q操作
    begin
      t:=ask(num+b[i,2]);
      writeln(t);
    end
    else if b[i,1]=2 then//A操作
    begin
      update(num,(b[i,2]+t) mod d);
      dec(num);//更新一个数，num就减一
    end;
  end;
end.
```