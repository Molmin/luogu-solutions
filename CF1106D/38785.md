## 一.思路

显然可以用贪心的思想：要使序列字典序最小，只需在每一次到达**新的点**时，保证到达的是**当前能走到的**编号最小的点。

自然就可以想到做法了：用一个**小根堆**来存放下一步可以到达的点。每次到达堆顶的那个点（并且进行标记），并将所有与它相邻且未被标记的点加入堆中。时间复杂度$ \mathcal{O(m+nlogn)} $。

------------

## 二，代码

P党只能手写堆qwq

再说句废话：图**只能**用链式前向星存，别的应该都要炸QAQ

```
type
    node=record
                y,pre:longint; 
                //个人喜欢用记录（结构体）表示边
            end;
var
    n,m,cnt,x,y,top,fa,son,i,j,l:longint;
    a:array[-10..300010] of node; 
    //这个数组一定要开2e5以上，因为是双向边
    last,t:array[-10..300010] of longint;
    v:array[-10..300010] of boolean;
    //数组开大，我以前因为这个玄学RE
begin
    readln(n,m);
    fillchar(last,sizeof(last),0);
    cnt:=0;
    for i:=1 to m do
    begin
        read(x,y);
        inc(cnt);
        a[cnt].y:=y;
        a[cnt].pre:=last[x];
        last[x]:=cnt; //连x->y的边
        inc(cnt);
        a[cnt].y:=x;
        a[cnt].pre:=last[y];
        last[y]:=cnt; //连y->x的边
    end;
    top:=1; t[1]:=1; //最初从1开始
    fillchar(v,sizeof(v),false); //都未经过
    v[1]:=true; //标记起点
    for i:=1 to n do
    begin
        l:=t[1];
        j:=last[l];
        write(t[1],' ');
        t[1]:=t[top]; dec(top); //弹出堆顶元素
        fa:=1; son:=2;
        if t[2]>t[3] then son:=3;
        while (son<=top) and (t[fa]>t[son]) do
        begin
            x:=t[fa]; t[fa]:=t[son]; t[son]:=x;
            fa:=son; son:=fa*2;
            if t[son]>t[son+1] then inc(son);
        end; //维护小根堆
        while j<>0 do
        begin
            y:=a[j].y;
            if not v[y] then //该点没有走过
            begin
                inc(top); t[top]:=y;
                v[y]:=true; //进行标记
                son:=top; fa:=top div 2;
                while (fa>=1) and (t[fa]>t[son]) do
                begin
                    x:=t[fa]; t[fa]:=t[son]; t[son]:=x;
                    son:=fa; fa:=son div 2;
                end; //维护小根堆
            end;
            j:=a[j].pre; //继续看下一条边
        end;
    end;
    writeln;
end.
```

-----------

## 三，一个值得注意的地方

建议先弹出堆顶元素，再加入新点。写得不好的话，程序可能会输出**修改后的**堆顶元素值。这个提示可能对您的调试会有些许帮助。

------------

> 2019年2月6日 星期三 于slay.one：

> 	我bcr就是饿死，死外面，从这里跳下去，也不会再手写一个堆！    

> …………