先概括一下题意，就是要在n座客栈里找两座**色调相同**的客栈使得他们之间（**含端点**）有一家客栈的咖啡厅满足条件最低消费不超过p，并统计**方案数**。


------------

## 思路：

由题目可以很简单地知道要把客栈以色调为依据分组，然后就可以在每一组中~~暴力~~枚举两座不同的客栈与它们之间符合要求的咖啡厅的数量，统计出答案，但这样子在最坏的情况下时间复杂度会达到O（n^3），显然会TLE。

 _**优化→**_ 因为客栈的位置单调递增，所以可以用二分来进行优化
 
 具体先预先用二分预处理出对于客栈i来说**位于右侧，且符合条件**的**最近**的咖啡厅nearest[i]，再枚举每一个客栈，再次用二分来查找在选出的咖啡厅（nearest[i]）右侧的同色调客栈，统计答案
 

------------

##  上代码：
 
```pascal
var
  id:array[0..55,0..200005]of longint;
  //id[i,j]表示色调为i的第j个客栈的编号
  dr,nearest:array[0..200005]of longint;
  //dr[i]表示第i个符合要求的咖啡厅的编号
  //nearest[i]表示出对于客栈i来说位于右侧，且符合条件的最近的咖啡厅
  top:array[0..50]of longint;
  //top[i]表示色调为i的客栈数量
  n,k,p,i,col,cos,m,j,cofe,l,r,mid,ttt:longint;
  ans:int64;
begin
  read(n,k,p);
  for i:=1 to n do
  begin
    read(col,cos);
    //col表示色调，cos表示费用（cost）
    inc(col);
    inc(top[col]);
    id[col,top[col]]:=i;
    //按色调分组
    if cos<=p then
    begin inc(m); dr[m]:=i; end;
    //统计符合要求的咖啡厅并存入数组
  end;
  for i:=1 to n do
  begin
    l:=1; r:=m;
    while (l<=r) do
    begin
      mid:=(l+r)>>1;
      if dr[mid]=i then
      begin cofe:=mid; break；end;
      if dr[mid]<i then
      begin cofe:=mid; l:=mid+1; end
      else r:=mid-1;
    end;//二分
    nearest[i]:=cofe;
  end;//预处理Nearest[i]
  for i:=1 to k do
    for j:=1 to top[i] do
    begin
      cofe:=nearest[id[i,j]];
      //找出咖啡厅
      l:=0; r:=j-1;
      while l<r do
      begin
        mid:=(l+r+1) div 2;
        if id[i,mid]<=dr[cofe] then l:=mid
        else r:=mid-1;
      end;//二分出第二个客栈
      if id[i,l]<=dr[cofe] then ans:=ans+l;
      //统计答案，ans++
    end;
  write(ans);
end.
```


------------

这样就ac了

![](https://cdn.luogu.com.cn/upload/image_hosting/0cwcn74b.png)
