其实这道题目的思想就是**bfs**，有些困难的是数据太大，不能裸搜

因为当（x1,y1）和（x2,y2）是**连通**的时候，它们的答案**必定相等**

所以我们就将搜到的点的答案都记录下来，到时候直接拿出来用就可以了！

## $Code$
```pascal
const dx:array[1..4] of longint=(0,1,0,-1);
      dy:array[1..4] of longint=(1,0,-1,0);
var n,m,xx,yy,i,j:longint;
    ans,a:array[0..1001,0..1001] of longint;
    f:array[0..1001,0..1001] of boolean;
    b:array[0..1000001] of record x,y:longint; end;
    k:char;

procedure bfs(x1,y1:longint);
  var h,t,i,t1,t2:longint;
  begin
    h:=0;t:=1;
    b[1].x:=x1;
    b[1].y:=y1;//本人的程序刚开始前面加了fillchar就TLE……
    f[x1][y1]:=true;
    while(h<t)do
    begin
      inc(h);
      for i:=1 to 4 do
      begin
        t1:=b[h].x+dx[i];
        t2:=b[h].y+dy[i];
        if (t1<1) or (t1>n) or (t2<1) or (t2>n) then continue;
        if (not f[t1][t2]) and (1-a[b[h].x][b[h].y]=a[t1][t2]) then
        begin
          inc(t);
          b[t].x:=t1;
          b[t].y:=t2;
          f[t1][t2]:=true;
        end;
      end;
    end;
    for i:=1 to t do ans[b[i].x][b[i].y]:=t;//将已经走过的点的答案保存下来
  end;

begin
  readln(n,m);
  for i:=1 to n do
  begin
    for j:=1 to n do
    begin
      read(k);
      a[i][j]:=ord(k)-48;
    end;
    readln;
  end;
  fillchar(ans,sizeof(ans),255);
  for i:=1 to n do//先全部扫一遍
    for j:=1 to n do
      if not f[i][j] then bfs(i,j);
  for i:=1 to m do
  begin
    readln(xx,yy);
    writeln(ans[xx][yy]);
  end;
end.
```
第一次发题解，如有更好的建议，欢迎在评论区留言哦~~