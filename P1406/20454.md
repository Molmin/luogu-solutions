此题..

$n <= 4$当然是搜索了..

题意要求.给出$n^2$个数.要求填入$n^2$的方格中,每个数只出现1次.且满足,每行每列与2条对角线的和相同.输出相同的和与字典序最小的解.

字典序的话.排个序.第1次找到就行了..

和可以推出来..  $ns = \sum_{i = 1}^{n}{a_i}$

然后可以发现.每行每列只要填$n-1$个数.

剩下的都可以推出来了.

于是n少了1..

```cpp
n--;
```
另外1篇题解的剪枝是在填完1行或者1列去判断符不符合条件.

我就直接判断最后那个数是否存在了...

这样跑出来.0ms.效率高...

因为填完后回溯的个数不定..代码长度....

Diu代码看吧...

```cpp
program P1406;
 var
  aatt:array[0..5,0..5] of longint;           //已经填好的数.
  a,b:array[0..17] of longint;
  i,j,n,t,lpass,ssum:longint;
 function hahacon(x,y,ix,iy:longint):longint; //这个根据不同的.
  var                                           //方向来判断.
   j,sum:longint;                               //行列是否满足.
  begin
   sum:=0;
   for j:=1 to n-1 do
    inc(sum,aatt[x+ix*(j-1),y+iy*(j-1)]);     //通过填好的.
   sum:=ssum-sum;                               //算出最后1个.
   for j:=1 to n*n do
    if (b[j]=0) and (sum=a[j]) then exit(j);  //如果存在.
   exit(-1);                                    //也没用过的话.
  end;                                          //返回"可以".
 procedure hahaDFS(x,y:longint);              //主要搜索过程.
  type                                          //x,y是当前位置.
   w=array[0..17] of longint;
  var
   wgl:w;
   i,j,wgls,upass,ucon,sum:longint;
  procedure hahaincgg(x,y:longint); //函数内套函数.
   begin
    if upass=-1 then                //如果这个数不行.
     begin
      ucon:=1;                      //标记跳过..
      exit;
     end;
    inc(wgls);                      //产生新位置.
    wgl[wgls]:=upass;                 //因为不知道1次性填了几个数.
    aatt[x,y]:=a[upass];              //要弄数组存下...
    b[upass]:=1;                      //方便回溯...
   end;
  begin
   if lpass=1 then exit;             //结果已产生.不必继续.
   if x=n then                       //填完了..
    begin
     sum:=0;
     for i:=0 to n-1 do             //左下-右上对角线.
      inc(sum,aatt[i+1,n-i]);         //因为上面hahacon没法判断.
     if sum<>ssum then exit;          //放在这里..
     for i:=1 to n do               //输出.
      begin
       for j:=1 to n-1 do
        write(aatt[i,j],' ');
       writeln(aatt[i,n]);
      end;
     lpass:=1;                      //结果产生标记.
    end;
   for i:=1 to n*n do               //枚举没填过的数.
    if b[i]=0 then
     begin
      ucon:=0;
      wgls:=0;
      upass:=i;
      hahaincgg(x,y);               //当前位置填上.
      if x=n-1 then                 //判断1列.
       begin
        upass:=hahacon(1,y,1,0);
        hahaincgg(x+1,y);
       end;
      if y=n-1 then                 //判断1行.
       begin
        upass:=hahacon(x,1,0,1);
        hahaincgg(x,y+1);
       end;
      if (x=n-1) and (y=n-1) then   //判断左上-右下对角线.
       begin
        upass:=hahacon(1,1,1,1);
        hahaincgg(x+1,y+1);
       end;
      if ucon=0 then
       if y=n-1 then hahaDFS(x+1,1) //搜索..
                else hahaDFS(x,y+1);
      for j:=1 to wgls do           //回溯..
       b[wgl[j]]:=0;                  //标记为没有用过.
      wgls:=0;
     end;
  end; 
 begin
  readln(N);
  for i:=1 to n*n do
   begin
    read(A[i]);
    inc(ssum,a[i]);
   end;
  for i:=1 to n*n-1 do              //排序..
   for j:=i+1 to n*n do
    if a[i]>a[j] then
     begin
      t:=a[i];
      a[i]:=a[j];
      a[j]:=t;
     end;
  readln;
  ssum:=ssum div n;                //和...
  writeln(ssum);
  filldword(b,length(b),0);
  lpass:=0;
  aatt[1,1]:=a[1];
  hahaDFS(1,1);
 end.
```
(ಡωಡ).
