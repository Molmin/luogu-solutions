f[i,j]表示取了i列，且第i列是j

我们可以先搜索行

在动归之前，我们要思考，怎么才能枚举最小的c列？我们是否可以先预处理那些需要计算的值？


比如第一个样例，假如我们已经搜到了2行和4行，那么我们可以得到以下的矩阵:

9 4 8 7 4

6 8 5 6 9

我们可以先求出每列的上下之间的差，放在一个一维数组里(d[i]表示第i列相邻数之间上下的值)


那么当一列一列合起来时，我们还需要计算左右的值，那么就可以用一个二维数组来表示(e[i,j]表示i列和j列之间左右值 i<j)


完成了以下铺垫，我们就可以动归了，枚举接上去的那个点，并累加相应的值，这种直线型的动归类似于马厩问题和80分的羊羊列队，是dp的一个经典模型









```cpp
var n,m,r,c,i,j,ans:longint;
b,d,p:array[0..16]of longint;
a,e,f:array[1..16,1..16]of longint;
function min(a,b:longint):longint;
begin
  if a<b then exit(a) else exit(b);
end;
procedure dp;
var i,j,k:longint;
begin
  fillchar(d,sizeof(d),0);
  fillchar(e,sizeof(e),0);
  fillchar(f,sizeof(f),$7f);
  for i:=1 to m do
  for j:=1 to r-1 do d[i]:=d[i]+abs(a[b[j],i]-a[b[j+1],i]);
  for i:=1 to m do
  for j:=i+1 to m do
  for k:=1 to r do e[i,j]:=e[i,j]+abs(a[b[k],j]-a[b[k],i]);
  for i:=1 to m do f[1,i]:=d[i];
  for i:=2 to c do
  for j:=i to m do
  for k:=i-1 to j-1 do
  f[i,j]:=min(f[i,j],f[i-1,k]+e[k,j]+d[j]);
  for i:=c to m do
  ans:=min(ans,f[c,i]);
end;
procedure ldy(k:longint);
var i:longint;
begin
  if k>r then
  begin
    dp;
    exit;
  end;
  for i:=1 to n do
  if(p[i]=0)and(i>b[k-1]) then
  begin
    p[i]:=1;
    b[k]:=i;
    ldy(k+1);
    p[i]:=0;
  end;
end;
begin
  readln(n,m,r,c);
  ans:=maxlongint;
  for i:=1 to n do
  for j:=1 to m do read(a[i,j]);
  ldy(1);
  writeln(ans);
end.
```