给P党发点福利，毕竟FreePascal在NOIP的日子不长了。

很显然打两个暴力组合是可以拿分的，暴力出所有可能的组合方式，记录最优值。


那么满分算法该如何实现。首先如果说是一个数列里取x个数，求一个分度最小的方案是可以用DP比较容易实现的。用f[i,j]表示取i个数，以原数列中第j个数结尾的最优值。k表示取的第i-1个数，即j的前导。状态转移方程大概是这样



    f[i,j]:=max(f[i,j],f[i-1,k]+abs(a[j]-a[k]));

那么有了这样的方法再来看子矩阵这道题就比较简单了。


两个暴力组合超时，但一个16个以内的暴力组合时间还是绰绰有余的。那么就可以用暴力一半，DP一半的方式进行维度和时间压缩AC掉。


用暴力组合求出每种行的组合方案，然后DP列求最优。唯一要注意的就是要在每次DP前初始化DP和预处理数组。当确定某一个行的组合时，取每列的分度显然已经是可以预求得的，同样，取任意两列的分度也是可以预处理出来的。那么就已经可以把取每列的分度看做一个长m的序列，而我们要做的就是求这个序列的最优子序列。用c\_ud[i]记录取第i列的分度，用c\_lr[i,j]记录取第i,j两列的分度。i,j,k的代表不变。状态转移方程大概长这样



  f[i,j]:=max(f[i,j],f[i-1,k]+c\_lr[j,k])+c\_ud[j];

程序赠送~




```cpp
var i,j,k,n,m,x,y,s,oo,ans,nn,mm:longint;
    t,c_ud:array[0..20]of longint;
    a,f,c_lr:array[0..20,0..20]of longint;
     //f[i,j]表示选i列以j结尾的最优子矩阵；
function min(a,b:longint):longint;
begin
  if a<b then exit(a); exit(b);
end;
procedure dp;
var i,j:longint;
begin
  fillchar(f,sizeof(f),$7f);
  fillchar(c_ud,sizeof(c_ud),0);
  fillchar(c_lr,sizeof(c_lr),0);
  for i:=1 to mm do
   for j:=2 to n do
    c_ud[i]:=c_ud[i]+abs(a[t[j],i]-a[t[j-1],i]);
  for i:=1 to mm do
   for j:=1 to mm do
    for k:=1 to n do
     c_lr[i,j]:=c_lr[i,j]+abs(a[t[k],i]-a[t[k],j]);
  for i:=1 to mm do f[1,i]:=c_ud[i];
  for i:=2 to m do
   for j:=i to mm do
    begin
    for k:=i-1 to j-1 do
     f[i,j]:=min(f[i,j],f[i-1,k]+c_lr[k,j]);
    inc(f[i,j],c_ud[j]);
    end;
  for i:=m to mm do ans:=min(ans,f[m,i]);
end;
procedure ysq(k:longint);
var i:longint;
begin
  if k=n+1 then begin dp; exit; end;
  for i:=t[k-1]+1 to nn do
   begin t[k]:=i; ysq(k+1); t[k]:=0; end;
end;
begin
  oo:=maxlongint div 3;
  readln(nn,mm,n,m);
  for i:=1 to nn do
   for j:=1 to mm do read(a[i,j]);
  ans:=oo;
  ysq(1);
  writeln(ans);
end.
```