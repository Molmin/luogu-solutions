## 【pascal题解】
上次那个弱化版的堆优化加了似乎跟没加一样[弱化版](https://www.luogu.org/problemnew/show/P3371)（原谅我一个蒟蒻挑了几个晚上才调出来……）其实就是把数组开大，在弱化版的基础上维护小根堆时判断点是否已经在堆中即可，如果在堆中，则更改heap值，否则加入队列【详见代码】。

代码：
```
  var n,m,k,ledge,i,b,x,y,z:longint;
      ff,head,num:array[0..200001]of longint;
      //比上次多了个ff记录是否在堆中
      heap,dis:array[0..200001]of int64;
      edge:array[0..200001]of record
        y,nxt,z:longint;
      end;
  procedure swap(var a,b:longint);
  var t:longint;
  begin
        t:=a;a:=b;b:=t;
  end;


  procedure swap1(var a,b:int64);
  var t:int64;
  begin
        t:=a;a:=b;b:=t;
  end;


  procedure add(x,y,z:longint);
  begin
        inc(ledge);
        edge[ledge].y:=y;
        edge[ledge].z:=z;
        edge[ledge].nxt:=head[x];
        head[x]:=ledge;
  end;  //链式前向星储存


  procedure insert(a,x:longint);
  var i:longint;
  begin
        if ff[x]<>0 then  //如果已经在堆中，返回堆中位置
        begin
                heap[ff[x]]:=a;
                i:=ff[x];
        end
        else
        begin
                inc(heap[0]);
                heap[heap[0]]:=a;
                num[heap[0]]:=x;
                i:=heap[0];
                ff[x]:=heap[0];
        end;
        while (i div 2>0)and(heap[i]<heap[i div 2]) do
        begin
                swap(ff[num[i]],ff[num[i div 2]]);
                swap1(heap[i],heap[i div 2]);
                swap(num[i],num[i div 2]);
                i:=i div 2;
        end;
  end;


  procedure del;
  var mid,i:longint;
  begin
        ff[num[1]]:=0;  //弹出堆顶元素
        heap[1]:=heap[heap[0]];
        num[1]:=num[heap[0]];
        dec(heap[0]);
        i:=1;
        if (heap[i*2]<heap[i*2+1])or(i*2=heap[0]) then mid:=i*2
        else mid:=i*2+1;
        while (mid<=heap[0])and(heap[mid]<heap[i]) do
        begin
                swap(ff[num[i]],ff[num[mid]]);
                swap1(heap[i],heap[mid]);
                swap(num[i],num[mid]);
                i:=mid;
                if (heap[i*2]<heap[i*2+1])or(i*2=heap[0]) then mid:=i*2
                else mid:=i*2+1;
        end;
  end;


  begin
        readln(n,m,k);
        for i:=1 to m do
        begin
                readln(x,y,z);
                add(x,y,z);
        end;
        fillchar(dis,sizeof(dis),$7f);
        dis[k]:=0;
        b:=k;
        heap[0]:=1;heap[1]:=0;num[1]:=k;
        while heap[0]<>0 do
        begin
                i:=head[b];
                while i<>0 do
                begin
                        if dis[b]+edge[i].z<dis[edge[i].y] then
                        begin
                                dis[edge[i].y]:=dis[b]+edge[i].z;
                                insert(dis[edge[i].y],edge[i].y);
                        end;
                        i:=edge[i].nxt;
                end;
                del;
                b:=num[1];
        end;
        for i:=1 to n do
                write(dis[i],' ');
        writeln;
  end.
```


推广：[【蒟蒻的博客 欢迎来踩】](https://www.luogu.org/blog/czxshenshen/)