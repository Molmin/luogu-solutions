这道题没多少人做，~~赶紧来水一发~~


------分割线------


**题意**

就是让我们模拟一个贪吃蛇最基本的操作，而且不会变长

问你这条虫子执行指令后的情况，超出格子、撞到自己或是正常结束。

**输入、输出**

详见题面

**算法**

模拟（不然你说怎么搞。。）


以下为代码（我才不会告诉你这是我贪吃蛇游戏代码改的呢）：



```cpp
const                       //常量定义错误序号
Error_die=1;                //错误序号1：撞到自己
Error_OutOfMap=2;           //错误序号2：超出边界
Normal=0;                   //正常退出
//P.S. 如果你英文够好，其实不需要注释。。
var
n,i:longint;
s:string;                  //指令集
bug:array[0..20]of record x,y,direction:longint;end;
//用于储存虫子每节的信息
//bug[i].x意为第i节虫子的横坐标，bug[i].y为纵坐标，bug[i].direction为方向
error:boolean;            //处理指令时有无错误
function go(n:longint):longint;//过程，表示在第n个方向上前进1个单位
  var i,j:longint;
  begin
    bug[0]:=bug[1];bug[0].direction:=n;
    case bug[0].direction of
    1:inc(bug[0].y);//向东
    2:dec(bug[0].x);//向南
    3:dec(bug[0].y);//向西
    4:inc(bug[0].x);//向北
    end;//头先前进
    if bug[0].x>49 then exit(Error_OutOfMap);
    //我一开始一直90分，就是这里打的是bug[0].x>50。。
    //因为虫子是一条线，所以一开始是25，向上、向下都只有25。
    //如果打成>50，会出现26步才出界
    if bug[0].x<1 then exit(Error_OutOfMap);
    if bug[0].y>50 then exit(Error_OutOfMap);
    if bug[0].y<1 then exit(Error_OutOfMap);
    //如果超出边界，抛出错误
    for i:=20 downto 1 do bug[i]:=bug[i-1];
    //前进一格，bug[i]=bug[i-1]，其实就是把虫子的当前节变成上一节
    for i:=1 to 19 do
      for j:=i+1 to 20 do
        if(bug[i].x=bug[j].x)and(bug[i].y=bug[j].y)then exit(Error_die)；
        //如果同一只虫子有两节坐标相同，说明撞到一起了，抛出错误
    exit(Normal);//正常退出
  end;
function work(ch:char):longint;//处理指令集中的每一个指令
  var k:longint;//k表示方向
  begin
    case ch of
    'E':k:=1;//向东
    'S':k:=2;//向南
    'W':k:=3;//向西
    'N':k:=4;//向北
    end;
    //P.S. 不需要判指令是否正确，比如当前向北，指令让虫子向南，照指令来
    //如果判错，只有50分（这是题面的坑，说好的“不能向自己移动”呢？）
    exit(go(k));//向方向k前进
  end;
begin
while true do
  begin
    readln(n);if n=0 then break;//如果读到0，说明数据结束了，退出
    readln(s);                  //读入指令集
    for i:=1 to 20 do
     begin
       bug[i].y:=31-i;
       bug[i].x:=25;bug[i].direction:=1;
     end;
     //由题意初始化虫子
    error:=false;               //假设没有错误
    for i:=1 to n do            //逐个处理指令
      case work(s[i]) of        //分错误类别
      Error_OutOfMap:begin
                       writeln('The worm ran off the board on move ',i,'.');
                       error:=true;//有错误直接跳出，不必执行后面的指令了
                       break;
                     end;
      Error_die:begin
                  writeln('The worm ran into itself on move ',i,'.');
                  error:=true;
                  break;//同上
                end;
      end;
    if not error then writeln('The worm successfully made all ',n,' moves.');
    //如果没有错误，那么输出正常地结束
  end;
end.
```
------------


以上就是这题的题解了，希望管理员大大能给过，谢谢。
